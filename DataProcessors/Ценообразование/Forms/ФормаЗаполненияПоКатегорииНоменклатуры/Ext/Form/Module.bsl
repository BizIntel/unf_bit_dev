
#Область СлужебныеПроцедурыФункции

&НаСервере
Процедура ПолучитьНоменклатуру(АдресВременногоХранилища)
	
	ДеревоНоменклатуры = Новый ДеревоЗначений;
	
	// 1. Получим СКД
	ИмяСхемыКД = "ПоКатегориямНоменклатуры";
	СхемаКомпоновкиДанных = Обработки.Ценообразование.ПолучитьМакет(ИмяСхемыКД);
	
	// 2. создаем настройки для схемы 
	НастройкиКомпоновкиДанных = СхемаКомпоновкиДанных.НастройкиПоУмолчанию;
	
	// 2.1 установим значения параметров
	ПараметрКД = СхемаКомпоновкиДанных.Параметры.Найти("МассивКатегорийНоменклатуры");
	ПараметрКД.Значение = КатегорииНоменклатуры.ВыгрузитьЗначения();
	
	Если ИспользоватьХарактеристики = 0 Тогда
		
		НастройкиКомпоновкиДанных.Структура[0].Структура[0].Использование = Ложь;
		
	КонецЕсли;
	
	// 3. готовим макет 
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Макет = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиКомпоновкиДанных, , , Тип("ГенераторМакетаКомпоновкиДанныхДляКоллекцииЗначений"));
	
	// 4. исполняем макет 
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(Макет);
	ПроцессорКомпоновки.Сбросить();
	
	// 5. выводим результат 
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВКоллекциюЗначений;
	ПроцессорВывода.УстановитьОбъект(ДеревоНоменклатуры);
	ПроцессорВывода.Вывести(ПроцессорКомпоновки);
	
	СтруктураТаблицДанных = Обработки.Ценообразование.РазобратьДеревоНоменклатуры(ДеревоНоменклатуры);
	АдресВременногоХранилища = ПоместитьВоВременноеХранилище(СтруктураТаблицДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьОтчетСПредварительнымРезультатом()
	
	ПараметрыОткрытия = Новый Структура();
	ПараметрыОткрытия.Вставить("ИмяСКД", 					  "ПоКатегориямНоменклатуры");
	ПараметрыОткрытия.Вставить("МассивКатегорийНоменклатуры", КатегорииНоменклатуры.ВыгрузитьЗначения());
	ПараметрыОткрытия.Вставить("ИспользоватьХарактеристики",ИспользоватьХарактеристики);
	
	ОткрытьФорму("Обработка.Ценообразование.Форма.ФормаПредварительногоПросмотра", ПараметрыОткрытия, ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьВХранилищеДанныеТабличнойЧасти(АдресВременногоХранилища)
	
	ПолучитьНоменклатуру(АдресВременногоХранилища);
	
КонецПроцедуры

&НаКлиенте
Процедура ВывестиСообщениеОНеправильномВыборе()
	
	ТекстСообщения = НСтр("ru ='Необходимо заполнить список категорий номенклатуры'");
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "КатегорииНоменклатуры");
	
КонецПроцедуры

#КонецОбласти

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Только номенклатуру(0); Номенкатуру и ее характеристики(1);
	ИспользоватьХарактеристики = ?(Параметры.ХарактеристикиВидны = Истина, 1, 0);
	
КонецПроцедуры

#КонецОбласти

#Область СобытияРеквизитовФормы



#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура ОК(Команда)
	
	Если КатегорииНоменклатуры.Количество() > 0 тогда
		
		АдресВременногоХранилища = "";
		ЗаписатьВХранилищеДанныеТабличнойЧасти(АдресВременногоХранилища);
		
		Закрыть(Новый Структура("ВыборПроизведен, АдресВременногоХранилища", Истина, АдресВременногоХранилища));
		
	Иначе
		
		ВывестиСообщениеОНеправильномВыборе();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Новый Структура("ВыборПроизведен", Ложь));
	
КонецПроцедуры

&НаКлиенте
Процедура ПредварительныйПросмотр(Команда)
	
	Если КатегорииНоменклатуры.Количество() > 0 Тогда
		
		ПоказатьОтчетСПредварительнымРезультатом();
		
	Иначе
		
		ВывестиСообщениеОНеправильномВыборе();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
