
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры, "АлкогольнаяПродукция, Номенклатура, ИдентификаторУпаковки");
	
	ИспользуютсяХарактеристики = ПолучитьФункциональнуюОпцию("ИспользоватьХарактеристики")
		И Номенклатура.ИспользоватьХарактеристики;
	
	УстановитьВидимостьЭлементовФормы();
	
	ЗаполнитьСписокВыбораЕдиницИзмерений();
	
	ПараметрыВыбораЕдиниц = Новый Структура;
	
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("Владелец", Номенклатура);
	
	ПараметрыВыбораЕдиниц.Вставить("Отбор", СтруктураОтбора);
	
	ДанныеВыбора = Справочники.ЕдиницыИзмерения.ПолучитьДанныеВыбора(ПараметрыВыбораЕдиниц);
	Для каждого ЭлементСписка Из ДанныеВыбора Цикл
		НовыйЭлемент = Элементы.Упаковка.СписокВыбора.Добавить();
		ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементСписка);
	КонецЦикла;
	Упаковка = Номенклатура.ЕдиницаИзмерения;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИдентификаторУпаковкиИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьСоответствие(Команда)
	
	Если НЕ ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если ЕстьПредупреждения() Тогда
		ОповещениеПриЗавершении = Новый ОписаниеОповещения("ЗаписатьСоответствие_Завершение", ЭтотОбъект);
		ПоказатьВопрос(ОповещениеПриЗавершении, НСтр("ru = 'Между связываемыми элементами есть расхождения. Продолжить?'"), РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
	Иначе
		ЗаписатьСоответствие_Завершение(КодВозвратаДиалога.ОК, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьСоответствие_Завершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьСоответствиеНаСервере();
	
	Закрыть(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСоответствиеНаСервере()
	
	Запись = РегистрыСведений.СоответствиеНоменклатурыЕГАИС.СоздатьМенеджерЗаписи();
	ЗаполнитьЗначенияСвойств(Запись, ЭтотОбъект);
	Запись.Записать();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораЕдиницИзмерений()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("АлкогольнаяПродукция", АлкогольнаяПродукция);
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТоварноТранспортнаяНакладнаяЕГАИСТовары.ИдентификаторУпаковки
	|ИЗ
	|	Документ.ТТНВходящаяЕГАИС.Товары КАК ТоварноТранспортнаяНакладнаяЕГАИСТовары
	|ГДЕ
	|	ТоварноТранспортнаяНакладнаяЕГАИСТовары.АлкогольнаяПродукция = &АлкогольнаяПродукция
	|	И ТоварноТранспортнаяНакладнаяЕГАИСТовары.ИдентификаторУпаковки <> """"
	|	И ТоварноТранспортнаяНакладнаяЕГАИСТовары.Ссылка.СтатусОбработки В (ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.ПринятИзЕГАИС), ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиТТНВходящейЕГАИС.ПустаяСсылка))";
	
	Элементы.ИдентификаторУпаковки.СписокВыбора.ЗагрузитьЗначения(Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("ИдентификаторУпаковки"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьЭлементовФормы()
	
	Элементы.Характеристика.Видимость = ИспользуютсяХарактеристики;
	Элементы.ДекорацияХарактеристика.Видимость = ИспользуютсяХарактеристики;
	
	Если АлкогольнаяПродукция.Импортер.Пустая() Тогда
		Элементы.АлкогольнаяПродукцияИмпортер.Видимость = Ложь;
		Элементы.НоменклатураПроизводитель.Видимость = Ложь;
		Элементы.НоменклатураПроизводительИмпортерАлкогольнойПродукции.Заголовок = "Производитель";
	КонецЕсли;
	
	Элементы.ПредупреждениеОбъем.Видимость = Номенклатура.ОбъемДАЛ <> 0 И Номенклатура.ОбъемДАЛ <> АлкогольнаяПродукция.Объем / 10;
	Элементы.ПредупреждениеКрепость.Видимость = Номенклатура.Крепость <> 0 И Номенклатура.Крепость <> АлкогольнаяПродукция.Крепость;
	Элементы.ПредупреждениеВидПродукции.Видимость = Номенклатура.ВидАлкогольнойПродукции <> АлкогольнаяПродукция.ВидПродукции;
	Элементы.ПредупреждениеПроизводитель.Видимость = Элементы.НоменклатураПроизводитель.Видимость 
														И НЕ Номенклатура.Производитель.Пустая()
														И НЕ АлкогольнаяПродукция.Производитель.Пустая()
														И НЕ ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Производитель) = Номенклатура.Производитель;
	Элементы.ПредупреждениеИмпортер.Видимость = НЕ Номенклатура.ПроизводительИмпортерАлкогольнойПродукции.Пустая() И
													(НЕ АлкогольнаяПродукция.Импортер.Пустая()
													И НЕ ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Импортер) = Номенклатура.ПроизводительИмпортерАлкогольнойПродукции
													ИЛИ НЕ АлкогольнаяПродукция.Производитель.Пустая()
														И АлкогольнаяПродукция.Импортер.Пустая()
														И НЕ ПолучитьКонтрагентаПоСоответствию(АлкогольнаяПродукция.Производитель) = Номенклатура.ПроизводительИмпортерАлкогольнойПродукции);
	
КонецПроцедуры

&НаКлиенте
Функция ЕстьПредупреждения()
	
	Возврат Элементы.ПредупреждениеОбъем.Видимость
		ИЛИ Элементы.ПредупреждениеКрепость.Видимость
		ИЛИ Элементы.ПредупреждениеВидПродукции.Видимость
		ИЛИ Элементы.ПредупреждениеПроизводитель.Видимость
		ИЛИ Элементы.ПредупреждениеИмпортер.Видимость;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьКонтрагентаПоСоответствию(ОрганизацияЕГАИС)
	
	Если ОрганизацияЕГАИС.Пустая() Тогда
		Возврат Справочники.Контрагенты.ПустаяСсылка();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ОрганизацияЕГАИС", ОрганизацияЕГАИС);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СоответствиеОрганизацийЕГАИС.Контрагент
	|ИЗ
	|	РегистрСведений.СоответствиеОрганизацийЕГАИС КАК СоответствиеОрганизацийЕГАИС
	|ГДЕ
	|	СоответствиеОрганизацийЕГАИС.ОрганизацияЕГАИС = &ОрганизацияЕГАИС";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		Возврат Неопределено;
	Иначе
		Возврат РезультатЗапроса.Выгрузить()[0].Контрагент;
	КонецЕсли;
	
КонецФункции

#КонецОбласти
