
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Параметры.Свойство("ТекстФайла") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ТекстФайла = Параметры.ТекстФайла;
	Кодировка = Параметры.Кодировка;
	
	РасскрашенныйТекст = КлиентБанкФорматированиеФайла.ОтформатироватьИсходныйФайл(ТекстФайла);
	
	СпособПредставления = Истина;
	
	НачальноеПредставлениеФорматировано = СпособПредставления;
	Элементы.ФормаФорматированныйВид.Пометка = НачальноеПредставлениеФорматировано;
	
	УстановитьПредставление();
	
КонецПроцедуры

&НаКлиенте
Процедура ФорматированныйВид(Команда)
	Элементы.ФормаФорматированныйВид.Пометка = НЕ Элементы.ФормаФорматированныйВид.Пометка;
	УстановитьПредставление();
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ (НачальноеПредставлениеФорматировано = Элементы.ФормаФорматированныйВид.Пометка)
		И НЕ ЗавершениеРаботы Тогда
		
		СохранитьНастройкуНаСервере(Элементы.ФормаФорматированныйВид.Пометка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьПредставление()
	
	Элементы.РасскрашенныйТекст.Видимость = Элементы.ФормаФорматированныйВид.Пометка;
	Элементы.НеРасскрашенныйТекст.Видимость = НЕ Элементы.ФормаФорматированныйВид.Пометка;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура СохранитьНастройкуНаСервере(ЗначениеНастройки)
	
	ХранилищеОбщихНастроек.Сохранить("ОбменСКлиентомБанка", "ПоказыватьФорматированно", ЗначениеНастройки);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьНаДиск(Команда)
	
	ПараметрыЗаписи = ПолучитьТекстФайлаИКодировку(КлючУникальности);
	Если ПараметрыЗаписи = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не удалось получить данные из информационной базы!'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстовыйПоток = ПараметрыЗаписи.ТекстовыйПоток;
	
	Попытка
		
		ВыгружатьИнтерактивно = Истина;
		Результат = ПолучитьФайл(ТекстовыйПоток, "1c_to_kl.txt", ВыгружатьИнтерактивно);
		
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Не удалось записать данные в файл. Возможно, диск защищен от записи.'");
		ПоказатьПредупреждение(Неопределено, ТекстСообщения);
		
	КонецПопытки
	
КонецПроцедуры

&НаСервере
Функция ПолучитьТекстФайлаИКодировку(Ключ)

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("ТекстФайла", ТекстФайла);
	СтруктураВозврата.Вставить("Кодировка", Кодировка);
	
	ТекстовыйПоток = Новый ТекстовыйДокумент;
	ТекстовыйПоток.ДобавитьСтроку(СтруктураВозврата.ТекстФайла);
	
	ИмяВременногоФайла = ПолучитьИмяВременногоФайла("txt");
	Если Кодировка = "DOS" Тогда
		ТекстовыйПоток.Записать(ИмяВременногоФайла, КодировкаТекста.OEM);
	Иначе
		ТекстовыйПоток.Записать(ИмяВременногоФайла, КодировкаТекста.ANSI);
	КонецЕсли;
	ТекстовыйПоток = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяВременногоФайла), Ключ);
	
	СтруктураВозврата.Вставить("ТекстовыйПоток", ТекстовыйПоток);
	
	Возврат СтруктураВозврата;
	
КонецФункции