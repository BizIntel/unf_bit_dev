
// Служебные

&НаСервере
Процедура УстановитьДоступность(Параметры)
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Количество",		"Доступность", Параметры.ЗапрашиватьКоличество);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕдиницаИзмерения",	"Доступность", Параметры.ЗапрашиватьЦену);
	
	РазрешеноИзменятьЦену = ?(Параметры.ЗапрашиватьЦену, Параметры.РазрешеноИзменятьЦену, Параметры.ЗапрашиватьЦену);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Цена", 				"Доступность", РазрешеноИзменятьЦену);
	
	// Если запрашиваем только цену, сразу позиционируем фокус на реквизите Цена
	Если НЕ Параметры.ЗапрашиватьКоличество
		И Параметры.ЗапрашиватьЦену Тогда
		
		ЭтаФорма.ТекущийЭлемент = Элементы.Цена;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьИтоговаяСумма()
	
	Элементы.ДекорацияСумма.Заголовок = 
		ПодборНоменклатурыВДокументахКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	
КонецПроцедуры

// Форма

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Количество			= Параметры.Количество;
	ЕдиницаИзмерения	= Параметры.ЕдиницаИзмерения;
	Цена				= Параметры.Цена;
	
	УстановитьДоступность(Параметры.КешНастройкиПодбора);
	
	Элементы.ДекорацияСумма.Заголовок = 
		ПодборНоменклатурыВДокументахКлиентСервер.СуммаФорматированнойСтрокой(Количество * Цена);
	
КонецПроцедуры

//Команды

&НаКлиенте
Процедура ОК(Команда)
	
	Закрыть(Новый Структура("Количество, ЕдиницаИзмерения, Цена", Количество, ЕдиницаИзмерения, Цена));
	
КонецПроцедуры

// Реквизиты формы

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

&НаКлиенте
Процедура ЦенаПриИзменении(Элемент)
	
	ОбновитьНадписьИтоговаяСумма();
	
КонецПроцедуры

