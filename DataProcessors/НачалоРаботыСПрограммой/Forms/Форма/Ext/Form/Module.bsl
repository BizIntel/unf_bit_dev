
#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция ПолучитьНаименованиеДляБюджета(Индекс)
	
	ДанныеСтрокиОрганизации = Объект.Организации[Индекс - 1];
	
	// Для организации заполняем из полного наименования
	Если ДанныеСтрокиОрганизации.ЮридическоеФизическоеЛицо = КэшЗначений.ЮрЛицо Тогда
		
		Возврат ДанныеСтрокиОрганизации.НаименованиеПолное;
		
	КонецЕсли;
	
	// Для ИП:
	// п.4 Приложения 1 к Приказу Минфина 107н:
	// Фамилия имя отчество "(ИП)" "//"Адрес регистрации"//"
	Шаблон = "%1 (ИП) //%2//";
	
	ФИО = ДанныеСтрокиОрганизации.Фамилия 
		+ ?(ПустаяСтрока(ДанныеСтрокиОрганизации.Имя), "", " " + ДанныеСтрокиОрганизации.Имя) 
		+ ?(ПустаяСтрока(ДанныеСтрокиОрганизации.Отчество), "", " " + ДанныеСтрокиОрганизации.Отчество);
	
	АдресРегистрации = ДанныеСтрокиОрганизации.АдресЮридический;
	
	Возврат СтрШаблон(Шаблон, ФИО, АдресРегистрации);
	
КонецФункции

&НаКлиенте
Процедура ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
	ЗначениеНаименованиеПлательщикаПриПеречисленииНалогов = ПолучитьНаименованиеДляБюджета(Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПлательщикаПриПеречисленииНалогов", ЗначениеНаименованиеПлательщикаПриПеречисленииНалогов, Индекс);
	
КонецПроцедуры

&НаСервере
Процедура ЗагрузитьВидыБизнеса(ВидыБизнесаАдресВХранилище)
	
	ВидыБизнесаТЗ = ПолучитьИзВременногоХранилища(ВидыБизнесаАдресВХранилище);
	ВидыБизнеса.Загрузить(ВидыБизнесаТЗ);
	
КонецПроцедуры

&НаСервере
Функция ПодобратьВидыБизнеса(Текст)
	
	Если ПустаяСтрока(Текст) Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ЗапросТекстРазделитель =
	";
	|
	|////////////////////////////////////////////////////////////////////////////////
	|";
	
	ЗапросТекстОбъединить =
	"
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|";
	
	ЗапросТекстВыборНаименование =
	"ВЫБРАТЬ
	|	ВидыБизнеса№.Наименование КАК Наименование,
	|	ВидыБизнеса№.Синонимы КАК Синонимы,
	|	ВидыБизнеса№.Идентификатор КАК Идентификатор,
	|	1,
	|	№
	|ИЗ
	|	ВидыБизнесаВрем КАК ВидыБизнеса№
	|ГДЕ
	|	ВидыБизнеса№.Наименование ПОДОБНО &Текст№";
	
	ЗапросТекстВыборСинонимы =
	"ВЫБРАТЬ
	|	ВидыБизнеса№.Наименование КАК Наименование,
	|	ВидыБизнеса№.Синонимы КАК Синонимы,
	|	ВидыБизнеса№.Идентификатор КАК Идентификатор,
	|	1,
	|	№
	|ИЗ
	|	ВидыБизнесаВрем КАК ВидыБизнеса№
	|ГДЕ
	|	ВидыБизнеса№.Синонимы ПОДОБНО &Текст№";
	
	ЗапросТекстИтог =
	"ВЫБРАТЬ
	|	Список1.Наименование,
	|	Список1.Синонимы,
	|	Список1.Идентификатор,
	|	СУММА(Список1.Релевантность) КАК Поле1,
	|	СУММА(Список1.Релевантность2) КАК Поле2
	|ИЗ
	|	Список1 КАК Список1
	|
	|СГРУППИРОВАТЬ ПО
	|	Список1.Наименование,
	|	Список1.Синонимы,
	|	Список1.Идентификатор
	|
	|УПОРЯДОЧИТЬ ПО
	|	Поле1 УБЫВ,
	|	Поле2 УБЫВ
	|";
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВидыБизнеса.Наименование,
	|	ВидыБизнеса.Синонимы,
	|	ВидыБизнеса.Идентификатор
	|ПОМЕСТИТЬ ВидыБизнесаВрем
	|ИЗ
	|	&ВидыБизнеса КАК ВидыБизнеса
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВидыБизнеса1.Наименование КАК Наименование,
	|	ВидыБизнеса1.Синонимы КАК Синонимы,
	|	ВидыБизнеса1.Идентификатор КАК Идентификатор,
	|	1 КАК Релевантность,
	|	0 КАК Релевантность2
	|ПОМЕСТИТЬ Список1
	|ИЗ
	|	ВидыБизнесаВрем КАК ВидыБизнеса1
	|ГДЕ
	|	ВидыБизнеса1.Наименование ПОДОБНО &Текст1
	|";
	Запрос.УстановитьПараметр("ВидыБизнеса", ВидыБизнеса.Выгрузить());
	Запрос.УстановитьПараметр("Текст1", Текст);
	
	Итератор = 2;
	Отборы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, " ");
	Для каждого ТекстОтбор Из Отборы Цикл
		
		ЧастьЗапроса = СтрЗаменить(ЗапросТекстВыборНаименование, "№", Итератор);
		Запрос.Текст = Запрос.Текст + ЗапросТекстОбъединить + ЧастьЗапроса;
		Запрос.УстановитьПараметр("Текст" + Итератор, "%" + ТекстОтбор + "%");
		
		Итератор = Итератор + 1;
		
	КонецЦикла;
	
	Отборы = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, " ");
	Для каждого ТекстОтбор Из Отборы Цикл
		
		ЧастьЗапроса = СтрЗаменить(ЗапросТекстВыборСинонимы, "№", Итератор);
		Запрос.Текст = Запрос.Текст + ЗапросТекстОбъединить + ЧастьЗапроса;
		Запрос.УстановитьПараметр("Текст" + Итератор, "%" + ТекстОтбор + "%");
		
		Итератор = Итератор + 1;
		
	КонецЦикла;
	
	Запрос.Текст = Запрос.Текст + ЗапросТекстРазделитель  + ЗапросТекстИтог;
	
	Выборка = Запрос.Выполнить().Выбрать();
	//Запрос.Выполнить().Выгрузить()
	Результат = Новый СписокЗначений;
	
	Пока Выборка.Следующий() Цикл
		
		Идентификатор = Выборка.Идентификатор;
		Представление = Выборка.Наименование;
		
		Результат.Добавить(Идентификатор, Представление);
		
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ОписаниеВидаБизнесаОбработкаВыбораНаСевере(ВыбранноеЗначение)
	
	ИдентификаторВидаБизнеса = ВыбранноеЗначение;
	ПараметрыОтбора = Новый Структура("Идентификатор", ВыбранноеЗначение);
	Строки = ВидыБизнеса.НайтиСтроки(ПараметрыОтбора);
	Если Строки.Количество() > 0 Тогда
		ОписаниеВидаБизнеса = Строки[0].Наименование;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеПользователя(РежимРаботыПрограммы)
	
	ПользовательСсылка	= КэшЗначений.Пользователь;
	
	Если РежимРаботыПрограммы.МодельСервиса Тогда
		
		ПользовательИмя = ПользовательСсылка.Наименование;
		
	КонецЕсли;
	
	ПользовательEmail	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПользовательСсылка, Справочники.ВидыКонтактнойИнформации.EmailПользователя);
	ПользовательТелефон = УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПользовательСсылка, Справочники.ВидыКонтактнойИнформации.ТелефонПользователя);
	ПользовательСайт	= УправлениеКонтактнойИнформацией.КонтактнаяИнформацияОбъекта(ПользовательСсылка, Справочники.ВидыКонтактнойИнформации.СайтПользователя);
	
КонецПроцедуры

&НаКлиенте
Функция СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации)
	
	НайденныеСтроки = Объект.Организации.НайтиСтроки(Новый Структура("ИндексОрганизации", ИндексОрганизации));
	Возврат НайденныеСтроки[0];
	
КонецФункции

&НаСервере
Функция СтрокаОрганизацииПоИндексуНаСервере(ИндексОрганизации)
	
	НайденныеСтроки = Объект.Организации.НайтиСтроки(Новый Структура("ИндексОрганизации", ИндексОрганизации));
	Возврат НайденныеСтроки[0];
	
КонецФункции

&НаСервере
Функция СформироватьУникальныйИндексОрганизации(СтрокаОрганизации)
	
	ИндексОрганизации = Объект.Организации.Количество();
	
	Пока Истина Цикл
		
		НайденныеСтроки = Объект.Организации.НайтиСтроки(Новый Структура("ИндексОрганизации", ИндексОрганизации));
		Если НайденныеСтроки.Количество() = 0 Тогда
			
			Прервать;
			
		Иначе
			
			ИндексОрганизации = ИндексОрганизации + 1;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ИндексОрганизации;
	
КонецФункции

&НаКлиенте
Функция ИндексПоИмениЭлемента(ИмяЭлемента, ОбщаяЧастьИмени)
	
	Возврат Число(СтрЗаменить(ИмяЭлемента, ОбщаяЧастьИмени, ""));
	
КонецФункции

&НаСервере
Процедура ДобавитьРеквизитыИЭлементыФормы(ИндексОрганизации)
	
	ПостФикс = ИндексОрганизации;
	
	//:::Реквизиты
	МассивРеквизитов = Новый Массив;
	
	НовыйРеквизитИНН = Новый РеквизитФормы("ИНН" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(12)), , "");
	МассивРеквизитов.Добавить(НовыйРеквизитИНН);
	
	НовыйЮридическоеФизическоеЛицо = Новый РеквизитФормы("ЮридическоеФизическоеЛицо" + ПостФикс, Новый ОписаниеТипов("ПеречислениеСсылка.ЮридическоеФизическоеЛицо"), , "");
	МассивРеквизитов.Добавить(НовыйЮридическоеФизическоеЛицо);
	
	НовыйНаименование = Новый РеквизитФормы("Наименование" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйНаименование);
	
	НовыйИПФамилия = Новый РеквизитФормы("ИП_Фамилия" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйИПФамилия);
	
	НовыйИПИмя = Новый РеквизитФормы("ИП_Имя" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйИПИмя);
	
	НовыйИПОтчество = Новый РеквизитФормы("ИП_Отчество" + ПостФикс, Новый ОписаниеТипов("Строка", , , , Новый КвалификаторыСтроки(50)), , "");
	МассивРеквизитов.Добавить(НовыйИПОтчество);
	
	НовыйЕстьУСН = Новый РеквизитФормы("ЕстьУСН" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйЕстьУСН);
	
	НовыйЕстьЕНВД = Новый РеквизитФормы("ЕстьЕНВД" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйЕстьЕНВД);
	
	НовыйЕстьПатент = Новый РеквизитФормы("ЕстьПатент" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйЕстьПатент);
	
	НовыйВидыОбъектовНалогообложения = Новый РеквизитФормы("ВидыОбъектовНалогообложения" + ПостФикс, Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОбъектовНалогообложения"), , "");
	МассивРеквизитов.Добавить(НовыйВидыОбъектовНалогообложения);
	
	НовыйСтавкаНалога = Новый РеквизитФормы("СтавкаНалога" + ПостФикс, Новый ОписаниеТипов("Число", , , Новый КвалификаторыЧисла(2, 0, ДопустимыйЗнак.Неотрицательный)), , "Ставка налога");
	МассивРеквизитов.Добавить(НовыйСтавкаНалога);
	
	НовыйИПИспользуетТрудНаемныхРаботников = Новый РеквизитФормы("ИПИспользуетТрудНаемныхРаботников" + ПостФикс, Новый ОписаниеТипов("Булево"), , "");
	МассивРеквизитов.Добавить(НовыйИПИспользуетТрудНаемныхРаботников);
	
	НовыйАдресЛоготипа = Новый РеквизитФормы("АдресЛоготипа" + ПостФикс, Новый ОписаниеТипов("Строка"), , "");
	МассивРеквизитов.Добавить(НовыйАдресЛоготипа);
	
	ЭтотОбъект.ИзменитьРеквизиты(МассивРеквизитов);
	
	//:::Структура групп
	//
	//ГруппаОрганизация
	//	ГруппаОсновныеСведения
	//		ГруппаИНН_ВидСобственности
	//			ГруппаИНН
	//			ГруппаЮридическоеФизическоеЛицо
	//		ГруппаНаименованиеОрганизации
	//			ГруппаНаименованиеОрганизацииФИО
	//		ГруппаУСН_ЕНВД
	//		ГруппаНалогообжение
	//		ГруппаСотрудники
	//
	//	ГруппаЛоготип
	
	ГруппаОрганизация = Элементы.Добавить("ГруппаОрганизация" + ПостФикс, Тип("ГруппаФормы"), Элементы.ГруппаОрганизации);
	ГруппаОрганизация.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОрганизация.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОрганизация.ОтображатьЗаголовок = Ложь;
	ГруппаОрганизация.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаОсновныеСведения = Элементы.Добавить("ГруппаОсновныеСведения" + ПостФикс, Тип("ГруппаФормы"), ГруппаОрганизация);
	ГруппаОсновныеСведения.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаОсновныеСведения.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаОсновныеСведения.ОтображатьЗаголовок = Ложь;
	
	ГруппаИНН_ВидСобственности = Элементы.Добавить("ГруппаИНН_ВидСобственности" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаИНН_ВидСобственности.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаИНН_ВидСобственности.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаИНН_ВидСобственности.ОтображатьЗаголовок = Ложь;
	ГруппаИНН_ВидСобственности.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаИНН = Элементы.Добавить("ГруппаИНН" + ПостФикс, Тип("ГруппаФормы"), ГруппаИНН_ВидСобственности);
	ГруппаИНН.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаИНН.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаИНН.ОтображатьЗаголовок = Ложь;
	ГруппаИНН.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаЮридическоеФизическоеЛицо = Элементы.Добавить("ГруппаЮридическоеФизическоеЛицо" + ПостФикс, Тип("ГруппаФормы"), ГруппаИНН_ВидСобственности);
	ГруппаЮридическоеФизическоеЛицо.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаЮридическоеФизическоеЛицо.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаЮридическоеФизическоеЛицо.ОтображатьЗаголовок = Ложь;
	ГруппаЮридическоеФизическоеЛицо.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаНаименованиеОрганизации = Элементы.Добавить("ГруппаНаименованиеОрганизации" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаНаименованиеОрганизации.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНаименованиеОрганизации.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаНаименованиеОрганизации.ОтображатьЗаголовок = Ложь;
	ГруппаНаименованиеОрганизации.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаУСН_ЕНВД = Элементы.Добавить("ГруппаУСН_ЕНВД" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаУСН_ЕНВД.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаУСН_ЕНВД.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаУСН_ЕНВД.ОтображатьЗаголовок = Ложь;
	ГруппаУСН_ЕНВД.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	ГруппаНалогообжение = Элементы.Добавить("ГруппаНалогообжение" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаНалогообжение.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНалогообжение.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаНалогообжение.ОтображатьЗаголовок = Ложь;
	ГруппаНалогообжение.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаНалогообжение.Видимость = Ложь;
	
	ГруппаСотрудники = Элементы.Добавить("ГруппаСотрудники" + ПостФикс, Тип("ГруппаФормы"), ГруппаОсновныеСведения);
	ГруппаСотрудники.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаСотрудники.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаСотрудники.ОтображатьЗаголовок = Ложь;
	ГруппаСотрудники.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаСотрудники.Видимость = Ложь;
	
	ГруппаЛоготип = Элементы.Добавить("ГруппаЛоготип" + ПостФикс, Тип("ГруппаФормы"), ГруппаОрганизация);
	ГруппаЛоготип.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаЛоготип.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаЛоготип.ОтображатьЗаголовок = Ложь;
	ГруппаЛоготип.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	
	//:::ГруппаИНН
	ДекорацияЗаголовокИНН = Элементы.Добавить("ДекорацияЗаголовокИНН" + ПостФикс, Тип("ДекорацияФормы"), ГруппаИНН);
	ДекорацияЗаголовокИНН.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияЗаголовокИНН.Заголовок = Нстр("ru ='ИНН:'");
	ДекорацияЗаголовокИНН.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ДекорацияЗаголовокИНН.Ширина = 14;
	ДекорацияЗаголовокИНН.Шрифт = Элементы.ДекорацияЗаголовокИНН1.Шрифт;
	
	ЭлементИНН = Элементы.Добавить("ИНН" + ПостФикс, Тип("ПолеФормы"), ГруппаИНН);
	ЭлементИНН.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИНН.ПутьКДанным = "ИНН" + ПостФикс;
	ЭлементИНН.РежимРедактирования = РежимРедактированияКолонки.Вход;
	ЭлементИНН.КнопкаСоздания = Ложь;
	ЭлементИНН.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементИНН.Ширина = 11;
	ЭлементИНН.РастягиватьПоГоризонтали = Ложь;
	ЭлементИНН.УстановитьДействие("ПриИзменении", "ИННПриИзменении");
	
	//:::ГруппаЮридическоеФизическоеЛицо
	ДекорацияОтступЮрФизЛицо = Элементы.Добавить("ДекорацияОтступЮрФизЛицо" + ПостФикс, Тип("ДекорацияФормы"), ГруппаЮридическоеФизическоеЛицо);
	ДекорацияОтступЮрФизЛицо.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОтступЮрФизЛицо.Ширина = 2;
	ДекорацияОтступЮрФизЛицо.Шрифт = Элементы.ДекорацияЗаголовокИНН1.Шрифт;
	
	ЭлементЮридическоеФизическоеЛицо = Элементы.Добавить("ЮридическоеФизическоеЛицо" + ПостФикс, Тип("ПолеФормы"), ГруппаЮридическоеФизическоеЛицо);
	ЭлементЮридическоеФизическоеЛицо.ПутьКДанным = "ЮридическоеФизическоеЛицо" + ПостФикс;
	ЭлементЮридическоеФизическоеЛицо.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементЮридическоеФизическоеЛицо.Вид = ВидПоляФормы.ПолеПереключателя;
	ЭлементЮридическоеФизическоеЛицо.ВидПереключателя = ВидПереключателя.Тумблер;
	ЭлементЮридическоеФизическоеЛицо.СписокВыбора.Добавить(Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо, "Юр. лицо");
	ЭлементЮридическоеФизическоеЛицо.СписокВыбора.Добавить(Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо, "Предприниматель");
	ЭлементЮридическоеФизическоеЛицо.ОдинаковаяШиринаКолонок = Ложь;
	ЭлементЮридическоеФизическоеЛицо.Шрифт = Элементы.ЮридическоеФизическоеЛицо1.Шрифт;
	ЭлементЮридическоеФизическоеЛицо.УстановитьДействие("ПриИзменении", "ЮрФизЛицоПриИзменении");
	
	//:::ГруппаНаименованиеОрганизации
	ДекорацияЗаголовокНаименование = Элементы.Добавить("ДекорацияЗаголовокНаименование" + ПостФикс, Тип("ДекорацияФормы"), ГруппаНаименованиеОрганизации);
	ДекорацияЗаголовокНаименование.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияЗаголовокНаименование.Заголовок = Нстр("ru ='Название:'");
	ДекорацияЗаголовокНаименование.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ДекорацияЗаголовокНаименование.Ширина = 14;
	ДекорацияЗаголовокНаименование.Шрифт = Элементы.ДекорацияЗаголовокНаименование1.Шрифт;
	
	ЭлементНаименование = Элементы.Добавить("Наименование" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизации);
	ЭлементНаименование.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементНаименование.ПутьКДанным = "Наименование" + ПостФикс;
	ЭлементНаименование.РежимРедактирования = РежимРедактированияКолонки.Вход;
	ЭлементНаименование.КнопкаСоздания = Ложь;
	ЭлементНаименование.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементНаименование.Ширина = 44;
	ЭлементНаименование.РастягиватьПоГоризонтали = Ложь;
	ЭлементНаименование.АвтоОтметкаНезаполненного = Истина;
	ЭлементНаименование.ПодсказкаВвода = НСтр("ru ='Наименование организации'");
	ЭлементНаименование.УстановитьДействие("ПриИзменении", "НаименованиеПриИзменении");
	
	//:::ГруппаНаименованиеОрганизацииФИО (создаем строго после декорации-заголовка)
	ГруппаНаименованиеОрганизацииФИО = Элементы.Добавить("ГруппаНаименованиеОрганизацииФИО" + ПостФикс, Тип("ГруппаФормы"), ГруппаНаименованиеОрганизации);
	ГруппаНаименованиеОрганизацииФИО.Вид = ВидГруппыФормы.ОбычнаяГруппа;
	ГруппаНаименованиеОрганизацииФИО.Отображение = ОтображениеОбычнойГруппы.Нет;
	ГруппаНаименованиеОрганизацииФИО.ОтображатьЗаголовок = Ложь;
	ГруппаНаименованиеОрганизацииФИО.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Горизонтальная;
	ГруппаНаименованиеОрганизацииФИО.РастягиватьПоГоризонтали = Ложь;
	ГруппаНаименованиеОрганизацииФИО.Видимость = Ложь;
	
	ЭлементИП_Фамилия = Элементы.Добавить("ИП_Фамилия" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизацииФИО);
	ЭлементИП_Фамилия.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИП_Фамилия.ПутьКДанным = "ИП_Фамилия" + ПостФикс;
	ЭлементИП_Фамилия.РежимРедактирования = РежимРедактированияКолонки.Вход;
	ЭлементИП_Фамилия.КнопкаСоздания = Ложь;
	ЭлементИП_Фамилия.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементИП_Фамилия.АвтоОтметкаНезаполненного = Истина;
	ЭлементИП_Фамилия.ПодсказкаВвода = НСтр("ru ='Фамилия'");
	ЭлементИП_Фамилия.АвтоМаксимальнаяШирина = Ложь;
	ЭлементИП_Фамилия.МаксимальнаяШирина = 14;
	ЭлементИП_Фамилия.УстановитьДействие("ПриИзменении", "ИПФамилияПриИзменении");
	
	ЭлементИП_Имя = Элементы.Добавить("ИП_Имя" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизацииФИО);
	ЭлементИП_Имя.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИП_Имя.ПутьКДанным = "ИП_Имя" + ПостФикс;
	ЭлементИП_Имя.РежимРедактирования = РежимРедактированияКолонки.Вход;
	ЭлементИП_Имя.КнопкаСоздания = Ложь;
	ЭлементИП_Имя.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементИП_Имя.АвтоОтметкаНезаполненного = Истина;
	ЭлементИП_Имя.ПодсказкаВвода = НСтр("ru ='Имя'");
	ЭлементИП_Имя.АвтоМаксимальнаяШирина = Ложь;
	ЭлементИП_Имя.МаксимальнаяШирина = 14;
	ЭлементИП_Имя.УстановитьДействие("ПриИзменении", "ИПИмяПриИзменении");
	
	ЭлементИП_Отчество = Элементы.Добавить("ИП_Отчество" + ПостФикс, Тип("ПолеФормы"), ГруппаНаименованиеОрганизацииФИО);
	ЭлементИП_Отчество.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементИП_Отчество.ПутьКДанным = "ИП_Отчество" + ПостФикс;
	ЭлементИП_Отчество.РежимРедактирования = РежимРедактированияКолонки.Вход;
	ЭлементИП_Отчество.КнопкаСоздания = Ложь;
	ЭлементИП_Отчество.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементИП_Отчество.АвтоОтметкаНезаполненного = Ложь;
	ЭлементИП_Отчество.ПодсказкаВвода = НСтр("ru ='Отчество'");
	ЭлементИП_Отчество.АвтоМаксимальнаяШирина = Ложь;
	ЭлементИП_Отчество.МаксимальнаяШирина = 12;
	ЭлементИП_Отчество.УстановитьДействие("ПриИзменении", "ИПОтчествоПриИзменении");
	
	//:::ГруппаУСН_ЕНВД
	ДекорацияЗаголовокНалогооблажение = Элементы.Добавить("ДекорацияЗаголовокНалогооблажение" + ПостФикс, Тип("ДекорацияФормы"), ГруппаУСН_ЕНВД);
	ДекорацияЗаголовокНалогооблажение.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияЗаголовокНалогооблажение.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
	ДекорацияЗаголовокНалогооблажение.Ширина = 14;
	ДекорацияЗаголовокНалогооблажение.Шрифт = Элементы.ДекорацияЗаголовокНалогооблажение1.Шрифт;
	
	ЭлементЕстьУСН = Элементы.Добавить("ЕстьУСН" + ПостФикс, Тип("ПолеФормы"), ГруппаУСН_ЕНВД);
	ЭлементЕстьУСН.ПутьКДанным = "ЕстьУСН" + ПостФикс;
	ЭлементЕстьУСН.Заголовок = НСтр("ru ='УСН'");
	ЭлементЕстьУСН.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементЕстьУСН.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементЕстьУСН.ШрифтЗаголовка = Элементы.ЕстьУСН1.ШрифтЗаголовка;
	ЭлементЕстьУСН.УстановитьДействие("ПриИзменении", "ЕстьУСНПриИзменении");
	
	ДекорацияОтступЕНВД = Элементы.Добавить("ДекорацияОтступЕНВД" + ПостФикс, Тип("ДекорацияФормы"), ГруппаУСН_ЕНВД);
	ДекорацияОтступЕНВД.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОтступЕНВД.Ширина = 3;
	
	ЭлементЕстьЕНВД = Элементы.Добавить("ЕстьЕНВД" + ПостФикс, Тип("ПолеФормы"), ГруппаУСН_ЕНВД);
	ЭлементЕстьЕНВД.ПутьКДанным = "ЕстьЕНВД" + ПостФикс;
	ЭлементЕстьЕНВД.Заголовок = НСтр("ru ='ЕНВД'");
	ЭлементЕстьЕНВД.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементЕстьЕНВД.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементЕстьЕНВД.ШрифтЗаголовка = Элементы.ЕстьЕНВД1.ШрифтЗаголовка;
	ЭлементЕстьЕНВД.УстановитьДействие("ПриИзменении", "ЕстьЕНВДПриИзменении");
	
	ДекорацияОтступПатент = Элементы.Добавить("ДекорацияОтступПатент" + ПостФикс, Тип("ДекорацияФормы"), ГруппаУСН_ЕНВД);
	ДекорацияОтступПатент.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОтступПатент.Ширина = 3;
	
	ЭлементЕстьПатент = Элементы.Добавить("ЕстьПатент" + ПостФикс, Тип("ПолеФормы"), ГруппаУСН_ЕНВД);
	ЭлементЕстьПатент.ПутьКДанным = "ЕстьПатент" + ПостФикс;
	ЭлементЕстьПатент.Заголовок = НСтр("ru ='Патент'");
	ЭлементЕстьПатент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементЕстьПатент.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементЕстьПатент.ШрифтЗаголовка = Элементы.ЕстьПатент1.ШрифтЗаголовка;
	ЭлементЕстьПатент.УстановитьДействие("ПриИзменении", "ЕстьПатентПриИзменении");
	
	//:::ГруппаНалогообжение
	ДекорацияОтступНалогооблажение = Элементы.Добавить("ДекорацияОтступНалогооблажение" + ПостФикс, Тип("ДекорацияФормы"), ГруппаНалогообжение);
	ДекорацияОтступНалогооблажение.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОтступНалогооблажение.Ширина = 14;
	
	ЭлементВидыОбъектовНалогообложения = Элементы.Добавить("ВидыОбъектовНалогообложения" + ПостФикс, Тип("ПолеФормы"), ГруппаНалогообжение);
	ЭлементВидыОбъектовНалогообложения.ПутьКДанным = "ВидыОбъектовНалогообложения" + ПостФикс;
	ЭлементВидыОбъектовНалогообложения.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементВидыОбъектовНалогообложения.Вид = ВидПоляФормы.ПолеПереключателя;
	ЭлементВидыОбъектовНалогообложения.КоличествоКолонок = 2;
	ЭлементВидыОбъектовНалогообложения.СписокВыбора.Добавить(Перечисления.ВидыОбъектовНалогообложения.Доходы, Нстр("ru ='Доходы'"));
	ЭлементВидыОбъектовНалогообложения.СписокВыбора.Добавить(Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы, Нстр("ru ='Доходы - расходы по налоговой ставке'"));
	ЭлементВидыОбъектовНалогообложения.Шрифт = Элементы.ВидыОбъектовНалогообложения1.Шрифт;
	ЭлементВидыОбъектовНалогообложения.УстановитьДействие("ПриИзменении", "НалогообложениеПриИзменении");
	
	ЭлементСтавкаНалога = Элементы.Добавить("СтавкаНалога" + ПостФикс, Тип("ПолеФормы"), ГруппаНалогообжение);
	ЭлементСтавкаНалога.Вид = ВидПоляФормы.ПолеВвода;
	ЭлементСтавкаНалога.ПутьКДанным = "СтавкаНалога" + ПостФикс;
	ЭлементСтавкаНалога.РежимРедактирования = РежимРедактированияКолонки.Вход;
	ЭлементСтавкаНалога.КнопкаСоздания = Ложь;
	ЭлементСтавкаНалога.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Авто;
	ЭлементСтавкаНалога.Ширина = 4;
	ЭлементСтавкаНалога.РастягиватьПоГоризонтали = Ложь;
	ЭлементСтавкаНалога.Заголовок = НСтр("ru ='%'");
	ЭлементСтавкаНалога.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементСтавкаНалога.ОтображениеПодсказки = ОтображениеПодсказки.Нет;
	ЭлементСтавкаНалога.УстановитьДействие("ПриИзменении", "СтавкаНалогаПриИзменении");
	
	//:::ГруппаСотрудники
	ДекорацияОтступСотрудники = Элементы.Добавить("ДекорацияОтступСотрудники" + ПостФикс, Тип("ДекорацияФормы"), ГруппаСотрудники);
	ДекорацияОтступСотрудники.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияОтступСотрудники.Ширина = 14;
	ДекорацияОтступСотрудники.Видимость = Ложь;
	
	ЭлементИПИспользуетТрудНаемныхРаботников = Элементы.Добавить("ИПИспользуетТрудНаемныхРаботников" + ПостФикс, Тип("ПолеФормы"), ГруппаСотрудники);
	ЭлементИПИспользуетТрудНаемныхРаботников.ПутьКДанным = "ИПИспользуетТрудНаемныхРаботников" + ПостФикс;
	ЭлементИПИспользуетТрудНаемныхРаботников.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Право;
	ЭлементИПИспользуетТрудНаемныхРаботников.Вид = ВидПоляФормы.ПолеФлажка;
	ЭлементИПИспользуетТрудНаемныхРаботников.Шрифт = Элементы.ИПИспользуетТрудНаемныхРаботников1.Шрифт;
	ЭлементИПИспользуетТрудНаемныхРаботников.Заголовок = НСтр("ru ='Есть официально трудоустроенные сотрудники'");
	ЭлементИПИспользуетТрудНаемныхРаботников.УстановитьДействие("ПриИзменении", "ЕстьСотрудникиПриИзменении");
	ЭлементИПИспользуетТрудНаемныхРаботников.Видимость = Ложь;
	
	//:::АдресЛоготипа
	ЭлементАдресЛоготипа = Элементы.Добавить("АдресЛоготипа" + ПостФикс, Тип("ПолеФормы"), ГруппаЛоготип);
	ЭлементАдресЛоготипа.ПутьКДанным = "АдресЛоготипа" + ПостФикс;
	ЭлементАдресЛоготипа.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
	ЭлементАдресЛоготипа.Вид = ВидПоляФормы.ПолеКартинки;
	ЭлементАдресЛоготипа.Гиперссылка = Истина;
	ЭлементАдресЛоготипа.Шрифт = Элементы.АдресЛоготипа1.Шрифт;
	ЭлементАдресЛоготипа.РазмерКартинки = РазмерКартинки.Пропорционально;
	ЭлементАдресЛоготипа.ЦветТекста = Элементы.АдресЛоготипа1.ЦветТекста;
	ЭлементАдресЛоготипа.Ширина = 12;
	ЭлементАдресЛоготипа.Высота = 4;
	ЭлементАдресЛоготипа.ТекстНевыбраннойКартинки = НСтр("ru ='Добавить логотип'");
	ЭлементАдресЛоготипа.РастягиватьПоВертикали = Ложь;
	ЭлементАдресЛоготипа.РастягиватьПоГоризонтали = Ложь;
	ЭлементАдресЛоготипа.УстановитьДействие("Нажатие", "АдресЛоготипаНажатие");
	
	ЭлементКомандаДобавитьИзображениеЛоготипа = Элементы.Добавить("ДобавитьИзображениеЛоготипа" + ПостФикс, Тип("КнопкаФормы"), ЭлементАдресЛоготипа.КонтекстноеМеню);
	ЭлементКомандаДобавитьИзображениеЛоготипа.ИмяКоманды = "ДобавитьИзображениеЛоготипа";
	
	ЭлементКомандаОчиститьИзображение = Элементы.Добавить("ОчиститьИзображение" + ПостФикс, Тип("КнопкаФормы"), ЭлементАдресЛоготипа.КонтекстноеМеню);
	ЭлементКомандаОчиститьИзображение.ИмяКоманды = "ОчиститьИзображениеЛоготипа";
	
	ЭлементУдалитьОрганизацию = Элементы.Добавить("УдалитьОрганизацию" + ПостФикс, Тип("КнопкаФормы"), ГруппаЛоготип);
	ЭлементУдалитьОрганизацию.Вид = ВидКнопкиФормы.Гиперссылка;
	ЭлементУдалитьОрганизацию.ИмяКоманды = "УдалитьОрганизацию";
	ЭлементУдалитьОрганизацию.Ширина = 2;
	
	ДекорацияМалыйОтступ = Элементы.Добавить("ДекорацияМалыйОтступ1" + ПостФикс, Тип("ДекорацияФормы"), Элементы.ГруппаОрганизации);
	ДекорацияМалыйОтступ.Вид = ВидДекорацииФормы.Надпись;
	ДекорацияМалыйОтступ.Шрифт = Элементы.ДекорацияМалыйОтступ1.Шрифт;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИзображениеЛоготипа(СтрокаОрганизации)
	
	ЭтотОбъект["АдресЛоготипа" + СтрокаОрганизации.ИндексОрганизации] = 
		?(ЗначениеЗаполнено(СтрокаОрганизации.ФайлЛоготип), НавигационнаяСсылкаКартинки(СтрокаОрганизации.ФайлЛоготип, УникальныйИдентификатор), "");
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция НавигационнаяСсылкаКартинки(ФайлКартинки, ИдентификаторФормы)
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(ФайлКартинки, ИдентификаторФормы).СсылкаНаДвоичныеДанныеФайла;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьДанныеФайла(ЛоготипРеквизит, УникальныйИдентификатор)
	
	Возврат ПрисоединенныеФайлы.ПолучитьДанныеФайла(ЛоготипРеквизит, УникальныйИдентификатор);
	
КонецФункции

&НаКлиенте
Процедура ПросмотретьПрисоединенныйФайл(ФайлЛоготип)
	
	ДанныеФайла = ПолучитьДанныеФайла(ФайлЛоготип, УникальныйИдентификатор);
	ПрисоединенныеФайлыКлиент.ОткрытьФайл(ДанныеФайла);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеНаКлиенте(ИндексОрганизации)
	Перем ИдентификаторФайла, ИмяРеквизитаОбъекта, Фильтр;
	
	КэшЗначений.ТекущийИндексОрганизации = ИндексОрганизации;
	
	СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации);
	
	ЭтотОбъект["АдресЛоготипа" + СтрокаОрганизации.ИндексОрганизации] = "";
	
	ИдентификаторФайла = Новый УникальныйИдентификатор;
	Фильтр = НСтр("ru = 'Все картинки (*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf)|*.bmp;*.gif;*.png;*.jpeg;*.dib;*.rle;*.tif;*.jpg;*.ico;*.wmf;*.emf"
	+ "|Все файлы(*.*)|*.*"
	+ "|Формат bmp(*.bmp*;*.dib;*.rle)|*.bmp;*.dib;*.rle"
	+ "|Формат GIF(*.gif*)|*.gif"
	+ "|Формат JPEG(*.jpeg;*.jpg)|*.jpeg;*.jpg"
	+ "|Формат PNG(*.png*)|*.png"
	+ "|Формат TIFF(*.tif)|*.tif"
	+ "|Формат icon(*.ico)|*.ico"
	+ "|Формат метафайл(*.wmf;*.emf)|*.wmf;*.emf'");
	
	ПрисоединенныеФайлыКлиент.ДобавитьФайлы(СтрокаОрганизации.Ссылка, ИдентификаторФайла, Фильтр);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗначениеОрганизации(ИмяРеквизита, ЗначениеРеквизита, Индекс, НеЗатиратьТекущее = Ложь)
	
	ДанныеСтрокиОрганизации = Объект.Организации[Индекс - 1];
	
	Если НеЗатиратьТекущее Тогда
		
		Если НЕ ЗначениеЗаполнено(ДанныеСтрокиОрганизации[ИмяРеквизита]) Тогда
			
			ДанныеСтрокиОрганизации[ИмяРеквизита] = ЗначениеРеквизита;
			
		КонецЕсли;
		
	Иначе
		
		ДанныеСтрокиОрганизации[ИмяРеквизита] = ЗначениеРеквизита;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьЗначениеОрганизацииЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, Индекс)
	
	ЗаписатьЗначениеОрганизации("ЮридическоеФизическоеЛицо", ЗначениеЮрФизЛицо, Индекс);
	
	ПослеЗаписиЗначенияЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, Индекс);
	
КонецПроцедуры

&НаКлиенте 
Процедура ПослеЗаписиЗначенияЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, Индекс)
	
	Если ЗначениеЮрФизЛицо = КэшЗначений.ЮрЛицо Тогда
		
		ЭтотОбъект["ИПИспользуетТрудНаемныхРаботников" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ИПИспользуетТрудНаемныхРаботников", Ложь, Индекс);
		
		ЭтотОбъект["ИП_Фамилия" + Индекс] = "";
		ЗаписатьЗначениеОрганизации("Фамилия", "", Индекс);
		
		ЭтотОбъект["ИП_Имя" + Индекс] = "";
		ЗаписатьЗначениеОрганизации("Имя", "", Индекс);
		
		ЭтотОбъект["ИП_Отчество" + Индекс] = "";
		ЗаписатьЗначениеОрганизации("Отчество", "", Индекс);
		
		ЭтотОбъект["ЕстьПатент" + Индекс] = Ложь;
		ЗаписатьЗначениеОрганизации("ЕстьПатент", Ложь, Индекс);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНалогообжение" + Индекс, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступСотрудники" + Индекс, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИПИспользуетТрудНаемныхРаботников" + Индекс, "Видимость", Ложь);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияЗаголовокНаименование" + Индекс, "Заголовок", Нстр("ru ='Название:'"));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Наименование" + Индекс, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНаименованиеОрганизацииФИО" + Индекс, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕстьПатент" + Индекс, "Доступность", Ложь);
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНалогообжение" + Индекс, "Видимость", ЗначениеЕстьУСН);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступСотрудники" + Индекс, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИПИспользуетТрудНаемныхРаботников" + Индекс, "Видимость", Истина);
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияЗаголовокНаименование" + Индекс, "Заголовок", Нстр("ru ='ФИО:'"));
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Наименование" + Индекс, "Видимость", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНаименованиеОрганизацииФИО" + Индекс, "Видимость", Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕстьПатент" + Индекс, "Доступность", Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РеквизитыЗаполненыКорректно(Ошибки)
	
	Если ПустаяСтрока(ПользовательИмя) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ПользовательИмя", НСтр("ru ='Заполните имя пользователя'"), "");
		
	КонецЕсли;
	
	Если ПустаяСтрока(ПользовательEmail) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ПользовательEmail", НСтр("ru ='Укажите e-mail'"), "")
		
	КонецЕсли;
	
	Если ПользовательПароль <> ПользовательПарольПодтверждение Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ПользовательПароль", НСтр("ru ='Не совпадают пароли'"), "")
		
	КонецЕсли;
	
	Для каждого СтрокаОрганизации Из Объект.Организации Цикл
		
		Если ПустаяСтрока(СтрокаОрганизации.Наименование) Тогда
			
			ТекстОшибки = НСтр("ru ='Заполните название (организация %1)'");
			ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаОрганизации.НомерСтроки);
			ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "Наименование" + СтрокаОрганизации.НомерСтроки, ТекстОшибки, "")
			
		КонецЕсли;
		
		Если СтрокаОрганизации.ЮридическоеФизическоеЛицо = КэшЗначений.ФизЛицо Тогда
			
			Если ПустаяСтрока(СтрокаОрганизации.Фамилия) Тогда
				
				ТекстОшибки = НСтр("ru ='Заполните фамилию (организация %1)'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаОрганизации.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИП_Фамилия" + СтрокаОрганизации.НомерСтроки, ТекстОшибки, "")
				
			КонецЕсли;
			
			Если ПустаяСтрока(СтрокаОрганизации.Имя) Тогда
				
				ТекстОшибки = НСтр("ru ='Заполните имя (организация %1)'");
				ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстОшибки, СтрокаОрганизации.НомерСтроки);
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, "ИП_Имя" + СтрокаОрганизации.НомерСтроки, ТекстОшибки, "")
				
			КонецЕсли;
			
			// Отчество - не является обязательным
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьОрганизацию(СоздатьРеквизитыЭлементы, ОрганизацияСсылка = Неопределено)
	
	ДобавленнаяСтрока = Объект.Организации.Добавить();
	
	ИндексОрганизации = СформироватьУникальныйИндексОрганизации(ДобавленнаяСтрока);
	
	Если СоздатьРеквизитыЭлементы Тогда
		
		ДобавитьРеквизитыИЭлементыФормы(ИндексОрганизации);
		
	КонецЕсли;
	
	Если ОрганизацияСсылка = Неопределено Тогда
		
		ОрганизацияСсылка = Справочники.Организации.ПолучитьСсылку();
		
	КонецЕсли;
	
	СтрокаТаблицы = Объект.Организации.НайтиПоИдентификатору(ДобавленнаяСтрока.ПолучитьИдентификатор());
	
	СтрокаТаблицы.Ссылка = ОрганизацияСсылка;
	СтрокаТаблицы.ИндексОрганизации = ИндексОрганизации;
	
	СтрокаТаблицы.Наименование = НСтр("ru ='Наша фирма'");
	СтрокаТаблицы.НаименованиеПолное = НСтр("ru ='Наша фирма'");
	ЭтотОбъект["Наименование" + ИндексОрганизации] = СтрокаТаблицы.Наименование;
	
	СтрокаТаблицы.ЮридическоеФизическоеЛицо = ?(ЗначениеЗаполнено(ОрганизацияСсылка.ЮридическоеФизическоеЛицо), ОрганизацияСсылка.ЮридическоеФизическоеЛицо, КэшЗначений.ЮрЛицо);
	ЭтотОбъект["ЮридическоеФизическоеЛицо" + ИндексОрганизации] = СтрокаТаблицы.ЮридическоеФизическоеЛицо;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕстьПатент" + ИндексОрганизации, "Доступность", (СтрокаТаблицы.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо));
	
	СтрокаТаблицы.ВидыОбъектовНалогообложения = Перечисления.ВидыОбъектовНалогообложения.Доходы;
	ЭтотОбъект["ВидыОбъектовНалогообложения" + ИндексОрганизации] = СтрокаТаблицы.ВидыОбъектовНалогообложения;
	
КонецПроцедуры

&НаСервере
Процедура УдалитьОрганизациюНаСервере(ИндексОрганизации)
	
	СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаСервере(ИндексОрганизации);
	Объект.Организации.Удалить(СтрокаОрганизации);
	
	УдаляемыеРеквизиты = Новый Массив;
	УдаляемыеРеквизиты.Добавить("ИНН" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЮридическоеФизическоеЛицо" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("Наименование" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИП_Фамилия" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИП_Имя" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИП_Отчество" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЕстьУСН" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЕстьЕНВД" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ЕстьПатент" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ВидыОбъектовНалогообложения" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("СтавкаНалога" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("ИПИспользуетТрудНаемныхРаботников" + ИндексОрганизации);
	УдаляемыеРеквизиты.Добавить("АдресЛоготипа" + ИндексОрганизации);
	
	ЭтотОбъект.ИзменитьРеквизиты(, УдаляемыеРеквизиты);
	
	Элементы.Удалить(Элементы["ГруппаОрганизация" + ИндексОрганизации]);
	Элементы.Удалить(Элементы["ДекорацияМалыйОтступ1" + ИндексОрганизации]);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОтделенииПФР(ДанныеГосРеестра, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ДанныеГосРеестра.КодОрганаПФР) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РеквизитыОтделенияПФР = ДанныеГосударственныхОрганов.РеквизитыОтделенияПФРПоКоду(Лев(ДанныеГосРеестра.КодОрганаПФР, 3));
	
	Если ЗначениеЗаполнено(РеквизитыОтделенияПФР.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = РеквизитыОтделенияПФР.ОписаниеОшибки;
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОтделенияПФР.Ссылка) Тогда
		
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыОтделенияПФР);
		
	КонецЕсли;
	
	ПлатежныеРеквизитыПФРПредставление = ДанныеГосударственныхОрганов.ПредставлениеПлатежныхРеквизитовГосударственногоОргана(РеквизитыОтделенияПФР);
	ДанныеГосРеестра.Вставить("ПлатежныеРеквизитыПФРПредставление", ПлатежныеРеквизитыПФРПредставление);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОбОтделенииФСС(ДанныеГосРеестра, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ДанныеГосРеестра.КодОрганаФСС) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	КодПодчиненностиФСС = ДанныеГосРеестра.КодОрганаФСС + ?(ДанныеГосРеестра.ЭтоЮрЛицо, 1, 3);
	РеквизитыОтделенияФСС = ДанныеГосударственныхОрганов.РеквизитыОтделенияФССПоКоду(КодПодчиненностиФСС);
	
	Если ЗначениеЗаполнено(РеквизитыОтделенияФСС.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = РеквизитыОтделенияФСС.ОписаниеОшибки;
		Возврат;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыОтделенияФСС.Ссылка) Тогда
		
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыОтделенияФСС);
		
	КонецЕсли;
	
	ПлатежныеРеквизитыФССПредставление = ДанныеГосударственныхОрганов.ПредставлениеПлатежныхРеквизитовГосударственногоОргана(РеквизитыОтделенияФСС);
	ДанныеГосРеестра.Вставить("ПлатежныеРеквизитыФССПредставление", ПлатежныеРеквизитыФССПредставление);
	ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", РеквизитыОтделенияФСС.Наименование);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра, ОписаниеОшибки = "")
	
	Если НЕ ЗначениеЗаполнено(ДанныеГосРеестра.КодНалоговогоОрганаПолучателя) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	РеквизитыНалоговогоОргана = ДанныеГосударственныхОрганов.РеквизитыНалоговогоОрганаПоКоду(ДанныеГосРеестра.КодНалоговогоОрганаПолучателя);
	
	Если ЗначениеЗаполнено(РеквизитыНалоговогоОргана.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = РеквизитыНалоговогоОргана.ОписаниеОшибки;
		Возврат;
	
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыНалоговогоОргана.Ссылка) Тогда
		
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыНалоговогоОргана);
		
	КонецЕсли;
	
	ПлатежныеРеквизитыФНСПредставление = ДанныеГосударственныхОрганов.ПредставлениеПлатежныхРеквизитовГосударственногоОргана(РеквизитыНалоговогоОргана);
	
	РегистрацияВНалоговомОргане = Новый Структура;
	РегистрацияВНалоговомОргане.Вставить("Наименование", РеквизитыНалоговогоОргана.Наименование);
	РегистрацияВНалоговомОргане.Вставить("НаименованиеИФНС", РеквизитыНалоговогоОргана.ПолноеНаименование);
	
	ДанныеГосРеестра.Вставить("РегистрацияВНалоговомОргане", РегистрацияВНалоговомОргане);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСведенияОНалоговойИнспекцииПоАдресу(ЗначениеРеквизитов, ДанныеГосРеестра, ОписаниеОшибки = "")
	
	ЭтоФизЛицо = НЕ ДанныеГосРеестра.ЭтоЮрЛицо;
	ЗначенияПолейАдреса = ЗначениеРеквизитов.ЮридическийАдрес.КонтактнаяИнформация;
	
	Если НЕ ЗначениеЗаполнено(ЗначенияПолейАдреса) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СведенияОНалоговомОрганеПоАдресу = ДанныеАдресногоКлассификатора.НалоговыйОрганПоАдресу(ЗначенияПолейАдреса);
	Если ЗначениеЗаполнено(СведенияОНалоговомОрганеПоАдресу.ОписаниеОшибки) Тогда
		
		ОписаниеОшибки = СведенияОНалоговомОрганеПоАдресу.ОписаниеОшибки;
		Возврат;
		
	КонецЕсли;
	
	КодНалоговогоОрганаПолучателя = ?(ЭтоФизЛицо, 
		СведенияОНалоговомОрганеПоАдресу.КодНалоговойДляФизическихЛиц,
		СведенияОНалоговомОрганеПоАдресу.КодНалоговойДляЮридическихЛиц);
		
	ДанныеГосРеестра.Вставить("КодНалоговогоОрганаПолучателя", КодНалоговогоОрганаПолучателя);
	
	ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра);
	
	ДанныеГосРеестра.РегистрацияВНалоговомОргане.Вставить("КодПоОКАТО", СведенияОНалоговомОрганеПоАдресу.КодПоОКАТО);
	ДанныеГосРеестра.РегистрацияВНалоговомОргане.Вставить("КодПоОКТМО", СведенияОНалоговомОрганеПоАдресу.КодПоОКТМО);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыПоИНННаСервере(ИНН, ДанныеГосРеестра, ИндексОрганизации, Ошибки)
	
	ЭтоЮрЛицо = ?(СтрДлина(ИНН) = 12, Ложь, Истина);
	
	ЗначениеРеквизитов = ?(ЭтоЮрЛицо,
		ДанныеЕдиныхГосРеестров.РеквизитыЮридическогоЛицаПоИНН(ИНН),
		ДанныеЕдиныхГосРеестров.РеквизитыПредпринимателяПоИНН(ИНН));
	
	Если ЗначениеЗаполнено(ЗначениеРеквизитов.ОписаниеОшибки) Тогда
		
		ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(Ошибки, , ЗначениеРеквизитов.ОписаниеОшибки, "");
		Возврат;
		
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("ЭтоЮрЛицо", ЭтоЮрЛицо);
	ДанныеГосРеестра.Вставить("Наименование", ЗначениеРеквизитов.Наименование);
	ДанныеГосРеестра.Вставить("НаименованиеПолное", ЗначениеРеквизитов.НаименованиеСокращенное);
	ДанныеГосРеестра.Вставить("ИНН", ЗначениеРеквизитов.ИНН);
	ДанныеГосРеестра.Вставить("ОГРН", ЗначениеРеквизитов.РегистрационныйНомер);
	
	Если ЭтоЮрЛицо Тогда
		
		ДанныеГосРеестра.Вставить("КПП", ЗначениеРеквизитов.КПП);
		ДанныеГосРеестра.Вставить("КодОКВЭД", ЗначениеРеквизитов.КодОКВЭД);
		ДанныеГосРеестра.Вставить("ЮридическийАдрес", ЗначениеРеквизитов.ЮридическийАдрес);
		ДанныеГосРеестра.Вставить("Телефон", ЗначениеРеквизитов.Телефон);
		
	Иначе
		
		ДанныеГосРеестра.Вставить("КодОКВЭД", Неопределено);
		ДанныеГосРеестра.Вставить("ЮридическийАдрес", Неопределено);
		ДанныеГосРеестра.Вставить("Телефон", Неопределено);
		
		Если ЗначениеРеквизитов.СвидетельствоОРегистрации <> Неопределено Тогда
			
			СвидетельствоСерияНомер = "" + ЗначениеРеквизитов.СвидетельствоОРегистрации.Серия + " " + ЗначениеРеквизитов.СвидетельствоОРегистрации.Номер;
			СвидетельствоДатаВыдачи = ЗначениеРеквизитов.СвидетельствоОРегистрации.Дата;
			
			ДанныеГосРеестра.Вставить("СвидетельствоСерияНомер", СвидетельствоСерияНомер);
			ДанныеГосРеестра.Вставить("СвидетельствоДатаВыдачи", СвидетельствоДатаВыдачи);
			
		КонецЕсли;
		
		ДанныеГосРеестра.Вставить("Фамилия", ЗначениеРеквизитов.Фамилия);
		ДанныеГосРеестра.Вставить("Имя", ЗначениеРеквизитов.Имя);
		ДанныеГосРеестра.Вставить("Отчество", ЗначениеРеквизитов.Отчество);
		ДанныеГосРеестра.Вставить("Пол", ?(ЗначениеРеквизитов.Пол = "1", Перечисления.ПолФизическогоЛица.Мужской, Перечисления.ПолФизическогоЛица.Женский));
		
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("ЮридическоеФизическоеЛицо", ?(ЭтоЮрЛицо, Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо, Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо));
	
	ДанныеГосРеестра.Вставить("КодНалоговогоОргана", Неопределено);
	ДанныеГосРеестра.Вставить("КодНалоговогоОрганаПолучателя", Неопределено);
	ДанныеГосРеестра.Вставить("КодОрганаПФР", Неопределено);
	ДанныеГосРеестра.Вставить("РегистрационныйНомерПФР", Неопределено);
	ДанныеГосРеестра.Вставить("РегистрационныйНомерПФР_ИП", Неопределено);
	
	//Устанавливаем сведения о налоговой инспекции
	//В том случае, если указан юридический адрес, получаем код налоговой инспекции по адресу
	//и заполняем реквизиты инспеции по коду
	//В том случае, если юридического адреса нет - заполняем данные о налоговой инспекции
	//из кода в ЕГРЮЛ/ЕГРИП
	Если ЗначениеРеквизитов.Свойство("ЮридическийАдрес")
		И ЗначениеЗаполнено(ЗначениеРеквизитов.ЮридическийАдрес) Тогда
		
		ОписаниеОшибкиАдрес = "";
		ЗаполнитьСведенияОНалоговойИнспекцииПоАдресу(ЗначениеРеквизитов, ДанныеГосРеестра, ОписаниеОшибкиАдрес);
		
		Если ЗначениеЗаполнено(ОписаниеОшибкиАдрес)
			И ЗначениеЗаполнено(ЗначениеРеквизитов.РегистрацияВНалоговомОргане)
			И ЗначениеЗаполнено(ЗначениеРеквизитов.РегистрацияВНалоговомОргане.Код) Тогда
			
			ДанныеГосРеестра.Вставить("КодНалоговогоОргана", ЗначениеРеквизитов.РегистрацияВНалоговомОргане.Код);
			ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра);
			
		КонецЕсли;
		
	ИначеЕсли ЗначениеЗаполнено(ЗначениеРеквизитов.РегистрацияВНалоговомОргане) Тогда
		
		ДанныеГосРеестра.Вставить("КодНалоговогоОрганаПолучателя", ЗначениеРеквизитов.РегистрацияВНалоговомОргане.Код);
		ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ДанныеГосРеестра);
		
	КонецЕсли;
	
	Если ЗначениеРеквизитов.Свойство("РегистрацияВПенсионномФонде")
		И ЗначениеЗаполнено(ЗначениеРеквизитов.РегистрацияВПенсионномФонде) Тогда
		
		Ключ = "РегистрационныйНомерПФР" + ?(ЭтоЮрЛицо, "", "_ИП");
		
		ДанныеГосРеестра.Вставить("КодОрганаПФР", ЗначениеРеквизитов.РегистрацияВПенсионномФонде.КодОрганаПФР);
		ДанныеГосРеестра.Вставить(Ключ, ЗначениеРеквизитов.РегистрацияВПенсионномФонде.РегистрационныйНомерПФР);
		
		ЗаполнитьСведенияОбОтделенииПФР(ДанныеГосРеестра);
		
	КонецЕсли;
	
	ДанныеГосРеестра.Вставить("КодОрганаФСС", Неопределено);
	ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", Неопределено);
	ДанныеГосРеестра.Вставить("РегистрационныйНомерФСС", Неопределено);
	ДанныеГосРеестра.Вставить("ПлатежныеРеквизитыФССПредставление", Неопределено);
	ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", Неопределено);
	
	Если ЗначениеРеквизитов.Свойство("РегистрацияВФСС")
		И ЗначениеЗаполнено(ЗначениеРеквизитов.РегистрацияВФСС) Тогда
		
		ДанныеГосРеестра.Вставить("КодОрганаФСС", ЗначениеРеквизитов.РегистрацияВФСС.КодОрганаФСС);
		ДанныеГосРеестра.Вставить("НаименованиеТерриториальногоОрганаФСС", ЗначениеРеквизитов.РегистрацияВФСС.НаименованиеОрганаФСС);
		ДанныеГосРеестра.Вставить("РегистрационныйНомерФСС", ЗначениеРеквизитов.РегистрацияВФСС.РегистрационныйНомерФСС);
		
		ЗаполнитьСведенияОбОтделенииФСС(ДанныеГосРеестра);
		
	КонецЕсли;
	
	ЗаполнитьСписокВыбораНаименования(ДанныеГосРеестра, ИндексОрганизации);
	ДанныеГосРеестра.Вставить("НаименованиеПлательщикаПриПеречисленииНалогов", НаименованиеПлательщикаПоУмолчанию(ДанныеГосРеестра));
	
КонецПроцедуры

&НаСервере
Функция НаименованиеПлательщикаПоУмолчанию(ДанныеГосРеестра)
	
	// Для организации заполняем из полного наименования
	Если ДанныеГосРеестра.ЭтоЮрЛицо Тогда
		
		Возврат ДанныеГосРеестра.НаименованиеПолное;
		
	КонецЕсли;
	
	// Для ИП:
	// п.4 Приложения 1 к Приказу Минфина 107н:
	// Фамилия имя отчество "(ИП)" "//"Адрес регистрации"//"
	Шаблон = "%1 (ИП) //%2//";
	
	ФИО = ДанныеГосРеестра.Фамилия + ?(ПустаяСтрока(ДанныеГосРеестра.Имя), "", " " + ДанныеГосРеестра.Имя) + ?(ПустаяСтрока(ДанныеГосРеестра.Отчество), "", " " + ДанныеГосРеестра.Отчество);
	АдресРегистрации = ДанныеГосРеестра.ЮридическийАдрес;
	
	Возврат СтрШаблон(Шаблон, ФИО, АдресРегистрации);
	
КонецФункции

&НаСервере
Функция ВыделитьИзНаименованияОПФ(КлассификаторОПФ, знач НаименованиеОрганизации)
	
	Результат = Новый Структура("НаименованиеБезОПФ, КраткаяФорма, ПолнаяФорма", НаименованиеОрганизации, "", "");
	МассивСлов = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивСлов(НаименованиеОрганизации);
	
	Для Каждого Слово Из МассивСлов Цикл
		Для Каждого ДанныеОПФ Из КлассификаторОПФ Цикл
			Если ВРег(СокрЛП(Слово)) = ВРег(ДанныеОПФ.ПолнаяФорма) Тогда
				Результат.КраткаяФорма = ДанныеОПФ.КраткаяФорма;
				Результат.ПолнаяФорма = ДанныеОПФ.ПолнаяФорма;
				Результат.НаименованиеБезОПФ = СтроковыеФункцииКлиентСервер.СократитьДвойныеКавычки(СокрЛП(СтрЗаменить(НаименованиеОрганизации, Слово, "")));
				Возврат Результат;
			ИначеЕсли Не ПустаяСтрока(ДанныеОПФ.КраткаяФорма) И ВРег(СокрЛП(Слово)) = ВРег(ДанныеОПФ.КраткаяФорма) Тогда
				Результат.КраткаяФорма = ДанныеОПФ.КраткаяФорма;
				Результат.ПолнаяФорма = ДанныеОПФ.ПолнаяФорма;
				Результат.НаименованиеБезОПФ = СтроковыеФункцииКлиентСервер.СократитьДвойныеКавычки(СокрЛП(СтрЗаменить(НаименованиеОрганизации, Слово, "")));
				Возврат Результат;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСписокВыбораНаименования(ДанныеГосРеестра, ИндексОрганизации)
	
	КлассификаторОПФ = РегламентированныеДанныеПовтИсп.КлассификаторОрганизационноПравовыхФорм();
	
	СписокВыбора = Элементы["Наименование" + ИндексОрганизации].СписокВыбора;
	СписокВыбора.Очистить();
	
	Если Не ПустаяСтрока(ДанныеГосРеестра.НаименованиеПолное) Тогда
		
		ОрганизационноПравоваяФорма = ВыделитьИзНаименованияОПФ(КлассификаторОПФ, ДанныеГосРеестра.НаименованиеПолное);
		
		СписокВыбора.Добавить(ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
		
		Если НЕ ПустаяСтрока(ОрганизационноПравоваяФорма.КраткаяФорма) Тогда
			
			СписокВыбора.Вставить(0, ОрганизационноПравоваяФорма.НаименованиеБезОПФ + " " + ОрганизационноПравоваяФорма.КраткаяФорма);
			СписокВыбора.Добавить(ОрганизационноПравоваяФорма.КраткаяФорма + " " + ОрганизационноПравоваяФорма.НаименованиеБезОПФ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если СписокВыбора.Количество() > 0 Тогда
		
		ДанныеГосРеестра.Наименование = СписокВыбора[0].Значение;
		Элементы["Наименование" + ИндексОрганизации].КнопкаВыпадающегоСписка = Истина;
		
	Иначе
		
		ДанныеГосРеестра.Наименование = "";
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СформироватьНаименованиеИП(Индекс)
	
	//::: Краткое наименование
	Наименование = СокрЛП(ЭтотОбъект["ИП_Фамилия" + Индекс]);
	Если НЕ ПустаяСтрока(ЭтотОбъект["ИП_Имя" + Индекс]) Тогда
		
		Наименование = Наименование + " " + Лев(СокрЛ(ЭтотОбъект["ИП_Имя" + Индекс]), 1) + ".";
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ЭтотОбъект["ИП_Отчество" + Индекс]) Тогда
		
		Наименование = Наименование + " " + Лев(СокрЛ(ЭтотОбъект["ИП_Отчество" + Индекс]), 1) + ".";
		
	КонецЕсли;
	
	Наименование = Наименование + НСтр("ru = ' ИП'");
	
	//::: Полное наименование
	НаименованиеПолное = "ИП " + СокрЛП(ЭтотОбъект["ИП_Фамилия" + Индекс]);
	Если НЕ ПустаяСтрока(ЭтотОбъект["ИП_Имя" + Индекс]) Тогда
		
		НаименованиеПолное = НаименованиеПолное + " " + Лев(СокрЛ(ЭтотОбъект["ИП_Имя" + Индекс]), 1) + ".";
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ЭтотОбъект["ИП_Отчество" + Индекс]) Тогда
		
		НаименованиеПолное = НаименованиеПолное + " " + Лев(СокрЛ(ЭтотОбъект["ИП_Отчество" + Индекс]), 1) + ".";
		
	КонецЕсли;
	
	Возврат Новый Структура("Наименование, НаименованиеПолное", Наименование, НаименованиеПолное);
	
КонецФункции

#КонецОбласти

#Область ОбработчикиФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВидыБизнесаАдресВХранилище") Тогда
		ЗагрузитьВидыБизнеса(Параметры.ВидыБизнесаАдресВХранилище);
	КонецЕсли;
	
	ДобавитьОрганизацию(Ложь, Справочники.Организации.ОсновнаяОрганизация);
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("Пользователь", Пользователи.АвторизованныйПользователь());
	КэшЗначений.Вставить("РазрешитьЗакрытие", Ложь);
	КэшЗначений.Вставить("ДополнительныйПараметр", НачалоРаботыСПрограммойСервер.ЗначениеДополнительногоПараметра());
	КэшЗначений.Вставить("Доходы", Перечисления.ВидыОбъектовНалогообложения.Доходы);
	КэшЗначений.Вставить("ДоходыМинусРасходы", Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы);
	КэшЗначений.Вставить("ЮрЛицо", Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо);
	КэшЗначений.Вставить("ФизЛицо", Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо);
	КэшЗначений.Вставить("ТекущийИндексОрганизации", Неопределено);
	
	РежимРаботыПрограммы = ОбщегоНазначенияПовтИсп.РежимРаботыПрограммы();
	
	ЗаполнитьДанныеПользователя(РежимРаботыПрограммы);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПользовательЛокально", "Видимость", НЕ РежимРаботыПрограммы.МодельСервиса);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаПользовательСервис", "Видимость", РежимРаботыПрограммы.МодельСервиса);
	
	ЭтоФизическоеЛицо = (Справочники.Организации.ОсновнаяОрганизация.ЮридическоеФизическоеЛицо = КэшЗначений.ФизЛицо);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ДекорацияОтступСотрудники1", "Видимость", ЭтоФизическоеЛицо);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ИПИспользуетТрудНаемныхРаботников1", "Видимость", ЭтоФизическоеЛицо);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ЕстьПатент1", "Доступность", ЭтоФизическоеЛицо);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНавигация", "Видимость", НЕ СтандартныеПодсистемыСервер.ЭтоБазоваяВерсияКонфигурации());
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_ПрисоединенныйФайл" Тогда
		
		СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(КэшЗначений.ТекущийИндексОрганизации);
		СтрокаОрганизации.ФайлЛоготип = ?(ТипЗнч(Источник) = Тип("Массив"), Источник[0], Источник);
		
		ОбновитьИзображениеЛоготипа(СтрокаОрганизации);
		
		КэшЗначений.ТекущийИндексОрганизации = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	
	Если НЕ КэшЗначений.РазрешитьЗакрытие Тогда
		
		ТекстПредупреждения = НСтр("ru ='Заполните, пожалуйста, обязательные поля.
			|Работа программы без указанных сведений затруднительна.'");
			
		ПоказатьПредупреждение(, ТекстПредупреждения);
		//Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиРеквизитов

&НаКлиенте
Процедура ЕстьУСНПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЕстьУСН");
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + Индекс];
	ЗначениеЕстьЕНВД = ЭтотОбъект["ЕстьЕНВД" + Индекс];
	ЗначениеСтавкаНалога = 6;
	ЗначениеНалогообложение = КэшЗначений.Доходы;
	
	ЗаписатьЗначениеОрганизации("ЕстьУСН", ЗначениеЕстьУСН, Индекс);
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс, Истина);
	ЗаписатьЗначениеОрганизации("ВидыОбъектовНалогообложения", ЗначениеНалогообложение, Индекс);
	
	ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога;
	ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс] = ЗначениеНалогообложение;
	
	ЭтоПредприниматель = (ЭтотОбъект["ЮридическоеФизическоеЛицо" + Индекс] = КэшЗначений.ФизЛицо);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаНалогообжение" + Индекс, "Видимость", ЗначениеЕстьУСН И ЭтоПредприниматель);
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСотрудники" + Индекс, "Видимость", ЗначениеЕстьЕНВД ИЛИ ЗначениеЕстьУСН);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьЕНВДПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЕстьЕНВД");
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + Индекс];
	ЗначениеЕстьЕНВД = ЭтотОбъект["ЕстьЕНВД" + Индекс];
	
	ЗаписатьЗначениеОрганизации("ЕстьЕНВД", ЗначениеЕстьЕНВД, Индекс);
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ГруппаСотрудники" + Индекс, "Видимость", ЗначениеЕстьЕНВД ИЛИ ЗначениеЕстьУСН);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьПатентПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЕстьПатент");
	ЗначениеЕстьПатент = ЭтотОбъект["ЕстьПатент" + Индекс];
	ЗаписатьЗначениеОрганизации("ЕстьПатент", ЗначениеЕстьПатент, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЮрФизЛицоПриИзменении(Элемент)
	
	// Внимание! 
	// При изм УСН или ЕНВД редактируется видимость группы элементов,
	// а при изменении ЮР/ФИЗ лица элементов управления.
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ЮридическоеФизическоеЛицо");
	ЗначениеЮридическоеФизическоеЛицо = ЭтотОбъект["ЮридическоеФизическоеЛицо" + Индекс];
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + Индекс];
	
	ЗаписатьЗначениеОрганизацииЮрФизЛицо(ЗначениеЮридическоеФизическоеЛицо, ЗначениеЕстьУСН, Индекс);
	
	ЭтотОбъект["ИНН" + Индекс] = "";
	ЗаписатьЗначениеОрганизации("ИНН", "", Индекс);
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура НаименованиеПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "Наименование");
	ЗначениеНаименование = ЭтотОбъект["Наименование" + Индекс];
	
	ЗаписатьЗначениеОрганизации("Наименование", ЗначениеНаименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", ЗначениеНаименование, Индекс);
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ИПФамилияПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИП_Фамилия");
	
	СтруктураНазваний = СформироватьНаименованиеИП(Индекс);
	
	ЗаписатьЗначениеОрганизации("Фамилия", ЭтотОбъект["ИП_Фамилия" + Индекс], Индекс);
	ЗаписатьЗначениеОрганизации("Наименование", СтруктураНазваний.Наименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", СтруктураНазваний.НаименованиеПолное, Индекс);
	
	ЭтотОбъект["Наименование" + Индекс] = СтруктураНазваний.Наименование;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура ИПИмяПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИП_Имя");
	
	СтруктураНазваний = СформироватьНаименованиеИП(Индекс);
	
	ЗаписатьЗначениеОрганизации("Имя", ЭтотОбъект["ИП_Имя" + Индекс], Индекс);
	ЗаписатьЗначениеОрганизации("Наименование", СтруктураНазваний.Наименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", СтруктураНазваний.НаименованиеПолное, Индекс);
	
	ЭтотОбъект["Наименование" + Индекс] = СтруктураНазваний.Наименование;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура ИПОтчествоПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИП_Отчество");
	
	СтруктураНазваний = СформироватьНаименованиеИП(Индекс);
	
	ЗаписатьЗначениеОрганизации("Отчество", ЭтотОбъект["ИП_Отчество" + Индекс], Индекс);
	ЗаписатьЗначениеОрганизации("Наименование", СтруктураНазваний.Наименование, Индекс);
	ЗаписатьЗначениеОрганизации("НаименованиеПолное", СтруктураНазваний.НаименованиеПолное, Индекс);
	
	ЭтотОбъект["Наименование" + Индекс] = СтруктураНазваний.Наименование;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(Индекс)
	
КонецПроцедуры

&НаКлиенте
Процедура НалогообложениеПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ВидыОбъектовНалогообложения");
	ЗначениеНалогообложение = ЭтотОбъект["ВидыОбъектовНалогообложения" + Индекс];
	
	ЗаписатьЗначениеОрганизации("ВидыОбъектовНалогообложения", ЗначениеНалогообложение, Индекс);
	
	ЗначениеСтавкаНалога = ?(ЗначениеНалогообложение = КэшЗначений.Доходы, 6, 15);
	ЭтотОбъект["СтавкаНалога" + Индекс] = ЗначениеСтавкаНалога;
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура СтавкаНалогаПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "СтавкаНалога");
	ЗначениеСтавкаНалога = ЭтотОбъект["СтавкаНалога" + Индекс];
	
	ЗаписатьЗначениеОрганизации("СтавкаНалога", ЗначениеСтавкаНалога, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура ЕстьСотрудникиПриИзменении(Элемент)
	
	Индекс = ИндексПоИмениЭлемента(Элемент.Имя, "ИПИспользуетТрудНаемныхРаботников");
	ЗначениеЕстьСотрудники = ЭтотОбъект["ИПИспользуетТрудНаемныхРаботников" + Индекс];
	
	ЗаписатьЗначениеОрганизации("ИПИспользуетТрудНаемныхРаботников", ЗначениеЕстьСотрудники, Индекс);
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЛоготипаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИндексОрганизации = ИндексПоИмениЭлемента(Элемент.Имя, "АдресЛоготипа");
	СтрокаОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации);
	ЗначениеФайлЛоготипа = СтрокаОрганизации.ФайлЛоготип;
	
	Если ЗначениеЗаполнено(ЗначениеФайлЛоготипа) Тогда
		
		ПросмотретьПрисоединенныйФайл(ЗначениеФайлЛоготипа);
		
	Иначе
		
		ДобавитьИзображениеНаКлиенте(ИндексОрганизации);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИННПриИзменении(Элемент)
	Перем Ошибки;
	
	ИндексОрганизации = ИндексПоИмениЭлемента(Элемент.Имя, "ИНН");
	
	ЗначениеИНН = ЭтотОбъект["ИНН" + ИндексОрганизации];
	ЗаписатьЗначениеОрганизации("ИНН", ЗначениеИНН, ИндексОрганизации);
	ЗначениеЕстьУСН = ЭтотОбъект["ЕстьУСН" + ИндексОрганизации];
	
	ЗначениеЮрФизЛицо = ?(СтрДлина(ЗначениеИНН) = 12, КэшЗначений.ФизЛицо, КэшЗначений.ЮрЛицо);
	ЭтотОбъект["ЮридическоеФизическоеЛицо" + ИндексОрганизации] = ЗначениеЮрФизЛицо;
	
	ЗаписатьЗначениеОрганизацииЮрФизЛицо(ЗначениеЮрФизЛицо, ЗначениеЕстьУСН, ИндексОрганизации);
	
	/////////////////////////////////////////////////////////////////////////////////
	// Необходимо заполнить след реквизиты:
	//
	// Наименование +
	// НаименованиеПолное -
	// ЮридическоеФизическоеЛицо +
	// ИНН +
	// КПП -						// Юр
	// ОГРН -
	// КодОКВЭД -
	// АдресЮридический -
	// АдресФактический -
	// Телефон -
	//
	// СвидетельствоСерияНомер -	// ФЗ
	// СвидетельствоДатаВыдачи- 	// ФЗ
	// Фамилия -					// ФЗ
	// Имя -						// ФЗ
	// Отчество -					// ФЗ
	// Пол -						// ФЗ
	//
	// КодНалоговогоОргана -
	// КодНалоговогоОрганаПолучателя -
	// РегистрационныйНомерПФР -	// Юр
	// РегистрационныйНомерПФР_ИП -	// ФЗ
	// КодОрганаПФР -
	// КодОрганаФСС -
	// НаименованиеТерриториальногоОрганаФСС -
	// РегистрационныйНомерФСС -
	// НаименованиеПлательщикаПриПеречисленииНалогов -
	//
	// ЕстьУСН +
	// ЕстьЕНВД +
	// ИПИспользуетТрудНаемныхРаботников +
	// Логотип +
	// СтавкаНДСПоУмолчанию +
	
	ДанныеГосРеестра = Новый Структура;
	ЗаполнитьРеквизитыПоИНННаСервере(ЗначениеИНН, ДанныеГосРеестра, ИндексОрганизации, Ошибки);
	
	// Стартовое окно не должно сообщать о существующих ошибках в учетных данных. Цель быстро собрать сведения, 
	// если они ошибочные - продолжить работу.
	//
	// ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки); 
	
	Если ДанныеГосРеестра.Количество() > 0 Тогда
		
		ДанныеСтрокиОрганизации = СтрокаОрганизацииПоИндексуНаКлиенте(ИндексОрганизации);
		
		Если ДанныеГосРеестра.Свойство("Наименование") 
			И НЕ ПустаяСтрока(ДанныеГосРеестра.Наименование) Тогда
			
			ЭтотОбъект["Наименование" + ИндексОрганизации] = ДанныеГосРеестра.Наименование;
			ДанныеСтрокиОрганизации.Наименование = ДанныеГосРеестра.Наименование;
			
		КонецЕсли;
		
		Если ДанныеГосРеестра.Свойство("КПП") Тогда
			
			ДанныеСтрокиОрганизации.КПП = ДанныеГосРеестра.КПП;
			
		КонецЕсли;
		
		ДанныеСтрокиОрганизации.НаименованиеПолное = ДанныеГосРеестра.НаименованиеПолное;
		ДанныеСтрокиОрганизации.ОГРН = ДанныеГосРеестра.ОГРН;
		ДанныеСтрокиОрганизации.КодОКВЭД = ДанныеГосРеестра.КодОКВЭД;
		ДанныеСтрокиОрганизации.АдресЮридический = ДанныеГосРеестра.ЮридическийАдрес;
		ДанныеСтрокиОрганизации.АдресФактический = ДанныеГосРеестра.ЮридическийАдрес;
		ДанныеСтрокиОрганизации.Телефон = ДанныеГосРеестра.Телефон;
		
		Если НЕ ДанныеГосРеестра.ЭтоЮрЛицо Тогда
			
			ДанныеСтрокиОрганизации.СвидетельствоСерияНомер = ДанныеГосРеестра.СвидетельствоСерияНомер;
			ДанныеСтрокиОрганизации.СвидетельствоДатаВыдачи = ДанныеГосРеестра.СвидетельствоДатаВыдачи;
			
			ДанныеСтрокиОрганизации.Фамилия = ДанныеГосРеестра.Фамилия;
			ЭтотОбъект["ИП_Фамилия" + ИндексОрганизации] = ДанныеГосРеестра.Фамилия;
			
			ДанныеСтрокиОрганизации.Имя = ДанныеГосРеестра.Имя;
			ЭтотОбъект["ИП_Имя" + ИндексОрганизации] = ДанныеГосРеестра.Имя;
			
			ДанныеСтрокиОрганизации.Отчество = ДанныеГосРеестра.Отчество;
			ЭтотОбъект["ИП_Отчество" + ИндексОрганизации] = ДанныеГосРеестра.Отчество;
			
			ДанныеСтрокиОрганизации.Пол = ДанныеГосРеестра.Пол;
			
		КонецЕсли;
		
		ДанныеСтрокиОрганизации.КодНалоговогоОргана = ДанныеГосРеестра.КодНалоговогоОрганаПолучателя;
		ДанныеСтрокиОрганизации.КодНалоговогоОрганаПолучателя = ДанныеГосРеестра.КодНалоговогоОрганаПолучателя;
		
		Если ДанныеГосРеестра.Свойство("РегистрацияВПенсионномФонде")
			И ЗначениеЗаполнено(ДанныеГосРеестра.РегистрацияВПенсионномФонде) Тогда
			
			ДанныеСтрокиОрганизации.КодОрганаПФР = ДанныеГосРеестра.РегистрацияВПенсионномФонде.КодОрганаПФР;
			
			Если ДанныеГосРеестра.ЭтоЮрЛицо Тогда
				
				ДанныеСтрокиОрганизации.РегистрационныйНомерПФР = ДанныеГосРеестра.РегистрационныйНомерПФР;
				
			Иначе
				
				ДанныеСтрокиОрганизации.РегистрационныйНомерПФР_ИП = ДанныеГосРеестра.РегистрационныйНомерПФР_ИП;
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДанныеСтрокиОрганизации.КодОрганаФСС = ДанныеГосРеестра.КодОрганаФСС;
		ДанныеСтрокиОрганизации.НаименованиеТерриториальногоОрганаФСС = ДанныеГосРеестра.НаименованиеТерриториальногоОрганаФСС;
		ДанныеСтрокиОрганизации.РегистрационныйНомерФСС = ДанныеГосРеестра.РегистрационныйНомерФСС;
		ДанныеСтрокиОрганизации.НаименованиеПлательщикаПриПеречисленииНалогов = ДанныеГосРеестра.НаименованиеПлательщикаПриПеречисленииНалогов;
		
	КонецЕсли;
	
	ЗаписатьНаименованиеПлательщикаПриПеречисленииНалогов(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольПодтверждениеПриИзменении(Элемент)
	
	Если ПользовательПароль <> ПользовательПарольПодтверждение Тогда
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru ='Не совпадают пароли'"), , "ПользовательПарольПодтверждение");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеВидаБизнесаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Текст) Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ДанныеВыбора = ПодобратьВидыБизнеса(Текст);
	
КонецПроцедуры

&НаКлиенте
Процедура ОписаниеВидаБизнесаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОписаниеВидаБизнесаОбработкаВыбораНаСевере(ВыбранноеЗначение);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд

&НаКлиенте
Процедура НачатьРаботуСПрограммой(Команда)
	Перем Ошибки;
	
	ДополнительныйПараметр = "";
	Если ВРЕГ(ПользовательИмя) = КэшЗначений.ДополнительныйПараметр Тогда
		
		ДополнительныйПараметр = КэшЗначений.ДополнительныйПараметр;
		КэшЗначений.РазрешитьЗакрытие = Истина;
		
	Иначе
	
		РеквизитыЗаполненыКорректно(Ошибки);
		КэшЗначений.РазрешитьЗакрытие = (Ошибки = Неопределено);
		
		Если НЕ КэшЗначений.РазрешитьЗакрытие Тогда
			
			ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыЗакрытия = Новый Структура;
	ПараметрыЗакрытия.Вставить("НачатьРаботу", 					Истина);
	
	ПараметрыЗакрытия.Вставить("Пользователь",					КэшЗначений.Пользователь);
	ПараметрыЗакрытия.Вставить("ПользовательEmail",				ПользовательEmail);
	ПараметрыЗакрытия.Вставить("ПользовательИмя",				ПользовательИмя);
	ПараметрыЗакрытия.Вставить("ПользовательПароль",			ПользовательПароль);
	ПараметрыЗакрытия.Вставить("ПользовательСайт",				ПользовательСайт);
	ПараметрыЗакрытия.Вставить("ПользовательТелефон",			ПользовательТелефон);
	
	ПараметрыЗакрытия.Вставить("ВидБизнесаОрганизации",			ИдентификаторВидаБизнеса);
	
	ПараметрыЗакрытия.Вставить("Организации",					Объект.Организации);
	ПараметрыЗакрытия.Вставить("ДополнительныйПараметр",		ДополнительныйПараметр);
	
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработчикКомандыДобавитьОрганизацию(Команда)
	
	ДобавитьОрганизацию(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьИзображениеЛоготипа(Команда)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(ЭтаФорма.ТекущийЭлемент.Имя, "АдресЛоготипа");
	
	ДобавитьИзображениеНаКлиенте(ИндексОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьИзображениеЛоготипа(Команда)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(ЭтаФорма.ТекущийЭлемент.Имя, "АдресЛоготипа");
	
	ЗаписатьЗначениеОрганизации("ФайлЛоготип", Неопределено, ИндексОрганизации);
	ЭтотОбъект["АдресЛоготипа" + ИндексОрганизации] = "";
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОрганизацию(Команда)
	
	ИндексОрганизации = ИндексПоИмениЭлемента(ЭтаФорма.ТекущийЭлемент.Имя, "УдалитьОрганизацию");
	УдалитьОрганизациюНаСервере(ИндексОрганизации);
	
КонецПроцедуры

#КонецОбласти