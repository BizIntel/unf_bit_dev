
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;

	НачалоПериода = Параметры.ДатаНачала;
	КонецПериода = Параметры.ДатаОкончания;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Ок(Команда)
	
	МинимальнаяДата = КонецПериода - 60*60*24*14;
	Если МинимальнаяДата > НачалоПериода Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите период не больше 14 дней.'"), , "КонецПериода");
		Возврат;
	КонецЕсли;
	
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Если НачалоПериода > ТекущаяДатаСеанса Тогда
		СообщениеТекст = НСтр("ru = 'Дата начала периода запроса не должна быть позднее текущей даты.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "НачалоПериода");
		Возврат;
	КонецЕсли;
	
	Если КонецПериода > КонецДня(ТекущаяДатаСеанса) Тогда
		СообщениеТекст = НСтр("ru = 'Дата конца периода запроса не должна быть позднее текущей даты.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "КонецПериода");
		Возврат;
	КонецЕсли;
	
	Если НачалоПериода > КонецПериода Тогда
		СообщениеТекст = НСтр("ru = 'Дата начала периода запроса не должна быть позднее даты конца периода.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "НачалоПериода");
		Возврат;
	КонецЕсли;
	
	Период = Новый СтандартныйПериод(НачалоПериода, КонецПериода);
	
	Закрыть(Период);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = Новый СтандартныйПериод(НачалоПериода, КонецПериода);
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НачалоПериода = РезультатВыбора.ДатаНачала;
	КонецПериода  = РезультатВыбора.ДатаОкончания;
	
КонецПроцедуры

#КонецОбласти