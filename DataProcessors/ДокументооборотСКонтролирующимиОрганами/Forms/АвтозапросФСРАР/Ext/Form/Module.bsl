&НаКлиенте
Перем МаксСекунд, ЧислоСекунд, ЧислоПопыток, ПовторныйЗапрос, ОтправкаНаВторойСервер, ВнутриОбработчикаОжиданияЗапроса, КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ТипЗнчСсылкаНаОтчет = ТипЗнч(ОтправкаОбъект.ОтчетСсылка);
	
	Если ТипЗнчСсылкаНаОтчет = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		ОтчетПредставление = РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(ОтправкаОбъект.ОтчетСсылка);
	Иначе //электронное представление
		ОтчетПредставление = ОтправкаОбъект.ОтчетСсылка.Наименование
	КонецЕсли;
	
	ФедеральныйПортал = Параметры.ФедеральныйПортал;
	КодРегиона = Параметры.КодРегиона;
	ЗначениеCookie = Параметры.ЗначениеCookie;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Отобразить появление новой записи в журнале обмена
	ПараметрыОповещения = Новый Структура(); 
	ПараметрыОповещения.Вставить("Ссылка", ОтправкаОбъект.ОтчетСсылка);
	ПараметрыОповещения.Вставить("Организация", ОтправкаОбъект.Организация);
	Оповестить("Запись_ОтправкиФСРАР", ПараметрыОповещения, ОтправкаОбъект.Ссылка);
	
	// Обновление статуса отправки на форме отправляемого отчета
	Если ЭтаФорма.ВладелецФормы <> Неопределено 
		И ЭтаФорма.ВладелецФормы.ИмяФормы <> "ОбщаяФорма.РегламентированнаяОтчетность" Тогда
		РегламентированнаяОтчетностьКлиент.ОбновитьПанельСостоянияОтправкиВРегламентированномОтчете(ЭтаФорма, "ФСРАР");
	КонецЕсли;
	
	ВнутриОбработчикаОжиданияЗапроса = Ложь;
	ОтправкаНаВторойСервер = Ложь;
	ПовторныйЗапрос = Ложь;
	ЧислоПопыток = 0;
	МаксСекунд = 60;
	ЧислоСекунд = МаксСекунд;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ОтчетПредставлениеНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, ОтправкаОбъект.ОтчетСсылка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	ОбновитьПанельЗапроса();
	ПодключитьОбработчикОжидания("ОжиданиеЗапроса", 1);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьПанельЗапроса()
	
	Если ЧислоПопыток = 3 Тогда
		Если ПротоколЗаполнен(ОтправкаОбъект.Ссылка) Тогда
			НадписьОсталосьСекунд = НСтр("ru = 'Обработка отчета порталом не завершена. "
				+ "Попробуйте через некоторое время использовать в отчете кнопку ""Обновить"".'");
		Иначе
			НадписьОсталосьСекунд = НСтр("ru = 'Результатов отправки отчета получить не удалось. "
				+ "Возможно, сервер перегружен. Попробуйте через некоторое время использовать в отчете кнопку ""Обновить"".'");
		КонецЕсли;
	Иначе
		Если НЕ ПовторныйЗапрос Тогда
			НадписьОсталосьСекунд = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Отчет отправлен. " +
				"Результаты отправки будут получены через некоторое время (%1 с)'"), Строка(ЧислоСекунд));
		Иначе
			НадписьОсталосьСекунд = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Результатов отправки отчета получить не удалось. " +
				"Через некоторое время будет предпринята еще одна попытка (%1 с)'"), Строка(ЧислоСекунд));
		КонецЕсли;
	КонецЕсли;
	
	Индикатор = МаксСекунд - ЧислоСекунд;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПослеОбновленияРезультата()
	
	Если ОтправкаОбъект.СтатусОтправки = ПредопределенноеЗначение("Перечисление.СтатусыОтправки.Отправлен") Тогда
		
		ЧислоСекунд = МаксСекунд;
		
		Если ОтправкаНаВторойСервер Тогда
			ОтправкаНаВторойСервер = Ложь;
		Иначе
			ЧислоПопыток = ЧислоПопыток + 1;
			ПовторныйЗапрос = Истина;
		КонецЕсли;
		
		ОбновитьПанельЗапроса();
		
		Если ЧислоПопыток = 3 Тогда
			ОтключитьОбработчикОжидания("ОжиданиеЗапроса");
			Активизировать();
			ОповеститьОЗавершенииАвтообмена();
			Если ПротоколЗаполнен(ОтправкаОбъект.Ссылка) Тогда
				КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФСРАР(ОтправкаОбъект.Ссылка);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		ОтключитьОбработчикОжидания("ОжиданиеЗапроса");
		Активизировать();
		ОповеститьОЗавершенииАвтообмена();
		КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФСРАР(ОтправкаОбъект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДанныеФормыНаСервере()
	
	Прочитать();
	ОбновитьФорму();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьФорму()
	
	СтатусОтправки = ОтправкаОбъект.СтатусОтправки;
	
	Если СтатусОтправки <> ПредопределенноеЗначение("Перечисление.СтатусыОтправки.Отправлен") Тогда
		
		ЦветШрифта = Новый Цвет(255, 0, 0);
		ЗаголовокРезультата = НСтр("ru = 'Ошибка механизма отправки.'");
			
		Если СтатусОтправки = ПредопределенноеЗначение("Перечисление.СтатусыОтправки.НеПринят") Тогда
			ЦветШрифта = Новый Цвет(255, 0, 0);
			ЗаголовокРезультата = НСтр("ru = 'Отчет не принят: обнаружены ошибки.'");
		ИначеЕсли СтатусОтправки = ПредопределенноеЗначение("Перечисление.СтатусыОтправки.Сдан") Тогда
			ЦветШрифта = Новый Цвет(0, 179, 16);
			ЗаголовокРезультата = НСтр("ru = 'Отчет сдан: получен протокол, ошибок не обнаружено.'");
		КонецЕсли;
		
		Элементы.НадписьОсталосьСекунд.ЦветТекста = ЦветШрифта;
		НадписьОсталосьСекунд = ЗаголовокРезультата;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжиданиеЗапроса()
	
	Если ВнутриОбработчикаОжиданияЗапроса Тогда
		Возврат;
	КонецЕсли;
	
	ВнутриОбработчикаОжиданияЗапроса = Истина;
	
	Если ЧислоСекунд < 1 Тогда
		
		//обновим результат отправки
		ОтправкаНаВторойСервер = Ложь;
		ОписаниеОповещения = Новый ОписаниеОповещения("ОжиданиеЗапросаЗавершение", ЭтотОбъект);
		КонтекстЭДОКлиент.ОбновитьРезультатОтправленногоОтчетаФСРАР(ЭтаФорма, ФедеральныйПортал, КодРегиона, ЗначениеCookie, ОтправкаНаВторойСервер, Истина, ОписаниеОповещения);
		Возврат;
		
	Иначе
		ЧислоСекунд = ЧислоСекунд - 1;
		ОбновитьПанельЗапроса();
	КонецЕсли;
	
	ВнутриОбработчикаОжиданияЗапроса = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОжиданиеЗапросаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		
		ОбновитьДанныеФормыНаСервере();
		
	КонецЕсли;
	
	// Обновление статуса отправки в форме Управления обменом
	ПараметрыОповещения = Новый Структура;
	ПараметрыОповещения.Вставить("Ссылка", ОтправкаОбъект.ОтчетСсылка);
	ПараметрыОповещения.Вставить("Организация", ОтправкаОбъект.Организация);
	Оповестить("Редактирование_ОтправкиФСРАР", ПараметрыОповещения, ОтправкаОбъект.Ссылка);
	
	// Обновление статуса отправки на форме отправляемого отчета
	Если ЭтаФорма.ВладелецФормы <> Неопределено 
		И ЭтаФорма.ВладелецФормы.ИмяФормы <> "ОбщаяФорма.РегламентированнаяОтчетность" Тогда
		РегламентированнаяОтчетностьКлиент.ОбновитьПанельСостоянияОтправкиВРегламентированномОтчете(ЭтаФорма.ВладелецФормы , "ФСРАР");
	КонецЕсли;
	ДействияПослеОбновленияРезультата();
	
	ВнутриОбработчикаОжиданияЗапроса = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповеститьОЗавершенииАвтообмена()
	
	ПараметрыОповещения = Новый Структура(); 
	ПараметрыОповещения.Вставить("Ссылка", ОтправкаОбъект.ОтчетСсылка);
	ПараметрыОповещения.Вставить("Организация", ОтправкаОбъект.Организация);
	Оповестить("Завершение автообмена с сервером ФСРАР", ПараметрыОповещения, ОтправкаОбъект.Ссылка);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПротоколЗаполнен(ОтправкаСсылка)
	
	Возврат ЗначениеЗаполнено(ОтправкаСсылка.Протокол.Получить());
	
КонецФункции

#КонецОбласти