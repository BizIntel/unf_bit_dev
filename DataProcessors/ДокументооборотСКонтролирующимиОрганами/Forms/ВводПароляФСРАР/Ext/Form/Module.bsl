
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Регион = Параметры.Регион;
	Портал = Параметры.Портал;
	ИНН = Параметры.Организация.ИНН;
	Пароль = Параметры.Пароль;
	СохранитьПароль = Параметры.СохранитьПароль;
	
	Элементы.СсылкаНаРегистрацию.Заголовок = Портал + "Account/Register";
	
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	АдресСервера = КонтекстЭДО.ЭлементыСсылкиНаПортал(Портал, Ложь).АдресСервера;
	ПозицияРазделителя = СтрНайти(АдресСервера, ":");
	
	Если ПозицияРазделителя = 0 Тогда
		Элементы.ПредупреждениеОНестандартномПорте.Видимость = Ложь;
	Иначе
		
		НомерПорта = Сред(АдресСервера, ПозицияРазделителя + 1);
		Элементы.ПредупреждениеОНестандартномПорте.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.ПредупреждениеОНестандартномПорте.Заголовок, НомерПорта);
		
	КонецЕсли;
	
	ДопустимоеКоличествоПопытокВводаПароля = ?(Лев(Регион, 2) = "03", 10, 5);
	Элементы.ПредупреждениеОбОшибочномВводеПароля.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.ПредупреждениеОбОшибочномВводеПароля.Заголовок, ДопустимоеКоличествоПопытокВводаПароля);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПорталНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	СкорректированнаяСсылкаНаПортал = Портал;
	Если Прав(СкорректированнаяСсылкаНаПортал, 12) = "/WebCabinet/" Тогда
		СкорректированнаяСсылкаНаПортал = Лев(СкорректированнаяСсылкаНаПортал, СтрДлина(СкорректированнаяСсылкаНаПортал) - 12);
	КонецЕсли;
	ПерейтиПоНавигационнойСсылке(СкорректированнаяСсылкаНаПортал);
	
КонецПроцедуры

&НаКлиенте
Процедура СсылкаНаРегистрациюНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	ПерейтиПоНавигационнойСсылке(Элементы.СсылкаНаРегистрацию.Заголовок);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Результат = Новый Структура("Пароль, СохранитьПароль");
	Результат.Пароль = Пароль;
	Результат.СохранитьПароль = СохранитьПароль;
	Закрыть(Результат);
	
КонецПроцедуры

#КонецОбласти
