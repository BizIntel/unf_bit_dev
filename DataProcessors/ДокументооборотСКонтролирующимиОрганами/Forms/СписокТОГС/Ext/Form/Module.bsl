&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ТекущийКодТОГС = Параметры.ТекущийКодТОГС;
	СтарыйКодТОГС  = ТекущийКодТОГС; 
	
	КодРегиона = Неопределено;
	Если НЕ ЗначениеЗаполнено(ТекущийКодТОГС) Тогда
		Параметры.Свойство("КодРегиона", КодРегиона);
	КонецЕсли;
	
	КлассификаторXML = РеквизитФормыВЗначение("Объект").ПолучитьМакет("КодыТОГС").ПолучитьТекст();
	КлассификаторТаблица = ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
	Отбор = Новый Структура();
	Если Параметры.Свойство("Спецоператор") Тогда
		Спецоператор            = Параметры.Спецоператор;
		ИмяКолонкиСпецоператора = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(Спецоператор, "СпецоператорСвязи");
		ТаблицаТОГС.Загрузить(КлассификаторТаблица.Скопировать(Новый Структура(ИмяКолонкиСпецоператора, "")));
	Иначе
		ТаблицаТОГС.Загрузить(КлассификаторТаблица);
	КонецЕсли;
	
	ТаблицаТОГС.Сортировать("Код");
	
	МассивНайденныхСтрок = Новый Массив;
	Если ЗначениеЗаполнено(ТекущийКодТОГС) Тогда
		МассивНайденныхСтрок = ТаблицаТОГС.НайтиСтроки(Новый Структура("Код", ТекущийКодТОГС));
	ИначеЕсли ЗначениеЗаполнено(КодРегиона) Тогда
		МассивНайденныхСтрок = ТаблицаТОГС.НайтиСтроки(Новый Структура("КодРегиона", КодРегиона));
	КонецЕсли;
	
	Если МассивНайденныхСтрок.Количество() > 0 Тогда
		Элементы.ТаблицаТОГС.ТекущаяСтрока = МассивНайденныхСтрок[0].ПолучитьИдентификатор();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ТаблицаТОГСВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ВыбратьСтроку();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьСтроку();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыбратьСтроку()
	
	Если Элементы.ТаблицаТОГС.ТекущиеДанные = Неопределено Тогда
		
		Если ОписаниеОповещенияОЗакрытии = Неопределено Тогда
			ОповеститьОВыборе(Неопределено);
		Иначе
			Закрыть(Неопределено);
		КонецЕсли;
		
	Иначе 
		
		ТекущиеДанные = Элементы.ТаблицаТОГС.ТекущиеДанные;
		Если ОписаниеОповещенияОЗакрытии = Неопределено Тогда
			ОповеститьОВыборе(Новый Структура("КодТОГС, НаименованиеТОГС", ТекущиеДанные.Код, ТекущиеДанные.Наименование));
		Иначе
			Закрыть(Новый Структура("КодТОГС, НаименованиеТОГС", ТекущиеДанные.Код, ТекущиеДанные.Наименование));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти







