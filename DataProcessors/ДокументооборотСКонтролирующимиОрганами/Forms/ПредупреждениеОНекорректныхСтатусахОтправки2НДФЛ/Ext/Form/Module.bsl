&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	АдресВременногоХранилища = Параметры.Адрес;
	Если ЭтоАдресВременногоХранилища(АдресВременногоХранилища) Тогда
		
		ПереданныеОтчеты = ПолучитьИзВременногоХранилища(Параметры.Адрес);
		Отчеты.Загрузить(ПереданныеОтчеты);
		
		НапомнитьПозжеОНекорректныхСтатусахОтправки2НДФЛ(Истина);
		
	Иначе
		
		Отказ = Истина;
		
	КонецЕслИ;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.Отчеты.ТекущиеДанные <> Неопределено Тогда 
		
		Если Поле.Имя = "ОтчетыПредставление" Тогда 
			
			СтандартнаяОбработка = Ложь;
			ПоказатьЗначение(, Элементы.Отчеты.ТекущиеДанные.Отчет);
			
		ИначеЕсли Поле.Имя = "ОтчетыСостояние" Тогда 
			
			СтандартнаяОбработка = Ложь;
			КонтекстЭДОКлиент.ПоказатьФормуСтатусовОтправки(Элементы.Отчеты.ТекущиеДанные.Отчет);
			
		ИначеЕсли Поле.Имя = "ОтчетыНаименованиеПротокола" Тогда
			
			СтандартнаяОбработка = Ложь;
			
			Протокол 	= Элементы.Отчеты.ТекущиеДанные.Протокол;
			ЦиклОбмена 	= Элементы.Отчеты.ТекущиеДанные.ЦиклОбмена;
			
			КонтекстЭДОКлиент.ПоказатьДокументыЦикловОбмена(ЦиклОбмена, Истина, Протокол);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НапомнитьПозжеКоманда(Команда)
	
	НапомнитьПозжеОНекорректныхСтатусахОтправки2НДФЛ(Истина);
	ПрограммноеЗакрытие = Истина;
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура БольшеНеПоказыватьКоманда(Команда)
	
	НапомнитьПозжеОНекорректныхСтатусахОтправки2НДФЛ(Ложь);
	ПрограммноеЗакрытие = Истина;
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура НапомнитьПозжеОНекорректныхСтатусахОтправки2НДФЛ(Ответ)
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.НапомнитьПозжеОНекорректныхСтатусахОтправки2НДФЛ(Ответ);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;

КонецПроцедуры

#КонецОбласти




