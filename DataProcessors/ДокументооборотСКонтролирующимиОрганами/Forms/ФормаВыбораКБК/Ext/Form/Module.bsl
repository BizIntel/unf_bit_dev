&НаКлиенте
Перем СтрокаПоискаВрем;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КБК.Загрузить(КонтекстЭДО.ПолучитьТаблицуКБК());
	
	Если ЗначениеЗаполнено(Параметры.ТекущийКБК) Тогда
		РезультатПоиска = КБК.НайтиСтроки(Новый Структура("КБК", Параметры.ТекущийКБК));
		Если РезультатПоиска.Количество() <> 0 Тогда
			Элементы.КБК.ТекущаяСтрока = КБК.Индекс(РезультатПоиска[0]);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СтрокаПоискаПриИзменении(Элемент)
	
	Если Не ЗначениеЗаполнено(СтрокаПоиска) Тогда
		Элементы.КБК.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Текст) Тогда
		СтрокаПоискаВрем = Текст;
		ПодключитьОбработчикОжидания("ВыполнитьПоиск", 0.5, Истина);
	Иначе
		Элементы.КБК.ОтборСтрок = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыКБК

&НаКлиенте
Процедура КБКВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Закрыть(Новый Структура("КБК,Наименование", Элементы.КБК.ТекущиеДанные.КБК, Элементы.КБК.ТекущиеДанные.Наименование));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Если Элементы.КБК.ТекущиеДанные <> Неопределено Тогда
		Закрыть(Новый Структура("КБК,Наименование", Элементы.КБК.ТекущиеДанные.КБК, Элементы.КБК.ТекущиеДанные.Наименование));
	Иначе
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыполнитьПоиск()
	
	Если ЗначениеЗаполнено(СтрокаПоискаВрем) Тогда
		ИскатьСтрокуНаСервере(СтрокаПоискаВрем);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИскатьСтрокуНаСервере(СтрокаПоискаВрем)
	
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	ПодстрокаПоиска = КонтекстЭДО.ОбработатьСтрокуПоискаКБК(СтрокаПоискаВрем);
	
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ПодстрокаПоиска) Тогда 
		ОбластьПоиска = "КБК";
	Иначе
		ОбластьПоиска = "Наименование";
	КонецЕсли;
	
	Элементы.КБК.ОтборСтрок = Новый ФиксированнаяСтруктура(ОбластьПоиска, ПодстрокаПоиска);

КонецПроцедуры

#КонецОбласти




