&НаКлиенте
Перем КонтекстЭДОКлиент;

&НаКлиенте
Перем РеквизитыНеХранящиесяВБазе;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПаспортРФ = ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.ВидыДокументовФизическихЛиц.ПаспортРФ");
	
	// Получаем значение параметров
	Организация = Параметры.Организация;
		
	Реквизит = Параметры.Реквизит;
	
	ИспользуетсяОднаОрганизация = РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация();
	Если ИспользуетсяОднаОрганизация Тогда
		
		Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
		Организация = Модуль.ОрганизацияПоУмолчанию();
		
	Иначе
		
		Если НЕ ЗначениеЗаполнено(Организация) И ЗначениеЗаполнено(Реквизит) Тогда 
			Организация = Реквизит.Организация;
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			Организация = ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ОсновнаяОрганизация();
		КонецЕсли;
		
	КонецЕсли;
	
	УчетнаяЗапись = УчетнаяЗаписьОрганизации(Организация);

	ЗаполнитьСлужебныеДанныеНаСервере();
	
	ТекущаяДатаСервер = ТекущаяДатаСеанса();
	
	УправлениеФормой(ЭтаФорма);
	
	ВерсияБСП = СтандартныеПодсистемыСервер.ВерсияБиблиотеки();
	
	ФНС  = Перечисления.ТипыКонтролирующихОрганов.ФНС;
	ФСГС = Перечисления.ТипыКонтролирующихОрганов.ФСГС;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	//#Если ВебКлиент Тогда
	//	Элементы.ТелефонМобильный.Маска = "+9 999 XXX-XX-99";
	//	ТелефонМобильный = "+7        -  -  ";
	//#Иначе
	//	Элементы.ТелефонМобильный.Маска = "+7 999 XXX-XX-99";
	//	ТелефонМобильный = "";
	//#КонецЕсли
	Элементы.ТелефонМобильный.ОбновитьТекстРедактирования();
	Элементы.ЭлектроннаяПочтаДляПаролей.ОбновитьТекстРедактирования();
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Элементы.Закрыть.Заголовок = "Закрыть" Тогда
		ПрограммноеЗакрытие = Истина;
	КонецЕсли;
	
	ТекстПредупреждения = НСтр("ru = 'Прервать работу помощника?'");
	
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияПроизвольнойФормы(
		ЭтотОбъект, 
		Отказ, 
		ЗавершениеРаботы,
		ТекстПредупреждения, 
		"ПрограммноеЗакрытие");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИзменитьРеквизитыПодключенияК1СОтчетностиПриИзменении(Элемент)
	
	// Определяем, нужно ли перездавать сертификат 
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура СдаватьВРосстатПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВРосстатПриИзмененииЗавершение", ЭтотОбъект);
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВРосстат", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСГС"), СдаватьВРосстатИсходный, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВПФРПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВПФРПриИзмененииЗавершение", ЭтотОбъект);
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВПФР", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР"), СдаватьВПФРИсходный, ОписаниеОповещения);
		
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФНСПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВФНСПриИзмененииЗавершение", ЭтотОБъект);
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВФНС", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС"), СдаватьВФНСИсходный, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФССПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВФССПриИзмененииЗавершение", ЭтотОБъект);
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВФСС", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС"), СдаватьВФССИсходный, ОписаниеОповещения);
		
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодыФНСНажатие(Элемент)
	
	ОткрытьФормуРедактированияНаправленийСдачиОтчетности(ПолучателиФНС, ФНС);

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВладелецЭЦПНажатие(Элемент)
	
	ОткрытьФормуВыбораВладельцаЭЦП();

КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
		
	УчетнаяЗапись = УчетнаяЗаписьОрганизации(Организация);
	
	ЗаполнитьСлужебныеДанныеНаСервере();
	
	ЗаполнитьРеквизитыОрганизации(ДанныеЗаполнения);
	
	ОпределитьРеквизитыНеХранящиесяВБазе();
	
	ОбновитьТекстПодсказки();
								
	УправлениеФормой(ЭтаФорма);
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьТекстПолей", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьТекстПолей()
	
	Элементы.ТелефонДляПаролей.ОбновитьТекстРедактирования();
	Элементы.ЭлектроннаяПочтаДляПаролей.ОбновитьТекстРедактирования();
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодРосстатНажатие(Элемент)
	
	ОткрытьФормуРедактированияНаправленийСдачиОтчетности(ПолучателиФСГС, ФСГС);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияКодПФРНажатие(Элемент)
	
	ОткрытьФормуРедактированияРеквизита("КодПФР", "Код управления ПФР", КодПФР, "999-999");

КонецПроцедуры

&НаКлиенте
Процедура ПродлитьЛицензиюНа1СОтчетностьПриИзменении(Элемент)
	
	// Оформляем текст под галкой
	ОформитьПодсказкуДляГалкиПродлитьЛицензиюНа1СОтчетность();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьСертификатПриИзменении(Элемент)
	
	Если ПродлитьСертификатИсходный И НЕ ПродлитьСертификат Тогда
		// Пользователь решил отказаться от переиздания сертификата
		
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru = 'Да'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru = 'Отмена'"));
		
		ТекстВопроса = НСтр("ru = 'Отправка отчетов станет недоступной после окончания срока действия сертификата.
			|Все равно отказаться от продления сертификата?'");
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтветПользователяНаПредложениеПереиздатьСертификат", ЭтотОбъект);
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки, , Кнопки[0].Значение);
	Иначе
		ПродлитьСертификатПриИзмененииЗавершение();
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереиздатьСертификатПриИзменении(Элемент)
	
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания(Истина);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьМобильныйТелефонПриИзменении(Элемент)
	
	ОформитьПодсказкуДляГалкиИзменитьМобильныйТелефон();
	
	//ПоказатьТекстПроОтличиеНомеровТелефонов(ТелефонМобильный);
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФСРАРПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВФСРАРПриИзмененииЗавершение", ЭтотОбъект);
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВФСРАР", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСРАР"), СдаватьВФСРАРИсходный, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзятьКодПФРИзОрганизацииНажатие(Элемент)
	
	КодПФР = КодПФР(ДанныеОрганизации);
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ВзятьКодРосстатаИзОрганизацииНажатие(Элемент)
	
	ПолучателиФСГС.Очистить();
	
	НоваяСтрокаНаправления = ПолучателиФСГС.Добавить();
	НоваяСтрокаНаправления.ТипПолучателя 	= ФСГС;
	НоваяСтрокаНаправления.КодПолучателя 	= КодРосстатаИзОрганизации(ДанныеОрганизации);
	
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьВладельцаСертификатаПриИзменении(Элемент)

	// Если пользователь сначала изменил владельца ЭЦП, а потом решил отказаться от этого, то возвращаем все значения по владельцу в исходное состояние
	Если ИзменитьВладельцаСертификата Тогда
		Если НЕ ЗначениеЗаполнено(ВладелецЭЦП) Тогда
			ВладелецЭЦПИзменился = Истина;
		КонецЕсли;
	Иначе
		ВладелецЭЦПИзменился = Ложь;
		ОпределитьВладельцаЭЦП();
	КонецЕсли;
	
	// После изменения владельца ЭЦП выполняем сравнение старых и новый значений
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	// Оформляем текст под галкой
	ОформитьПодсказкуДляГалкиИзменитьВладельцаСертификата();
	
	// Определяем, нужно ли перездавать сертификат 
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ИзменитьСоставКонтролирующихОргановПриИзменении(Элемент)
	
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВРПНПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВРПНПриИзмененииЗавершение", ЭтотОбъект);
	
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВРПН", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РПН"), СдаватьВРПНИсходный, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФТСПриИзменении(Элемент)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СдаватьВФТСПриИзмененииЗавершение", ЭтотОбъект);
	
	НаправлениеСдачиОтчетностиПодключено(
		"СдаватьВФТС", ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФТС"), СдаватьВФТСИсходный, ОписаниеОповещения);
	
КонецПроцедуры


&НаКлиенте
Процедура ПолучатьУведомленияРасширеннаяПодсказкаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ПолучатьУведомленияРасширеннаяПодсказкаОбработкаНавигационнойСсылкиПослеВводаТелефона", ЭтотОбъект);
	ПоказатьВводЗначения(Оповещение, ТелефонМобильный, "Телефон для SMS-уведомлений", Новый ОписаниеТипов("Строка",,,, Новый КвалификаторыСтроки(20)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучатьУведомленияРасширеннаяПодсказкаОбработкаНавигационнойСсылкиПослеВводаТелефона(Результат, ВходящийКонтекст) Экспорт
	
	Телефон = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(Результат);
	Если СтрДлина(Телефон) = 11 Тогда
		ТелефонМобильный = СтрШаблон("+7 %1 %2-%3-%4", Сред(Телефон, 2, 3), Сред(Телефон, 5, 3), Сред(Телефон, 8, 2), Сред(Телефон, 10, 2));
		ОбновитьТекстПодсказки();	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияТелефонПриИзменении(Элемент)
	
	Если СтрДлина(СокрЛП(КодПодтверждения)) = 6 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения", 0.5, Истина); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияТелефонАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)

	Если СтрДлина(СокрЛП(Текст)) = 6 Тогда
		КодПодтверждения = СокрЛП(Текст);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения", 0.5, Истина); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияЭлектроннаяПочтаПриИзменении(Элемент)
	
	Если СтрДлина(СокрЛП(КодПодтверждения)) = 6 Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения", 0.5, Истина); 
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КодПодтвержденияЭлектроннаяПочтаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Если СтрДлина(СокрЛП(Текст)) = 6 Тогда
		КодПодтверждения = СокрЛП(Текст);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьКодПодтверждения", 0.5, Истина); 
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТелефонДляПаролейИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Телефон = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(Текст);
	
	ТелефонИзмененИсходный = ТелефонИзменен;
	ТелефонИзменен = СтрДлина(Телефон) = 11;
	Если ТелефонИзменен <> ТелефонИзмененИсходный Тогда
		ТелефонДляПаролей = Текст;
		ТелефонМобильный = Текст;
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПояснениеПодтвержденияИзмененияТелефонаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "#Инструкция" Тогда
		
		ПараметрыФормы = Новый Структура("Сертификат,НовыйТелефон", Сертификат, ТелефонДляПаролей);
		ОткрытьФорму("ОбщаяФорма.ИзменениеНомераТелефона", ПараметрыФормы, Неопределено);
		ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьФормуСЗадержкой", 0.1, Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектроннаяПочтаДляПаролейИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	ЭлектроннаяПочтаДляПаролей = Текст;
	
	ОтключитьОбработчикОжидания("Подключаемый_ПоказатьКнопкуПроверкиАдреса");
	ПодключитьОбработчикОжидания("Подключаемый_ПоказатьКнопкуПроверкиАдреса", 1.5, Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

&НаКлиенте
Процедура ТаблицаДанныхЗаявленияНаПодключениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если НЕ Элемент.ТекущиеДанные.РеквизитРедактируется Тогда
		Возврат;
	КонецЕсли;
	
	ТекущийРеквизит 		= Элемент.ТекущиеДанные.ИзмененныйРеквизит;
	НаименованиеРеквизита 	= Строка(ТекущийРеквизит);
	
	// Если это реквизит, который отсутсвует у организации, то он должен редактироваться напрямую в таблице.
	Если Элемент.ТекущиеДанные.ЭтоРеквизитНеХранящийсяВБазе Тогда
		
		Маска = Неопределено;
		Если ТекущийРеквизит = ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС") Тогда
			Маска = "999-999-999 99";
		ИначеЕсли ТекущийРеквизит = ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПКодПодразделения") Тогда
			Маска = "999-999";
		КонецЕсли; 
		
		ИмяРеквизита = ИмяПеречисленияПараметрыПодключенияК1СОтчетности(ТекущийРеквизит);
		ЗадатьНовоеЗначениеРеквизиту(ИмяРеквизита, НаименованиеРеквизита, ЭтаФорма[ИмяРеквизита], Маска);
	ИначеЕсли Элемент.ТекущиеДанные.СодержитОшибку Тогда // Это реквизит Владельца ЭП
		 ПоказатьЗначение(, ВладелецЭЦП);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура НапечататьЗаявление(Команда)
	
	СоздатьНовыйДокументЗаявление(3);
	
	ЗаявлениеПолучатель = КонтекстЭДОКлиент.ПолучитьПараметрСпецоператораКлиент(Спецоператор, Новый Структура("ЗаявлениеПолучатель"),
		СтруктураДанныхСпецоператорыСвязи.Макет).ЗаявлениеПолучатель;
	
	ПараметрыПечати = Новый Структура();
	ПараметрыПечати.Вставить("ЗаявлениеПолучатель", 				ЗаявлениеПолучатель);
	ПараметрыПечати.Вставить("ПереиздатьСертификат", 				ИзменилисьРеквизитыТребующиеПереизданияСертификата());
	ПараметрыПечати.Вставить("ИзменитьСоставКонтролирующихОрганов", ИзменитьСоставКонтролирующихОрганов);
	ПараметрыПечати.Вставить("ПродлитьЛицензиюНа1СОтчетность", 		ПродлитьЛицензиюНа1СОтчетность);
	
	МакетДляПечати = КонтекстЭДОКлиент.ПодготовитьМакет(ДокументЗаявление, ПараметрыПечати, "ПАРАМЕТРЫ_ПЕЧАТИ_ПечатьЗаявкиНаИзменениеРеквизитов", 
		"ПечатьЗаявкаНаИзменениеРеквизитов");
		
	КонтекстЭДОКлиент.НапечататьДокумент(МакетДляПечати, "Заявление на изменение реквизитов подключения к 1С-Отчетности");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьМастер(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельМастерДалее(Команда)
	
	ТекущаяСтраница = Элементы.ОсновнаяПанель.ТекущаяСтраница;
	ОчиститьСообщения();
	МастерДалее = Истина;
	
	Если ТекущаяСтраница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.ВыборДействия Тогда
		
		// проверка организации
		Если НЕ ЗначениеЗаполнено(Организация) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Организация""'"), ,"Организация");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// проверка кодов
		Если ИзменитьСоставКонтролирующихОрганов Тогда
			
			// коды ФНС
			Если СдаватьВФНС И ПолучателиФНС.Количество()=0 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните коды ФНС'"), ,"СдаватьВФНС");
				МастерДалее = Ложь;
			КонецЕсли;
			
			// код отделения ПФР
			Если СдаватьВПФР Тогда
				КодПФРВОрганизации = КодПФР(ДанныеОрганизации);
				Если ПустаяСтрока(СтрЗаменить(КодПФР,"-","")) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Код отделения ПФР""'"), ,"СдаватьВПФР");
					МастерДалее = Ложь;
				ИначеЕсли СтрДлина(СокрЛП(КодПФР))<> 7 Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Код отделения ПФР должен состоять из 6 цифр'"), ,"СдаватьВПФР");
					МастерДалее = Ложь;
				ИначеЕсли СокрЛП(КодПФР) <> КодПФРВОрганизации Тогда
					ТекстСообщения = НСтр("ru = 'Код отделения ПФР не совпадает с кодом, указанным в организации (%1)'");
					Если ПустаяСтрока(СтрЗаменить(КодПФРВОрганизации, "-", "")) Тогда
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "(%1)", "");
					Иначе
						ТекстСообщения = СтрЗаменить(ТекстСообщения, "(%1)", "(" + КодПФРВОрганизации + ")");
					КонецЕсли;
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ,"СдаватьВПФР");
					МастерДалее = Ложь;
				КонецЕсли;
			КонецЕсли;
			
			// код органа Росстата
			Если СдаватьВРосстат Тогда
				
				Если ПолучателиФСГС.Количество() = 0 Тогда
				
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните коды органа Росстата'"), ,"СдаватьВРосстат");
					МастерДалее = Ложь;
					
				ИначеЕсли ПолучателиФСГС.Количество() > 0 Тогда
					
					Для каждого Получатель Из ПолучателиФСГС Цикл
						
						Если ПустаяСтрока(СтрЗаменить(Получатель.КодПолучателя,"-","")) Тогда
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Код органа Росстата""'"), ,"СдаватьВРосстат");
							МастерДалее = Ложь;
						ИначеЕсли СтрДлина(СокрЛП(Получатель.КодПолучателя))<> 5 Тогда
							ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Код органа Росстата должен состоять из 4 цифр'"), ,"СдаватьВРосстат");
							МастерДалее = Ложь;
						ИначеЕсли КонтекстЭДОКлиент.НайденыЗапрещенныеСимволы(Получатель.КодПолучателя, НСтр("ru = 'Код органа Росстата'"), "СдаватьВРосстат") Тогда
							МастерДалее = Ложь;
						КонецЕсли;
					
					КонецЦикла;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		// проверка сочетания выбранных контролирующих органов
		Если ИзменитьСоставКонтролирующихОрганов Тогда
			Если НЕ (СдаватьВФНС ИЛИ СдаватьВПФР ИЛИ СдаватьВФСС ИЛИ СдаватьВРосстат ИЛИ СдаватьВФСРАР ИЛИ СдаватьВРПН ИЛИ СдаватьВФТС) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите хотя бы один контролирующий орган, в который будет сдаваться отчетность'")
					, ,"ИзменитьСоставКонтролирующихОрганов");
				МастерДалее = Ложь;
			ИначеЕсли НЕ СдаватьВФНС И НЕ СдаватьВПФР Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					НСтр("ru = 'Среди контролирующих органов, в которые будет сдаваться отчетность, должены быть ФНС или ПФР'")
					, ,"ИзменитьСоставКонтролирующихОрганов");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Владелец ЭЦП
		
		// проверка заполненности сведений о руководителе
		Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) 
			И ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель")
			И НЕ ЗначениеЗаполнено(Руководитель) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите руководителя'"), ,"ДекорацияРуководитель");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// проверка заполненности сведений о бухгалтере
		Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) 
			И ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер")
			И НЕ ЗначениеЗаполнено(ГлБухгалтер) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите бухгалтера'"), ,"ДекорацияГлБухгалтер");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// проверка заполненности сведений о сотруднике
		Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) 
			И ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник")
			И НЕ ЗначениеЗаполнено(СотрудникВыбор) Тогда 
			ТекстПредупреждения = НСтр("ru = 'Переиздание сертификата требует обязательного указания сотрудника-владельца сертификата.
											|Установите флажок ""Изменение сотрудника-владельца сертификата"" и выберите владельца сертификата'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			МастерДалее = Ложь;
		КонецЕсли;
		
		// проверка заполненности Владельца ЭЦП
		Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) 
			И ВладелецЭЦП = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка") Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Выберите сотрудника-владельца сертификата'"), ,"СотрудникВыбор");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// если ни один флажек не отмечен - сообщаем об этом пользователю
		Если НЕ ПереиздатьСертификат 
			И НЕ ИзменитьВладельцаСертификата 
			И НЕ ПродлитьЛицензиюНа1СОтчетность 
			И НЕ ИзменитьСоставКонтролирующихОрганов
			И НЕ ИзменитьРеквизитыПодключенияК1СОтчетности 
			И НЕ ПродлитьСертификат 
			И НЕ ИзменитьМобильныйТелефон Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Вы не выбрали никакого действия'"), ,"");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// проверка мобильного телефона
		Если ИзменитьМобильныйТелефон Тогда
			Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
				ПолучатьУведомленияИзменен = 
					ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйЗаполнен(ТелефонМобильныйИсходный)
					И Не ПолучатьУведомления;
				
				Если Не ТелефонИзменен И Не ЭлектроннаяПочтаИзменена И НЕ ПолучатьУведомленияИзменен Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите номер мобильного телефона'"),, "ТелефонДляПаролей");
					МастерДалее = Ложь;
				ИначеЕсли ТелефонИзменен И Не ТелефонПодтвержден Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Подтвердите номер мобильного телефона'"),, "ТелефонДляПаролей");
					МастерДалее = Ложь;
				ИначеЕсли ЭлектроннаяПочтаИзменена И Не ЭлектроннаяПочтаПодтверждена И ЗначениеЗаполнено(ЭлектроннаяПочтаДляПаролей) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Подтвердите адрес электронной почты'"),, "ЭлектроннаяПочтаДляПаролей");
					МастерДалее = Ложь;
				КонецЕсли;
			Иначе
				Если ПолучатьУведомления Тогда
					ТелефонМобильныйБезРазделителей = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонМобильный);
					Если ПустаяСтрока(ТелефонМобильныйБезРазделителей) Тогда
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Укажите номер мобильного телефона'"),, "ТелефонМобильный");
						МастерДалее = Ложь;
					ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ТелефонМобильныйБезРазделителей, 11, Истина) Тогда 
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Мобильный телефон должен иметь формат 
							|+7 XXX XXX-XX-XX'"), ,"ТелефонМобильный");
						МастерДалее = Ложь;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если МастерДалее Тогда
			
			// Телефон мобильный
			Если ОператорПоддерживаетСМСУведомление Тогда
				ТелефонМобильныйБезРазделителей = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонМобильный);
				ТелефонМобильныйИзменился = ТелефонМобильныйБезРазделителей <> ТелефонМобильныйИсходный;
			КонецЕсли;

			ПроверитьИзменилисьЛиРеквизитыПодключенияК1СОтчетности();
			СравнитьНаправленияИКодыСдачиОтчетностиСИсходными();
			
			ЗаполнитьПараметрыЭДО();
			Если НЕ ОтображатьПодключениеЭДО Тогда
				СформироватьТаблицуДляПодтвержденияДанных();
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.ПодключениеЭДО Тогда
		
		// подключение ЭДО
		Если ПодключитьЭДО Тогда
			
			// оператор ЭДО
			Если НЕ ЗначениеЗаполнено(ОператорЭДО) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Оператор ЭДО""'"), ,"ОператорЭДО");
				МастерДалее = Ложь;
			КонецЕсли;
			
			// код ФНС
			Если ПустаяСтрока(КодНалоговогоОрганаЭДО) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Код ФНС""'"), ,"КодНалоговогоОрганаЭДО");
				МастерДалее = Ложь;
			ИначеЕсли СтрДлина(СокрЛП(КодНалоговогоОрганаЭДО))<> 4 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Код ФНС должен состоять из 4 цифр'"), ,"КодНалоговогоОрганаЭДО");
				МастерДалее = Ложь;
			КонецЕсли;
			
			// юр.адрес
			Если ПустаяСтрока(СтрЗаменить(АдрЮР,",","")) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните юридический адрес'"), ,"АдресЮридический");
				МастерДалее = Ложь;
			Иначе	
				СтрокаОшибкаАдреса = КонтекстЭДОКлиент.ПроверитьАдрес(АдрЮР);
				Если НЕ ПустаяСтрока(СтрокаОшибкаАдреса) Тогда
					ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Юридический адрес: %1'"),СтрокаОшибкаАдреса), ,"АдресЮридический");
					МастерДалее = Ложь;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
		Если МастерДалее Тогда
			СформироватьТаблицуДляПодтвержденияДанных();
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.ЗаполнениеЗаявления Тогда
		
		ПроверитьЗаполнениеЗаявления(МастерДалее);
		Возврат;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.ИнструкцияПоСозданиюКлючаЭЦП Тогда
		
		Если МастерДалее Тогда
			// создание заявления и его отправка
			СформироватьИОтправитьЗаявление();
			Возврат;
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.ПолучениеРезультатаОтправкиЗаявления Тогда
		
		Если ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено") Тогда
			
			ПараметрыОткрытияФормы = Новый Структура();
			ПараметрыОткрытияФормы.Вставить("Организация", Организация);
			ПараметрыОткрытияФормы.Вставить("Реквизит",ДокументЗаявление.Ссылка);
			ПараметрыОткрытияФормы.Вставить("ИзменитьРеквизитыПодключенияК1СОтчетности", ИзменитьРеквизитыПодключенияК1СОтчетности);
			ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.МастерФормированияЗаявкиНаИзменениеПараметровПодключения", ПараметрыОткрытияФормы);
			ПрограммноеЗакрытие = Истина;
			Закрыть();
			
		ИначеЕсли ДокументЗаявление.Статус =  ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Подготовлено") Тогда
			
			СформироватьИОтправитьЗаявление();
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
		
	Если МастерДалее Тогда
		ПоказатьСледующуюСтраницу();
	Иначе
		ЭтаФорма.Активизировать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоманднаяПанельМастерНазад(Команда)
	ПоказатьПредыдущуюСтраницу();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьИнструкциюПоСозданиюКлючаЭЦП(Команда)
	
	КонтекстЭДОКлиент.ОткрытьИнструкциюИнструкциюПоСозданиюКлючаЭЦП(ТипКриптопровайдера);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьДиректора(Команда)
	ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель");
	УстановитьНовогоВладельцаЭЦП();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьГлБухгалтера(Команда)
	ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер");
	УстановитьНовогоВладельцаЭЦП();
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьСотрудника(Команда)
	ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник");
	УстановитьНовогоВладельцаЭЦП();
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьНомер(Команда)

	КодОтправлен = ПроверитьНомерНаСервере();
	Если КодОтправлен Тогда
		ЗапуститьОбратныйОтсчет();
		ВыполняетсяПроверкаТелефона = Истина;
		
		УправлениеФормой(ЭтаФорма);
		
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьАктивнымЭлементомКодПодтвержденияТелефон", 0.1, Истина);
	Иначе
		Сообщить(СтрШаблон(НСтр("ru = 'Не удалось выполнить отправку SMS на номер %1. Возможно номер указан неверно.'"), ТелефонДляПаролей));
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьАдрес(Команда)
	
	КодОтправлен = ПроверитьАдресНаСервере();
	Если КодОтправлен Тогда
		ЗапуститьОбратныйОтсчет();
		ВыполняетсяПроверкаЭлектроннойПочты = Истина;
		
		УправлениеФормой(ЭтаФорма);
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьАктивнымЭлементомКодПодтвержденияЭлектроннаяПочта", 0.1, Истина);
		
	Иначе
		Сообщить(СтрШаблон(НСтр("ru = 'Не удалось выполнить отправку письма на адрес %1. Возможно адрес указан неверно.'"), ЭлектроннаяПочтаДляПаролей));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКодПовторноТелефон(Команда)
	
	КодПодтверждения = Неопределено;
	КодОтправлен = ПроверитьНомерНаСервере(Истина);
	Если КодОтправлен Тогда
		ЗапуститьОбратныйОтсчет();
		
		УправлениеФормой(ЭтаФорма);
		
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьАктивнымЭлементомКодПодтвержденияТелефон", 0.1, Истина);
	Иначе
		Сообщить(СтрШаблон(НСтр("ru = 'Не удалось выполнить отправку SMS на номер %1. Возможно номер указан неверно.'"), ТелефонДляПаролей));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьКодПовторноЭлектроннаяПочта(Команда)
	
	КодПодтверждения = Неопределено;
	КодОтправлен = ПроверитьАдресНаСервере(Истина);
	Если КодОтправлен Тогда
		ЗапуститьОбратныйОтсчет();
		
		УправлениеФормой(ЭтаФорма);
		
		ПодключитьОбработчикОжидания("Подключаемый_УстановитьАктивнымЭлементомКодПодтвержденияЭлектроннаяПочта", 0.1, Истина);
	Иначе
		Сообщить(СтрШаблон(НСтр("ru = 'Не удалось выполнить отправку письма на адрес %1. Возможно адрес указан неверно.'"), ЭлектроннаяПочтаДляПаролей));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроставитьПризнакОшибкиИДоступностиДляРедактированияКонтролируемыхРеквизитов()
	
	// НомерОсновнойПоставки1с
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.НомерОсновнойПоставки1С");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если НомерОсновнойПоставки1сИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		СодержитОшибку 			= ПустаяСтрока(НомерОсновнойПоставки1с);
		РеквизитРедактируется 	= ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// телефон организации
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ТелефонОсновнойИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		СодержитОшибку = СтрДлина(СокрЛП(ТелефонОсновной)) = 0;
		РеквизитРедактируется 	= ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ТелефонДополнительный
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		РеквизитРедактируется = ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ЭлектроннаяПочта
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьРеквизитыПодключенияК1СОтчетности И ЭлектроннаяПочтаИзменилась Тогда
		СодержитОшибку = НЕ КонтекстЭДОКлиент.ЭлектроннаяПочтаВведенаКорректно(
				ЭлектроннаяПочта,
				ПредопределенноеЗначение("Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение"));
		РеквизитРедактируется = ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ДополнительныйКодФСС
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ДополнительныйКодФСС");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если СдаватьВФСС И ИзменитьСоставКонтролирующихОрганов Тогда
		
		Если ПризнакОбособленногоПодразделения 
			И ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЭтоЮрЛицо(Организация) 
			И СдаватьВФСС 
			И ЕстьРеквизитДопКодФССУОрганизации() Тогда
			
			Если ПустаяСтрока(ДополнительныйКодФСС) Тогда 
				СодержитОшибку = Истина;
			ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ДополнительныйКодФСС, 10, Истина) Тогда
				СодержитОшибку = Истина;
			КонецЕсли;
			
		КонецЕсли;
		
		РеквизитРедактируется = ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
		
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);

	// ВладелецЭЦП
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦП");
	СодержитОшибку 			= Ложь;
	//РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = НЕ ЗначениеЗаполнено(ВладелецЭЦП);
		//РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// Должность. 
	// Позволяем редактировать должность из таблицы только если она не хранится в базе.
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДолжность");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит)
		И (ВладелецЭЦПДолжностьИзменилась И ИзменитьРеквизитыПодключенияК1СОтчетности 
		ИЛИ ПереиздатьСертификат ИЛИ ИзменитьВладельцаСертификата) Тогда
		
		Если ЭтоЮридическоеЛицо И ПустаяСтрока(ВладелецЭЦПДолжность) Тогда
			СодержитОшибку = Истина;
		КонецЕсли;
		РеквизитРедактируется = ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
		
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦППодразделение
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППодразделение");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ПереиздатьСертификат ИЛИ ИзменитьВладельцаСертификата Тогда
		РеквизитРедактируется 	= ЭтоРеквизитНеХранящийсяВБазе(ИзмененныйРеквизит);
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦПСНИЛС
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ВладелецЭЦПСНИЛСИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности 
		ИЛИ ПереиздатьСертификат ИЛИ ИзменитьВладельцаСертификата Тогда
		
		Если ПустаяСтрока(СтрЗаменить(ВладелецЭЦПСНИЛС, "-","")) Тогда
			СодержитОшибку = Истина;
		Иначе	
			Если НЕ КонтекстЭДОКлиент.ПроверитьСНИЛС(ВладелецЭЦПСНИЛС) Тогда
				СодержитОшибку = Истина;
			ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьСНИЛС(ВладелецЭЦПСНИЛС, Ложь, Истина) Тогда
				СодержитОшибку = Истина;
			КонецЕсли;
		КонецЕсли;
		
		РеквизитРедактируется 	= Истина;
		
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// Дата рождения
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДатаРождения");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = НЕ ЗначениеЗаполнено(ВладелецЭЦПДатаРождения);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// Пол
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППол");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = НЕ ЗначениеЗаполнено(ВладелецЭЦППол);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// Гражданство
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПГражданство");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат  Тогда
		СодержитОшибку = НЕ ЗначениеЗаполнено(ВладелецЭЦПГражданство)
			ИЛИ (НЕ ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.КодАльфа2Заполнен(ВладелецЭЦПГражданство)
			И ВладелецЭЦПГражданство <> ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтраныМира.Россия"));
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦПВидДокумента
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПВидДокумента");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = ПустаяСтрока(ВладелецЭЦПВидДокумента);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦПСерияДокумента
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСерияДокумента");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = ПустаяСтрока(ВладелецЭЦПСерияДокумента) И НЕ КонтекстЭДОКлиент.ПроверитьСериюДокумента(ВладелецЭЦПВидДокумента, ВладелецЭЦПСерияДокумента);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦПНомерДокумента
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПНомерДокумента");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = ПустаяСтрока(ВладелецЭЦПНомерДокумента);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦПКемВыданДокумент
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПКемВыданДокумент");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = ПустаяСтрока(ВладелецЭЦПКемВыданДокумент);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);
	
	// ВладелецЭЦПДатаВыдачиДокумента
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДатаВыдачиДокумента");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		Если НЕ ЗначениеЗаполнено(ВладелецЭЦПДатаВыдачиДокумента) Тогда
			СодержитОшибку = Истина;
		ИначеЕсли ВладелецЭЦПДатаВыдачиДокумента > ТекущаяДатаСервер ИЛИ Год(ВладелецЭЦПДатаВыдачиДокумента) < 1900 Тогда	
			СодержитОшибку = Истина;
		КонецЕсли;
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);

	// Код подразделения. Только для паспорта РФ.
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПКодПодразделения");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		
		Если ВладелецЭЦПВидДокумента = ПаспортРФ Тогда
			КодПодразделенияБезТире = СтрЗаменить(ВладелецЭЦПКодПодразделения, "-","");
			Если ПустаяСтрока(КодПодразделенияБезТире) Тогда
				СодержитОшибку = Истина;
			ИначеЕсли СтрДлина(ВладелецЭЦПКодПодразделения) <> 7 
				ИЛИ НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(КодПодразделенияБезТире, 6, Истина) Тогда
				СодержитОшибку = Истина;
			КонецЕсли;
		КонецЕсли;
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);

	// ВладелецЭЦПМестоРождения
	ИзмененныйРеквизит		= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПМестоРождения");
	СодержитОшибку 			= Ложь;
	РеквизитРедактируется 	= Ложь;
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		СодержитОшибку = ПустаяСтрока(ВладелецЭЦПМестоРождения);
		РеквизитРедактируется = Истина;
	КонецЕсли;
	ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку, РеквизитРедактируется);	

КонецПроцедуры

&НаКлиенте
Функция ЭтоРеквизитНеХранящийсяВБазе(ПроверяемыйРеквизит)

	Возврат РеквизитыНеХранящиесяВБазе.Найти(ПроверяемыйРеквизит) <> Неопределено;

КонецФункции

&НаКлиенте
Процедура ОтметитьНеХранящиесяРеквизиты()
	
	Для Каждого СтрокаТаблицы Из ТаблицаДанныхЗаявленияНаПодключение Цикл
		
		Если ЭтоРеквизитНеХранящийсяВБазе(СтрокаТаблицы.ИзмененныйРеквизит) Тогда
			СтрокаТаблицы.ЭтоРеквизитНеХранящийсяВБазе = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроставитьПризнакОшибкиИДоступностиДляРедактирования(ИзмененныйРеквизит, СодержитОшибку = Ложь, РеквизитРедактируется = Ложь)
	
	Отбор = Новый Структура;
	Отбор.Вставить("ИзмененныйРеквизит", ИзмененныйРеквизит);
	
	СтрокиТаблицы = ТаблицаДанныхЗаявленияНаПодключение.НайтиСтроки(Отбор);
	
	Для каждого СтрокаТаблицы Из СтрокиТаблицы Цикл
		СтрокаТаблицы.СодержитОшибку 		= СодержитОшибку;
		СтрокаТаблицы.РеквизитРедактируется = РеквизитРедактируется;
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьЗаполнениеЗаявления(МастерДалее)
	
	// проверка регистрационного номер
	Если НомерОсновнойПоставки1сИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		Если ПустаяСтрока(НомерОсновнойПоставки1с)  Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните регистрационный номер программы'"), ,"НомерОсновнойПоставки1с");
			МастерДалее = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// полное наименование
	Если ПолноеНаименованиеИзменилось И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		Если ПустаяСтрока(ПолноеНаименование) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните полное наименование организации'"), ,"ДекорацияПолноеНаименование");
			МастерДалее = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЮридическоеЛицо Тогда
		
		// ИНН
		Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
			Если ПустаяСтрока(ИНН) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните ИНН'"), ,"ДекорацияИНН");
				МастерДалее = Ложь;
			ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ИНН,10) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'ИНН должен состоять из 10 цифр'"), ,"ДекорацияИНН");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// КПП
		Если КППИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
			Если ПустаяСтрока(КПП) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните КПП'"), ,"ДекорацияКПП");
				МастерДалее = Ложь;
			ИначеЕсли НЕ (КонтекстЭДОКлиент.ПроверитьКПП(КПП)) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'КПП должен состоять из 9 цифр'"), ,"ДекорацияКПП");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// ОГРН
		Если ОГРНИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
			Если ПустаяСтрока(ОГРН) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните ОГРН'"), ,"ОГРН");
				МастерДалее = Ложь;
			ИначеЕсли НЕ (КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ОГРН,13)) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'ОГРН должен состоять из 13 цифр'"), ,"ОГРН");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		// ИНН
		Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
			Если ПустаяСтрока(ИНН) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните ИНН'"), ,"ДекорацияИНН");
				МастерДалее = Ложь;
			ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ИНН,12) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'ИНН должен состоять из 12 цифр'"), ,"ДекорацияИНН");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// ОГРН
		Если ОГРНИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
			Если ПустаяСтрока(ОГРН) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните ОГРН'"), ,"ОГРН");
				МастерДалее = Ложь;
			ИначеЕсли НЕ (КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ОГРН,15, Истина)) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'ОГРНИП должен состоять из 15 цифр'"), ,"ОГРН");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// регистрационный номер в ПФР
	Если СдаватьВПФР И ИзменитьСоставКонтролирующихОрганов Тогда
		Если ПустаяСтрока(СтрЗаменить(РегНомерПФР,"-","")) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните регистрационный номер в ПФР'"), ,"ДекорацияРегНомерПФР");
			МастерДалее = Ложь;
		ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьРегистрационныйНомерПФР(РегНомерПФР, Истина) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Регистрационный номер в ПФР должен состоять из 12 цифр (ХХХ-ХХХ-ХХХХХХ)'"), ,"ДекорацияРегНомерПФР");
			МастерДалее = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если СдаватьВФСС И ИзменитьСоставКонтролирующихОрганов Тогда
		
		// регистрационный номер в ФСС
		Если ПустаяСтрока(РегНомерФСС) Тогда 
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните регистрационный номер в ФСС'"), ,"ДекорацияРегНомерФСС");
			МастерДалее = Ложь;
		ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(РегНомерФСС, 10, Истина) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Регистрационный номер в ФСС должен состоять из 10 цифр'"), ,"ДекорацияРегНомерФСС");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// дополнительный код ФСС
		Если ПризнакОбособленногоПодразделения и ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЭтоЮрЛицо(Организация) и СдаватьВФСС И ЕстьРеквизитДопКодФССУОрганизации() Тогда
			Если ПустаяСтрока(ДополнительныйКодФСС) Тогда 
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните дополнительный код ФСС у организации'"), ,"ДополнительныйКодФСС");
				МастерДалее = Ложь;
			ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(ДополнительныйКодФСС, 10, Истина) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дополнительный код ФСС должен состоять из 10 цифр'"), ,"ДополнительныйКодФСС");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// юридический адрес
	Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		Если ПустаяСтрока(СтрЗаменить(АдрЮР,",","")) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните юридический адрес'"), ,"АдресЮридический");
			МастерДалее = Ложь;
		Иначе	
			СтрокаОшибкаАдреса = КонтекстЭДОКлиент.ПроверитьАдрес(АдрЮР);
			Если НЕ ПустаяСтрока(СтрокаОшибкаАдреса) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Юридический адрес: %1'"),СтрокаОшибкаАдреса), ,"АдресЮридический");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	// телефон организации
	Если ТелефонОсновнойИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		Если СтрДлина(СокрЛП(ТелефонОсновной)) = 0 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните телефон организации'"), ,"ТелефонОсновной");
			МастерДалее = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// электронная почта (теперь обязательна)
	Если ИзменитьРеквизитыПодключенияК1СОтчетности И ЭлектроннаяПочтаИзменилась Тогда
		Если НЕ КонтекстЭДОКлиент.ЭлектроннаяПочтаВведенаКорректно(
				ЭлектроннаяПочта,
				ПредопределенноеЗначение("Перечисление.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение")) Тогда
			МастерДалее = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если СдаватьВФСРАР И ИзменитьСоставКонтролирующихОрганов Тогда
		
		Если НЕ ЗначениеЗаполнено(КодРегионаФСРАР) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните регион в адресе организации'"), ,"КодРегионаФСРАР");
			МастерДалее = Ложь;
		КонецЕсли;
		
		Если МастерДалее = Истина Тогда
			
			РегионЮрАдреса = РегламентированнаяОтчетностьКлиентСервер.РазложитьАдрес(АдрЮр).Регион;
			
			Если КодРегионаФСРАР <> РегионЮрАдреса И ЗначениеЗаполнено(РегионЮрАдреса) Тогда
				
				НаименованиеРегионаЮрАдреса = НаименованиеСубъектаРФ(Число(РегионЮрАдреса));
				
				КодРегионаСНаименованием = Формат(Число(РегионЮрАдреса), "ЧН=0; ЧГ=; ЧЦ=2; ЧВН=;") + 
					?(ЗначениеЗаполнено(НаименованиеРегионаЮрАдреса), " - ", "") + НаименованиеРегионаЮрАдреса;
					
				ДополнительныеПараметры = Новый Структура("МастерДалее", МастерДалее);
				
				ОписаниеОповещения = Новый ОписаниеОповещения(
					"ЗавершениеПодтвержденияОтличающегосяКодаФСРАР", 
					ЭтотОбъект, 
					ДополнительныеПараметры);
					
				ТекстВопроса = "Регион заявки для Росалкогольрегулирования отличается от указанного в юридическом адресе (""" + 
					КодРегионаСНаименованием + 
					"""). Продолжить?";
				
				ПоказатьВопрос(
					ОписаниеОповещения, 
					ТекстВопроса, 
					РежимДиалогаВопрос.ДаНет, 
					0, 
					КодВозвратаДиалога.Да);
				
			Иначе
				ПроверитьКодРегионаФСРАР(МастерДалее);
			КонецЕсли;
		Иначе
			ПроверитьДанныеВладельцаЭП(МастерДалее);
		КонецЕсли;
	Иначе
		ПроверитьДанныеВладельцаЭП(МастерДалее);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодтвержденияОтличающегосяКодаФСРАР(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	МастерДалее = РезультатВопроса = КодВозвратаДиалога.Да;
	ПроверитьКодРегионаФСРАР(МастерДалее);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКодРегионаФСРАР(МастерДалее)
	
	Если МастерДалее И ЗначениеЗаполнено(КодРегионаФСРАР) Тогда
		
		ИнформацияОРегионе = ИнформацияОРегионеФСРАРНаСервере(КодРегионаФСРАР);
		
		СхемаСдачиОтчетности = ПредопределенноеЗначение("Перечисление.СхемыСдачиОтчетностиФСРАР.СдачаВручную");
		Если ИнформацияОРегионе <> Неопределено Тогда
			СхемаСдачиОтчетности = КонтекстЭДОКлиент.СхемаСдачиОтчетностиФСРАР(Ложь, ИнформацияОРегионе.ТипПортала, КодРегионаФСРАР);
		КонецЕсли;
		
		Если СхемаСдачиОтчетности = ПредопределенноеЗначение("Перечисление.СхемыСдачиОтчетностиФСРАР.СдачаВручную") Тогда
			
			ТекстВопроса = 
			"Для выбранного региона поддерживается функция формирования пакета, содержащего подписанную отчетность, 
			|для последующей загрузки на соответствующий портал в интернете.
			|
			|Обратите внимание, функция отправки отчетности непосредственно из программы (кнопка ""Отправить"") 
			|в текущей версии не поддерживается. Продолжить?";
			
			ДополнительныеПараметры = Новый Структура("МастерДалее", МастерДалее);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ЗавершениеПодтвержденияВыбораРегионаФСРАР", 
				ЭтотОбъект, 
				ДополнительныеПараметры);
				
			ПоказатьВопрос(
				ОписаниеОповещения, 
				ТекстВопроса, 
				РежимДиалогаВопрос.ДаНет, 
				0, 
				КодВозвратаДиалога.Да);
			
		Иначе
			ПроверитьДанныеВладельцаЭП(МастерДалее);
		КонецЕсли;
	Иначе
		ПроверитьДанныеВладельцаЭП(МастерДалее);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеПодтвержденияВыбораРегионаФСРАР(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	МастерДалее = ДополнительныеПараметры.МастерДалее;
	Ответ = РезультатВопроса;
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		МастерДалее = Ложь;
		ПроверитьДанныеВладельцаЭП(МастерДалее);
	Иначе
		ПроверитьДанныеВладельцаЭП(МастерДалее);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте

&НаКлиенте
Процедура ПроверитьДанныеВладельцаЭП(МастерДалее)
	
	// фамилия
	Если ПустаяСтрока(ВладелецЭЦПФамилия) И (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните фамилию сотрудника-владельца ЭП'"), ,"ВладелецЭЦПФамилия");
		МастерДалее = Ложь;
	КонецЕсли;
	
	// имя
	Если ПустаяСтрока(ВладелецЭЦПИмя) И (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните имя сотрудника-владельца ЭП'"), ,"ВладелецЭЦПИмя");
		МастерДалее = Ложь;
	КонецЕсли;	
	
	// СНИЛС
	Если ВладелецЭЦПСНИЛСИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности 
		ИЛИ ПереиздатьСертификат ИЛИ ИзменитьВладельцаСертификата Тогда
		
		Если ПустаяСтрока(СтрЗаменить(ВладелецЭЦПСНИЛС, "-","")) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните СНИЛС сотрудника-владельца ЭП'"), ,"ВладелецЭЦПСНИЛС");
			МастерДалее = Ложь;
		Иначе	
			Если НЕ КонтекстЭДОКлиент.ПроверитьСНИЛС(ВладелецЭЦПСНИЛС) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно указан СНИЛС сотрудника. Не соответствует маске ХХХ-ХХХ-ХХХ ХХ, где X - любая цифра'"), ,"ВладелецЭЦПСНИЛС");
				МастерДалее = Ложь;
			ИначеЕсли НЕ КонтекстЭДОКлиент.ПроверитьСНИЛС(ВладелецЭЦПСНИЛС, Ложь, Истина) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно указан СНИЛС сотрудника. Не сошлось контрольное число (СНИЛС не существует)'"), ,"ВладелецЭЦПСНИЛС");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// Дата рождения
	Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) И НЕ ЗначениеЗаполнено(ВладелецЭЦПДатаРождения) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните дату рождения сотрудника-владельца ЭП'"), ,"ВладелецЭЦПДатаРождения");
		МастерДалее = Ложь;
	КонецЕсли;
	
	// Пол
	Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) И НЕ ЗначениеЗаполнено(ВладелецЭЦППол) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните пол сотрудника-владельца ЭП'"), ,"ВладелецЭЦППол");
		МастерДалее = Ложь;
	КонецЕсли;	
	
	// должность
	Если ВладелецЭЦПДолжностьИзменилась И ИзменитьРеквизитыПодключенияК1СОтчетности 
		ИЛИ ПереиздатьСертификат ИЛИ ИзменитьВладельцаСертификата Тогда
		Если ЭтоЮридическоеЛицо И ПустаяСтрока(ВладелецЭЦПДолжность) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните должность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПДолжность");
			МастерДалее = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Гражданство
	Если (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) И НЕ ЗначениеЗаполнено(ВладелецЭЦПГражданство) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните гражданство сотрудника-владельца ЭП'"), ,"ВладелецЭЦПГражданство");
		МастерДалее = Ложь;
	ИначеЕсли (ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат) 
		И НЕ ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.КодАльфа2Заполнен(ВладелецЭЦПГражданство)
		И ВладелецЭЦПГражданство <> ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.СтраныМира.Россия") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните код альфа-2 у страны, указанной в качестве гражданства сотрудника-владельца ЭП'"), ,"ВладелецЭЦПГражданство");
		МастерДалее = Ложь;
	КонецЕсли;
	
	// документ, удостоверяющий личность
	Если ИзменитьВладельцаСертификата ИЛИ ПереиздатьСертификат Тогда
		
		// Вид документа.
		Если ПустаяСтрока(ВладелецЭЦПВидДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните документ, удостоверяющий личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПВидДокумента");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// серия
		Если ПустаяСтрока(ВладелецЭЦПСерияДокумента) И НЕ КонтекстЭДОКлиент.ПроверитьСериюДокумента(ВладелецЭЦПВидДокумента, ВладелецЭЦПСерияДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните серию документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПСерияДокумента");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// номер
		Если ПустаяСтрока(ВладелецЭЦПНомерДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните номер документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПНомерДокумента");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// дата выдачи
		Если НЕ ЗначениеЗаполнено(ВладелецЭЦПДатаВыдачиДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните дату выдачи документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПДатаВыдачиДокумента");
			МастерДалее = Ложь;
		ИначеЕсли ВладелецЭЦПДатаВыдачиДокумента > ТекущаяДатаСервер ИЛИ Год(ВладелецЭЦПДатаВыдачиДокумента) < 1900 Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно указана дата выдачи документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПДатаВыдачиДокумента");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// кем выдан
		Если ПустаяСтрока(ВладелецЭЦПКемВыданДокумент) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните поле ""Кем выдан"" документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПКемВыданДокумент");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// дата выдачи
		Если НЕ ЗначениеЗаполнено(ВладелецЭЦПДатаВыдачиДокумента) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните дату выдачи документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПДатаВыдачиДокумента");
			МастерДалее = Ложь;
		ИначеЕсли Год(ВладелецЭЦПДатаВыдачиДокумента) < 1900 Тогда	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно указана дата выдачи документа, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПДатаВыдачиДокумента");
			МастерДалее = Ложь;
		КонецЕсли;
		
		// Код подразделения. Только для паспорта РФ.
		Если ВладелецЭЦПВидДокумента = ПаспортРФ Тогда
			КодПодразделенияБезТире = СтрЗаменить(ВладелецЭЦПКодПодразделения, "-","");
			Если ПустаяСтрока(КодПодразделенияБезТире) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните код подразделения, выдавшего документ, удостоверяющего личность сотрудника-владельца ЭП'"), ,"ВладелецЭЦПКодПодразделения");
				МастерДалее = Ложь;
			ИначеЕсли СтрДлина(ВладелецЭЦПКодПодразделения) <> 7
				ИЛИ НЕ КонтекстЭДОКлиент.ПроверитьЦифровойКодЗаданнойДлины(КодПодразделенияБезТире, 6, Истина) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно указан код подразделения, выдавшего документ, удостоверяющего личность сотрудника-владельца ЭП. Не соответствует маске ХХХ-ХХХ, где X – любая цифра'"), ,"ВладелецЭЦПКодПодразделения");
				МастерДалее = Ложь;
			КонецЕсли;
		КонецЕсли;
		
		// Место рождения
		Если ПустаяСтрока(ВладелецЭЦПМестоРождения) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Заполните место рождения сотрудника-владельца ЭП'"), ,"ВладелецЭЦПМестоРождения");
			МастерДалее = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	// Проверяем, что пользователь отправляет не пустое заявление
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("ЭтотПараметрИзменился", Истина);
	ПараметрыОтбора.Вставить("ВыделятьСтрокуЖелтым", Истина);
	
	Если ТаблицаДанныхЗаявленияНаПодключение.НайтиСтроки(ПараметрыОтбора).Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не изменено ни одной настройки подключения к 1С-Отчетности'"), ,"");
		МастерДалее = Ложь;
	КонецЕсли;
	
	Если МастерДалее Тогда
		
		ОтключитьОбработчикОжидания("ОбработкаОжиданияОбновитьДанныеГлБухгалтера");
		ОтключитьОбработчикОжидания("ОбработкаОжиданияОбновитьДанныеРуководителя");
		
		// создание документа заявления
		СоздатьНовыйДокументЗаявление(2);
		ОповеститьОбИзменении(ДокументЗаявление.Ссылка);
		
		// Если переиздания сертификата не требуется, то пропускаем страницу ИнструкцияПоСозданиюКлючаЭЦП
		Если НЕ ПереиздатьСертификат ИЛИ ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
			СформироватьИОтправитьЗаявление();
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если МастерДалее Тогда
		ПоказатьСледующуюСтраницу();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИнформацияОРегионеФСРАРНаСервере(Знач КодРегиона)
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер.ИнформацияОРегионеФСРАР(КодРегиона);
	
КонецФункции

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Организации" Тогда
		
		Если Источник = Организация ИЛИ Параметр = Организация Тогда
			ДополнительныеПараметры = Новый Структура("ДанныеЗаполнения");
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаОповещенияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ОбновитьРеквизитыОрганизации(Истина, ОписаниеОповещения);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Запись_ФизическиеЛица"
		ИЛИ ИмяСобытия = "Запись_ВладельцаИлиБухгалтера" Тогда
		
		Если ИмяСобытия = "Запись_ВладельцаИлиБухгалтера" Тогда
			Руководитель	= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.Руководитель(Организация); 
			ГлБухгалтер		= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ГлБухгалтер(Организация);
		КонецЕсли;
		
		Если Параметр = Руководитель ИЛИ Источник = Руководитель Тогда
			ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель");
			УстановитьНовогоВладельцаЭЦП();
		ИначеЕсли Параметр = ГлБухгалтер ИЛИ Источник = ГлБухгалтер Тогда
			ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер");
			УстановитьНовогоВладельцаЭЦП();
		ИначеЕсли Параметр = СотрудникВыбор ИЛИ Источник = СотрудникВыбор Тогда
			ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник");
			УстановитьНовогоВладельцаЭЦП();
		КонецЕсли;
		
		// Обновляем данные на второй закладке только если мы находимся на этой закладке
		Если Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.ЗаполнениеЗаявления Тогда
			СформироватьТаблицуДляПодтвержденияДанных();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзменениеНастроекДокументооборота" Тогда
		Если КонтекстЭДОКлиент <> Неопределено Тогда
			СравнитьКриптопровайдера();
			СформироватьТаблицуДляПодтвержденияДанных();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ПроверитьЧтоМастерФормированияЗаявкиНаПодключениеИлиИзменениеПодключенияОткрыт" И Источник = Организация Тогда
		Параметр.ФормаМастераФормированияЗаявкиНаПодключениеИлиИзменениеПодключенияОткрыта = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещенияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	СравнитьНаправленияИКодыСдачиОтчетностиСИсходными();
	ОформитьЭлементыФормыНаПервомШаге(ДанныеОрганизации);
	
	//Если изменились реквизиты, то взводим галку ИзменитьРеквизитыПодключенияК1СОтчетности
	ПроверитьИзменилисьЛиРеквизитыПодключенияК1СОтчетности();
	Если ИзменилисьРеквизитыПодключенияК1СОтчетности Тогда
		ИзменитьРеквизитыПодключенияК1СОтчетности = Истина;
	КонеЦесли;
	
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
	// Обновляем данные на второй закладке только если мы находимся на этой закладке
	Если Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.ЗаполнениеЗаявления Тогда
		СформироватьТаблицуДляПодтвержденияДанных();
	КонецЕсли;
	
	ЭтаФорма.Активизировать();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
	// Проверка заполненнности регистра
	ТекстОшибокДляМастераПодключенияК1СОтчетности = "";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ДополнительныеПараметры = Новый Структура("УчетнаяЗапись", УчетнаяЗапись);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЕстьДанныеДляФормированияЗаявленияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		КонтекстЭДОКлиент.ЕстьДанныеДляФормированияВторичногоЗаявления(
			ОписаниеОповещения,
			УчетнаяЗапись,
			ТекстОшибокДляМастераПодключенияК1СОтчетности);
			
	Иначе
		ПриОткрытииИнициализация();
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьТекстПолей", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЕстьДанныеДляФормированияЗаявленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЕстьДанные = Результат.ЕстьДанные;
	ТекстОшибок = Результат.ТекстОшибок;
	УчетнаяЗапись = ДополнительныеПараметры.УчетнаяЗапись;
	
	Если ЕстьДанные Тогда
		ПриОткрытииИнициализация();
	Иначе
		ПоддерживаетсяВторичноеЗаявление = Ложь;
		ОформитьЭлементыФормыНаПервомШаге(ДанныеОрганизации);
		
		ПодключитьОбработчикОжидания("Подключаемый_ОткрытиеПредупрежденияОНевозможностиПолучитьНастройкиУчетнойЗаписи", 1, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОткрытиеПредупрежденияОНевозможностиПолучитьНастройкиУчетнойЗаписи()
	
	УправлениеКнопкамиНавигации();
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
	ПараметрыФормы.Вставить("ТекстОшибки", ТекстОшибок);
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ПредупреждениеОНевозможностиПолучитьНастройкиУчетнойЗаписи", ПараметрыФормы,,,,,);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииИнициализация()

	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииИнициализацияЗначенийЗавершение", ЭтотОбъект);
	
	ИнициализацияЗначений(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииИнициализацияЗначенийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Организация) Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииИнициализацияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		// Заполняем параметры организации
		ДанныеЗаполнения = Новый Структура();
		ЗаполнитьРеквизитыОрганизации(ДанныеЗаполнения,,ОписаниеОповещения);
		
	Иначе
		ПоддерживаетсяВторичноеЗаявление = Ложь;
		ОформитьЭлементыФормыНаПервомШаге(ДанныеОрганизации);
		УправлениеКнопкамиНавигации();
		ЭтаФорма.Активизировать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииИнициализацияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	УправлениеКнопкамиНавигации();
	
	ОбновитьТекстПодсказки();
	
	ЭтаФорма.Активизировать();
	
КонецПроцедуры
	
&НаКлиенте
Процедура СдаватьВРосстатПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВРосстат = Результат;
	
	// Если установлена галка СдаватьВРосстат, то автоматически ставим галку СдаватьВФНС
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		Если СдаватьВРосстат И НЕ СдаватьВФНС Тогда 
			СдаватьВФНС = Истина;
		КонецЕсли;
	КонецЕсли;
	
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВПФРПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВПФР = Результат;
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);

КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФНСПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВФНС = Результат;
	
	// Снимаем галку росстата, если снята галка ФНС
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		Если НЕ СдаватьВФНС Тогда 
			СдаватьВРосстат = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);

КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФССПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВФСС = Результат;
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	Если ПоддерживаетсяВторичноеЗаявление И РегНомерФССИзменился И СдаватьВФСС И ЗначениеЗаполнено(РегНомерФСС) Тогда
		ИзменитьРеквизитыПодключенияК1СОтчетности = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОтветПользователяНаПредложениеПереиздатьСертификат(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		// Продолжить
		ПродлитьСертификатПриИзмененииЗавершение();
	Иначе
		// Не продолжать
		ПродлитьСертификат = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьСертификатПриИзмененииЗавершение()
	
	// Оформляем текст под галкой
	ОформитьПодсказкуДляГалкиПродлитьСертификат();
	
	// Определяем, нужно ли перездавать сертификат 
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФСРАРПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВФСРАР = Результат;
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТребуетсяЛиПродлениеСертификата(ВходящийКонтекст)
	
	Если НЕ ЗначениеЗаполнено(ОтпечатокСертификата) Тогда
		Попытка 
			ОтпечатокСертификата = КонтекстЭДОКлиент.ПолучитьОтпечаток(УчетнаяЗапись, "Руководитель", Неопределено);
		Исключение
			ОтпечатокСертификата = Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьТребуетсяЛиПродлениеСертификатаПослеПолученияСвойствСертификата", ЭтотОбъект, ВходящийКонтекст);
	
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		
		СвойстваСертификата = КонтекстЭДОКлиент.ПолучитьСвойстваСертификатаПоОтпечаткуНаСервере(ОтпечатокСертификата, "MY");
		Результат = Новый Структура;
		Результат.Вставить("Выполнено", 			СвойстваСертификата <> Неопределено);
		Результат.Вставить("СвойстваСертификата", 	СвойстваСертификата);
		Результат.Вставить("СертификатНайден", 		Истина);
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
		
	Иначе
		КриптографияЭДКОКлиент.НайтиСертификатПоОтпечатку(ОписаниеОповещения, ОтпечатокСертификата, "MY");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТребуетсяЛиПродлениеСертификатаПослеПолученияСвойствСертификата(Результат, ВходящийКонтекст) ЭКспорт
	
	Если Результат.Выполнено Тогда
		
		Если Результат.СертификатНайден Тогда
		
			СвойстваСертификата = Результат.СвойстваСертификата;
			
			СертификатДействителенПо = СвойстваСертификата.ДействителенПо;
			СекундВОдномДне = 24 * 60 * 60;
			// Прибавляем один день, так как в день окончания сертификат еще действует.
			СертификатДействителенПо = СертификатДействителенПо + СекундВОдномДне;
			
			КоличествоДнейДоОкончанияСертификата = (НачалоДня(СертификатДействителенПо) - НачалоДня(ТекущаяДатаСервер))/СекундВОдномДне;
			
			ПродлитьСертификатИсходный = (КоличествоДнейДоОкончанияСертификата <= КоличествоДнейЗаКотороеНужноПредупреждатьОбИстеченииСрока);
		КонецЕсли;
		
	КонецЕсли;
	
	СертификатДоступен = ЗначениеЗаполнено(СертификатДействителенПо);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОпределитьИсходныеПараметрыПодключенияК1СОтчетностиПослеОпределенияСвойствСертификата", 
		ЭтотОбъект, 
		ВходящийКонтекст); 
		
	ВыполнитьОбработкуОповещения(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьТребуетсяЛиПродлениеЛицензии()
	
	СекундВОдномДне = 24 * 60 * 60;
	КоличествоДнейДоОкончанияЛицензии = (НачалоДня(ЛицензияДатаОкончания) - НачалоДня(ТекущаяДатаСервер))/СекундВОдномДне;
	ПродлитьЛицензиюНа1СОтчетностьИсходный = КоличествоДнейДоОкончанияЛицензии <= КоличествоДнейЗаКотороеНужноПредупреждатьОбИстеченииСрока;
	
КонецПроцедуры

&НаКлиенте
Процедура СравнитьТекущиеРеквизитыПодключенияСИсходными()
	
	// Изменились ли реквизиты подключения к 1С-Отчетности ?
	//
	НомерОсновнойПоставки1сИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(НомерОсновнойПоставки1с) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(НомерОсновнойПоставки1сИсходный);
	
	// Краткое наименование
	КраткоеНаименованиеИзменилось = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КраткоеНаименование) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КраткоеНаименованиеИсходное);
	
	// Полное наименование
	ПолноеНаименованиеИзменилось = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ПолноеНаименование) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ПолноеНаименованиеИсходное); 
	
	// КПП
	КППИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КПП) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КППИсходный);
	
	// ОГРН
	ОГРНИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ОГРН) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ОГРНИсходный); 
	
	// Регистрационный номер в ПФР
	РегНомерПФРИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(РегНомерПФР) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(РегНомерПФРИсходный)
		И (ИзменитьСоставКонтролирующихОрганов И СдаватьВПФР ИЛИ НЕ ИзменитьСоставКонтролирующихОрганов И СдаватьВПФРИсходный); 
	
	// Регистрационный номер в ФСС
	РегНомерФССИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(РегНомерФСС) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(РегНомерФССИсходный)
		И (ИзменитьСоставКонтролирующихОрганов И СдаватьВФСС ИЛИ НЕ ИзменитьСоставКонтролирующихОрганов И СдаватьВФССИсходный); 
	
	// Регион
	КодРегионаФСРАРИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КодРегионаФСРАР) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КодРегионаФСРАРИсходный) 
		И (ИзменитьСоставКонтролирующихОрганов И СдаватьВФСРАР ИЛИ НЕ ИзменитьСоставКонтролирующихОрганов И СдаватьВФСРАРИсходный);
	
	// Электронная почта
	ЭлектроннаяПочтаИзменилась = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ЭлектроннаяПочта) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ЭлектроннаяПочтаИсходная)
		ИЛИ НЕ ЗначениеЗаполнено(ЭлектроннаяПочта)
		ИЛИ НЕ ЗначениеЗаполнено(ЭлектроннаяПочтаИсходная);
	
	// Телефон основной
	ТелефонОсновнойИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ТелефонОсновной) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ТелефонОсновнойИсходный);
	
	// Телефон дополнительный
	ТелефонДополнительныйИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ТелефонДополнительный) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ТелефонДополнительныйИсходный);
	
	// Сведения о сотруднике - владельце электронной подписи
	
	// Подразделение
	ВладелецЭЦППодразделениеИзменилось = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦППодразделение) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦППодразделениеИсходное) 
		И ИзменитьВладельцаСертификата И ЗначениеЗаполнено(ВладелецЭЦП);

	// Должность
	ВладелецЭЦПДолжностьИзменилась = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПДолжность) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПДолжностьИсходная) 
		И ИзменитьВладельцаСертификата И ЗначениеЗаполнено(ВладелецЭЦП);
	
	// СНИЛС. Проверяем только для руководителя и бухгалтера.
	ИсходныйВладелецЭтоРуководительИлиБухгалтер = НЕ ИзменитьВладельцаСертификата 
		И (ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель")  
			ИЛИ ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер")); 
			
	НовыйВладелецЭтоРуководительИлиБухгалтер = ИзменитьВладельцаСертификата
		И (ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель") 
		ИЛИ ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер"));
	
	ВладелецЭЦПСНИЛСИзменился = 
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПСНИЛС) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПСНИЛСИсходный) 
		И ИзменитьВладельцаСертификата И ЗначениеЗаполнено(ВладелецЭЦП)
		И (ИсходныйВладелецЭтоРуководительИлиБухгалтер ИЛИ НовыйВладелецЭтоРуководительИлиБухгалтер); // это бухгалтер или директор
	
	Если Не ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		СравнитьКриптопровайдера();
	КонецЕсли;
	
	СформироватьТекстПредупреждения();
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТекстПредупреждения()
	
	ИзмененныеРеквизиты = "";
	
	// НомерОсновнойПоставки1С
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.НомерОсновнойПоставки1С"), 
		НомерОсновнойПоставки1сИзменился,
		ИзмененныеРеквизиты);
	
	// Краткое наименование
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КраткоеНаименование"), 
		КраткоеНаименованиеИзменилось,
		ИзмененныеРеквизиты);
	
	// Полное наименование
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПолноеНаименование"), 
		ПолноеНаименованиеИзменилось,
		ИзмененныеРеквизиты);
	
	// КПП
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КПП"), 
		КППИзменился,
		ИзмененныеРеквизиты);
	
	// ОГРН
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ОГРН"), 
		ОГРНИзменился,
		ИзмененныеРеквизиты);
	
	// Регистрационный номер в ПФР
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.РегНомерПФР"), 
		РегНомерПФРИзменился,
		ИзмененныеРеквизиты);
	
	// Регистрационный номер в ФСС
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.РегНомерФСС"), 
		РегНомерФССИзменился,
		ИзмененныеРеквизиты);
	
	// Регион
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРегионаФСРАР"), 
		КодРегионаФСРАРИзменился,
		ИзмененныеРеквизиты);
	
	// Электронная почта
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта"), 
		ЭлектроннаяПочтаИзменилась,
		ИзмененныеРеквизиты);
	
	// Телефон основной
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной"), 
		ТелефонОсновнойИзменился,
		ИзмененныеРеквизиты);
	
	// Телефон основной
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный"), 
		ТелефонДополнительныйИзменился,
		ИзмененныеРеквизиты);
	
	Если НЕ ПустаяСтрока(ИзмененныеРеквизиты) Тогда
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + НСтр("ru = ' организации'");
	КонецЕсли;
	
	// Сведения о сотруднике - владельце электронной подписи
	
	// Подразделение
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППодразделение"), 
		ВладелецЭЦППодразделениеИзменилось,
		ИзмененныеРеквизиты);

	// Должность
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДолжность"), 
		ВладелецЭЦПДолжностьИзменилась,
		ИзмененныеРеквизиты);
	
	// СНИЛС
	ДобавитьТекст(
		ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС"), 
		ВладелецЭЦПСНИЛСИзменился,
		ИзмененныеРеквизиты);
	
	Если ВладелецЭЦППодразделениеИзменилось ИЛИ ВладелецЭЦПДолжностьИзменилась ИЛИ ВладелецЭЦПСНИЛСИзменился Тогда
		ИзмененныеРеквизиты = ИзмененныеРеквизиты + НСтр("ru = ' сотрудника-владельца ЭП'");
	КонецЕсли;
	
	ЗаменитьЗапятуюНаИВСтроке(ИзмененныеРеквизиты);
		
КонецПроцедуры

&НаКлиенте
Процедура СравнитьНаправленияИКодыСдачиОтчетностиСИсходными()
	
	// ФНС
	СдаватьВФНСИзменился = СдаватьВФНС <> СдаватьВФНСИсходный;
	КодыФНСИзменились = КодыИзменились(ПолучателиФНС, ПолучателиФНСИсходные);
	
	// ПФР
	СдаватьВПФРИзменился = СдаватьВПФР <> СдаватьВПФРИсходный;
	КодПФРИзменился = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КодПФР) 
		<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(КодПФРИсходный);
	
	// ФСС
	СдаватьВФССИзменился = СдаватьВФСС <> СдаватьВФССИсходный;
	
	// Росстат
	СдаватьВРосстатИзменился = СдаватьВРосстат <> СдаватьВРосстатИсходный;
	КодыРосстатаИзменились = КодыИзменились(ПолучателиФСГС, ПолучателиФСГСИсходные);
	
	// ФСРАР
	СдаватьВФСРАРИзменился = СдаватьВФСРАР <> СдаватьВФСРАРИсходный;
	
	// РПН
	СдаватьВРПНИзменился = СдаватьВРПН <> СдаватьВРПНИсходный;
	
	// ФТС
	СдаватьВФТСИзменился = СдаватьВФТС <> СдаватьВФТСИсходный;
	
КонецПроцедуры

&НаКлиенте
Функция КодыИзменились(ТаблицаНаправлений, ТаблицаНаправленийИсходная)
	
	КодыИзменились = Ложь;
	
	Если ТаблицаНаправлений.Количество() <> ТаблицаНаправленийИсходная.Количество() Тогда
		КодыИзменились = Истина;
	Иначе
		// Сравнение двух таблиц
		Для каждого СтрокаПолучателей Из ТаблицаНаправлений Цикл
			ПараметрыОтбора = Новый Структура();
			ПараметрыОтбора.Вставить("КодПолучателя", ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(СтрокаПолучателей.КодПолучателя));
			ПараметрыОтбора.Вставить("КПП", ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(СтрокаПолучателей.КПП));
			МассивНайденныхСтрок = ТаблицаНаправленийИсходная.НайтиСтроки(ПараметрыОтбора);
			Если МассивНайденныхСтрок.Количество() = 0 Тогда 
				КодыИзменились = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат КодыИзменились;
	
КонецФункции

&НаКлиенте
Функция ИзменилисьРеквизитыТребующиеПереизданияСертификата()

	Возврат КраткоеНаименованиеИзменилось И ИзменитьРеквизитыПодключенияК1СОтчетности
			ИЛИ ОГРНИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности
			// Код региона ФСРАР важен только если подключено направление ФСРАР
			ИЛИ КодРегионаФСРАРИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности И 
				(ИзменитьСоставКонтролирующихОрганов И СдаватьВФСРАР ИЛИ НЕ ИзменитьСоставКонтролирующихОрганов И СдаватьВФСРАРИсходный)
			ИЛИ ЭлектроннаяПочтаИзменилась И ИзменитьРеквизитыПодключенияК1СОтчетности
			ИЛИ ВладелецЭЦПИзменился И ИзменитьВладельцаСертификата
			ИЛИ ВладелецЭЦПСНИЛСИзменился И (ИзменитьВладельцаСертификата ИЛИ ИзменитьРеквизитыПодключенияК1СОтчетности)
			ИЛИ ВладелецЭЦППодразделениеИзменилось И (ИзменитьВладельцаСертификата ИЛИ ИзменитьРеквизитыПодключенияК1СОтчетности)
			ИЛИ ВладелецЭЦПДолжностьИзменилась И (ИзменитьВладельцаСертификата ИЛИ ИзменитьРеквизитыПодключенияК1СОтчетности)
			ИЛИ ПродлитьСертификат;

КонецФункции

&НаСервере
Процедура УстановитьФлажкиВыбранныхДействийИзПредыдущегоЗаявления()
	
	ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	
	СписокИзмененныхРеквизитов = Реквизит.ИзменившиесяРеквизитыВторичногоЗаявления;
	
 	ИзменитьРеквизитыПодключенияК1СОтчетности = КонтекстЭДОСервер.БылиИзменененыРеквизитыПодключенияК1СОтчетности(
		Реквизит.ИзменившиесяРеквизитыВторичногоЗаявления);
		
	ПродлитьСертификат = КонтекстЭДОСервер.ЭтотПараметрИзменился(СписокИзмененныхРеквизитов, 
		Перечисления.ПараметрыПодключенияК1СОтчетности.ПродлениеАбонентскогоСертификата);
		
	ИзменитьВладельцаСертификата = КонтекстЭДОСервер.ЭтотПараметрИзменился(СписокИзмененныхРеквизитов, 
		Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦП);
		
	ПродлитьЛицензиюНа1СОтчетность = КонтекстЭДОСервер.ЭтотПараметрИзменился(СписокИзмененныхРеквизитов, 
		Перечисления.ПараметрыПодключенияК1СОтчетности.ПродлениеЛицензии);
		
	ИзменитьМобильныйТелефон = КонтекстЭДОСервер.ЭтотПараметрИзменился(СписокИзмененныхРеквизитов, 
		Перечисления.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный);
		
	ПереиздатьСертификат = ?(КонтекстЭДОСервер.ЭтотПараметрИзменился(СписокИзмененныхРеквизитов, 
		Перечисления.ПараметрыПодключенияК1СОтчетности.ПереизданиеСертификата), 2, 0);
		
	// Состав контролирующих органов
	ИзменитьСоставКонтролирующихОрганов = КонтекстЭДОСервер.БылИзмененСоставКонтролирующихОрганов(СписокИзмененныхРеквизитов);
	
КОнецПроцедуры

&НаКлиенте
Процедура УстановитьГалкуИзменитьРеквизитыПодключенияК1СОтчетности()
	
	ПроверитьИзменилисьЛиРеквизитыПодключенияК1СОтчетности();
		
	Если ИзменилисьРеквизитыПодключенияК1СОтчетности Тогда
		ИзменитьРеквизитыПодключенияК1СОтчетности = ИзменилисьРеквизитыПодключенияК1СОтчетности;
	КонецЕсли;
	
КОнецПроцедуры

&НаСервере
Процедура ПроверитьИзменилисьЛиРеквизитыПодключенияК1СОтчетности()
	
	ИзменилисьРеквизитыПодключенияК1СОтчетности = ИзменилисьРеквизитыОрганизации() ИЛИ ИзменилисьРеквизитыВладельцаЭЦП() ИЛИ ИзменилисьПрочиеРеквизиты();
	
КонецПроцедуры

&НаСервере
Функция ИзменилисьПрочиеРеквизиты()
	
	Возврат  НомерОсновнойПоставки1сИзменился ИЛИ ТелефонМобильныйИзменился;
	
КонецФункции

&НаСервере
Функция ИзменилисьРеквизитыОрганизации()
	
	Возврат КраткоеНаименованиеИзменилось ИЛИ ПолноеНаименованиеИзменилось ИЛИ КППИзменился ИЛИ ОГРНИзменился
		ИЛИ РегНомерПФРИзменился ИЛИ РегНомерФССИзменился ИЛИ КодРегионаФСРАРИзменился	ИЛИ ЭлектроннаяПочтаИзменилась
		ИЛИ ТелефонОсновнойИзменился ИЛИ ТелефонДополнительныйИзменился;
	
КонецФункции
	
&НаСервере
Функция ИзменилисьРеквизитыВладельцаЭЦП()
	
	Возврат ВладелецЭЦППодразделениеИзменилось ИЛИ ВладелецЭЦПДолжностьИзменилась ИЛИ ВладелецЭЦПСНИЛСИзменился;
	
КонецФункции	

&НаКлиенте
Процедура УстановитьГалкуИзменитьВладельцаСертификата()
	
	Если ВладелецЭЦПИсходный = ПредопределенноеЗначение("Справочник.ФизическиеЛица.ПустаяСсылка") Тогда
		// Если в базе не найден владелец - значит говорим, что он не изменился
		ВладелецЭЦПИзменился = Ложь;
	Иначе
		ПроверитьИзменилсяЛиВладелецЭЦП();
		
		// Проверяем, изменились ли данные владельца ЭЦП только если нашли владельца ЭЦП в базе
		Если (ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦППодразделение) 
			<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦППодразделениеИсходное)
			//ИЛИ ВладелецЭЦПСНИЛС <> ВладелецЭЦПСНИЛСИсходный - СНИЛС не сравниваем
			ИЛИ ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПДолжность) 
			<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПДолжностьИсходная))
			И ЗначениеЗаполнено(ВладелецЭЦП) Тогда
			ВладелецЭЦПИзменился = Истина;
		КонецЕсли;
		
	КонецЕсли;

	Если ВладелецЭЦПИзменился Тогда
		ИзменитьВладельцаСертификата = ВладелецЭЦПИзменился;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИзменилсяЛиВладелецЭЦП()
	
	ВладелецЭЦПИзменился = (ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПФамилия)
			<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПФамилияИсходный) 
		ИЛИ ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПИмя)
			<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПИмяИсходный) 
		ИЛИ ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПОтчество) 
			<> ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(ВладелецЭЦПОтчествоИсходный)) 
		ИЛИ НЕ ЗначениеЗаполнено(ВладелецЭЦП);

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьИзменилсяЛиСоставКонтролирующихОрганов()
	
	ИзменилсяСоставКонтролирующихОрганов = 
		СдаватьВФНС И КодыФНСИзменились 
		ИЛИ СдаватьВПФР И КодПФРИзменился 
		ИЛИ СдаватьВРосстат И КодыРосстатаИзменились;
		
КонецПроцедуры

&НаКлиенте
Процедура УстановитьГалкуИзменитьСоставКонтролирующихОрганов()
	
	ПроверитьИзменилсяЛиСоставКонтролирующихОрганов();

	Если ИзменилсяСоставКонтролирующихОрганов Тогда
		ИзменитьСоставКонтролирующихОрганов = ИзменилсяСоставКонтролирующихОрганов;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВРПНПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВРПН = Результат;
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
КонецПроцедуры

&НаКлиенте
Процедура СдаватьВФТСПриИзмененииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	СдаватьВФТС = Результат;
	
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
КонецПроцедуры

#Область РаботаСВладельцемЭЦП

&НаКлиенте
Процедура ЗаполнитьДанныеСотрудника(ПеричитатьДанныеОСотруднике = Истина)
	
	Если ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ПустаяСсылка") 
		ИЛИ НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат;
	КонецЕсли;
	
	Если ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель") Тогда
		ВладелецЭЦП = Руководитель;
	ИначеЕсли ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер") Тогда
		ВладелецЭЦП = ГлБухгалтер;
	ИначеЕсли ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник") Тогда
		ВладелецЭЦП = СотрудникВыбор;
	КонецЕсли;
	
	ОформитьПодсказкуДляГалкиИзменитьВладельцаСертификата();
	
	Если НЕ ЗначениеЗаполнено(ВладелецЭЦП) Тогда
		Возврат;
	КонецЕсли;

	// Обновляем данные о сотрудниках
	Если ПеричитатьДанныеОСотруднике Тогда
		КонтекстЭДОКлиент.ЗаполнитьДанныеОрганизации(СтруктураРеквизитов);
		ДанныеЗаполнения = КонтекстЭДОКлиент.ДополнитьДанныеОрганизацииДаннымиПоОтветственнымЛицам(СтруктураРеквизитов);
		Если ДанныеЗаполнения.Свойство("СтруктураДанныхОрганизации") Тогда
			ДанныеОрганизации = ДанныеЗаполнения.СтруктураДанныхОрганизации;
		КонецЕсли;
	КонецЕсли;
	
 	ДанныеОрганизации.Вставить("Организация", Организация);
 
	ДанныеСотрудника = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьДанныеСотрудника(
		ВладелецЭЦПТип, 
		ДанныеОрганизации, 
		ВладелецЭЦП);
	
	ВладелецЭЦПИмя					= ДанныеСотрудника.ФИО.Имя;
	ВладелецЭЦПФамилия				= ДанныеСотрудника.ФИО.Фамилия;
	ВладелецЭЦПОтчество				= ДанныеСотрудника.ФИО.Отчество;
	ВладелецЭЦПВидДокумента			= ДанныеСотрудника.ВидДокумента;
	ВладелецЭЦПСерияДокумента		= ДанныеСотрудника.Серия;
	ВладелецЭЦПНомерДокумента		= ДанныеСотрудника.Номер;
	ВладелецЭЦПДатаВыдачиДокумента	= ДанныеСотрудника.ДатаВыдачи;
	ВладелецЭЦПКемВыданДокумент		= ДанныеСотрудника.КемВыдан;
	ВладелецЭЦПДолжность			= ДанныеСотрудника.Должность;
	ВладелецЭЦППодразделение		= ДанныеСотрудника.Подразделение;
	ВладелецЭЦПСНИЛС				= ДанныеСотрудника.СНИЛС;
	ВладелецЭЦПДатаРождения         = ДанныеСотрудника.ДатаРождения;
	ВладелецЭЦПМестоРождения        = ДанныеСотрудника.МестоРождения;
	ВладелецЭЦППол                  = ДанныеСотрудника.Пол;
	ВладелецЭЦПГражданство          = ДанныеСотрудника.Гражданство;
	
	Если ВладелецЭЦПВидДокумента = ПаспортРФ Тогда
		ВладелецЭЦПКодПодразделения = ДанныеСотрудника.КодПодразделения;
	Иначе
		ВладелецЭЦПКодПодразделения = "";
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжиданияОбновитьДанныеРуководителя()
	
	ОтключитьОбработчикОжидания("ОбработкаОжиданияОбновитьДанныеРуководителя");
	Руководитель 	= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.Руководитель(Организация);
	ВладелецЭЦПТип 	= ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель");
	УстановитьНовогоВладельцаЭЦП();
	
	// Обновляем данные на второй закладке только если мы находимся на этой закладке
	Если Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.ЗаполнениеЗаявления Тогда
		СформироватьТаблицуДляПодтвержденияДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОжиданияОбновитьДанныеГлБухгалтера()
	
	ОтключитьОбработчикОжидания("ОбработкаОжиданияОбновитьДанныеГлБухгалтера");
	ГлБухгалтер 	= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ГлБухгалтер(Организация);
	ВладелецЭЦПТип 	= ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер");
	УстановитьНовогоВладельцаЭЦП();
	
	// Обновляем данные на второй закладке только если мы находимся на этой закладке
	Если Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.ЗаполнениеЗаявления Тогда
		СформироватьТаблицуДляПодтвержденияДанных();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьНовогоВладельцаЭЦП()

	// Заменяем владельца ЭЦП на нового 
	ОчиститьДанныеСотрудника(Истина);
	
	ЗаполнитьДанныеСотрудника(Истина);
	ПроверитьИзменилсяЛиВладелецЭЦП();
	ОформитьПодсказкуДляГалкиИзменитьВладельцаСертификата();
	
	// После изменения владельца ЭЦП выполняем сравнение старых и новый значений
	СравнитьТекущиеРеквизитыПодключенияСИсходными();
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	
	// Если Владелец ЭЦП изменился, устанавливаем галку "Переиздать сертификат"
	УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРуководителя()
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентПереопределяемый.ОткрытьФормуРуководителя(Организация);
	ПодключитьОбработчикОжидания("ОбработкаОжиданияОбновитьДанныеРуководителя",1);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуГлБухгалтера()
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентПереопределяемый.ОткрытьФормуГлБухгалтера(Организация);
	ПодключитьОбработчикОжидания("ОбработкаОжиданияОбновитьДанныеГлБухгалтера",1);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораВладельцаЭЦП()
	
	Если ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель") Тогда
		ОткрытьФормуРуководителя();
	ИначеЕсли ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер") Тогда
		ОткрытьФормуГлБухгалтера();
	ИначеЕсли ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник") Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ОткрытьФормуВыбораВладельцаЭЦПЗавершение", ЭтотОбъект);
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентПереопределяемый.ПолучитьИсполнителя(
			Организация, 
			СотрудникВыбор, 
			ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораВладельцаЭЦПЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда 
		СотрудникВыбор = Результат;
		ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник");
		УстановитьНовогоВладельцаЭЦП();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Очистка

&НаКлиенте
Процедура ОчиститьРеквизитыФормы() 

	АдресЮридический                                = "";
	СотрудникВыбор                                  = Неопределено;
	ИНН                                             = "";
	КПП                                             = "";
	КППИзменился                                    = Ложь;
	ОГРН                                            = "";
	ОГРНИзменился                                   = Ложь;
	КраткоеНаименование                             = "";
	КраткоеНаименованиеИзменилось                   = Ложь;
	ПолноеНаименование                              = "";
	ПолноеНаименованиеИзменилось                    = Ложь;
	ТелефонДополнительныйИзменился                  = Ложь;
	ТелефонОсновной                                 = "";
	ТелефонОсновнойИзменился                        = Ложь;
	ЭлектроннаяПочта                                = "";
	ЭлектроннаяПочтаИзменилась                      = Ложь;
	РегНомерПФР                                     = "";
	РегНомерПФРИзменился                            = Ложь;
	РегНомерФСС                                     = "";
	РегНомерФССИзменился                            = Ложь;
	ПризнакОбособленногоПодразделения               = Ложь;
	СотрудникВыбор                                  = "";
	СотрудникВыборИсходный                          = "";
	ОчиститьДанныеСотрудника();
	ГлБухгалтер                                     = Неопределено;
	Руководитель                                    = Неопределено;
	// сбрасываем все галки на первом шаге
	СброситьВсеФлажки(ЭтотОбъект);
	СброситьНаправленияСдачиОтчетности(ЭтотОбъект);
	ОтпечатокСертификата                            = "";
	СертификатДоступен                              = "";
	ПричиныПереизданияСертификата                   = "";

КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СброситьНаправленияСдачиОтчетности(Форма) 
	
	Форма.СдаватьВФНС 			= Ложь; 
	Форма.СдаватьВПФР 			= Ложь;
	Форма.КодПФР			 	= "";
	Форма.СдаватьВФСС 			= Ложь;
	Форма.СдаватьВРосстат 		= Ложь;
	Форма.СдаватьВФСРАР 		= Ложь;
	Форма.КодРегионаФСРАР 		= "";
	
	Форма.ПолучателиФНС.Очистить();
	Форма.ПолучателиФСГС.Очистить();
	
	Форма.СдаватьВФНСИзменился 		= Ложь; 
	Форма.СдаватьВПФРИзменился 		= Ложь;
	Форма.КодПФРИзменился 			= Ложь;
	Форма.СдаватьВФССИзменился 		= Ложь;
	Форма.СдаватьВРосстатИзменился 	= Ложь;
	Форма.КодыРосстатаИзменились 	= Ложь;
	Форма.СдаватьВФСРАРИзменился 	= Ложь;
	Форма.КодРегионаФСРАРИзменился 	= Ложь;
	Форма.СдаватьВРПНИзменился 		= Ложь;
	Форма.СдаватьВФТСИзменился 		= Ложь;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура СброситьВсеФлажки(Форма)
	
	Форма.ПродлитьСертификат 						= Ложь;
	Форма.ИзменитьВладельцаСертификата 				= Ложь;
	Форма.ПродлитьЛицензиюНа1СОтчетность 			= Ложь;
	Форма.ИзменитьСоставКонтролирующихОрганов 		= Ложь;
	Форма.ИзменитьРеквизитыПодключенияК1СОтчетности = Ложь;
	Форма.ИзменитьМобильныйТелефон 					= Ложь;
	Форма.ПереиздатьСертификат 						= 0;
	
КонецПроцедуры
	
&НаКлиенте
Процедура ОчиститьИсходныеЗначения() 
	
	КППИсходный 				= "";
	ОГРНИсходный 				= "";
	КраткоеНаименованиеИсходное = "";
	ПолноеНаименованиеИсходное 	= "";
	ЭлектроннаяПочтаИсходная 	= "";
	РегНомерПФРИсходный 		= "";
	РегНомерФССИсходный 		= "";
	
	СдаватьВФНСИсходный 		= Ложь; 
	СдаватьВПФРИсходный 		= Ложь;
	КодПФРИсходный 				= "";
	СдаватьВФССИсходный 		= Ложь;
	СдаватьВРосстатИсходный 	= Ложь;
	СдаватьВФСРАРИсходный 		= Ложь;
	КодРегионаФСРАРИсходный		= "";
	СдаватьВРПНИсходный 		= Ложь;
	СдаватьВФТСИсходный 		= Ложь;
	ПолучателиФНС.Очистить();
	
	СотрудникВыборИсходный 				= "";
	ВладелецЭЦППодразделениеИсходное 	= "";
	ВладелецЭЦПДолжностьИсходная 		= "";
	ВладелецЭЦПФамилияИсходный 			= "";
	ВладелецЭЦПИмяИсходный 				= "";
	ВладелецЭЦПОтчествоИсходный 		= "";
	ВладелецЭЦПСНИЛСИсходный			= "";

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДанныеСотрудника(Очистить = Ложь)
	
	Если ЗначениеЗаполнено(Реквизит) И НЕ Очистить Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецЭЦП                        = Неопределено;
	ВладелецЭЦППодразделение           = "";
	ВладелецЭЦППодразделениеИзменилось = Ложь;
	ВладелецЭЦПДолжность               = "";
	ВладелецЭЦПДолжностьИзменилась     = Ложь;
	ВладелецЭЦПФамилия                 = "";
	ВладелецЭЦПИмя                     = "";
	ВладелецЭЦПОтчество                = "";
	ВладелецЭЦПСНИЛС                   = "";
	ВладелецЭЦПСНИЛСИзменился          = Ложь;
	ВладелецЭЦПВидДокумента            = Неопределено;
	ВладелецЭЦПСерияДокумента          = "";
	ВладелецЭЦПНомерДокумента          = "";
	ВладелецЭЦПДатаВыдачиДокумента     = "";
	ВладелецЭЦПКемВыданДокумент        = "";
	ВладелецЭЦПДатаРождения            = Неопределено;
	ВладелецЭЦПМестоРождения           = "";
	ВладелецЭЦПКодПодразделения        = "";
	ВладелецЭЦППол                     = Неопределено;
	ВладелецЭЦПГражданство             = Неопределено;
		
КонецПроцедуры

#КонецОбласти

#Область НаправленияСдачиОтчетности

&НаКлиенте
Процедура СделатьРеквизитыРавнымиИсходным()

	СделатьНаправленияСдачиОтчетностиРавнымиИсходным();
	
	// Если мобильный телефон не хранится в базе, то приравниваем его исходному
	Если ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный")) Тогда

		ТелефонМобильный = ТелефонМобильныйИсходный;
	КонецЕсли;
	
	// Если основной телефон не хранится в базе, то приравниваем его исходному
	Если ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной")) Тогда

		ТелефонОсновной = ТелефонОсновнойИсходный;
	КонецЕсли;
	
	// Если дополнительный телефон не хранится в базе, то приравниваем его исходному
	Если ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный")) Тогда

		ТелефонДополнительный = ТелефонДополнительныйИсходный;
	КонецЕсли;
	
	// Если электронная не хранится в базе, то приравниваем ее исходному значению
	Если ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта")) Тогда

		ЭлектроннаяПочта = ЭлектроннаяПочтаИсходная;
	КонецЕсли;
	
	// Если СНИЛС не хранится в базе, то приравниваем его к исходному
	Если ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС")) Тогда

		ВладелецЭЦПСНИЛС = ВладелецЭЦПСНИЛСИсходный;
	КонецЕсли;
	
	ПродлитьСертификат = ПродлитьСертификатИсходный;
	
	ПродлитьЛицензиюНа1СОтчетность = ПродлитьЛицензиюНа1СОтчетностьИсходный;
	
	// Регистрационный номер программы 
	НомерОсновнойПоставки1с = НомерОсновнойПоставки1сИсходный;
	
КонецПроцедуры

&НаКлиенте
Процедура СделатьНаправленияСдачиОтчетностиРавнымиИсходным()

	СдаватьВФНС 	= Ложь;
	СдаватьВПФР 	= Ложь;
	СдаватьВФСС 	= Ложь;
	СдаватьВФСРАР 	= Ложь;
	СдаватьВРПН 	= Ложь;
	СдаватьВФТС 	= Ложь;
	СдаватьВРосстат = Ложь;
	
	ПолучателиФНС.Очистить();
	ПолучателиФСГС.Очистить();
	
	// ФНС
	СдаватьВФНС 	= СдаватьВФНСИсходный;
	СкопироватьИзОднойТаблицыВДругую(ПолучателиФНСИсходные, ПолучателиФНС);
	КодыФНСПрописью = КодыФНСПрописьюИсходные;
	
	// ПФР
	СдаватьВПФР = СдаватьВПФРИсходный;
	КодПФР 		= КодПФР(ДанныеОрганизации);
	
	// ФСС
	СдаватьВФСС = СдаватьВФССИсходный;
	
	// Росстат
	СдаватьВРосстат 	= СдаватьВРосстатИсходный;
	СкопироватьИзОднойТаблицыВДругую(ПолучателиФСГСИсходные, ПолучателиФСГС);
	КодыРосстатПрописью = КодыРосстатПрописьюИсходные;

	// ФСРАР
	СдаватьВФСРАР = СдаватьВФСРАРИсходный;
	
	// РПН
	СдаватьВРПН = СдаватьВРПНИсходный;
	
	// ФТС
	СдаватьВФТС = СдаватьВФТСИсходный;
	
КонецПроцедуры

&НаКлиенте
Процедура СкопироватьИзОднойТаблицыВДругую(ТаблицаИсточник, ТаблицаПриемник)
	ТаблицаПриемник.Очистить();
	Для Каждого СтрокаНаправлений Из ТаблицаИсточник Цикл
		НоваяСтрока = ТаблицаПриемник.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНаправлений); 
	КонецЦикла;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьКодыФНСПрописью(Знач Получатели)
	
	Результат = "";
	КоличествоКодовФНС = Получатели.Количество();
	Если КоличествоКодовФНС = 1 Тогда
		Результат = Получатели[0].КодПолучателя + ?(Получатели[0].КПП = "","" ,"-") + Получатели[0].КПП;
	ИначеЕсли КоличествоКодовФНС > 1 Тогда
		Результат = НСтр("ru = 'Коды ФНС (%1)'");
		Результат = СтрЗаменить(Результат, "%1", Строка(КоличествоКодовФНС));
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьКодыРосстатПрописью(Знач Получатели)
	
	Результат = "";
	КоличествоКодовФСГС = Получатели.Количество();
	Если КоличествоКодовФСГС = 1 Тогда
		Результат = Получатели[0].КодПолучателя;
	ИначеЕсли КоличествоКодовФСГС > 1 Тогда
		Результат = НСтр("ru = 'Коды Росстата (%1)'");
		Результат = СтрЗаменить(Результат, "%1", Строка(КоличествоКодовФСГС));
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

&НаКлиентеНаСервереБезКонтекста 
Функция КодПФР(ДанныеОрганизации)
	
	ПолученыйКодПФР = "";
	
	Если ПустаяСтрока(ДанныеОрганизации.КодОрганаПФР) ИЛИ СтрДлина(ДанныеОрганизации.КодОрганаПФР) < 7 Тогда
		ПолученыйКодПФР = Лев(ДанныеОрганизации.КодОрганаПФР,7); 
	Иначе
		ПолученыйКодПФР = ДанныеОрганизации.КодОрганаПФР;
	КонецЕсли;
	
	Возврат ПолученыйКодПФР;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция КодРосстатаИзОрганизации(ДанныеОрганизации)
	
	Возврат ДанныеОрганизации.КодОрганаФСГС;
	
КонецФункции

#КонецОбласти

#Область Навигация

&НаКлиенте
Процедура ПоказатьСледующуюСтраницу()
	
	ТекущаяСтраница = Элементы.ОсновнаяПанель.ТекущаяСтраница;
	ИндексТекущейСтраницы = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Индекс(ТекущаяСтраница);
	
	Пока Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Индекс(ТекущаяСтраница) < Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Количество() - 1 Цикл		
		Страница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Получить(Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Индекс(ТекущаяСтраница) + 1);		
		Если Страница.Видимость Тогда
			Элементы.ОсновнаяПанель.ТекущаяСтраница = Страница;
			Прервать;
		Иначе
			ТекущаяСтраница = Страница;
		КонецЕсли;		
	КонецЦикла;	
	
	УправлениеКнопкамиНавигации();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеКнопкамиНавигации()
	
	ТекущаяСтраница = Элементы.ОсновнаяПанель.ТекущаяСтраница;
	ВсегоСтраниц 	= Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Количество();
	
	КнопкаДалее	  = Элементы.Далее;
	КнопкаНазад   = Элементы.Назад;
	КнопкаЗакрыть = Элементы.Закрыть;
	
	КнопкаДалее.Видимость 	= Истина;
	КнопкаНазад.Видимость 	= Истина;
	КнопкаЗакрыть.Видимость = Истина;
	
	Если ТекущаяСтраница = Элементы.ВыборДействия Тогда
		// Шаг 1
		
		УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Ложь,	Ложь);
		УстановитьСвойстваКнопки(КнопкаДалее, 	"Далее  >", Истина,	Истина);
		УстановитьСвойстваКнопки(КнопкаЗакрыть, "Отмена", 	Истина,	Ложь);
		
	ИначеЕсли ТекущаяСтраница = Элементы.ПодключениеЭДО Тогда
		// Шаг 2 (если НЕ ЭП в облаке)
		
		УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Истина,	Ложь);
		УстановитьСвойстваКнопки(КнопкаДалее, 	"Далее >", Истина, Истина);
		УстановитьСвойстваКнопки(КнопкаЗакрыть, "Отмена", 	Истина,	Ложь);
		

	ИначеЕсли ТекущаяСтраница = Элементы.ЗаполнениеЗаявления Тогда
		// Шаг 3
		
		УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Истина,	Ложь);
		УстановитьСвойстваКнопки(КнопкаДалее, 	"Отправить заявление", Истина,	Истина);
		УстановитьСвойстваКнопки(КнопкаЗакрыть, "Отмена", 	Истина,	Ложь);
		
	ИначеЕсли ТекущаяСтраница = Элементы.ИнструкцияПоСозданиюКлючаЭЦП Тогда
		// Шаг 4
		
		УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Истина,	Ложь);
		УстановитьСвойстваКнопки(КнопкаДалее, 	"Создать ключ электронной подписи", Истина,	Истина);
		УстановитьСвойстваКнопки(КнопкаЗакрыть, "Отмена", 	Истина,	Ложь);
		
	ИначеЕсли ТекущаяСтраница = Элементы.ПолучениеРезультатаОтправкиЗаявления Тогда
		// Шаг 5
		
		//последняя закладка
		Если ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено") Тогда 
			
			УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Ложь,	Ложь);
			УстановитьСвойстваКнопки(КнопкаДалее, 	"Далее >",	Ложь,	Ложь);
			УстановитьСвойстваКнопки(КнопкаЗакрыть, "Закрыть", 	Истина,	Истина);
			ПрограммноеЗакрытие 	= Истина;
			
		ИначеЕсли ДокументЗаявление.Статус =  ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Подготовлено") Тогда
			
			УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Ложь,	Ложь);
			УстановитьСвойстваКнопки(КнопкаДалее, 	"Отправить заявление еще раз",	Истина,	Истина);
			УстановитьСвойстваКнопки(КнопкаЗакрыть, "Закрыть", 	Истина,	Ложь);
			
		ИначеЕсли ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено") Тогда 	
			
			УстановитьСвойстваКнопки(КнопкаНазад, 	"<  Назад", Ложь,	Ложь);
			УстановитьСвойстваКнопки(КнопкаДалее, 	"Подготовить новое заявление",	Истина,	Истина);
			УстановитьСвойстваКнопки(КнопкаЗакрыть, "Закрыть", 	Истина,	Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЭтотОбъект.Команды.КоманднаяПанельМастерНазад.Подсказка = КнопкаНазад.Заголовок;
	ЭтотОбъект.Команды.КоманднаяПанельМастерДалее.Подсказка = КнопкаДалее.Заголовок;
	ЭтотОбъект.Команды.ЗакрытьМастер.Подсказка 				= КнопкаЗакрыть.Заголовок;
		
КонецПроцедуры

&НаСервере
Процедура УстановитьСвойстваКнопки(Кнопка, Заголовок, Видимость, КнопкаПоУмолчанию)
	
	Кнопка.Заголовок 			= Заголовок; 
	Кнопка.Видимость 			= Видимость;
	Кнопка.КнопкаПоУмолчанию 	= КнопкаПоУмолчанию;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПредыдущуюСтраницу()
	
	ТекущаяСтраница 		= Элементы.ОсновнаяПанель.ТекущаяСтраница;
	ИндексТекущейСтраницы 	= Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Индекс(ТекущаяСтраница);
	
	Если ИндексТекущейСтраницы > 0 Тогда
		
		Индекс = ИндексТекущейСтраницы;
		Пока Индекс > 0 Цикл
			Индекс = Индекс - 1;
			Страница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.Получить(Индекс);
			Если Страница.Видимость Тогда		
		Элементы.ОсновнаяПанель.ТекущаяСтраница = Страница;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	УправлениеКнопкамиНавигации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуНаСтраницеРезультатаОтправки()
	
	Элементы.ОсновнаяПанель.ТекущаяСтраница = Элементы.ОсновнаяПанель.ПодчиненныеЭлементы.ПолучениеРезультатаОтправкиЗаявления;
    ПанельРезультатов = Элементы.ГруппаРезультатовОтправки;
	// Заявление подготовилось
	Если  ДокументЗаявление.Статус =  ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Подготовлено") Тогда 
		ПанельРезультатов.ТекущаяСтраница = ПанельРезультатов.ПодчиненныеЭлементы.ГруппаНеОтправлено;
	// Заявление отправилось
	ИначеЕсли ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено") Тогда 
		ПанельРезультатов.ТекущаяСтраница = ПанельРезультатов.ПодчиненныеЭлементы.ГруппаОтправленоИПринято;
		ПрограммноеЗакрытие = Истина;
	// Заявление отклонил сервер из-за ошибок
	ИначеЕсли ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отклонено") Тогда 
		ПанельРезультатов.ТекущаяСтраница = ПанельРезультатов.ПодчиненныеЭлементы.ГруппаОтправленоИОтклонено;
		ПрограммноеЗакрытие = Истина;
	Иначе // заявление подготовилось, но не отправилось
		ПанельРезультатов.ТекущаяСтраница = ПанельРезультатов.ПодчиненныеЭлементы.ГруппаНеОтправлено;
		ПрограммноеЗакрытие = Истина;
	КонецЕсли;
	
	// Формируем текст заголовока
	Если Не ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		Элементы.ПодсказкаПоРезультатам8.Заголовок = НСтр("ru='Что делать?
				|1. Попробуйте отправить заявление еще раз.
				|2. Проверьте, что отключен контроль учетных записей (UAC) в Windows.'");
		Если ВыборКриптопровайдера = 1 Тогда
			 Элементы.ПодсказкаПоРезультатам8.Заголовок = Элементы.ПодсказкаПоРезультатам8.Заголовок + Символы.ПС + 
			 	НСтр("ru='3. Проверьте, что VipNet зарегистрирован.'");
		КонецЕсли;
	КонецЕсли;
	
	ЭтаФорма.Активизировать();
	
КонецПроцедуры

#КонецОбласти

#Область Таблица

&НаКлиенте
Процедура ДобавитьЖелтыеСтроки()
	
	КоличествоСтрок = ТаблицаДанныхЗаявленияНаПодключение.Количество();
	КоличествоИзмененныхСтрок = ТаблицаДанныхЗаявленияНаПодключение.НайтиСтроки(Новый Структура("ЭтотПараметрИзменился", Истина)).Количество();
	
	СтрокаТаблицыПодтверждения = ДобавитьВТаблицу("Изменяемые настройки подключения",,,,Истина); // заголовок
	ПорядковыйНомерСтроки = 0;
	СтрокаТаблицыПодтверждения.ПорядковыйНомерСтроки = 0;
	
	Для сч = 0 По КоличествоСтрок - 1 Цикл
		СтрокаТаблицаДанных = ТаблицаДанныхЗаявленияНаПодключение[сч];
		Если СтрокаТаблицаДанных.ЭтотПараметрИзменился Тогда
			
			ДобавитьВТаблицу(
				СтрокаТаблицаДанных.ИзмененныйРеквизит, 
				СтрокаТаблицаДанных.ЗначениеРеквизита, 
				СтрокаТаблицаДанных.ЭтотПараметрИзменился, 
				Истина);
			
		КонецЕсли;
		СтрокаТаблицаДанных.ПорядковыйНомерСтроки = СтрокаТаблицаДанных.ПорядковыйНомерСтроки + КоличествоИзмененныхСтрок + 1;
	КонецЦикла;
	
	СтрокаТаблицыПодтверждения 	= ДобавитьВТаблицу(""); // заголовок
	ПорядковыйНомерСтроки 		= ПорядковыйНомерСтроки - 1;
	СтрокаТаблицыПодтверждения.ПорядковыйНомерСтроки = КоличествоИзмененныхСтрок + 1; 
	
	ТаблицаДанныхЗаявленияНаПодключение.Сортировать("ПорядковыйНомерСтроки");

КонецПроцедуры

&НаКлиенте
Функция ДобавитьВТаблицу(
		ИзмененныйРеквизит,  
		ЗначениеРеквизита = "", 
		ЭтотПараметрИзменился = Ложь,
		ВыделятьСтрокуЖелтым = Ложь,
		ВыделятьСтрокуЖирным = Ложь)
		
	ПорядковыйНомерСтроки = ПорядковыйНомерСтроки + 1;
		
	НоваяСтрока = ТаблицаДанныхЗаявленияНаПодключение.Добавить();
	Если ТипЗнч(ИзмененныйРеквизит) = Тип("Строка") Тогда
		НоваяСтрока.НазваниеРеквизита = ИзмененныйРеквизит;
	Иначе
		НоваяСтрока.ИзмененныйРеквизит	= ИзмененныйРеквизит;
		НоваяСтрока.НазваниеРеквизита 	= Строка(ИзмененныйРеквизит);
	КонецЕсли;
	
	НоваяСтрока.ЗначениеРеквизита		= ЗначениеРеквизита;
	НоваяСтрока.ЭтотПараметрИзменился 	= ЭтотПараметрИзменился;
	НоваяСтрока.ВыделятьСтрокуЖелтым 	= ВыделятьСтрокуЖелтым;
	НоваяСтрока.ПорядковыйНомерСтроки	= ПорядковыйНомерСтроки;
	НоваяСтрока.ВыделятьСтрокуЖирным 	= ВыделятьСтрокуЖирным;
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Процедура ЗадатьНовоеЗначениеРеквизиту(ИмяРеквизита, НаименованиеРеквизита, ИзменяемыйРеквизит, МаскаРеквизита = Неопределено)
	
	ОткрытьФормуРедактированияРеквизита(ИмяРеквизита, НаименованиеРеквизита, ИзменяемыйРеквизит, МаскаРеквизита);

КонецПроцедуры

&НаСервере
Функция ОписаниеТиповРеквизитаФормы(НаименованиеРеквизита)
	
	ВсеРеквизитыФормы = ПолучитьРеквизиты();
	Для каждого РеквизитФормы Из ВсеРеквизитыФормы Цикл
		Если НРег(РеквизитФормы.Имя) = НРег(НаименованиеРеквизита) Тогда
			Возврат РеквизитФормы.ТипЗначения;
		КонецЕсли;
	КонецЦикла; 
	
	Возврат Неопределено;

КонецФункции

&НаКлиенте
Процедура ОткрытьФормуРедактированияРеквизита(ИмяРеквизита, НаименованиеРеквизита, ИзменяемыйРеквизит, МаскаРеквизита = Неопределено)
	
	ОписаниеТипаРеквизита = ОписаниеТиповРеквизитаФормы(ИмяРеквизита);
	
	ДополнительныеПараметры = Новый Структура("ИмяРеквизита", ИмяРеквизита);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуРедактированияРеквизитаЗавершение",
		ЭтотОбъект,
		ДополнительныеПараметры);
	
	ПараметрыОткрытияФормы = Новый Структура();
	ПараметрыОткрытияФормы.Вставить("ИмяРеквизита",          ИмяРеквизита);
	ПараметрыОткрытияФормы.Вставить("НаименованиеРеквизита", НаименованиеРеквизита + ":");
	ПараметрыОткрытияФормы.Вставить("ЗначениеРеквизита",     ИзменяемыйРеквизит);
	ПараметрыОткрытияФормы.Вставить("МаскаРеквизита",        МаскаРеквизита);
	ПараметрыОткрытияФормы.Вставить("ОписаниеТипаРеквизита", ОписаниеТипаРеквизита);
	
	ОткрытьФорму(
		КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ФормаРедактированияРеквизита", 
		ПараметрыОткрытияФормы,
		,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияРеквизитаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ИмяРеквизита = ДополнительныеПараметры.ИмяРеквизита;
	
	Если Результат <> Неопределено Тогда
		
		ЭтаФорма[ИмяРеквизита] = Результат;
		
		ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
		СравнитьТекущиеРеквизитыПодключенияСИсходными();
		СформироватьТаблицуДляПодтвержденияДанных();
		ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияНаправленийСдачиОтчетности(Получатели, ТипПолучателя)
	
	СписокЗначенийПолучатели = Новый СписокЗначений;
	Для каждого СтрокаПолучателя Из Получатели Цикл
		СписокЗначенийПолучатели.Добавить(СтрокаПолучателя.КодПолучателя, СтрокаПолучателя.КПП);
	КонецЦикла;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("Организация", 	Организация);
	ДополнительныеПараметры.Вставить("Получатели", 		СписокЗначенийПолучатели);
	ДополнительныеПараметры.Вставить("ТипПолучателя", 	ТипПолучателя);
	ДополнительныеПараметры.Вставить("Спецоператор", 	Спецоператор);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ОткрытьФормуРедактированияНаправленийСдачиОтчетностиЗавершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ФормаНаправленийСдачиОтчетности", 
		ДополнительныеПараметры,
		,
		,
		,
		,
		ОписаниеОповещения,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуРедактированияНаправленийСдачиОтчетностиЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ТипПолучателя = ДополнительныеПараметры.ТипПолучателя;
	
	Если Результат <> Неопределено Тогда
		
		Если ТипПолучателя = ФНС Тогда
			ПолучателиФНС.Очистить();
		Иначе
			ПолучателиФСГС.Очистить();
		КонецЕсли;
		
		Для каждого СтрокаНаправления Из Результат Цикл
			
			Если ТипПолучателя = ФНС Тогда
				НоваяСтрока = ПолучателиФНС.Добавить();
			Иначе
				НоваяСтрока = ПолучателиФСГС.Добавить();
			КонецЕсли;
			
			НоваяСтрока.ТипПолучателя 	= ТипПолучателя;
			НоваяСтрока.КодПолучателя 	= СтрокаНаправления.Значение;
			НоваяСтрока.КПП 			= СтрокаНаправления.Представление;
			
		КонецЦикла;
		
	КонецЕсли;
	
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
	
КонецПроцедуры

#КонецОбласти

#Область ОформлениеЭлементовУправления

&НаСервере
Процедура ОформитьПодсказкуДляГалкиПродлитьСертификат()
	
	Надпись = Элементы.ПодсказкаПоПродлениюСертификата;
	
	// Определяем цвет
	Надпись.ЦветТекста = СинийЦветПодсказки;
	
	// Определяем заголовок
	ТекстЗаголовка = "";
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		ТекстЗаголовка = "";
	ИначеЕсли НЕ СертификатДоступен Тогда
		ТекстЗаголовка = НСтр("ru = 'Сертификат недоступен'");
	ИначеЕсли ПродлитьСертификатИсходный Тогда
		КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
		КоличествоОставшегосяВремени = КонтекстЭДОСервер.ТекстЧерезСколькоЛетМесяцевНедельДней(ТекущаяДатаСервер, СертификатДействителенПо, "", "");
		Надпись.ЦветТекста = КрасныйЦвет;
		
		Если КоличествоОставшегосяВремени = Неопределено Тогда
			ТекстЗаголовка = НСтр("ru = 'Срок действия сертификата истек %1'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Формат(СертификатДействителенПо,"ДЛФ=DD"));
		Иначе
			ТекстЗаголовка = НСтр("ru = 'Срок действия сертификата истекает через %1'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", КоличествоОставшегосяВремени);
		КонецЕсли;
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Сертификат действует до %1'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Формат(СертификатДействителенПо,"ДЛФ=DD"));
	КонецЕсли;
	Надпись.Заголовок = ТекстЗаголовка;
		 
КонецПроцедуры

&НаСервере
Процедура ОформитьПодсказкуДляГалкиИзменитьВладельцаСертификата()
	
	ПоказыватьТриТипаСотрудников = ЭтоЮридическоеЛицо ИЛИ НЕ ЗначениеЗаполнено(Организация);
	
	// Определяем заголовок
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		Элементы.ГруппаСтраницВладельца.ТекущаяСтраница = Элементы.ГруппаСтраницВладельца.ПодчиненныеЭлементы.СтраницаВладелецНеИзменен;
	Иначе 
		Элементы.ГруппаИзменитьВладельцаСертификата.Доступность = ИзменитьВладельцаСертификата;
		Элементы.ГруппаСтраницВладельца.ТекущаяСтраница = Элементы.ГруппаСтраницВладельца.ПодчиненныеЭлементы.СтраницаВладелецИзменен;
	КонецЕсли;
		
	ОформитьГиперссылку("ДекорацияВладелецЭЦП", ВладелецЭЦП, Истина, Истина);
	Элементы.ВыбратьДиректора.Пометка 		= Ложь;
	Элементы.ВыбратьГлБухгалтера.Пометка 	= Ложь;
	Элементы.ВыбратьСотрудника.Пометка 		= Ложь;
	
	Элементы.ВыбратьГлБухгалтера.Видимость = ПоказыватьТриТипаСотрудников;
	НаименованиеРуководителя = ?(
		ПоказыватьТриТипаСотрудников, 
		НСтр("ru = 'Руководитель'"), 
		НСтр("ru = 'Предприниматель'"));
		
	Элементы.ВыбратьДиректора.Заголовок = НаименованиеРуководителя;
	
	Если ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель") Тогда
		Элементы.ВидВладельцаЭЦП.Заголовок = Элементы.ВыбратьДиректора.Заголовок;
		Элементы.ВыбратьДиректора.Пометка = Истина;
		
		Элементы.ДекорацияВладелецЭЦП.Гиперссылка = ПоказыватьТриТипаСотрудников;
		
	ИначеЕсли ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер") Тогда
		Элементы.ВидВладельцаЭЦП.Заголовок = Элементы.ВыбратьГлБухгалтера.Заголовок;
		Элементы.ВыбратьГлБухгалтера.Пометка = Истина;
	ИначеЕсли ВладелецЭЦПТип = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник") Тогда
		Элементы.ВидВладельцаЭЦП.Заголовок = Элементы.ВыбратьСотрудника.Заголовок;
		Элементы.ВыбратьСотрудника.Пометка = Истина;
	КонецЕсли;
		 
КонецПроцедуры

&НаСервере
Процедура ОформитьПодсказкуДляГалкиПродлитьЛицензиюНа1СОтчетность()
	
	Надпись = Элементы.ПодсказкаПоПродлениюЛицензии;
	
	// Определяем цвет
	Надпись.ЦветТекста = СинийЦветПодсказки;
	
	// Определяем заголовок
	ТекстЗаголовка = "";
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		ТекстЗаголовка = "";
	ИначеЕсли ПродлитьЛицензиюНа1СОтчетностьИсходный Тогда
		КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
		КоличествоОставшегосяВремени = КонтекстЭДОСервер.ТекстЧерезСколькоЛетМесяцевНедельДней(ТекущаяДатаСервер, ЛицензияДатаОкончания, "", "");
		Надпись.ЦветТекста = КрасныйЦвет;
		
		Если КоличествоОставшегосяВремени = Неопределено Тогда
			ТекстЗаголовка = НСтр("ru = 'Срок действия лицензии истек %1'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Формат(ЛицензияДатаОкончания,"ДЛФ=DD"));
		Иначе
			ТекстЗаголовка = НСтр("ru = 'Срок действия лицензии истекает через %1'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", КоличествоОставшегосяВремени);
		КонецЕсли;
	Иначе
		ТекстЗаголовка = НСтр("ru = 'Лицензия действует до %1'");
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Формат(ЛицензияДатаОкончания,"ДЛФ=DD"));
	КонецЕсли;
	Надпись.Заголовок = ТекстЗаголовка;
		 
КонецПроцедуры

&НаСервере
Процедура ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации)
	
	Элементы.ГруппаИзменитьСоставНалоговыхОрганов.Доступность = ИзменитьСоставКонтролирующихОрганов;
	
	СделатьНедоступнымиНеподдерживаемыеНаправления();
	
	Элементы.ДекорацияКодыФНС.Видимость 	= Истина;
	Элементы.ДекорацияКодПФР.Видимость 		= Истина;
	Элементы.ДекорацияКодРосстат.Видимость 	= Истина;
	
	Элементы.ВзятьКодПФРИзОрганизации.Видимость 		= Истина;
	Элементы.ВзятьКодРосстатаИзОрганизации.Видимость 	= Истина;
	
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		Элементы.ДекорацияКодыФНС.Видимость 	= Ложь;
		Элементы.ДекорацияКодПФР.Видимость 		= Ложь;
		Элементы.ДекорацияКодРосстат.Видимость 	= Ложь;
		
		Элементы.ВзятьКодПФРИзОрганизации.Видимость 		= Ложь;
		Элементы.ВзятьКодРосстатаИзОрганизации.Видимость 	= Ложь;
		
	Иначе
		// ФНС
		КодыФНСПрописью = ПолучитьКодыФНСПрописью(ПолучателиФНС);
		ОформитьГиперссылку("ДекорацияКодыФНС", КодыФНСПрописью, СдаватьВФНС, СдаватьВФНС И ИзменитьСоставКонтролирующихОрганов);
		
		// ПФР
		ОформитьГиперссылку("ДекорацияКодПФР", КодПФР, СдаватьВПФР, СдаватьВПФР И ИзменитьСоставКонтролирующихОрганов);
		// Гиперссылка для изменения кода ПФР на значение из организации
		КодПФРВОрганизации = КодПФР(ДанныеОрганизации);
		Если КодПФРВОрганизации <> КодПФР И НЕ ПустаяСтрока(СтрЗаменить(КодПФРВОрганизации,"-","")) Тогда
			Элементы.ВзятьКодПФРИзОрганизации.Видимость = Истина;
			ТекстЗаголовка = НСтр("ru = 'Изменить на %1'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Строка(КодПФРВОрганизации));
		Иначе
			Элементы.ВзятьКодПФРИзОрганизации.Видимость = Ложь;
		КонецЕсли;
		ОформитьГиперссылку("ВзятьКодПФРИзОрганизации", ТекстЗаголовка, Ложь, СдаватьВПФР И ИзменитьСоставКонтролирующихОрганов);
		
		// Росстат
		КодыРосстатПрописью = ПолучитьКодыРосстатПрописью(ПолучателиФСГС);
		ОформитьГиперссылку("ДекорацияКодРосстат", КодыРосстатПрописью, СдаватьВРосстат, СдаватьВРосстат И ИзменитьСоставКонтролирующихОрганов);
		
		// Гиперссылка для изменения кода Росстата на значение из организации
		КодРосстатаВОрганизации = КодРосстатаИзОрганизации(ДанныеОрганизации);
		Если ПолучателиФСГС.Количество() = 1
			И КодРосстатаВОрганизации <> ПолучателиФСГС[0].КодПолучателя
			И НЕ ПустаяСтрока(СтрЗаменить(КодРосстатаВОрганизации,"-","")) Тогда
			
			Элементы.ВзятьКодРосстатаИзОрганизации.Видимость = Истина;
			ТекстЗаголовка = НСтр("ru = 'Изменить на %1'");
			ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Строка(КодРосстатаВОрганизации));
			
		Иначе
			Элементы.ВзятьКодРосстатаИзОрганизации.Видимость = Ложь;
		КонецЕсли;
		
		ОформитьГиперссылку("ВзятьКодРосстатаИзОрганизации", ТекстЗаголовка, Ложь, СдаватьВРосстат И ИзменитьСоставКонтролирующихОрганов);
		
	КонецЕсли;
		 
КонецПроцедуры

&НаСервере
Процедура СделатьНедоступнымиНеподдерживаемыеНаправления()
	
	Элементы.СдаватьВФСРАР.ТолькоПросмотр = НЕ ОператорПоддерживаетФСРАР;
	Элементы.СдаватьВФСРАР.Подсказка = ?(ОператорПоддерживаетФСРАР, НСтр("ru = 'Федеральная служба по регулированию алкогольного рынка'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Федеральная служба по регулированию алкогольного рынка (не поддерживается %1)'"), Строка(Спецоператор)));
	Элементы.СдаватьВРПН.ТолькоПросмотр = НЕ ОператорПоддерживаетРПН;
	Элементы.СдаватьВРПН.Подсказка = ?(ОператорПоддерживаетРПН, НСтр("ru = 'Федеральная служба по надзору в сфере природопользования'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Федеральная служба по надзору в сфере природопользования (не поддерживается %1)'"), Строка(Спецоператор)));
	Элементы.СдаватьВФТС.ТолькоПросмотр = НЕ ОператорПоддерживаетФТС;
	Элементы.СдаватьВФТС.Подсказка = ?(ОператорПоддерживаетФТС, НСтр("ru = 'Федеральная таможенная служба'"),
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Федеральная таможенная служба (не поддерживается %1)'"), Строка(Спецоператор)));
	
КонецПроцедуры

&НаСервере
Процедура ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности()
	
	ПроверитьИзменилисьЛиРеквизитыПодключенияК1СОтчетности();
	Надпись = Элементы.ПодсказкаИзменитьРеквизитыПодключенияК1СОтчетности;
	
	// Определяем цвет
	Надпись.ЦветТекста = СинийЦветПодсказки;
	
	// Определяем заголовок
	ТекстЗаголовка = "";
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		ТекстЗаголовка = "";
	ИначеЕсли ИзменилисьРеквизитыПодключенияК1СОтчетности И НЕ ПустаяСтрока(ИзмененныеРеквизиты) Тогда
		ТекстЗаголовка = ИзмененныеРеквизиты;
	Иначе
		ТекстЗаголовка = НСтр("ru = 'реквизиты не менялись'");
	КонецЕсли;
	Надпись.Заголовок = ТекстЗаголовка;

КонецПроцедуры

&НаСервере
Процедура ОформитьПодсказкуДляГалкиИзменитьМобильныйТелефон()
	
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		Элементы.ГруппаЗакладокМобильногоТелефона.ТекущаяСтраница = Элементы.ГруппаМобильныйТелефонНеДоступен;
	Иначе
		Элементы.ГруппаЗакладокМобильногоТелефона.ТекущаяСтраница = Элементы.ГруппаМобильныйТелефонДоступен;
        Элементы.ГруппаМобильныйТелефонДоступен.Доступность = ИзменитьМобильныйТелефон;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьГиперссылку(ИмяЭлемента, Знач Значение, ОбязательныйДляЗаполнения, Доступно)
	
	Элемент = Элементы[ИмяЭлемента];
	
	ПроверяемоеЗначение	= Строка(Значение);
	ПроверяемоеЗначение	= СтрЗаменить(ПроверяемоеЗначение,"-","");
	ПроверяемоеЗначение	= СтрЗаменить(ПроверяемоеЗначение,",","");
	ПроверяемоеЗначение	= СтрЗаменить(ПроверяемоеЗначение," ","");
	
	ЗначениеЗаполнено 	= ЗначениеЗаполнено(ПроверяемоеЗначение);
	Элемент.Доступность = Доступно;
	
	// Заголовок
	Если ЗначениеЗаполнено Тогда
		Элемент.Заголовок = Строка(Значение);
	Иначе
		Элемент.Заголовок = "Заполнить";
	КонецЕсли;
	
	// Цвет
	Если НЕ Доступно Тогда
		Элемент.ЦветТекста 	= СерыйЦвет;
	ИначеЕсли ЗначениеЗаполнено Тогда
		Элемент.ЦветТекста 	= СинийЦвет;
	Иначе
		Элемент.ЦветТекста 	= ?(ОбязательныйДляЗаполнения, КрасныйЦвет, СинийЦвет);
	КонецЕсли;
	
	Элемент.Гиперссылка = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ОформитьЭлементыФормыНаПервомШаге(ДанныеОрганизации)
	
	Элементы.ГруппаВыбораДействия.Доступность = ПоддерживаетсяВторичноеЗаявление;
	
	ОформитьПодсказкуДляГалкиПродлитьСертификат();
	ОформитьПодсказкуДляГалкиИзменитьВладельцаСертификата();
	ОформитьПодсказкуДляГалкиПродлитьЛицензиюНа1СОтчетность();
	ОформитьЭлементыДляГалкиИзменитьСоставНалоговыхОргановШаг1(ДанныеОрганизации);
	ОформитьПодсказкуДляГалкиИзменитьРеквизитыПодключенияК1СОтчетности();
	ОформитьПодсказкуДляГалкиИзменитьМобильныйТелефон();
	
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		ИзменитьВладельцаСертификата = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВидимостьМобильногоТелефона()
	
	Элементы.ИзменитьМобильныйТелефон.Видимость = ОператорПоддерживаетСМСУведомление;
	Элементы.ГруппаЗакладокМобильногоТелефона.Видимость = ОператорПоддерживаетСМСУведомление;
	
	Если НЕ ОператорПоддерживаетСМСУведомление Тогда
		ТелефонМобильныйИзменился = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Инициализация

&НаКлиенте
Процедура УстановитьНаименованияКриптопровайдеровНаФорме(ИмяКриптопровайдера)

	ПолеТекстаСообщения1 = Элементы.ПодсказкаТекстСообщения1;
	ПолеТекстаСообщения1.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '" + ПолеТекстаСообщения1.Заголовок + "'"), ИмяКриптопровайдера);

КонецПроцедуры

&НаКлиенте
Процедура ИнициализацияЗначений(ВыполняемоеОповещение)
	
	ДополнительныеПараметры = Новый Структура("ВыполняемоеОповещение", ВыполняемоеОповещение);
	ОписаниеОповещения = Новый ОписаниеОповещения("ИнициализацияЗначенийЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ЗаполнитьСписокУстановленныхКриптопровайдеров(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализацияЗначенийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	// Цвета
	ЧерныйЦвет 			= Новый Цвет(65, 48, 3);
	СерыйЦвет 			= Новый Цвет(87, 87, 87);
	СинийЦвет 			= Новый Цвет(28, 85, 174);
	КрасныйЦвет 		= Новый Цвет(178,34, 34);
	ЦветТекстаФормы 	= Новый Цвет(65, 48, 3);
	СинийЦветПодсказки 	= Новый Цвет(70, 130,180);

	// Данные службы поддержки - контактные данные ЗАО "Калуга Астрал"
	ТелефонСлужбыПоддержки = "8-800-700-86-68";
	АдресЭлектроннойПочтыСлужбыПоддержки = "1c@astralnalog.ru";

	КоличествоДнейЗаКотороеНужноПредупреждатьОбИстеченииСрока = 30;
	
	ПрограммноеЗакрытие	= Ложь;
	
	ОпределитьРеквизитыНеХранящиесяВБазе();
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьИсходныеПараметрыПодключенияК1СОтчетности(УчетнаяЗапись, ВходящийКонтекст)
	
	ОчиститьИсходныеЗначения();
	
	// Инициализация на сервере 
	ПрочитатьРегистрДополнительныеРеквизитыУчетнойЗаписи(УчетнаяЗапись);
	
	// Нужно ли продление сертификата
	ПроверитьТребуетсяЛиПродлениеСертификата(ВходящийКонтекст); // асинхронная
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьИсходныеПараметрыПодключенияК1СОтчетностиПослеОпределенияСвойствСертификата(Результат, ВходящийКонтекст) Экспорт
	
	// Нужно ли продление лицензии
	ПроверитьТребуетсяЛиПродлениеЛицензии();
	
	СделатьРеквизитыРавнымиИсходным();
	
	ЗаполнитьРеквизитыОрганизацииПослеПолученияСвойствСертификата(ВходящийКонтекст);
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРегистрДополнительныеРеквизитыУчетнойЗаписи(УчетнаяЗапись)

	// Обновляем информацию по учетной записи
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДополнительныеРеквизитыУчетнойЗаписи.ТипКриптопровайдера,
		|	ДополнительныеРеквизитыУчетнойЗаписи.НомерОсновнойПоставки1с,
		|	ДополнительныеРеквизитыУчетнойЗаписи.КраткоеНаименование,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ПолноеНаименование,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ИНН,
		|	ДополнительныеРеквизитыУчетнойЗаписи.КПП,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ОГРН,
		|	ДополнительныеРеквизитыУчетнойЗаписи.РегНомерПФР,
		|	ДополнительныеРеквизитыУчетнойЗаписи.РегНомерФСС,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ЭлектроннаяПочта,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ВладелецЭЦПФамилия,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ВладелецЭЦПИмя,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ВладелецЭЦПОтчество,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ВладелецЭЦППодразделение,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ВладелецЭЦПСНИЛС,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ВладелецЭЦПДолжность,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ТелефонМобильный,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ТелефонОсновной,
		|	ДополнительныеРеквизитыУчетнойЗаписи.ТелефонДополнительный,
		|	ДополнительныеРеквизитыУчетнойЗаписи.КодРегионаФСРАР
		|ИЗ
		|	РегистрСведений.ДополнительныеРеквизитыУчетнойЗаписи КАК ДополнительныеРеквизитыУчетнойЗаписи
		|ГДЕ
		|	ДополнительныеРеквизитыУчетнойЗаписи.УчетнаяЗапись = &УчетнаяЗапись";

	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	ДополнительныеРеквизитыУчетнойЗаписи = Запрос.Выполнить().Выгрузить();
	
	Если ДополнительныеРеквизитыУчетнойЗаписи.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТипКриптопровайдераИсходный 		= ДополнительныеРеквизитыУчетнойЗаписи[0].ТипКриптопровайдера;
	НомерОсновнойПоставки1сИсходный 	= ДополнительныеРеквизитыУчетнойЗаписи[0].НомерОсновнойПоставки1с;
	КраткоеНаименованиеИсходное 		= ДополнительныеРеквизитыУчетнойЗаписи[0].КраткоеНаименование;
	ПолноеНаименованиеИсходное 			= ДополнительныеРеквизитыУчетнойЗаписи[0].ПолноеНаименование;
	КППИсходный 						= ДополнительныеРеквизитыУчетнойЗаписи[0].КПП;
	ОГРНИсходный 						= ДополнительныеРеквизитыУчетнойЗаписи[0].ОГРН;
	РегНомерПФРИсходный 				= ДополнительныеРеквизитыУчетнойЗаписи[0].РегНомерПФР;
	РегНомерФССИсходный 				= ДополнительныеРеквизитыУчетнойЗаписи[0].РегНомерФСС;
	ЭлектроннаяПочтаИсходная 			= ДополнительныеРеквизитыУчетнойЗаписи[0].ЭлектроннаяПочта;
	ВладелецЭЦПФамилияИсходный 			= ДополнительныеРеквизитыУчетнойЗаписи[0].ВладелецЭЦПФамилия;
	ВладелецЭЦПИмяИсходный 				= ДополнительныеРеквизитыУчетнойЗаписи[0].ВладелецЭЦПИмя;
	ВладелецЭЦПОтчествоИсходный			= ДополнительныеРеквизитыУчетнойЗаписи[0].ВладелецЭЦПОтчество;
	ВладелецЭЦППодразделениеИсходное 	= ДополнительныеРеквизитыУчетнойЗаписи[0].ВладелецЭЦППодразделение;
	ВладелецЭЦПСНИЛСИсходный 			= ДополнительныеРеквизитыУчетнойЗаписи[0].ВладелецЭЦПСНИЛС;
	ВладелецЭЦПДолжностьИсходная 		= ДополнительныеРеквизитыУчетнойЗаписи[0].ВладелецЭЦПДолжность;
	ТелефонМобильныйИсходный			= ПреобразоватьНомерТелефонаКМаскеСервер(ДополнительныеРеквизитыУчетнойЗаписи[0].ТелефонМобильный);
	ТелефонОсновнойИсходный 			= ДополнительныеРеквизитыУчетнойЗаписи[0].ТелефонОсновной;
	ТелефонДополнительныйИсходный 		= ДополнительныеРеквизитыУчетнойЗаписи[0].ТелефонДополнительный;
	КодРегионаФСРАРИсходный 			= ДополнительныеРеквизитыУчетнойЗаписи[0].КодРегионаФСРАР;
	ЛицензияНаименование 				= УчетнаяЗапись.ЛицензияНаименование;
	ЛицензияДатаНачала 					= УчетнаяЗапись.ЛицензияДатаНачала;
	ЛицензияДатаОкончания 				= УчетнаяЗапись.ЛицензияДатаОкончания + 24*60*60; // В последний день лицензия еще действует.

	ЛицензияТребуетсяНапоминаниеОбОкончанииСрокаДействия = УчетнаяЗапись.ЛицензияТребуетсяНапоминаниеОбОкончанииСрокаДействия;
	
	// Обновляем информацию о направлениях сдачи отчетности
	ПолучателиФНСИсходные.Очистить();
	ПолучателиФСГСИсходные.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ДополнительныеРеквизитыУчетнойЗаписиПолучатели.ТипПолучателя,
		|	ДополнительныеРеквизитыУчетнойЗаписиПолучатели.КодПолучателя,
		|	ДополнительныеРеквизитыУчетнойЗаписиПолучатели.КПП
		|ИЗ
		|	РегистрСведений.ДополнительныеРеквизитыУчетнойЗаписиПолучатели КАК ДополнительныеРеквизитыУчетнойЗаписиПолучатели
		|ГДЕ
		|	ДополнительныеРеквизитыУчетнойЗаписиПолучатели.УчетнаяЗапись = &УчетнаяЗапись";

	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Результат = Запрос.Выполнить().Выгрузить();
	
	Для Каждого СтрокаНаправлений Из Результат Цикл
		Если СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ПФР Тогда
			СдаватьВПФРИсходный = Истина;
			КодПФРИсходный 		= ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СтрокаВФорматеДляСравнения(СтрокаНаправлений.КодПолучателя);
		ИначеЕсли СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСС Тогда
			СдаватьВФССИсходный = Истина;
		ИначеЕсли СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФНС Тогда
			СдаватьВФНСИсходный = Истина;
			
			НоваяСтрока = ПолучателиФНСИсходные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНаправлений);
			
		ИначеЕсли СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСГС Тогда
			
			СдаватьВРосстатИсходный = Истина;
			
			НоваяСтрока = ПолучателиФСГСИсходные.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаНаправлений);
			
		ИначеЕсли СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСРАР Тогда
			СдаватьВФСРАРИсходный = Истина;
		ИначеЕсли СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.РПН Тогда
			СдаватьВРПНИсходный = Истина;
		ИначеЕсли СтрокаНаправлений.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФТС Тогда
			СдаватьВФТСИсходный = Истина;
		КонецЕсли;
	КонецЦикла;
	
	КодыФНСПрописьюИсходные 	= ПолучитьКодыФНСПрописью(ПолучателиФНСИсходные);
	КодыРосстатПрописьюИсходные = ПолучитьКодыРосстатПрописью(ПолучателиФСГСИсходные);
	
КонецПроцедуры

#КонецОбласти

#Область Основные

&НаКлиенте
Процедура ЗаполнитьРеквизитыОрганизации(ДанныеЗаполнения = Неопределено, ПрограммноеЗаполнение = Истина, ВыполняемоеОповещение = Неопределено)
	
	// Очистка
	ОчиститьСообщения();
	ОчиститьДанныеСотрудника(НЕ ПрограммноеЗаполнение);
	ОчиститьРеквизитыФормы();
	
	// Проверка поддержки отправки вторичного заявления
	УчетнаяЗапись = КонтекстЭДОКлиент.УчетнаяЗаписьОрганизации(Организация);
	ОператорПоддерживаетСМСУведомление 	= Ложь;
	ОператорПоддерживаетФСРАР 			= Ложь;
	ОператорПоддерживаетРПН 			= Ложь;
	ОператорПоддерживаетФТС 			= Ложь;
	Спецоператор = СпецоператорИзУчетнойЗаписи(УчетнаяЗапись, ОператорПоддерживаетСМСУведомление, ОператорПоддерживаетФСРАР, ОператорПоддерживаетРПН, ОператорПоддерживаетФТС);

	// Проверка поддержки вторичных заявлений
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ПрограммноеЗаполнение", 	ПрограммноеЗаполнение);
	ДополнительныеПараметры.Вставить("УчетнаяЗапись", 			УчетнаяЗапись);
	ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", 	ВыполняемоеОповещение);
	ДополнительныеПараметры.Вставить("ДанныеЗаполнения", 		ДанныеЗаполнения);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьРеквизитыОрганизацииЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ПоддерживаетсяОтправкаВторичныхЗаявлений(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыОрганизацииЗавершение(ПоддерживаетсяВторичноеЗаявление, ДополнительныеПараметры) Экспорт
	
	ПрограммноеЗаполнение 	= ДополнительныеПараметры.ПрограммноеЗаполнение;
	УчетнаяЗапись 			= ДополнительныеПараметры.УчетнаяЗапись;
	ВыполняемоеОповещение 	= ДополнительныеПараметры.ВыполняемоеОповещение;
	ДанныеЗаполнения 		= ДополнительныеПараметры.ДанныеЗаполнения;
	
	Если НЕ ПоддерживаетсяВторичноеЗаявление Тогда
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьРеквизитыОрганизацииПослеОбновленияЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбновитьРеквизитыОрганизации(ПрограммноеЗаполнение, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыОрганизацииПослеОбновленияЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ПрограммноеЗаполнение 	= ДополнительныеПараметры.ПрограммноеЗаполнение;
	УчетнаяЗапись 			= ДополнительныеПараметры.УчетнаяЗапись;
	ДанныеЗаполнения 		= ДополнительныеПараметры.ДанныеЗаполнения;

	// Определяем исходные параметры подключенния к 1С-Отчетности
	ОпределитьИсходныеПараметрыПодключенияК1СОтчетности(УчетнаяЗапись, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРеквизитыОрганизацииПослеПолученияСвойствСертификата(ВходящийКонтекст)
	
	ВыполняемоеОповещение = ВходящийКонтекст.ВыполняемоеОповещение;
	
	ОпределитьВладельцаЭЦП();
	
	// Если это заявление создается копированием предыдущего, то копируем реквизиты из предыдущего заявления
	Если ЗначениеЗаполнено(Реквизит) Тогда
		ЗаполнитьРеквизитыИзПредыдущегоЗаявления();
		ЗаполнитьДанныеСотрудника(Ложь);
		УстановитьНовогоВладельцаЭЦП();
		СравнитьТекущиеРеквизитыПодключенияСИсходными();
		УстановитьГалкуИзменитьРеквизитыПодключенияК1СОтчетности();
		УстановитьФлажкиВыбранныхДействийИзПредыдущегоЗаявления();
	Иначе
		УстановитьГалкуИзменитьВладельцаСертификата();
		СравнитьТекущиеРеквизитыПодключенияСИсходными();
		СравнитьНаправленияИКодыСдачиОтчетностиСИсходными();
		// Устанавливаем корректное значение галок
		УстановитьГалкуИзменитьСоставКонтролирующихОрганов();
		УстановитьГалкуИзменитьРеквизитыПодключенияК1СОтчетности();
		УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания();
	КонецЕсли;
	
	УстановитьВидимостьМобильногоТелефона();
	ОформитьЭлементыФормыНаПервомШаге(ДанныеОрганизации);
	
	СтруктураДанныхСпецоператорыСвязи = Новый Структура("СпецоператорыСвязи,Макет,ТекстМакетаСоглашение,ЗначениеЗаполненияСпецоператораСвязи",
												ДанныеЗаполнения.СпецоператорыСвязи, ДанныеЗаполнения.МакетПараметрыСпецоператоровСвязи,
												ДанныеЗаполнения.ТекстМакетаСоглашение, ДанныеЗаполнения.ЗначениеЗаполненияСпецоператораСвязи);

	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРеквизитыОрганизации(ПрограммноеЗаполнение, ВыполняемоеОповещение)
	
	ДополнительныеПараметры = Новый Структура(
		"ПрограммноеЗаполнение, ВыполняемоеОповещение", 
		ПрограммноеЗаполнение, 
		ВыполняемоеОповещение);
		
	ОписаниеОповещения = Новый 
		ОписаниеОповещения("ОбновитьРеквизитыОрганизацииЗавершение", 
		ЭтотОбъект, 
		ДополнительныеПараметры);
		
	ИнициализацияЗначений(ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьРеквизитыОрганизацииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	ПрограммноеЗаполнение = ДополнительныеПараметры.ПрограммноеЗаполнение;
	
	// Заполняем текущие реквизиты организации
	Если ДанныеЗаполнения = Неопределено Тогда
		ДанныеЗаполнения = Новый Структура();
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("Организация, ПриОткрытии", Организация, НЕ ДанныеЗаполнения = Неопределено);
	Если ДанныеЗаполнения <> Неопределено Тогда
		СтруктураРеквизитов.Вставить("АдресЮридический",);
		СтруктураРеквизитов.Вставить("АдресФактический",);
	КонецЕсли;
	КонтекстЭДОКлиент.ЗаполнитьДанныеОрганизации(СтруктураРеквизитов);
	ДанныеЗаполнения = КонтекстЭДОКлиент.ДополнитьДанныеОрганизацииДаннымиПоОтветственнымЛицам(СтруктураРеквизитов);
	ДанныеОрганизации = ДанныеЗаполнения.СтруктураДанныхОрганизации;
	ЗаполнитьДанныеПоОрганизации(ДанныеЗаполнения, ПрограммноеЗаполнение);
	
	УстановитьЗаголовокФормы();
	
	ВыполняемоеОповещение.ДополнительныеПараметры.ДанныеЗаполнения = ДанныеЗаполнения;
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокФормы()

	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ДобавитьОрганизациюВЗаголовок(
		ЭтотОбъект.Заголовок, 
		ИспользуетсяОднаОрганизация, 
		КраткоеНаименование,
		// Разный заголовок!
		НСтр("ru = 'Изменение настроек подключения к 1С-Отчетности'"));

КонецПроцедуры

&НаКлиенте
Процедура ОпределитьВладельцаЭЦП()
	
	// пытаемся определить тип владельца ЭП исходя из ФИО
	СтруктураФИОРуководителя = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ФИОФизЛица(Руководитель);
	СтруктураФИОГлБухгалтер = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ФИОФизЛица(ГлБухгалтер);
	// Сравниваем с ФИО руководителя
	Если СтруктураФИОРуководителя.Имя = ВладелецЭЦПИмяИсходный И СтруктураФИОРуководителя.Отчество = ВладелецЭЦПОтчествоИсходный
		И СтруктураФИОРуководителя.Фамилия = ВладелецЭЦПФамилияИсходный Тогда
		ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель");
	// Сравниваем с ФИО бухгалтера
	ИначеЕсли СтруктураФИОГлБухгалтер.Имя = ВладелецЭЦПИмяИсходный И СтруктураФИОГлБухгалтер.Отчество = ВладелецЭЦПОтчествоИсходный
		И СтруктураФИОГлБухгалтер.Фамилия = ВладелецЭЦПФамилияИсходный Тогда
		ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер");
	Иначе
		// Если владелец ЭЦП не руководитель и не бухгалтер, то мы можем найти его только по ФИО 
		ВладелецЭЦПИсходный = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ФизЛицоПоФИО(
			ВладелецЭЦПФамилияИсходный, ВладелецЭЦПИмяИсходный, ВладелецЭЦПОтчествоИсходный, ВладелецЭЦПСНИЛСИсходный, Организация);

		СотрудникВыборИсходный  = ВладелецЭЦПИсходный;
		СотрудникВыбор 			= СотрудникВыборИсходный;
		
		ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ДругойСотрудник");
	КонецЕсли; 
	
	ВладелецЭЦПТип = ВладелецЭЦПТипИсходный;
	
	ЗаполнитьДанныеСотрудника(Ложь);
	
	Если ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.Руководитель") 
		И НЕ ВладелецЭЦПИзменился Тогда
		ВладелецЭЦПИсходный = Руководитель;
	ИначеЕсли ВладелецЭЦПТипИсходный = ПредопределенноеЗначение("Перечисление.ТипыВладельцевЭЦП.ГлавныйБухгалтер") 
		И НЕ ВладелецЭЦПИзменился Тогда
		ВладелецЭЦПИсходный = ГлБухгалтер;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДанныеПоОрганизации(ДанныеЗаполнения, ПрограммноеЗаполнение = Истина)

	Руководитель	= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.Руководитель(Организация); 
	ГлБухгалтер		= ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ГлБухгалтер(Организация);
	
	ЭтоЮридическоеЛицо		 = ДанныеОрганизации.ТипОрганизации;
	ПолноеНаименование		 = ДанныеОрганизации.НаимЮЛПол;
	КраткоеНаименование		 = ДанныеОрганизации.КраткоеНаименование;
	ИНН						 = ДанныеОрганизации.ИННЮЛ;
	КПП						 = ДанныеОрганизации.КППЮЛ;
	ОГРН					 = ДанныеОрганизации.ОГРН;
	РегНомерПФР				 = ДанныеОрганизации.РегНомПФР;
	РегНомерФСС				 = ДанныеОрганизации.РегистрационныйНомерФСС;
	ДополнительныйКодФСС 	 = ДанныеОрганизации.РеквизитДопКодФСС;
	
	// Если реквизита нет в базе, то не обновляем его, потому в этом случае данные, введенные пользователем вручную, затрутся
	Если НЕ ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной")) Тогда
		ТелефонОсновной = ДанныеОрганизации.ТелОрганизации;

	КонецЕсли;

	// Если реквизита нет в базе, то не обновляем его, потому в этом случае данные, введенные пользователем вручную, затрутся
	Если НЕ ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный")) Тогда

		ТелефонДополнительный	 = ДанныеОрганизации.ТелРук;
	КонецЕсли;
	АдрЮР					 = ДанныеЗаполнения.АдресЮридический;
	АдрФакт				 	 = ДанныеЗаполнения.АдресФактический;
	АдресЮридический		 = ДанныеЗаполнения.ЮрАдрес;
	АдресФактический		 = ДанныеЗаполнения.ФактАдрес;
	
	Если АдресЮридический = ",,,,,,,,," Тогда
		АдресЮридический = "";
	КонецЕсли;
	
	Если АдресФактический = ",,,,,,,,," Тогда
		АдресФактический = "";
	КонецЕсли;
	
	// Если реквизита нет в базе, то не обновляем его, потому в этом случае данные, введенные пользователем вручную, затрутся
	Если НЕ ЭтоРеквизитНеХранящийсяВБазе(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта")) Тогда

		ЭлектроннаяПочта = ДанныеОрганизации.ЭлектроннаяПочта;
	КонецЕсли;
	
	Если ЭтоЮридическоеЛицо Тогда
		ПризнакОбособленногоПодразделения	= ДанныеОрганизации.ПризнакОбособленногоПодразделения;
	КонецЕсли;
	
	КодРегионаФСРАР = РегламентированнаяОтчетностьКлиентСервер.РазложитьАдрес(АдрЮр).Регион;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьНовыйДокументЗаявление(СохранитьИВыгрузить)
	
    ТекстСообщения = "";
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	
	НовыйДокументЗаявление = РеквизитФормыВЗначение("ДокументЗаявление");
		
	НовыйДокументЗаявление.УстановитьНовыйНомер();
	НовыйДокументЗаявление.ЭлектроннаяПодписьВМоделиСервиса     = ЭтоЭлектроннаяПодписьВМоделиСервиса;
	НовыйДокументЗаявление.Дата									= ТекущаяДатаСеанса();
	НовыйДокументЗаявление.Организация							= Организация;
	НовыйДокументЗаявление.ТипЗаявления							= Перечисления.ТипыЗаявленияАбонентаСпецоператораСвязи.Изменение;
	НовыйДокументЗаявление.ИдентификаторДокументооборота		= нрег(СтрЗаменить(Строка(Новый("УникальныйИдентификатор")), "-", ""));
	НовыйДокументЗаявление.Статус								= Перечисления.СтатусыЗаявленияАбонентаСпецоператораСвязи.Подготовлено;
	НовыйДокументЗаявление.СпецоператорСвязи					= Спецоператор;
	НовыйДокументЗаявление.ТипКриптопровайдера					= ТипКриптопровайдера;
	НовыйДокументЗаявление.ТипОрганизации						= ЭтоЮридическоеЛицо;
	НовыйДокументЗаявление.ИНН									= ИНН;
	НовыйДокументЗаявление.КПП									= КПП;
	НовыйДокументЗаявление.ПолноеНаименование					= ПолноеНаименование;
	НовыйДокументЗаявление.КраткоеНаименование					= КраткоеНаименование;
	НовыйДокументЗаявление.ПризнакОбособленногоПодразделения	= ПризнакОбособленногоПодразделения;
	НовыйДокументЗаявление.НомерОсновнойПоставки1с				= НомерОсновнойПоставки1с;
	НовыйДокументЗаявление.ДополнительныйКодФСС					= ДополнительныйКодФСС;
	НовыйДокументЗаявление.ОГРН									= ОГРН;
	НовыйДокументЗаявление.АдресЮридический 					= РегламентированнаяОтчетностьВызовСервера.ЗаменитьТекстРегионаНаКодРегиона(АдрЮР);
	НовыйДокументЗаявление.РегНомерПФР							= РегНомерПФР;
	НовыйДокументЗаявление.РегНомерФСС							= ?(СдаватьВФСС, РегНомерФСС, "");
	НовыйДокументЗаявление.ТелефонОсновной						= ТелефонОсновной;
	НовыйДокументЗаявление.ТелефонДополнительный				= ТелефонДополнительный;
		
	ТелефонМобильныйБезРазделителей = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонМобильный);
	Если Не ПолучатьУведомления ИЛИ ПустаяСтрока(ТелефонМобильныйБезРазделителей) Тогда
		НовыйДокументЗаявление.ТелефонМобильный = "";
	Иначе
		НовыйДокументЗаявление.ТелефонМобильный = ТелефонМобильный;
	КонецЕсли;
	
	НовыйДокументЗаявление.ВладелецЭЦПТип						= ВладелецЭЦПТип;
	НовыйДокументЗаявление.ВладелецЭЦП							= ВладелецЭЦП;
	НовыйДокументЗаявление.ЭлектроннаяПочта						= ЭлектроннаяПочта;
	НовыйДокументЗаявление.ВладелецЭЦПФамилия					= СокрЛП(ВладелецЭЦПФамилия);
	НовыйДокументЗаявление.ВладелецЭЦПИмя						= СокрЛП(ВладелецЭЦПИмя);
	НовыйДокументЗаявление.ВладелецЭЦПОтчество					= СокрЛП(ВладелецЭЦПОтчество);
	НовыйДокументЗаявление.ВладелецЭЦПДолжность					= ВладелецЭЦПДолжность;
	НовыйДокументЗаявление.ВладелецЭЦППодразделение				= ВладелецЭЦППодразделение;
	НовыйДокументЗаявление.ВладелецЭЦПВидДокумента				= ВладелецЭЦПВидДокумента;
	НовыйДокументЗаявление.ВладелецЭЦПНомерДокумента			= ВладелецЭЦПНомерДокумента;
	НовыйДокументЗаявление.ВладелецЭЦПСерияДокумента			= ВладелецЭЦПСерияДокумента;
	НовыйДокументЗаявление.ВладелецЭЦПДатаВыдачиДокумента		= ВладелецЭЦПДатаВыдачиДокумента;
	НовыйДокументЗаявление.ВладелецЭЦПКемВыданДокумент			= ВладелецЭЦПКемВыданДокумент;
	НовыйДокументЗаявление.ВладелецЭЦПДатаРождения				= ВладелецЭЦПДатаРождения;
	НовыйДокументЗаявление.ВладелецЭЦПМестоРождения				= ВладелецЭЦПМестоРождения;
	НовыйДокументЗаявление.ВладелецЭЦПКодПодразделения			= ВладелецЭЦПКодПодразделения;
	НовыйДокументЗаявление.ВладелецЭЦППол						= ВладелецЭЦППол;
	НовыйДокументЗаявление.ВладелецЭЦПГражданство				= ВладелецЭЦПГражданство;
	НовыйДокументЗаявление.ВладелецЭЦПСНИЛС						= ВладелецЭЦПСНИЛС;
	
	НовыйДокументЗаявление.Ответственный						= Пользователи.ТекущийПользователь();
	НовыйДокументЗаявление.ДатаСозданияУчетнойЗаписи			= '00010101';
	
	// Контролирующие органы
	НовыйДокументЗаявление.ПодатьЗаявкуНаСертификатДляФСРАР 	= СдаватьВФСРАР;
	НовыйДокументЗаявление.КодРегионаФСРАР 						= КодРегионаФСРАР;
	НовыйДокументЗаявление.ПодатьЗаявкуНаПодключениеРПН 		= СдаватьВРПН;
	НовыйДокументЗаявление.ПодатьЗаявкуНаПодключениеФТС 		= СдаватьВФТС;
	
	НовыйДокументЗаявление.Получатели.Очистить();
	
	Если СдаватьВФНС Тогда
		Для Каждого СтрокаНаправлений Из ПолучателиФНС Цикл
			НоваяСтрокаНаправления = НовыйДокументЗаявление.Получатели.Добавить();
			НоваяСтрокаНаправления.ТипПолучателя 	= СтрокаНаправлений.ТипПолучателя;
			НоваяСтрокаНаправления.КодПолучателя 	= СтрокаНаправлений.КодПолучателя;
			НоваяСтрокаНаправления.КПП 				= СтрокаНаправлений.КПП;
		КонецЦикла;
	КонецЕсли;
	
	Если СдаватьВПФР Тогда 
		НоваяСтрокаНаправления = НовыйДокументЗаявление.Получатели.Добавить();
		НоваяСтрокаНаправления.ТипПолучателя =  ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР");
		НоваяСтрокаНаправления.КодПолучателя = КодПФР;
		НоваяСтрокаНаправления.КПП = "";
	КонецЕсли;
	
	Если СдаватьВФСС Тогда 
		НоваяСтрокаНаправления = НовыйДокументЗаявление.Получатели.Добавить();
		НоваяСтрокаНаправления.ТипПолучателя =  ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС");
		НоваяСтрокаНаправления.КПП = "";
	КонецЕсли;
	
	Если СдаватьВРосстат Тогда
		Для Каждого СтрокаНаправлений Из ПолучателиФСГС Цикл
			НоваяСтрокаНаправления = НовыйДокументЗаявление.Получатели.Добавить();
			НоваяСтрокаНаправления.ТипПолучателя 	= СтрокаНаправлений.ТипПолучателя;
			НоваяСтрокаНаправления.КодПолучателя 	= СтрокаНаправлений.КодПолучателя;
			НоваяСтрокаНаправления.КПП 				= СтрокаНаправлений.КПП;
		КонецЦикла;
	КонецЕсли;
	
	// Записываем перечень изменившихся реквизитов
	НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Очистить();
	Для каждого СтрокаТаблицыПодтверждения Из ТаблицаДанныхЗаявленияНаПодключение Цикл
		
		// Записываем в документ только изменившиеся реквизиты
		Если СтрокаТаблицыПодтверждения.ЭтотПараметрИзменился И СтрокаТаблицыПодтверждения.ВыделятьСтрокуЖелтым Тогда
			
			НоваяСтрока = НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Добавить();
			НоваяСтрока.ИзмененныйРеквизит = СтрокаТаблицыПодтверждения.ИзмененныйРеквизит;
			
		КонецЕсли;
		
	КонецЦикла;  
	
	НовыйДокументЗаявление.УчетнаяЗапись = УчетнаяЗапись;
	
	Если ПодключитьЭДО Тогда
		НовыйДокументЗаявление.ПодключитьЭДО 			= Истина;
		НовыйДокументЗаявление.ОператорЭДО 				= ОператорЭДО;
		НовыйДокументЗаявление.КодНалоговогоОрганаЭДО 	= КодНалоговогоОрганаЭДО;
	КонецЕсли;
	
	Если ТелефонДляПаролей <> ТелефонДляПаролейИсходный Тогда
		НовыйДокументЗаявление.ТелефонМобильныйДляАвторизации = ТелефонДляПаролей;
	КонецЕсли;
	Если ЭлектроннаяПочтаДляПаролей <> ЭлектроннаяПочтаДляПаролейИсходная Тогда
		НовыйДокументЗаявление.ЭлектроннаяПочтаАутентификация = ЭлектроннаяПочтаДляПаролей;
	КонецЕсли;
	
	Если НЕ (НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Найти(Перечисления.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный, "ИзмененныйРеквизит") <> Неопределено
		И НовыйДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Количество() = 1
		И Не ТелефонИзменен И ЭлектроннаяПочтаИзменена) ИЛИ НЕ ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		// Сохранение документа
		Если СохранитьИВыгрузить = 1 или СохранитьИВыгрузить = 2 Тогда
			НовыйДокументЗаявление.Записать();
		КонецЕсли;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(НовыйДокументЗаявление, "ДокументЗаявление");
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьИОтправитьЗаявление()
	
	Если ДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.НайтиСтроки(Новый Структура("ИзмененныйРеквизит", ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный"))).Количество() = 1
		И ДокументЗаявление.ИзменившиесяРеквизитыВторичногоЗаявления.Количество() = 1
		И Не ТелефонИзменен И ЭлектроннаяПочтаИзменена И ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		СформироватьИОтправитьЗаявлениеЗавершение(Новый Структура("ТолькоИзменениеТелефона"), Новый Структура);
		Возврат;
	КонецЕсли;
		
	// Получение идентификатора абонента
	ИдентификаторАбонента = "";
	УчетнаяЗаписьОбмена = КонтекстЭДОКлиент.НастройкиУчетнойЗаписиОрганизации(Организация);
	Если УчетнаяЗаписьОбмена <> Неопределено Тогда
		ИдентификаторАбонента = Строка(УчетнаяЗаписьОбмена.ИдентификаторАбонента);
		ИдентификаторСпецоператора = Строка(УчетнаяЗаписьОбмена.ИдентификаторСпецоператора);
		ДлинаИдентификатораСпецоператора = СтрДлина(ИдентификаторСпецоператора);
		ПрефиксИдентификатораАбонента = Лев(ИдентификаторАбонента, ДлинаИдентификатораСпецоператора);
		
		Если ЗначениеЗаполнено(ИдентификаторСпецоператора) И нрег(ПрефиксИдентификатораАбонента) = нрег(ИдентификаторСпецоператора) Тогда
			ИдентификаторАбонента = Сред(ИдентификаторАбонента, ДлинаИдентификатораСпецоператора + 1);
		КонецЕсли;
	КонецЕсли;
	
	// Отправка заявления
	
	ФормироватьЗакрытыйКлючИЗапросНаСертификат = ПереиздатьСертификат <> 0 ;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СформироватьИОтправитьЗаявлениеЗавершение", ЭтотОбъект);
	КонтекстЭДОКлиент.СформироватьИОтправитьЗаявление(
		ДокументЗаявление, 
		ИдентификаторАбонента, 
		Истина, 
		ОписаниеОповещения, 
		ФормироватьЗакрытыйКлючИЗапросНаСертификат
		);
		
КонецПроцедуры

&НаКлиенте
Процедура СформироватьИОтправитьЗаявлениеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Свойство("ТолькоИзменениеТелефона") Тогда
		Если Результат.Свойство("ТекстОшибки") Тогда
			ПолученныйТекстОшибокОтправки = Результат.ТекстОшибки;
		ИначеЕсли Результат.Свойство("ОписаниеОшибки") Тогда
			ПолученныйТекстОшибокОтправки = Результат.ОписаниеОшибки;
		КонецЕсли;
		
		// Анализируем 
		Если ТекстОшибокОтправки <> ПолученныйТекстОшибокОтправки Тогда
			ТекстОшибокОтправки = ПолученныйТекстОшибокОтправки;
		КонецЕсли;	
			
		ОповеститьОбИзменении(ДокументЗаявление.Ссылка);
		ЭтаФорма.Активизировать();
		
		// Меняем код ТОГС на тот, который был указан в заявлении
		Если ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено")
			И ИзменитьСоставКонтролирующихОрганов
			И ПолучателиФСГС.Количество() = 1
			И СокрЛП(ПолучателиФСГС[0].КодПолучателя) <> ДанныеОрганизации.КодОрганаФСГС Тогда
			
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗадатьКодОрганаФСГСВОрганизации(
				Организация, 
				СокрЛП(ПолучателиФСГС[0].КодПолучателя));
			
		КонецЕсли;
	Иначе
		ДокументЗаявление.Статус = ПредопределенноеЗначение("Перечисление.СтатусыЗаявленияАбонентаСпецоператораСвязи.Отправлено");
	КонецЕсли;	
		
	// Подтверждение смены номера телефона
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса 
		И (ТелефонИзменен ИЛИ ЭлектроннаяПочтаИзменена)
		И Не ПереиздатьСертификат Тогда
		Оповещение = Новый ОписаниеОповещения("СформироватьИОтправитьЗаявлениеПослеВводаКодаПодтвреждения", ЭтотОбъект);
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Сертификат", Сертификат);
		ПараметрыФормы.Вставить("Телефон", ?(ТелефонИзменен, ТелефонДляПаролей, Неопределено));
		ПараметрыФормы.Вставить("ЭлектроннаяПочта", ?(ЭлектроннаяПочтаИзменена, ЭлектроннаяПочтаДляПаролей, Неопределено));
		
		ОткрытьФорму(
			"Обработка.ДокументооборотСКонтролирующимиОрганами.Форма.ВводВременногоПароля",
			ПараметрыФормы,
			ЭтаФорма,,,,
			Оповещение);
	Иначе
		СформироватьИОтправитьЗаявлениеПослеВводаКодаПодтвреждения(КодВозвратаДиалога.ОК, Неопределено);		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьИОтправитьЗаявлениеПослеВводаКодаПодтвреждения(Результат, ВходящийКонтекст) Экспорт

	Если Результат = КодВозвратаДиалога.ОК Тогда
		ОткрытьФормуНаСтраницеРезультатаОтправки();
		
		ПоказатьСледующуюСтраницу();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Изменение номера телефона не было подтверждено'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТаблицуДляПодтвержденияДанных()
	
	ПорядковыйНомерСтроки = 0;
	
	ТаблицаДанныхЗаявленияНаПодключение.Очистить();
	
	// Общие сведения
	ДобавитьВТаблицу("Общие сведения",,,,Истина); // заголовок 
	
	Если Не ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		// Криптопровайдер
		Криптопровайдер = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПолучитьПараметрыКриптопровайдера(ТипКриптопровайдера);
		ОтображаемоеНазваниеКриптопровайдера = "";
		Если ЗначениеЗаполнено(Криптопровайдер) Тогда
			ОтображаемоеНазваниеКриптопровайдера = Криптопровайдер.ОтображаемоеНазвание;
		КонецЕсли;
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.Криптопровайдер"), 
			ОтображаемоеНазваниеКриптопровайдера, КриптопровайдерИзменился И ПереиздатьСертификат <> 0);
	КонецЕсли;	
		
	// Регистрационный номер программы
	Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда 
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.НомерОсновнойПоставки1С"),
			НомерОсновнойПоставки1с, НомерОсновнойПоставки1сИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.НомерОсновнойПоставки1С"), 
			НомерОсновнойПоставки1сИсходный, Ложь);
	КонецЕсли;
	
	Если ПереиздатьСертификат = 0 Тогда
		// Ложь
		ДобавитьВТаблицу(
			ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПереизданиеСертификата"), Ложь, Ложь);
	Иначе
		// Истина
		ДобавитьВТаблицу(
			ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПереизданиеСертификата"), Истина, Истина);
	КонецЕсли;
	
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПродлениеАбонентскогоСертификата"), 
		ПродлитьСертификат, ПродлитьСертификат);
	
	Если ПродлитьЛицензиюНа1СОтчетность Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПродлениеЛицензии"),
			"Да (может потребоваться дополнительная оплата)", ПродлитьЛицензиюНа1СОтчетность);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПродлениеЛицензии"), 
			"Нет", ПродлитьЛицензиюНа1СОтчетность);
	КонецЕсли;
	
	// Сведения об организации
	ДобавитьВТаблицу("Сведения об организации",,,,Истина); // заголовок
	
	Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КраткоеНаименование"), 
			КраткоеНаименование, КраткоеНаименованиеИзменилось);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПолноеНаименование"), 
			ПолноеНаименование, ПолноеНаименованиеИзменилось);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ИНН"), ИНН, Ложь); // ИНН запрещено менять
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КПП"), КПП, КППИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КраткоеНаименование"), 
			КраткоеНаименованиеИсходное, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ПолноеНаименование"), 
			ПолноеНаименованиеИсходное, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ИНН"), ИНН, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КПП"), КППИсходный, Ложь);
	КонецЕсли;
	
	Если ИзменитьРеквизитыПодключенияК1СОтчетности Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ОГРН"), 
			ОГРН, ОГРНИзменился);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.РегНомерПФР"), 
			РегНомерПФР, РегНомерПФРИзменился);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.РегНомерФСС"), 
			РегНомерФСС, РегНомерФССИзменился);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРегионаФСРАР"), 
			КодРегионаФСРАР, КодРегионаФСРАРИзменился);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной"), 
			ТелефонОсновной, ТелефонОсновнойИзменился);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный"), 
			ТелефонДополнительный, ТелефонДополнительныйИзменился);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта"), 
			ЭлектроннаяПочта, ЭлектроннаяПочтаИзменилась); 
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ОГРН"), 
			ОГРНИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.РегНомерПФР"), 
			РегНомерПФРИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.РегНомерФСС"), 
			РегНомерФССИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРегионаФСРАР"), 
			КодРегионаФСРАРИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной"), 
			ТелефонОсновнойИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный"), 
			ТелефонДополнительныйИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта"), 
			ЭлектроннаяПочтаИсходная, Ложь); 
	КонецЕсли;
	
	// Сведения о сотруднике - владельце электронной подписи
	ДобавитьВТаблицу("Сведения о владельце электронной подписи",,,,Истина); // заголовок
	
	Если ИзменитьВладельцаСертификата Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦП"), 
			ВладелецЭЦП, ВладелецЭЦПИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦП"), 
			ВладелецЭЦПИсходный, Ложь);
	КонецЕсли;
	
	Если (ИзменитьРеквизитыПодключенияК1СОтчетности ИЛИ ИзменитьВладельцаСертификата) Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДолжность"), 
			ВладелецЭЦПДолжность, ВладелецЭЦПДолжностьИзменилась);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППодразделение"), 
			ВладелецЭЦППодразделение, ВладелецЭЦППодразделениеИзменилось);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС"), 
			ВладелецЭЦПСНИЛС, ВладелецЭЦПСНИЛСИзменился);
	Иначе 
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДолжность"), 
			ВладелецЭЦПДолжностьИсходная, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППодразделение"), 
			ВладелецЭЦППодразделениеИсходное, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС"), 
			ВладелецЭЦПСНИЛСИсходный, Ложь);
	КонецЕсли;
		
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДатаРождения"), 
		Формат(ВладелецЭЦПДатаРождения, "ДЛФ=D"), Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППол"), 
		ВладелецЭЦППол, Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПГражданство"), 
		ВладелецЭЦПГражданство, Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПВидДокумента"), 
		ВладелецЭЦПВидДокумента, Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСерияДокумента"), 
		Строка(ВладелецЭЦПСерияДокумента), Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПНомерДокумента")
		, Строка(ВладелецЭЦПНомерДокумента), Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПКемВыданДокумент"), 
		ВладелецЭЦПКемВыданДокумент, Ложь);
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДатаВыдачиДокумента"), 
		Формат(ВладелецЭЦПДатаВыдачиДокумента, "ДЛФ=D"), Ложь);
		
	Если ВладелецЭЦПВидДокумента = ПаспортРФ Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПКодПодразделения"), 
			ВладелецЭЦПКодПодразделения, Ложь);
	КонецЕсли;
		
	ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПМестоРождения"), 
		ВладелецЭЦПМестоРождения, Ложь);
	
	Если ОператорПоддерживаетСМСУведомление Тогда
		Если ИзменитьМобильныйТелефон Тогда
			ТелефонМобильныйБезРазделителей = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонМобильный);
			Если ПустаяСтрока(ТелефонМобильныйБезРазделителей) Тогда
				ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный"), 
					"", ТелефонМобильныйИзменился);
			Иначе
				ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный"), 
					ТелефонМобильный, ТелефонМобильныйИзменился);
			КонецЕсли;
		Иначе
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ТелефонМобильный"), 
				ТелефонМобильныйИсходный, Ложь);
		КонецЕсли;
	КонецЕсли;
	
	// Сведения о контролирующих органах
	ДобавитьВТаблицу("Сведения о контролирующих органах",,,,Истина); // заголовок
	
	// ФНС
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФНС")
			, СдаватьВФНС, СдаватьВФНСИзменился);
		Если СдаватьВФНС Тогда
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодыФНС"), 
				КодыФНСПрописью, КодыФНСИзменились);
		Иначе
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодыФНС"), "", Ложь);
		КонецЕсли;
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФНС"), 
			СдаватьВФНСИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодыФНС"), 
			КодыФНСПрописьюИсходные, Ложь);
	КонецЕсли;
	
	// ПФР
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВПФР"), 
			СдаватьВПФР, СдаватьВПФРИзменился);
		Если СдаватьВПФР Тогда
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодПФР"), 
				КодПФР, КодПФРИзменился);
		Иначе
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодПФР"), "", Ложь);
		КонецЕсли;
	Иначе
	    ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВПФР"), 
			СдаватьВПФРИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодПФР"), 
			КодПФРИсходный, Ложь);
	КонецЕсли;
		
	// ФСС
	Если ИзменитьСоставКонтролирующихОрганов Тогда
 		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФСС"), 
			СдаватьВФСС, СдаватьВФССИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФСС"),
			СдаватьВФССИсходный, Ложь);
	КонецЕсли;
	
	// Росстат
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВРосстат"), 
			СдаватьВРосстат, СдаватьВРосстатИзменился);
			
		Если СдаватьВРосстат Тогда
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРосстата"), 
				КодыРосстатПрописью, КодыРосстатаИзменились);
		Иначе
			ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРосстата"), "", Ложь);
		КонецЕсли;
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВРосстат"), 
			СдаватьВРосстатИсходный, Ложь);
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРосстата"), 
			КодыРосстатПрописьюИсходные, Ложь);
	КонецЕсли;
	
	// Росалкогольрегулирование
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФСРАР"), 
			СдаватьВФСРАР, СдаватьВФСРАРИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФСРАР"), 
			СдаватьВФСРАРИсходный, Ложь);
	КонецЕсли;
	
	// Росприроднадзор
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВРПН"), 
			СдаватьВРПН, СдаватьВРПНИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВРПН"), 
			СдаватьВРПНИсходный, Ложь);
	КонецЕсли;
	
	// ФТС
	Если ИзменитьСоставКонтролирующихОрганов Тогда
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФТС"), 
			СдаватьВФТС, СдаватьВФТСИзменился);
	Иначе
		ДобавитьВТаблицу(ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.СдаватьВФТС"), 
			СдаватьВФТСИсходный, Ложь);
	КонецЕсли;
	
	// Отображение данных по подключению ЭДО
	Если ОтображатьПодключениеЭДО Тогда
		ДобавитьВТаблицу("Подключение к сервису 1С-ЭДО",,,,Истина); // заголовок 
		ДобавитьВТаблицу("Подключиться к сервису", ПодключитьЭДО);
		Если ПодключитьЭДО Тогда
			ДобавитьВТаблицу("Оператор ЭДО", ОператорЭДО);
			ДобавитьВТаблицу("Код ФНС", КодНалоговогоОрганаЭДО);
			ДобавитьВТаблицу("Юридический адрес", АдресЮридический);	
		КонецЕсли;
	КонецЕсли;
	
	// Выделяем желтым измененные строки
	ДобавитьЖелтыеСтроки();
	
	ОтметитьНеХранящиесяРеквизиты();
	
	ПроставитьПризнакОшибкиИДоступностиДляРедактированияКонтролируемыхРеквизитов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыИзПредыдущегоЗаявления()
	
	ПолучателиФНС.Очистить();
	СброситьНаправленияСдачиОтчетности(ЭтотОбъект);
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	ТелефонОсновной					= Реквизит.ТелефонОсновной;
	ТелефонДополнительный			= Реквизит.ТелефонДополнительный;
	ТелефонМобильный				= Реквизит.ТелефонМобильный;
	ЭлектроннаяПочта				= Реквизит.ЭлектроннаяПочта;

	ВладелецЭЦПФамилия				= Реквизит.ВладелецЭЦПФамилия;
	ВладелецЭЦПИмя					= Реквизит.ВладелецЭЦПИмя;
	ВладелецЭЦПОтчество				= Реквизит.ВладелецЭЦПОтчество;
	ВладелецЭЦПДолжность			= Реквизит.ВладелецЭЦПДолжность;
	ВладелецЭЦППодразделение		= Реквизит.ВладелецЭЦППодразделение;
	ВладелецЭЦПВидДокумента			= Реквизит.ВладелецЭЦПВидДокумента;
	ВладелецЭЦПСерияДокумента		= Реквизит.ВладелецЭЦПСерияДокумента;
	ВладелецЭЦПНомерДокумента		= Реквизит.ВладелецЭЦПНомерДокумента;
	ВладелецЭЦПДатаВыдачиДокумента	= Реквизит.ВладелецЭЦПДатаВыдачиДокумента;
	ВладелецЭЦПКемВыданДокумент		= Реквизит.ВладелецЭЦПКемВыданДокумент;
	ДополнительныйКодФСС			= Реквизит.ДополнительныйКодФСС;
	НомерОсновнойПоставки1с			= Реквизит.НомерОсновнойПоставки1с;
	СдаватьВФСРАР					= Реквизит.ПодатьЗаявкуНаСертификатДляФСРАР;
	КодРегионаФСРАР					= Реквизит.КодРегионаФСРАР;
	СдаватьВРПН						= Реквизит.ПодатьЗаявкуНаПодключениеРПН;
	СдаватьВФТС						= Реквизит.ПодатьЗаявкуНаПодключениеФТС;
	ВладелецЭЦПСНИЛС				= Реквизит.ВладелецЭЦПСНИЛС;
	ОГРН							= Реквизит.ОГРН;
	
	Для Каждого СтрокаНаправления из Реквизит.Получатели цикл 
		
		Если СтрокаНаправления.ТипПолучателя = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС") Тогда
			СдаватьВФНС = Истина;
			
			НоваяСтрокаНаправления = ПолучателиФНС.Добавить();
			НоваяСтрокаНаправления.ТипПолучателя	= СтрокаНаправления.ТипПолучателя;
			НоваяСтрокаНаправления.КодПолучателя	= СтрокаНаправления.КодПолучателя;
			НоваяСтрокаНаправления.КПП				= СтрокаНаправления.КПП;
			
		ИначеЕсли СтрокаНаправления.ТипПолучателя	= ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР") Тогда
			СдаватьВПФР = Истина;
			КодПФР = СтрокаНаправления.КодПолучателя;
		ИначеЕсли СтрокаНаправления.ТипПолучателя = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС") Тогда
			СдаватьВФСС = Истина;	
		ИначеЕсли СтрокаНаправления.ТипПолучателя = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСГС") Тогда	
			СдаватьВРосстат = Истина;
			
			НоваяСтрокаНаправления = ПолучателиФСГС.Добавить();
			НоваяСтрокаНаправления.ТипПолучателя	= СтрокаНаправления.ТипПолучателя;
			НоваяСтрокаНаправления.КодПолучателя	= СтрокаНаправления.КодПолучателя;
			НоваяСтрокаНаправления.КПП				= СтрокаНаправления.КПП;
			
		КонецЕсли;
	КонецЦикла;
	
	ВладелецЭЦПТип = Реквизит.ВладелецЭЦПТип;
	
	//заполнение дополнительных реквизитов формы, которых нет в документе
	СогласиеСЛицензионнымСоглашением = Истина;
	
	Если НЕ ПустаяСтрока(ТелефонМобильный) Тогда
		ПолучатьСМС = Истина;
	КонецЕсли;
	
	// Сбрасываем все установленнные флажки
	СброситьВсеФлажки(ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область Гиперссылки

&НаКлиенте
Процедура ОткрытьПодсказкуПоЭЦП(Команда)
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ФормаПодсказкиПоЭЦП");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокПартнеров1С(Команда)
	ОбщегоНазначенияКлиент.ПерейтиПоСсылке("http://www.1c.ru/rus/partners/onecrep.jsp");
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСсылкуНаСтатьюПоПодключению(Команда)
	
	ОбщегоНазначенияКлиент.ПерейтиПоСсылке("http://its.1c.ru/db/metod81#content:5274:1");
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСлужебныеДанныеНаСервере()	
	
	ЭтоЭлектроннаяПодписьВМоделиСервиса = Ложь;
	Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.РаботаВМоделиСервиса.ЭлектроннаяПодписьВМоделиСервиса") Тогда
		МодульЭлектроннаяПодписьВМоделиСервиса = ОбщегоНазначения.ОбщийМодуль("ЭлектроннаяПодписьВМоделиСервисаКлиентСервер");
		Если МодульЭлектроннаяПодписьВМоделиСервиса.ИспользованиеВозможно() Тогда
			Сертификат = ЭлектроннаяПодписьВМоделиСервисаБРОВызовСервера.ПолучитьСертификатПоОрганизации(Организация);
			Если ЗначениеЗаполнено(Сертификат) Тогда
				НастройкиПолученияВременныхПаролей = ПолучитьСпособыДоставкиПаролей(Сертификат.Идентификатор);
				ТелефонДляПаролейИсходный = НастройкиПолученияВременныхПаролей.Телефон;
				ЭлектроннаяПочтаДляПаролейИсходная = НастройкиПолученияВременныхПаролей.ЭлектроннаяПочта;
				
				ЭтоЭлектроннаяПодписьВМоделиСервиса = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		ТелефонДляПаролей = ТелефонДляПаролейИсходный;
		ЭлектроннаяПочтаДляПаролей = ЭлектроннаяПочтаДляПаролейИсходная;
	КонецЕсли;
	
	Элементы.ИнструкцияПоСозданиюКлючаЭЦП.Видимость = Не ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	ЧастиСтроки = Новый Массив;
	ЧастиСтроки.Добавить(СтрШаблон("Изменение номера необходимо будет подтвердить, введя код отправленный на %1." + Символы.ПС, ТелефонДляПаролейИсходный));
	ЧастиСтроки.Добавить("Если этот телефон больше не доступен, то воспользуйтесь ");
	ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока("инструкцией",,,, "#Инструкция"));
	ЧастиСтроки.Добавить(".");
	Элементы.ПояснениеПодтвержденияИзмененияТелефона.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтроки);
	ЧастиСтроки[0] = СтрШаблон("Изменение адреса необходимо будет подтвердить, введя код отправленный на %1." + Символы.ПС, ТелефонДляПаролейИсходный); 
	Элементы.ПояснениеПодтвержденияИзмененияЭлектроннойПочты.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтроки);
	
	
	Если ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		Элементы.ИзменитьМобильныйТелефон.Заголовок = НСтр("ru = 'Изменение настроек уведомлений о статусе отправки
                                                            |отчетов и входящих сообщениях, получения временных паролей:'");
		Элементы.ПолучатьУведомления.ОтображениеПодсказки = ОтображениеПодсказки.Кнопка;
	Иначе
		Элементы.ИзменитьМобильныйТелефон.Заголовок = НСтр("ru = 'Изменение настроек уведомлений о статусе отправки
                                                            |отчетов и входящих сообщениях:'");
		Элементы.ПолучатьУведомления.ОтображениеПодсказки = ОтображениеПодсказки.Нет;														
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСпособыДоставкиПаролей(Идентификатор)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("ТехнологияСервиса.РаботаВМоделиСервиса.ЭлектроннаяПодписьВМоделиСервиса") Тогда
		МодульСервисКриптографии = ОбщегоНазначения.ОбщийМодуль("СервисКриптографии");
		Попытка
			Возврат МодульСервисКриптографии.ПолучитьНастройкиПолученияВременныхПаролей(Идентификатор);
		Исключение
			Возврат Новый Структура("Телефон, ЭлектроннаяПочта", "", "");
		КонецПопытки;
	Иначе
		Возврат Новый Структура("Телефон, ЭлектроннаяПочта", "", "");
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция УчетнаяЗаписьОрганизации(Организация) Экспорт
	
	ВидОбменаСКонтролирующимиОрганами = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ВидОбменаСКонтролирующимиОрганами");
	Если ВидОбменаСКонтролирующимиОрганами = Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате Тогда
		Возврат Организация.УчетнаяЗаписьОбмена;
	Иначе
		Возврат Справочники.УчетныеЗаписиДокументооборота.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

&НаСервере
Функция СпецоператорИзУчетнойЗаписи(УчетнаяЗапись, ОператорПоддерживаетСМСУведомление = Неопределено, ОператорПоддерживаетФСРАР = Неопределено, ОператорПоддерживаетРПН = Неопределено, ОператорПоддерживаетФТС = Неопределено)
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	ОператорПоддерживаетСМСУведомление = (ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(УчетнаяЗапись.СпецоператорСвязи, "ПризнакПоддержкиСМС") = "Истина");
	
	ОператорПоддерживаетФСРАРЗначение = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(УчетнаяЗапись.СпецоператорСвязи, "ФСРАРПризнак");
	ОператорПоддерживаетФСРАР = (ОператорПоддерживаетФСРАРЗначение = Истина ИЛИ ОператорПоддерживаетФСРАРЗначение = "Истина");
	
	ОператорПоддерживаетРПНЗначение = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(УчетнаяЗапись.СпецоператорСвязи, "РПНПризнак");
	ОператорПоддерживаетРПН = (ОператорПоддерживаетРПНЗначение = Истина ИЛИ ОператорПоддерживаетРПНЗначение = "Истина");
	
	ОператорПоддерживаетФТСЗначение = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(УчетнаяЗапись.СпецоператорСвязи, "ФТСПризнак");
	ОператорПоддерживаетФТС = (ОператорПоддерживаетФТСЗначение = Истина ИЛИ ОператорПоддерживаетФТСЗначение = "Истина");
	
	Возврат УчетнаяЗапись.СпецоператорСвязи;
	
КонецФункции

&НаКлиенте
Процедура ЗаменитьЗапятуюНаИВСтроке(Текст)

	// В списке реквизитов заменяем последнюю запятую на букву "и"
	Если СтрЧислоВхождений(Текст, "%1") = 1 Тогда
		
		Текст = СтрЗаменить(Текст, "%1", " и ");
		
	ИначеЕсли СтрЧислоВхождений(Текст, "%1") > 1 Тогда
		
		МассивПодстрок 				= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Текст, "%1");
		ПоследнийЭлементМассива 	= МассивПодстрок[МассивПодстрок.Количество() - 1];
		Текст 						= СтрЗаменить(Текст, "%1" + ПоследнийЭлементМассива, " и " + ПоследнийЭлементМассива);
		
	КонецЕсли;
	
	Текст = СтрЗаменить(Текст, "%1", ", ");

КонецПроцедуры

&НаСервере
Функция ЕстьРеквизитДопКодФССУОрганизации()
	Возврат Метаданные.Справочники.Организации.Реквизиты.Найти("ДополнительныйКодФСС") <> Неопределено;
КонецФункции

&НаСервере
Функция НаименованиеСубъектаРФ(КодРегиона)
	
	Результат = "";
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	МакетКодыРегионов = КонтекстЭДОСервер.ПолучитьМакет("ФСРАРПорталыРегионов");
	
	НомерКолонкиКодРегиона = МакетКодыРегионов.Область("КодРегиона").Лево;
	НомерКолонкиНаименованиеРегиона = МакетКодыРегионов.Область("НаименованиеРегиона").Лево;
	ОбластьПоискаКодаРегиона = МакетКодыРегионов.Область(1, НомерКолонкиКодРегиона, МакетКодыРегионов.ВысотаТаблицы, НомерКолонкиКодРегиона);
	
	ОбластьСКодомРегиона = МакетКодыРегионов.НайтиТекст(Формат(Число(КодРегиона), "ЧН=0; ЧГ=; ЧЦ=2; ЧВН=;"), , ОбластьПоискаКодаРегиона, , Истина);
	
	Если ОбластьСКодомРегиона <> Неопределено Тогда
		Результат = МакетКодыРегионов.Область(ОбластьСКодомРегиона.Верх, НомерКолонкиНаименованиеРегиона, ОбластьСКодомРегиона.Верх, НомерКолонкиНаименованиеРегиона).Текст;
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПреобразоватьНомерТелефонаКМаскеСервер(НомерТелефона)
	
	// Маска +7 (999) 999-99-99
	// Номер телефона 89851234567
	КодВСкобках =  Сред(НомерТелефона, 2,3);
	ПервыйБлокЧисел = Сред(НомерТелефона, 5,3);
	ВторойБлокЧисел = Сред(НомерТелефона, 8,2);
	ТретийБлокЧисел = Сред(НомерТелефона, 10,2);

	Результат = "";
	Если НЕ ПустаяСтрока(СтрЗаменить(НомерТелефона, "-", "")) Тогда
		Результат = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					  "+7 (%1) %2-%3-%4",
					  КодВСкобках,
					  ПервыйБлокЧисел,
					  ВторойБлокЧисел,
					  ТретийБлокЧисел)
	КонецЕсли;
				  
	Возврат Результат;  

КонецФункции

&НаКлиенте
Процедура СравнитьКриптопровайдера()

	ТипКриптопровайдера = КонтекстЭДОКлиент.КриптопровайдерИзНастроекПрограммы();
	Если НЕ ЗначениеЗаполнено(ТипКриптопровайдера) Тогда
		ТипКриптопровайдера = ТипКриптопровайдераНаКомпьютере;
	КонецЕсли;
	
	// Сравниваем криптопровайдеры
	КриптопровайдерИзменился = ТипКриптопровайдераИсходный <> ТипКриптопровайдера;

	// Отображаем наименование на форме
	Криптопровайдер = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПолучитьПараметрыКриптопровайдера(ТипКриптопровайдера);
	ОтображаемоеНазваниеКриптопровайдера = "";
	Если ЗначениеЗаполнено(Криптопровайдер) Тогда
		ОтображаемоеНазваниеКриптопровайдера = Криптопровайдер.ОтображаемоеНазвание;
	КонецЕсли;
	
	УстановитьНаименованияКриптопровайдеровНаФорме(ОтображаемоеНазваниеКриптопровайдера);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокУстановленныхКриптопровайдеров(ВыполняемоеОповещение)
	
	Если НЕ ЗначениеЗаполнено(Организация) ИЛИ ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);
		Возврат;
	КонецЕсли;
	
	Инфотекс	 = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПолучитьПараметрыКриптопровайдера(КонтекстЭДОКлиент.СтруктураСсылочныхДанных.Перечисления_ТипыКриптоПровайдеров_VipNet);
	КриптоПро	 = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПолучитьПараметрыКриптопровайдера(КонтекстЭДОКлиент.СтруктураСсылочныхДанных.Перечисления_ТипыКриптоПровайдеров_CryptoPro);
	
	НоваяСтрока = ТаблицаЗначенийКС.Добавить();
	НоваяСтрока.Криптопровайдер	 = Инфотекс.ОтображаемоеНазвание;
	НоваяСтрока.УстановленОС	 = Ложь;
	НоваяСтрока.Выбран1с		 = Ложь;
	
	НоваяСтрока = ТаблицаЗначенийКС.Добавить();
	НоваяСтрока.Криптопровайдер	 = КриптоПро.ОтображаемоеНазвание;
	НоваяСтрока.УстановленОС	 = Ложь;
	НоваяСтрока.Выбран1с		 = Ложь;
	
	
	Контекст = Новый Структура("Инфотекс, КриптоПро, ВыполняемоеОповещение", Инфотекс, КриптоПро, ВыполняемоеОповещение);
	Оповещение = Новый ОписаниеОповещения("ЗаполнитьСписокУстановленныхКриптопровайдеровЗавершение", ЭтотОбъект, Контекст);
	КриптографияЭДКОКлиент.ПолучитьКриптопровайдеры(Оповещение);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьСписокУстановленныхКриптопровайдеровЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	ВыполняемоеОповещение = ВходящийКонтекст.ВыполняемоеОповещение;
	Инфотекс = ВходящийКонтекст.Инфотекс;
	КриптоПро = ВходящийКонтекст.КриптоПро;

	Если Результат.Выполнено Тогда
		Для Каждого Криптопровайдер Из Результат.Криптопровайдеры Цикл
			Если Криптопровайдер.Имя = Инфотекс.Имя И Строка(Криптопровайдер.Тип) = Инфотекс.Тип Тогда 
				ТаблицаЗначенийКС.Получить(0).УстановленОС = Истина;
			ИначеЕсли Криптопровайдер.Имя = КриптоПро.Имя И Строка(Криптопровайдер.Тип) = КриптоПро.Тип Тогда 
				ТаблицаЗначенийКС.Получить(1).УстановленОС = Истина;
			КонецЕсли;			
		КонецЦикла;
	КонецЕсли;
		
	КоличествоУстановленных = 0;
	
	// Определение, какой криптопровайдер установлен
	Для каждого Строка из ТаблицаЗначенийКС Цикл
		
		Инфотекс = Строка.Криптопровайдер =
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ПолучитьПараметрыКриптопровайдера(КонтекстЭДОКлиент.СтруктураСсылочныхДанных.Перечисления_ТипыКриптоПровайдеров_VipNet).ОтображаемоеНазвание;
		
		Если Строка.УстановленОС Тогда
			Строка.Статус = "(установлен на компьютере)";
			КоличествоУстановленных = КоличествоУстановленных + 1;
		ИначеЕсли  Инфотекс Тогда
			Строка.Статус = "(требуется установка)";
		Иначе
			Строка.Статус = "(требуется приобретение и установка)";
		КонецЕсли;
		
		Строка.ОтображаемыйТекст = Строка.Криптопровайдер + " " + Строка.Статус;
		
	КонецЦикла;
	
	Для каждого Строка из ТаблицаЗначенийКС Цикл
		Если Строка.УстановленОС Тогда
								
			Если Строка.Криптопровайдер = "VipNet CSP" Тогда
				ВыборКриптопровайдера = 1;
			Иначе
				ВыборКриптопровайдера = 2;
			КонецЕсли;
			
			Если ВыборКриптопровайдера = 1 Тогда
				ТипКриптопровайдераНаКомпьютере = КонтекстЭДОКлиент.СтруктураСсылочныхДанных.Перечисления_ТипыКриптоПровайдеров_VipNet;
			Иначе
				ТипКриптопровайдераНаКомпьютере = КонтекстЭДОКлиент.СтруктураСсылочныхДанных.Перечисления_ТипыКриптоПровайдеров_CryptoPro;
			КонецЕсли;
			
			УстановитьНаименованияКриптопровайдеровНаФорме(Строка.Криптопровайдер);
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение);

КонецПроцедуры

&НаСервере
Функция ИмяПеречисленияПараметрыПодключенияК1СОтчетности(ЭлементПеречисления)
	
	ИмяПеречисления = ЭлементПеречисления.Метаданные().Имя;
	ИндексЗначенияПеречисления = Перечисления.ПараметрыПодключенияК1СОтчетности.Индекс(ЭлементПеречисления);
	Возврат Метаданные.Перечисления.ПараметрыПодключенияК1СОтчетности.ЗначенияПеречисления[ИндексЗначенияПеречисления].Имя;

КонецФункции
	
&НаКлиенте
Процедура ПоддерживаетсяОтправкаВторичныхЗаявлений(ВыполняемоеОповещение)
	
	ДополнительныеПараметры = Новый Структура("ВыполняемоеОповещение", ВыполняемоеОповещение);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоддерживаетсяОтправкаВторичныхЗаявленийЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	
	КонтекстЭДОКлиент.ЕстьДанныеДляФормированияВторичногоЗаявления(
		ОписаниеОповещения,
		УчетнаяЗапись);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоддерживаетсяОтправкаВторичныхЗаявленийЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	ПоддерживаетсяВторичноеЗаявление = Истина;
	ЕстьДанныеДляФормированияВторичногоЗаявления = Результат.ЕстьДанные;
	УжеНастроенаУчетнаяЗаписьДокументооборота = ЗначениеЗаполнено(КонтекстЭДОКлиент.УчетнаяЗаписьОрганизации(Организация));
	СпецоператорПоддерживаетВторичныеЗаявления = КонтекстЭДОКлиент.ПоддерживаетсяВторичноеЗаявление(Организация);
	
	// Проверяем возможность создания вторичного заявления
	Если НЕ УжеНастроенаУчетнаяЗаписьДокументооборота ИЛИ НЕ СпецоператорПоддерживаетВторичныеЗаявления ИЛИ НЕ ЕстьДанныеДляФормированияВторичногоЗаявления Тогда
		
		ПоддерживаетсяВторичноеЗаявление = Ложь;
		ОформитьЭлементыФормыНаПервомШаге(ДанныеОрганизации);
		
		Если НЕ УжеНастроенаУчетнаяЗаписьДокументооборота Тогда
			
			// Случай, когда организация не подключена к 1С-Отчетности
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПоказатьФормуПредложениеОформитьЗаявлениеНаПодключение(Организация);
			
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление);

		ИначеЕсли НЕ СпецоператорПоддерживаетВторичныеЗаявления Тогда
			
			// Случай, когда оператор электронного документооборота не поддерживает отправку вторичных заявлений
			ПоказатьПредупреждение(, НСтр("ru = 'Оператор электронного документооборота не поддерживает отправку вторичных заявлений.
									|Для изменения настроек подключения к 1С-Отчетности свяжитесь со службой поддержки оператора электронного документооборота'"));
									
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление);
		
		ИначеЕсли НЕ ЕстьДанныеДляФормированияВторичногоЗаявления Тогда
			
			// Случай, когда невозможно получить новый рег файл 
			ТекстОшибокДляМастераПодключенияК1СОтчетности = "";
			
			ДополнительныеПараметры = Новый Структура("ТекстОшибокДляМастераПодключенияК1СОтчетности, УчетнаяЗапись, ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление", ТекстОшибокДляМастераПодключенияК1СОтчетности, УчетнаяЗапись, ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПоддерживаетсяОтправкаВторичныхЗаявленийПовторныйЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			
			КонтекстЭДОКлиент.ЕстьДанныеДляФормированияВторичногоЗаявления(
				ОписаниеОповещения,
				УчетнаяЗапись,
				ТекстОшибокДляМастераПодключенияК1СОтчетности);

		Иначе
			
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление);

		КонецЕсли;
	Иначе
		
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоддерживаетсяОтправкаВторичныхЗаявленийПовторныйЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ЕстьДанные = Результат.ЕстьДанные;
	ТекстОшибокДляМастераПодключенияК1СОтчетности = Результат.ТекстОшибок;
	УчетнаяЗапись = ДополнительныеПараметры.УчетнаяЗапись;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	ПоддерживаетсяВторичноеЗаявление = ДополнительныеПараметры.ПоддерживаетсяВторичноеЗаявление;
	
	Если НЕ ЕстьДанные Тогда
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("УчетнаяЗапись", УчетнаяЗапись);
		ПараметрыФормы.Вставить("ТекстОшибки", ТекстОшибокДляМастераПодключенияК1СОтчетности);
		ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ПредупреждениеОНевозможностиПолучитьНастройкиУчетнойЗаписи", ПараметрыФормы);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ПоддерживаетсяВторичноеЗаявление);

КонецПроцедуры

&НаКлиенте
Процедура НаправлениеСдачиОтчетностиПодключено(ИмяФлагаНаправленияСдачиОтчетности, НаправлениеСдачиОтчетности, ИзначальноНаправлениеБылоПодключено, ВыполняемоеОповещение)

	НаправлениеПодключено = ЭтаФорма[ИмяФлагаНаправленияСдачиОтчетности];
	
	Результат = НаправлениеПодключено;
	
	Если ИзначальноНаправлениеБылоПодключено И НЕ НаправлениеПодключено Тогда
		
		ЭтаФорма[ИмяФлагаНаправленияСдачиОтчетности] = Истина;
		
		ТекстВопрос = НСтр("ru = 'Отправка отчетности в %1 станет недоступной после отключения данного направления.
		|Вы уверены, что хотите отключить направление сдачи отчетности %1 ?'");
		ТекстВопрос = СтрЗаменить(ТекстВопрос, "%1", НаправлениеСдачиОтчетности);
		ОписаниеОповещения = Новый ОписаниеОповещения("НаправлениеСдачиОтчетностиПодключеноЗавершение", ЭтотОбъект, ВыполняемоеОповещение);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопрос, РежимДиалогаВопрос.ДаНет);
		
	Иначе
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НаправлениеСдачиОтчетностиПодключеноЗавершение(Ответ, ВыполняемоеОповещение) Экспорт
	
	Результат = (Ответ <> КодВозвратаДиалога.Да);
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	// Изменение номера телефона
	Элементы.ТелефонМобильный.Видимость = Не Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	Элементы.ТелефонДляПаролей.Видимость = Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	Элементы.ПояснениеПодтвержденияИзмененияТелефона.Видимость = 
		Не Форма.ПереиздатьСертификат И Форма.ТелефонИзменен И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	Элементы.ПояснениеПодтвержденияИзмененияЭлектроннойПочты.Видимость = 
		Не Форма.ПереиздатьСертификат И Форма.ЭлектроннаяПочтаИзменена И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	Элементы.ОтступМеждуТелефономИЭлектроннойПочтой.Видимость = Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	Элементы.ГруппаЭлектроннаяПочта.Видимость = Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	Элементы.ТелефонДляПаролей.ТолькоПросмотр = Форма.ВыполняетсяПроверкаТелефона ИЛИ Форма.ТелефонПодтвержден;
	Элементы.КартинкаТелефонПроверен.Видимость = Форма.ТелефонПодтвержден И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	Элементы.ПроверитьНомер.Видимость = 
		Не Форма.ВыполняетсяПроверкаТелефона И Не Форма.ТелефонПодтвержден 
		И Форма.ТелефонИзменен И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	Элементы.ГруппаКодПодтвержденияТелефон.Видимость = 
		Форма.ВыполняетсяПроверкаТелефона И Не Форма.ТелефонПодтвержден И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	Элементы.НадписьОбратногоОтсчетаТелефон.Видимость =
		Форма.КодОтправлен И Форма.ВыполняетсяПроверкаТелефона И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;	
	Элементы.ОтправитьКодПовторноТелефон.Видимость = 
		Не Форма.КодОтправлен И Форма.ВыполняетсяПроверкаТелефона И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	
	Элементы.ЭлектроннаяПочтаДляПаролей.ТолькоПросмотр = Форма.ВыполняетсяПроверкаЭлектроннойПочты ИЛИ Форма.ЭлектроннаяПочтаПодтверждена;
	Элементы.КартинкаЭлектроннаяПочтаПроверена.Видимость = Форма.ЭлектроннаяПочтаПодтверждена И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	Элементы.ПроверитьАдрес.Видимость = 
		Не Форма.ВыполняетсяПроверкаЭлектроннойПочты И Не Форма.ЭлектроннаяПочтаПодтверждена
		И Форма.ЭлектроннаяПочтаИзменена И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса И ЗначениеЗаполнено(Форма.ЭлектроннаяПочтаДляПаролей);
	Если Форма.ЭлектроннаяПочтаИзменена И ЗначениеЗаполнено(Форма.ЭлектроннаяПочтаДляПаролей) Тогда
		Элементы.ЭлектроннаяПочтаДляПаролей.ОтображениеПодсказки = ОтображениеПодсказки.Авто;
	КонецЕсли;
	
	Элементы.ГруппаКодПодтвержденияЭлектроннаяПочта.Видимость = 
		Форма.ВыполняетсяПроверкаЭлектроннойПочты И Не Форма.ЭлектроннаяПочтаПодтверждена И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	Элементы.НадписьОбратногоОтсчетаЭлектроннаяПочта.Видимость = 
		Форма.КодОтправлен И Форма.ВыполняетсяПроверкаЭлектроннойПочты И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;	
	Элементы.ОтправитьКодПовторноЭлектроннаяПочта.Видимость = 
		Не Форма.КодОтправлен И Форма.ВыполняетсяПроверкаЭлектроннойПочты И Форма.ЭтоЭлектроннаяПодписьВМоделиСервиса;
	
	
	//Подключение ЭДО
	Элементы.ПодключениеЭДО.Видимость = Форма.ОтображатьПодключениеЭДО;
	Если Форма.ОтображатьПодключениеЭДО Тогда
		Элементы.ГруппаПараметрыСервиса.Доступность	= Форма.ПодключитьЭДО;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура УстановитьГалкуПереиздатьСертификатИОпределитьПричиныПереиздания(ЭтоПопыткаРучногоИзменения = Ложь)
	
	ИзменилисьРеквизитыСертификата = ИзменилисьРеквизитыТребующиеПереизданияСертификата();
	
	Если ИзменилисьРеквизитыСертификата Тогда
		
		// Оставляем без изменения, так как нельзя снимать
		ПереиздатьСертификат = 2;
		
	Иначе
		
		НеобходимоСброситьФлаг = ЭтоПопыткаРучногоИзменения И ПереиздатьСертификат = 2 
			ИЛИ НЕ ЭтоПопыткаРучногоИзменения И СертификатДоступен;
		
		Если НеобходимоСброситьФлаг Тогда
			ПереиздатьСертификат = 0;
		Иначе
			ПереиздатьСертификат = 1;
		КонецЕсли;
				
	КонецЕсли;
	
	ОпределитьПричинуПереизданияСертификата();
	
КонецПроцедуры

&НаКлиенте
Процедура ОпределитьПричинуПереизданияСертификата()
	
	ТаблицаПричинПереизданияСертификата.Очистить();
	ПричиныПереизданияСертификата = "";
	
	Элементы.ПричиныПереизданияСертификата.ЦветТекста = СинийЦветПодсказки;
	
	Если ПереиздатьСертификат = 2 Тогда
		// Флажок установлен без возможности снятия
		
		// Краткое наименование
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КраткоеНаименование");
		НоваяСтрока.Изменился	= КраткоеНаименованиеИзменилось И ИзменитьРеквизитыПодключенияК1СОтчетности;
		НоваяСтрока.Группа		= "Организация";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменилось'");
		
		// ОГРН
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ОГРН");
		НоваяСтрока.Изменился	= ОГРНИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности;
		НоваяСтрока.Группа		= "Организация";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменился'");
		
		// Регион
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.КодРегионаФСРАР");
		НоваяСтрока.Изменился	= КодРегионаФСРАРИзменился И ИзменитьРеквизитыПодключенияК1СОтчетности И 
							(ИзменитьСоставКонтролирующихОрганов И СдаватьВФСРАР ИЛИ НЕ ИзменитьСоставКонтролирующихОрганов И СдаватьВФСРАРИсходный);
		НоваяСтрока.Группа		= "Организация";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменился'");
			
		// Электронная почта
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта");
		НоваяСтрока.Изменился	= ЭлектроннаяПочтаИзменилась И ИзменитьРеквизитыПодключенияК1СОтчетности;
		НоваяСтрока.Группа		= "Организация";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменилась'");
		
		// Владелец подписи
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦП");
		НоваяСтрока.Изменился	= ВладелецЭЦПИзменился И ИзменитьВладельцаСертификата;
		НоваяСтрока.Группа		= "Владелец";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменился'");
		
		// Подразделение
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППодразделение");
		НоваяСтрока.Изменился	= ВладелецЭЦППодразделениеИзменилось И (ИзменитьВладельцаСертификата ИЛИ ИзменитьРеквизитыПодключенияК1СОтчетности);
		НоваяСтрока.Группа		= "Владелец";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменилось'");
		
		// Должность
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДолжность");
		НоваяСтрока.Изменился	= ВладелецЭЦПДолжностьИзменилась И (ИзменитьВладельцаСертификата ИЛИ ИзменитьРеквизитыПодключенияК1СОтчетности);
		НоваяСтрока.Группа		= "Владелец";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменилась'");
		
		// СНИЛС
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ПредопределенноеЗначение("Перечисление.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС");
		НоваяСтрока.Изменился	= ВладелецЭЦПСНИЛСИзменился И (ИзменитьВладельцаСертификата ИЛИ ИзменитьРеквизитыПодключенияК1СОтчетности);
		НоваяСтрока.Группа		= "Владелец";
		НоваяСтрока.Префикс		= НСтр("ru = 'изменилась'");
		
		// Продление сертификата
		
		Если СертификатДоступен Тогда
			ИзменившийсяРеквизит = НСтр("ru = 'инициировано продление сертификата'")
		Иначе
			ИзменившийсяРеквизит = НСтр("ru = 'сертификат недоступен'");
		КонецЕсли;
		
		НоваяСтрока 			= ТаблицаПричинПереизданияСертификата.Добавить();
		НоваяСтрока.Реквизит	= ИзменившийсяРеквизит;
		НоваяСтрока.Изменился	= ПродлитьСертификат ИЛИ НЕ СертификатДоступен;
		НоваяСтрока.Группа		= "Прочее";
		
		// Анализируем причины продления сертификата
		Отбор = Новый Структура;
		Отбор.Вставить("Изменился", Истина);
		Отбор.Вставить("Группа", 	"Прочее");
		
		СформироватьТекстПричиныПереизданияСертификата(Отбор, , Ложь);

		// Анализируем реквизиты организации
		Отбор = Новый Структура;
		Отбор.Вставить("Изменился", Истина);
		Отбор.Вставить("Группа", 	"Организация");
		
		СформироватьТекстПричиныПереизданияСертификата(Отбор, НСтр("ru = 'организации'"));
		
		// Анализируем реквизиты владельца
		
		// Для предотвращение ошибки вида, когда выводится
		// "Изменился владелец ЭП сотрудника-владельца ЭП"
		// Должно выводиться просто: "Изменился владелец ЭП".
		Отбор = Новый Структура;
		Отбор.Вставить("Изменился", Истина);
		Отбор.Вставить("Группа", 	"Владелец");
		ОтобранныеСтроки = ТаблицаПричинПереизданияСертификата.НайтиСтроки(Отбор);
		
		ИзменилсяТолькоВладелец = ВладелецЭЦПИзменился 
			И ИзменитьВладельцаСертификата
			И ОтобранныеСтроки.Количество() = 1;
		
		Если ИзменилсяТолькоВладелец Тогда
			Постфикс = "";
		Иначе
			Постфикс = НСтр("ru = 'сотрудника-владельца ЭП'");
		КонецЕсли;
		
		Отбор = Новый Структура;
		Отбор.Вставить("Изменился", Истина);
		Отбор.Вставить("Группа", 	"Владелец");
		
		СформироватьТекстПричиныПереизданияСертификата(Отбор, Постфикс);
		
		ЗаменитьЗапятуюНаИВСтроке(ПричиныПереизданияСертификата);
		
		// Формируем итоговую фразу (множественное или единственное число)
		ПричиныПереизданияСертификата = НСтр("ru = 'Причины переиздания сертификата: '") + ПричиныПереизданияСертификата;
		
	ИначеЕсли ПереиздатьСертификат = 1 Тогда
		
		Если СертификатДоступен Тогда
			ПричиныПереизданияСертификата = НСтр("ru = 'Причины переиздания сертификата: принудительное переиздание сертификата'");
		Иначе
			ПричиныПереизданияСертификата = НСтр("ru = 'Причины переиздания сертификата: сертификат недоступен'");
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СформироватьТекстПричиныПереизданияСертификата(Отбор, Постфикс = "", ТребуетсяПрефикс = Истина)
	
	ОтобранныеСтроки = ТаблицаПричинПереизданияСертификата.НайтиСтроки(Отбор);
	
	Если ОтобранныеСтроки.Количество() > 0 Тогда
		
		// %1% по завершению формирования фразы будет заменен на запятую или "и"
		Если ПричиныПереизданияСертификата <> "" Тогда
			ПричиныПереизданияСертификата = ПричиныПереизданияСертификата + "%1";
		КонецЕсли;
		
		// Собираем изменившиеся реквизиты в одну строку
		Текст = "";
		Для каждого Строка Из ОтобранныеСтроки Цикл
			ДобавитьТекст(
				Строка.Реквизит, 
				Строка.Изменился,
				Текст);
		КонецЦикла;
		
		// Добавляем префикс перед изменившимися реквизитами. Например, с префиксом "изменился" фраза будет выглядеть так:
		// "изменился ИНН, КПП и ОГРН".
		// При этом, префикс нужен только перед первым реквизитом, перед остальными (КПП и ОГРН) префикс не требуется.
		// Префикс не нужен, например, если изменившийся реквизит выглядит как "продление сертификата"
		Если ОтобранныеСтроки[0].Префикс = "" Тогда
			ПричиныПереизданияСертификата = ПричиныПереизданияСертификата + Текст;
		Иначе
			ПричиныПереизданияСертификата = ПричиныПереизданияСертификата + ОтобранныеСтроки[0].Префикс + " " + Текст;
		КонецЕсли;
		
		// Добавляем постфикс.
		// Например, если постфикс "организации", то фраза будет выглядеть как
		// "изменился ИНН, КПП и ОГРН организации"
		ПричиныПереизданияСертификата = ПричиныПереизданияСертификата 
			+ ?(Постфикс = "", "", " " + Постфикс);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьТекст(ИзменившийсяРеквизит, РеквизитИзменился, Текст)
	
	НазваниеРеквизита = Строка(ИзменившийсяРеквизит);
	
	// Делаем так, чтобы все слова начинались с маленькой буквы
	// Если вторая буква большая, то считаем что слова все состоит из больших букв (например ИНН) и не переводим это слово в нижний регистр
	Если Сред(НазваниеРеквизита, 2, 1) <> Врег(Сред(НазваниеРеквизита, 2, 1)) Тогда
		НазваниеРеквизита = НРег(Лев(НазваниеРеквизита, 1)) + Сред(НазваниеРеквизита, 2);
	КонецЕсли;
	
	Если РеквизитИзменился Тогда
		Если Текст = "" Тогда
			Текст = НазваниеРеквизита;
		Иначе
			Текст = Текст + "%1" + НазваниеРеквизита;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры


#Область Прочие

&НаКлиенте
Процедура ОпределитьРеквизитыНеХранящиесяВБазе()
	
	РеквизитыНеХранящиесяВБазе = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.РеквизитыНеХранящиесяВБазе(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкаСервисЭДООбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(СсылкаОписаниеСервисаЭДО) Тогда
		ПерейтиПоНавигационнойСсылке(СсылкаОписаниеСервисаЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Адрес = АдрЮР;
	
	Если Лев(СокрЛП(ВерсияБСП) + ".", 4) = "2.1." Тогда
		НовыйАдрес = КонтекстЭДОКлиент.РедактироватьАдрес(Адрес, Элемент.Имя, Элемент);
		АдрЮР              = НовыйАдрес.Адрес;
		АдресЮридический   = НовыйАдрес.АдресПредставление;
	Иначе
		ДополнительныеПараметры = Элемент;
		
		ТипЗначения = Тип("ОписаниеОповещения");
		ПараметрыКонструктора = Новый Массив(3);
		ПараметрыКонструктора[0] = "ОткрытьФормуКонтактнойИнформацииЗавершение";
		ПараметрыКонструктора[1] = ЭтаФорма;
		ПараметрыКонструктора[2] = ДополнительныеПараметры;
		
		Оповещение = Новый(ТипЗначения, ПараметрыКонструктора);
		
		КонтекстЭДОКлиент.РедактироватьАдрес(Адрес, Элемент.Имя, Элемент, Оповещение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуКонтактнойИнформацииЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	НовыйАдрес = КонтекстЭДОКлиент.РедактироватьАдресКонвертацияРезультата(РезультатЗакрытия);
	ОбновитьАдрес(НовыйАдрес, ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьАдрес(НовыйАдрес, Элемент)
	
	Если НовыйАдрес.Модифицированность Тогда
		Если Элемент.Имя = "АдресЮридический" Тогда
			АдрЮР              = НовыйАдрес.Адрес;
			АдресЮридический   = НовыйАдрес.АдресПредставление;
		Иначе
			АдрФакт            = НовыйАдрес.Адрес;
			АдресФактический   = НовыйАдрес.АдресПредставление;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресОчистка(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьЭДОПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьПараметрыЭДО()
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() ИЛИ ЭтоЭлектроннаяПодписьВМоделиСервиса Тогда 
		// Облачная ЭП
		ОтображатьПодключениеЭДО 	= Ложь;
		ПодключитьЭДО 				= Ложь;
		ОператорЭДО 				= "";
		КодНалоговогоОрганаЭДО 		= "";
	Иначе
		// Локальная ЭП
		Если ЗначениеЗаполнено(Элементы.ОператорЭДО.СписокВыбора) Тогда
			// на этой странице уже были
			// повторного заполнения параметров не требуется
			Возврат;
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура("ЕстьПодключениеЭДО, МассивОператоровЭДО, СсылкаОписаниеСервиса");
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ЗаполнитьПараметрыСервисаЭлектронныхДокументовДляФормыПодключенияК1СООтчетности(Организация, СтруктураПараметров);
		
		//заполнение вспомогательных элементов: списка выбора операторов ЭДО, ссылки на описание сервиса
		Если ЗначениеЗаполнено(СтруктураПараметров.МассивОператоровЭДО) Тогда
			Элементы.ОператорЭДО.СписокВыбора.ЗагрузитьЗначения(СтруктураПараметров.МассивОператоровЭДО);
			Если СтруктураПараметров.МассивОператоровЭДО.Количество() = 1 Тогда
				ОператорЭДО = СтруктураПараметров.МассивОператоровЭДО[0]; 
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтруктураПараметров.СсылкаОписаниеСервиса) Тогда
				СсылкаОписаниеСервисаЭДО = СтруктураПараметров.СсылкаОписаниеСервиса;	
			КонецЕсли;
			
			Если СтруктураПараметров.ЕстьПодключениеЭДО = Ложь Тогда
				// Организация не подключена к ЭДО
				ОтображатьПодключениеЭДО 	= Истина;
				ПодключитьЭДО 				= Истина;
				
				Если НЕ ЗначениеЗаполнено(КодНалоговогоОрганаЭДО) Тогда
					КодНалоговогоОрганаЭДО = СокрЛП(РегламентированнаяОтчетностьВызовСервера.ПолучитьСведенияОбОрганизации(Организация, , "КодНО").КодНО);
				КонецЕсли;
				
			Иначе
				
				ОтображатьПодключениеЭДО 	= Ложь;
				ПодключитьЭДО 				= Ложь;
				ОператорЭДО 				= "";
				КодНалоговогоОрганаЭДО 		= "";
				
			КонецЕсли;
			
		Иначе
			// сервис не работает
			ОтображатьПодключениеЭДО 	= Ложь;
			ПодключитьЭДО 				= Ложь;
			ОператорЭДО 				= "";
			КодНалоговогоОрганаЭДО 		= "";
		КонецЕсли;
				
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти

#Область ИзменениеНомераТелефонаИЭлектроннойПочтыДляПаролей

&НаКлиенте
Процедура ЗапуститьОбратныйОтсчет()
	
	Таймер = 60;
	ПодключитьОбработчикОжидания("Подключаемый_ОбработчикОбратногоОтсчета", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОбратногоОтсчета()
	
	Таймер = Таймер - 1;
	Если Таймер >= 0 Тогда
		НадписьОбратногоОтсчета = СтрШаблон(НСтр("ru = 'Запросить код повторно можно будет через %1 сек.'"), Таймер);
		ПодключитьОбработчикОжидания("Подключаемый_ОбработчикОбратногоОтсчета", 1, Истина);		
	Иначе
		НадписьОбратногоОтсчета = "";
		КодОтправлен = Ложь;
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьКодПодтверждения()
	
	КодПодтверждения = СокрЛП(КодПодтверждения);
	Если СтрДлина(КодПодтверждения) = 6 Тогда
		Если ВыполняетсяПроверкаТелефона Тогда
			Результат = ПроверитьТелефонНаСервере(ТелефонДляПаролей, КодПодтверждения);
			Если Результат Тогда
				ТелефонПодтвержден = Истина;
				ВыполняетсяПроверкаТелефона = Ложь;
			КонецЕсли;
		Иначе
			Результат = ПроверитьЭлектроннуюПочтуНаСервере(ЭлектроннаяПочтаДляПаролей, КодПодтверждения);
			Если Результат Тогда
				ЭлектроннаяПочтаПодтверждена = Истина;
				ВыполняетсяПроверкаЭлектроннойПочты = Ложь;
			КонецЕсли;
		КонецЕсли;
		Если Результат Тогда
			ОтключитьОбработчикОжидания("Подключаемый_ОбработчикОбратногоОтсчета");
			УправлениеФормой(ЭтаФорма);
		Иначе
			ОписаниеОшибки = НСтр("ru = 'Неверный код подтверждения'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОписаниеОшибки,, "КодПодтверждения");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьТелефонНаСервере(Телефон, КодПодтверждения) 
	
	Возврат КриптосервисВМоделиСервиса.ПроверитьНомерТелефона(Телефон, КодПодтверждения);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПроверитьЭлектроннуюПочтуНаСервере(ЭлектроннаяПочта, КодПодтверждения) 
	
	Возврат КриптосервисВМоделиСервиса.ПроверитьАдресЭлектроннойПочты(ЭлектроннаяПочта, КодПодтверждения);
	
КонецФункции

&НаСервере
Функция ПроверитьНомерНаСервере(Повторно = Ложь)
	
	Возврат КриптосервисВМоделиСервиса.ПолучитьПроверочныйКод(ТелефонДляПаролей, Повторно, "phone");

КонецФункции

&НаСервере
Функция ПроверитьАдресНаСервере(Повторно = Ложь)
	
	Возврат КриптосервисВМоделиСервиса.ПолучитьПроверочныйКод(ЭлектроннаяПочтаДляПаролей, Повторно, "email");

КонецФункции

&НаКлиенте
Процедура Подключаемый_ЗакрытьФормуСЗадержкой()
	
	Если Открыта() Тогда
		ПрограммноеЗакрытие = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПоказатьКнопкуПроверкиАдреса()
	
	ЭлектроннаяПочтаИзмененаИсходная = ЭлектроннаяПочтаИзменена;
	ЭлектроннаяПочтаИзменена = ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(ЭлектроннаяПочтаДляПаролей);
	Если ЗначениеЗаполнено(ЭлектроннаяПочтаДляПаролейИсходная) И Не ЗначениеЗаполнено(ЭлектроннаяПочтаДляПаролей) Тогда
		ЭлектроннаяПочтаИзменена = Истина;
		Возврат;
	КонецЕсли;
	
	Если ЭлектроннаяПочтаИзмененаИсходная <> ЭлектроннаяПочтаИзменена Тогда
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьАктивнымЭлементомКодПодтвержденияТелефон()
	
	ТекущийЭлемент = Элементы.КодПодтвержденияТелефон;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_УстановитьАктивнымЭлементомКодПодтвержденияЭлектроннаяПочта()
	
	ТекущийЭлемент = Элементы.КодПодтвержденияЭлектроннаяПочта;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекстПодсказки()
	
	ТелефонМобильныйБезРазделителей = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонМобильный);
	ТелефонДляПаролейБезРазделителей = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТелефонМобильныйБезРазделителей(ТелефонДляПаролей);
	
	ТелефоныРавны = Лев(ТелефонМобильныйБезРазделителей, 4) = Лев(ТелефонДляПаролейБезРазделителей, 4) 
		И  Прав(ТелефонМобильныйБезРазделителей, 2) = Прав(ТелефонДляПаролейБезРазделителей, 2);
	
	ЧастиСтроки = Новый Массив;
	Если ЗначениеЗаполнено(ТелефонМобильныйБезРазделителей) И Не ТелефоныРавны Тогда
		ЧастиСтроки.Добавить("Вы указали отдельный номер для SMS-уведомлений - ");
		ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока(ТелефонМобильный,,,, "#ВводТелефонаДляОповещений"));
	Иначе
		ЧастиСтроки.Добавить("Если вы хотите получать SMS-уведомления на другой номер," + Символы.ПС);
		ЧастиСтроки.Добавить("укажите его, перейдя по ");
		ЧастиСтроки.Добавить(Новый ФорматированнаяСтрока("ссылке",,,, "#ВводТелефонаДляОповещений"));
	КонецЕсли;
	ЧастиСтроки.Добавить(".");
	
	Элементы.ПолучатьУведомления.РасширеннаяПодсказка.Заголовок = Новый ФорматированнаяСтрока(ЧастиСтроки);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти