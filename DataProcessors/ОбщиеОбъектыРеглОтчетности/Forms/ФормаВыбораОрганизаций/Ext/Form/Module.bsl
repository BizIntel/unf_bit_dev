
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
			
	НачальноеЗначениеВыбора = Параметры.НачальноеЗначениеВыбора;
	
	ВыбранныеОрганизации.ЗагрузитьЗначения(Параметры.Организация.ВыгрузитьЗначения());
	
	ТолькоОбособленныеОрганизации = Ложь;
		
	Если ТипЗнч(НачальноеЗначениеВыбора) = Тип("Структура") Тогда
					
		Если РегламентированнаяОтчетность.ГоловнаяОрганизация(НачальноеЗначениеВыбора.Организация) = НачальноеЗначениеВыбора.Организация Тогда
		   
			СписокОбособленныхПодразделений = РегламентированнаяОтчетность.ПолучитьСписокОбособленныхПодразделенийОрганизации(НачальноеЗначениеВыбора.Организация);
			
			Если СписокОбособленныхПодразделений.Количество() > 0 Тогда
				ТолькоОбособленныеОрганизации = Истина;
			КонецЕсли;
			
		КонецЕсли;
		        				     
	КонецЕсли;
	
	ПолучитьОрганизации(ТолькоОбособленныеОрганизации);
			
	Для Каждого Элемент Из ВыбранныеОрганизации Цикл
		
		МассивСтрок = Организации.НайтиСтроки(Новый Структура("Ссылка", Элемент.Значение));	
		
		Для Каждого Строка Из МассивСтрок Цикл
			Строка.Пометка = Истина;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьОрганизации(ТолькоОбособленныеОрганизации)
			
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР КОГДА Организации.ПометкаУдаления ТОГДА 2
	|	ИНАЧЕ 1 КОНЕЦ КАК ИндексКартинки,
	|	Организации.ИНН,
	|	Организации.Наименование,
	|	Организации.Ссылка
	|ИЗ
	|	Справочник.Организации КАК Организации
	|ГДЕ
	|   Организации.Ссылка <> &ОсновнаяОрганизация"
	+ ?(ТолькоОбособленныеОрганизации, "
	|	И Организации.ГоловнаяОрганизация = &ГоловнаяОрганизация", "")
	+ "
	|УПОРЯДОЧИТЬ ПО
	|	Наименование");
	
	Если ТипЗнч(НачальноеЗначениеВыбора) = Тип("Структура") Тогда
		Запрос.УстановитьПараметр("ОсновнаяОрганизация", НачальноеЗначениеВыбора.Организация);
	Иначе
		Запрос.УстановитьПараметр("ОсновнаяОрганизация", Неопределено);
	КонецЕсли;	
	
	Если ТолькоОбособленныеОрганизации Тогда
		Запрос.УстановитьПараметр("ГоловнаяОрганизация", НачальноеЗначениеВыбора.Организация);
	КонецЕсли;
		
	Организации.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьИлиСнятьФлажки(Пометка)
	
	Для Каждого Элемент Из Организации Цикл
		Элемент.Пометка = Пометка;
	КонецЦикла;	
	
КонецПроцедуры
                                            
&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(Истина);
		
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ВыбратьОрганизации();
	
	Попытка
		ВладелецФормы.Активизировать();
		ВладелецФормы.ЗаполнитьАвто(ВладелецФормы.Команды.Заполнить);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОрганизации()
	
	МассивСтрок = Организации.НайтиСтроки(Новый Структура("Пометка", Истина));
	
	ВыбранныеОрганизации.Очистить();
	
	Для Каждого Строка Из МассивСтрок Цикл
		ВыбранныеОрганизации.Добавить(Строка.Ссылка);
	КонецЦикла;
	
	ВыбранныеОрганизации.СортироватьПоЗначению();
	
	Если ТипЗнч(НачальноеЗначениеВыбора) <> Тип("Структура") Тогда
		ВладелецФормы.Организация = ВыбранныеОрганизации;
		ВладелецФормы.УстановитьОтборы();
	Иначе
		ВладелецФормы.СтруктураРеквизитовФормы.ГруппаОрганизаций = ВыбранныеОрганизации;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	УстановитьИлиСнятьФлажки(Ложь);
	
	Организации[ВыбраннаяСтрока].Пометка = Истина;
	
	ВыбратьОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацииПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
