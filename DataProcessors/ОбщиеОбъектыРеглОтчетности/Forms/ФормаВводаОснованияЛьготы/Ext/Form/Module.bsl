
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОснованиеЛьготы = Параметры.ОснованиеЛьготы;
	
	ПроверитьИИсправитьОснованиеЛьготы();
	
	НомерСтатьи = ПолучитьНомерСтатьи(ОснованиеЛьготы);
	Пункт       = ПолучитьПункт(ОснованиеЛьготы);
	Подпункт    = ПолучитьПодпункт(ОснованиеЛьготы);
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьИИсправитьОснованиеЛьготы()
	
	ОснованиеЛьготы = СтрЗаменить(ОснованиеЛьготы, " ", "");
	ОснованиеЛьготы = СокрЛП(ОснованиеЛьготы);
	
	ШаблонОснованияЛьготы = "0000" + "0000" + "0000";
	ДлинаШаблонаЛьготы    = СтрДлина(ШаблонОснованияЛьготы);
	ДлинаОснованияЛьготы  = СтрДлина(ОснованиеЛьготы);
	
	ОснованиеЛьготы = ОснованиеЛьготы + Сред(ШаблонОснованияЛьготы, ДлинаШаблонаЛьготы - ДлинаОснованияЛьготы);
	
КонецПроцедуры

&НаСервере
Функция ПолучитьНомерСтатьи(ЗначениеОснованияЛьготы)
	
	ПолныйНомерСтатьи = Сред(ЗначениеОснованияЛьготы, 1, 4);
	СокращенныйНомерСтатьи = УдалитьЛидирующиеНули(ПолныйНомерСтатьи);
	
	Возврат СокращенныйНомерСтатьи;
	
КонецФункции

&НаСервере
Функция ПолучитьПункт(ЗначениеОснованияЛьготы)
	
	ПолныйПункт = Сред(ЗначениеОснованияЛьготы, 5, 4);
	СокращенныйПункт = УдалитьЛидирующиеНули(ПолныйПункт);
	
	Возврат СокращенныйПункт;
	
КонецФункции

&НаСервере
Функция ПолучитьПодпункт(ЗначениеОснованияЛьготы)
	
	ПолныйПодпункт = Сред(ЗначениеОснованияЛьготы, 9, 4);
	СокращенныйПодпункт = УдалитьЛидирующиеНули(ПолныйПодпункт);
	
	Возврат СокращенныйПодпункт;
	
КонецФункции

&НаСервере
Функция УдалитьЛидирующиеНули(Знач ПолноеЗначение)
	
	ПолноеЗначение = СокрЛП(ПолноеЗначение);
	ДлинаПолногоЗначения = СтрДлина(ПолноеЗначение);
	
	СокращенноеЗначение = "";
	
	Для Инд = 1 По ДлинаПолногоЗначения Цикл
		СимволОтличаетсяОтНуля = (Сред(ПолноеЗначение, Инд, 1) <> "0");
		Если СимволОтличаетсяОтНуля Тогда
			СокращенноеЗначение = Сред(ПолноеЗначение, Инд);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СокращенноеЗначение;
	
КонецФункции

&НаКлиенте
Процедура OK(Команда)
	
	ПолныйНомерСтатьи = ПолучитьПолноеЗначение(НомерСтатьи);
	ПолныйПункт       = ПолучитьПолноеЗначение(Пункт);
	ПолныйПодпункт    = ПолучитьПолноеЗначение(Подпункт);
	
	НовоеОснованиеЛьготы = ПолныйНомерСтатьи + ПолныйПункт + ПолныйПодпункт;
	
	Если НовоеОснованиеЛьготы = ОснованиеЛьготы Тогда
		НовоеОснованиеЛьготы = Неопределено;
	КонецЕсли;
	
	Закрыть(НовоеОснованиеЛьготы);
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьПолноеЗначение(Знач ЗначениеЭлемента)
	
	ЗначениеЭлемента = СтрЗаменить(ЗначениеЭлемента, " ", "");
	ЗначениеЭлемента = СокрЛП(ЗначениеЭлемента);
	
	ШаблонПолногоЗначения = "0000";
	ДлинаШаблона          = СтрДлина(ШаблонПолногоЗначения);
	ДлинаЗначенияЭлемента = СтрДлина(ЗначениеЭлемента);
	
	ПолноеЗначение = Сред(ШаблонПолногоЗначения, 1, ДлинаШаблона - ДлинаЗначенияЭлемента) + ЗначениеЭлемента;
	
	Возврат ПолноеЗначение;
	
КонецФункции

#КонецОбласти