
#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	HTMLДокумент = Обработки.ОбщиеОбъектыРеглОтчетности.ПолучитьМакет("ПредложениеОформитьЗаявлениеНаПодключение").ПолучитьТекст();

	Если Параметры.Свойство("ПоказыватьКнопки") И Параметры.ПоказыватьКнопки Тогда
		
		Элементы.ГруппаКнопок.Видимость = Истина;
		
		БольшеНеПоказывать 	= ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение");
		
		Если БольшеНеПоказывать = Истина Тогда
			Отказ = Истина;
		КонецЕсли;
		
		ВремяПоследнегоНапоминания 	= ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "ВремяПоследнегоНапоминания");
		Если ВремяПоследнегоНапоминания <> Неопределено 
			И ВремяПоследнегоНапоминания + 4 * 60 * 60 > ТекущаяДатаСеанса() Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Организация = Параметры.Организация;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	
	СтандартнаяОбработка = ЛОЖЬ;
	
	Попытка
		Ссылка = ДанныеСобытия.href;
	Исключение
		Возврат;
	КонецПопытки;
	
	Если СтрНайти(Ссылка, "&ОформлениеЗаявления") <> 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ПолеHTMLДокументаПриНажатииЗавершение", ЭтотОбъект);
		ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	ИначеЕсли Ссылка <> Неопределено Тогда
		ПоказатьВебСтраницу(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолеHTMLДокументаПриНажатииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент <> Неопределено Тогда 
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("Организация", Организация);
		ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.МастерФормированияЗаявкиНаПодключение", ПараметрыФормы);
		Закрыть();
	Иначе
		ПоказатьПредупреждение(,"Недостаточно прав для использования модуля документооборота.
			|Пользователю требуется назначить роль ""Право на защищенный документооборот с контролирующими органами"".");
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВебСтраницу(АдресСтраницы) 
	
	Попытка
	
		ПерейтиПоНавигационнойСсылке(АдресСтраницы);

	Исключение
		
		ОбработкаИсключения(ИнформацияОбОшибке());
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаИсключения(ИнформацияОбОшибке)
	
	ПричинаОшибки = ИнформацияОбОшибке().Причина;
	
	ПоказатьПредупреждение(,?(ПричинаОшибки.Описание = Неопределено,
					 "При выполнении действия произошла ошибка.",
					 ПричинаОшибки.Описание));
	
КонецПроцедуры // ОбработкаИсключения() 

&НаСервереБезКонтекста
Процедура СохранитьЗначениеБольшеНеПоказыватьНаСервере(БольшеНеПоказывать)
	
	ХранилищеОбщихНастроек.Сохранить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение", БольшеНеПоказывать);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// КОМАНДЫ ФОРМЫ

&НаКлиенте
Процедура НажатиеБольшеНеПоказывать(Команда)
		
	СохранитьЗначениеБольшеНеПоказыватьНаСервере(Истина);
		
	Закрыть();
	
КонецПроцедуры


&НаКлиенте
Процедура НажатиеПоказатьПозже(Команда)
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти
