
&НаКлиенте
Перем ПараметрыВыбора;

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация",             Параметры.Организация);
	ПараметрыФормы.Вставить("ДатаНачалаПериодаОтчета", Параметры.ДатаНачалаПериодаОтчета);
	ПараметрыФормы.Вставить("ДатаКонцаПериодаОтчета",  Параметры.ДатаКонцаПериодаОтчета);
	ПараметрыФормы.Вставить("ПредставлениеВидаОчета",  Параметры.ПредставлениеВидаОтчета);
	
	ПараметрыФормы.Вставить("ЗаполнитьИзбранноеРанееСозданными",  Параметры.ЗаполнитьИзбранноеРанееСозданными);
	
	ДеревоВсехИспользуемыхОтчетовПоКатегориям = ДеревоВсехИспользуемыхОтчетов();
	
	Если ЗначениеЗаполнено(ПараметрыФормы.ПредставлениеВидаОчета) Тогда
		НайденныеОтчеты = ДеревоВсехИспользуемыхОтчетовПоКатегориям.Строки.НайтиСтроки(Новый Структура("Наименование", ПараметрыФормы.ПредставлениеВидаОчета), Истина);
		Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
			Если НайденныйОтчет.ЭтоГруппа <> Истина 
				И Не ЗначениеЗаполнено(НайденныйОтчет.ИмяФайлаШаблона) Тогда
				ДанныеНайденногоВидаОтчета = Новый Структура("Наименование,ПолноеИмя,Ссылка");
				ЗаполнитьЗначенияСвойств(ДанныеНайденногоВидаОтчета, НайденныйОтчет);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ДеревоВсехИспользуемыхОтчетовПоПолучателям  = ЗаполнитьДеревоОтчетовПоПолучателям(ДеревоВсехИспользуемыхОтчетовПоКатегориям);
	ДеревоВсехИспользуемыхОтчетовБезГруппировки = ЗаполнитьДеревоОтчетовБезГруппировки(ДеревоВсехИспользуемыхОтчетовПоКатегориям);
	
	ЗначениеВДанныеФормы(ДеревоВсехИспользуемыхОтчетовПоКатегориям,   ДеревоОтчетовПоКатегориям);
	ЗначениеВДанныеФормы(ДеревоВсехИспользуемыхОтчетовПоПолучателям,  ДеревоОтчетовПоПолучателям);
	ЗначениеВДанныеФормы(ДеревоВсехИспользуемыхОтчетовБезГруппировки, ДеревоОтчетовБезГруппировки);
	
	ДеревоОтчетовГруппировка = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "ФормаОтчетность_ФормаСозданияОтчета_ДеревоОтчетовГруппировка");
	Если ДеревоОтчетовГруппировка = 1 Тогда      // группировка по получателям
		КопироватьДанныеФормы(ДеревоОтчетовПоПолучателям, ДеревоОтчетов);
	ИначеЕсли ДеревоОтчетовГруппировка = 2 Тогда // без группировки
		КопироватьДанныеФормы(ДеревоОтчетовБезГруппировки, ДеревоОтчетов);
	Иначе                                        // группировка по категориям (по умолчанию)
		КопироватьДанныеФормы(ДеревоОтчетовПоКатегориям, ДеревоОтчетов);
	КонецЕсли;
	
	ПараметрыЗаполнения = Новый Структура;
	ПараметрыЗаполнения.Вставить("Организация",          Параметры.Организация);
	ПараметрыЗаполнения.Вставить("РанееСозданныеОтчеты", РанееСозданныеОтчеты);
	РегламентированнаяОтчетностьПереопределяемый.ЗаполнитьИзбранноеДоступнымиОтчетами(ПараметрыЗаполнения);
	ЕстьПоставляемыйСписок = РанееСозданныеОтчеты.Количество() <> 0;
	
	Если ЕстьПоставляемыйСписок Тогда
		
		ОтметитьИзбранноеВОбщемСпискеОтчетов(ДеревоОтчетов, РанееСозданныеОтчеты);
		
	Иначе
		
		ТаблицаВсехРанееСозданныхОтчетов = РегламентированнаяОтчетность.ТаблицаСозданныхВидовОтчетов();
		
		ТаблицаРанееСозданныхОтчетов = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты");
		Если ТипЗнч(ТаблицаРанееСозданныхОтчетов) = Тип("ТаблицаЗначений") Тогда
			ДобавитьРанееСозданныеВИзбранные(ДеревоОтчетов, ТаблицаРанееСозданныхОтчетов);
		Иначе
			ДобавитьРанееСозданныеВИзбранные(ДеревоОтчетов, ТаблицаВсехРанееСозданныхОтчетов);
		КонецЕсли;
		РанееСозданныеОтчеты.Сортировать("Наименование");
		
	КонецЕсли;
	
	КоличествоЭлементовВРанееСозданных = РанееСозданныеОтчеты.Количество();
	Если КоличествоЭлементовВРанееСозданных = 0 Тогда
		Элементы.ГруппаВидыСписков.ТекущаяСтраница = Элементы["ГруппаДеревоОтчетов"];
	Иначе
		Если НЕ ЕстьПоставляемыйСписок Тогда
			ТекСтраница = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "ФормаОтчетность_ФормаСозданияОтчета_ТекущаяСтраница");
			Если ТекСтраница <> Неопределено И Элементы.ГруппаВидыСписков.ПодчиненныеЭлементы.Найти(ТекСтраница) <> Неопределено Тогда
				Элементы.ГруппаВидыСписков.ТекущаяСтраница = Элементы[ТекСтраница];
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
			
	УправлениеЭУДереваОтчетов(Элементы, ДеревоОтчетовГруппировка);
	
	Элементы.СписокРанееСозданныеКонтекстноеМенюУдалитьИзИзбранных.Видимость = НЕ ЕстьПоставляемыйСписок;
	Элементы.ДеревоОтчетовСсылкаКонтекстноеМенюДобавитьВИзбранные.Видимость  = НЕ ЕстьПоставляемыйСписок;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ТипЗнч(ДанныеНайденногоВидаОтчета) = Тип("Структура") И ДанныеНайденногоВидаОтчета.Свойство("Наименование") Тогда
		Если ЗначениеЗаполнено(ДанныеНайденногоВидаОтчета.Наименование) Тогда
			
			РанееОткрытыеВидыОтчетов = ЭтаФорма.ВладелецФормы.РанееОткрытыеВидыОтчетов;
			НайденныеОтчеты = РанееОткрытыеВидыОтчетов.НайтиСтроки(Новый Структура("ПолноеИмя,Ссылка", ДанныеНайденногоВидаОтчета.ПолноеИмя, ДанныеНайденногоВидаОтчета.Ссылка));
			Если НайденныеОтчеты.Количество() = 0 Тогда
				НоваяСтрока = РанееОткрытыеВидыОтчетов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеНайденногоВидаОтчета);
			КонецЕсли;
			ФормаОтчета = "";
			ОткрытьФормуОтчетаНаКлиенте(ДанныеНайденногоВидаОтчета, ПараметрыФормы.Организация, 
										ПараметрыФормы.ДатаНачалаПериодаОтчета, ПараметрыФормы.ДатаКонцаПериодаОтчета);
			
			СохранитьНастройки();
			
			// При работе в режиме клиента автоматизированного тестирования
			// без метода "Закрыть()" окно текущей формы остается открытым.
			Закрыть();
			
			Отказ = Истина;
			
			Возврат;
			
		КонецЕсли;
	КонецЕсли;
	
	УправлениеЭУ();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура Выбрать(Команда)
	
	ТекДеревоОтчетов = ОпределитьТекущееДерево();
	
	Если ТекДеревоОтчетов.ТекущиеДанные <> Неопределено
		И НЕ ТекДеревоОтчетов.ТекущиеДанные.ЭтоГруппа Тогда
		
		СохранитьНастройки();
		
		ОткрытьФормуОтчетаНаКлиенте(ТекДеревоОтчетов.ТекущиеДанные, ПараметрыФормы.Организация, 
									ПараметрыФормы.ДатаНачалаПериодаОтчета, 
									ПараметрыФормы.ДатаКонцаПериодаОтчета, , Истина);
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПодробнееОбОтчетеНажатие(Элемент)
	
	ПараметрыОткрытияФормы = Новый Структура;
	
	ТекДеревоОтчетов = ОпределитьТекущееДерево();
	
	Если НЕ ЗначениеЗаполнено(ТекДеревоОтчетов.ТекущиеДанные.Ссылка) Тогда
		ПоказатьПредупреждение(, НСтр("ru='Для выбранного отчета вывод подробной информации не предусмотрен.'"));
		Возврат;
	КонецЕсли;
	
	Если ТекДеревоОтчетов.ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru='Выберите отчет.'"));
		Возврат;
	КонецЕсли;
	
	Если ТекДеревоОтчетов.ТекущиеДанные.ЭтоГруппа = Истина Тогда
		ПоказатьПредупреждение(, НСтр("ru='Функция недоступна для группы отчетов.'"));
		Возврат;
	КонецЕсли;
	
	СтандартноеВыполнение = Истина;
	ИмяФормыПодробнееОбОтчете = "ПодробнееОбОтчете";
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ПодробнееОбОчете(ТекДеревоОтчетов.ТекущиеДанные.Ссылка, ИмяФормыПодробнееОбОтчете, СтандартноеВыполнение);
	
	Если СтандартноеВыполнение = Истина Тогда
		
		ПараметрыОткрытияФормы.Вставить("НачальноеЗначениеВыбора", ТекДеревоОтчетов.ТекущиеДанные.Ссылка);
		ФормаПодробнееОФормах = РегламентированнаяОтчетностьКлиент.ПолучитьОбщуюФормуПоИмени(ИмяФормыПодробнееОбОтчете, ПараметрыОткрытияФормы, ЭтаФорма);
		ФормаПодробнееОФормах.РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
		ФормаПодробнееОФормах.Открыть();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРанееСозданныеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекДеревоОтчетов = Элементы.СписокРанееСозданные;
	
	Если ТекДеревоОтчетов.ТекущиеДанные <> Неопределено
		И НЕ ТекДеревоОтчетов.ТекущиеДанные.ЭтоГруппа Тогда

		ОткрытьФормуОтчетаНаКлиенте(ТекДеревоОтчетов.ТекущиеДанные, ПараметрыФормы.Организация, 
									ПараметрыФормы.ДатаНачалаПериодаОтчета, 
									ПараметрыФормы.ДатаКонцаПериодаОтчета, , Истина);
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРанееСозданныеПриАктивизацииСтроки(Элемент)
	
	УправлениеЭУ();
	УправлениеЭУДереваОтчетов(Элементы, ДеревоОтчетовГруппировка);
		
КонецПроцедуры

&НаКлиенте
Процедура СписокРанееСозданныеПередУдалением(Элемент, Отказ)
	
	УдалитьОтчетИзИзбранных();
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокРанееСозданныеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтчетовГруппировкаПриИзменении(Элемент)
	
	Если ДеревоОтчетовГруппировка = 1 Тогда      // группировка по получателям
		ЗаполнитьДеревоОтчетовИзДругогоДерева(ДеревоОтчетовПоПолучателям);
	ИначеЕсли ДеревоОтчетовГруппировка = 2 Тогда // без группировки
		ЗаполнитьДеревоОтчетовИзДругогоДерева(ДеревоОтчетовБезГруппировки);
	Иначе                                        // группировка по категориям (по умолчанию)
		ЗаполнитьДеревоОтчетовИзДругогоДерева(ДеревоОтчетовПоКатегориям);
	КонецЕсли;
	
	УправлениеЭУ();
	УправлениеЭУДереваОтчетов(Элементы, ДеревоОтчетовГруппировка);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтчетовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекДеревоОтчетов = Элементы.ДеревоОтчетов;
	
	Если ТекДеревоОтчетов.ТекущиеДанные <> Неопределено
		И НЕ ТекДеревоОтчетов.ТекущиеДанные.ЭтоГруппа Тогда
		
		Если Поле.Имя = "ДеревоОтчетовПометка" И НЕ ЕстьПоставляемыйСписок Тогда
			
			Если ТекДеревоОтчетов.ТекущиеДанные.Пометка = 0 Тогда
				ДобавитьОтчетВИзбранные();
			ИначеЕсли ТекДеревоОтчетов.ТекущиеДанные.Пометка = 1 Тогда
				УдалитьОтчетИзИзбранных(ТекДеревоОтчетов.ТекущиеДанные.ПолучитьИдентификатор());
			КонецЕсли;
			
		Иначе
			
			ОткрытьФормуОтчетаНаКлиенте(ТекДеревоОтчетов.ТекущиеДанные, ПараметрыФормы.Организация,
										ПараметрыФормы.ДатаНачалаПериодаОтчета,
										ПараметрыФормы.ДатаКонцаПериодаОтчета, , Истина);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОтчетовПриАктивизацииСтроки(Элемент)
	
	УправлениеЭУ();
	УправлениеЭУДереваОтчетов(Элементы, ДеревоОтчетовГруппировка);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппаВидыСписковПриСменеСтраницы(Элемент, ТекущаяСтраница)
	
	УправлениеЭУ();
	УправлениеЭУДереваОтчетов(Элементы, ДеревоОтчетовГруппировка);
	
	СохранитьНастройки();
			
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьВИзбранные(Команда)
	
	ДобавитьОтчетВИзбранные();
			
КонецПроцедуры

&НаКлиенте
Процедура УдалитьИзИзбранных(Команда)
	
	УдалитьОтчетИзИзбранных();
		
КонецПроцедуры

&НаКлиенте
Функция ОпределитьТекущееДерево()
	
	Если Элементы.ГруппаВидыСписков.ТекущаяСтраница.Имя = "ГруппаРанееСозданные" Тогда
		ТекДеревоОтчетов = Элементы.СписокРанееСозданные;
	Иначе
		ТекДеревоОтчетов = Элементы.ДеревоОтчетов;
	КонецЕсли;
	
	Возврат ТекДеревоОтчетов;
	
КонецФункции

&НаКлиенте
Процедура УправлениеЭУ()
	
	ТекДеревоОтчетов = ОпределитьТекущееДерево();
	
	ДоступностьЭУ = (ТекДеревоОтчетов.ТекущиеДанные <> Неопределено И НЕ ТекДеревоОтчетов.ТекущиеДанные.ЭтоГруппа);
	
	ТекДеревоОтчетов.КонтекстноеМеню.ПодчиненныеЭлементы[0].Доступность = ДоступностьЭУ;
	ТекДеревоОтчетов.КонтекстноеМеню.ПодчиненныеЭлементы[1].Доступность = ДоступностьЭУ И (Не ЗначениеЗаполнено(ТекДеревоОтчетов.ТекущиеДанные.ИмяФайлаШаблона));
	
	Элементы.Выбрать.Доступность   = ДоступностьЭУ;
	Элементы.Подробнее.Доступность = ДоступностьЭУ;
		
	ЭтаФорма.ТекущийЭлемент = ТекДеревоОтчетов;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭУДереваОтчетов(Элементы, ДеревоОтчетовГруппировка)
	
	Если ДеревоОтчетовГруппировка = 1 Тогда // группировка по получателям
		
		Элементы.ДеревоОтчетовКатегория.Видимость  = Истина;
		Элементы.ДеревоОтчетовПолучатель.Видимость = Ложь;
		Элементы.ДеревоОтчетов.Отображение = ОтображениеТаблицы.Дерево;
		
	ИначеЕсли ДеревоОтчетовГруппировка = 2 Тогда // без группировки
		
		Элементы.ДеревоОтчетовКатегория.Видимость  = Истина;
		Элементы.ДеревоОтчетовПолучатель.Видимость = Истина;
		Элементы.ДеревоОтчетов.Отображение = ОтображениеТаблицы.Список;
		
	Иначе // группировка по категориям
		
		ДеревоОтчетовГруппировка = 0;
		Элементы.ДеревоОтчетовКатегория.Видимость  = Ложь;
		Элементы.ДеревоОтчетовПолучатель.Видимость = Истина;
		Элементы.ДеревоОтчетов.Отображение = ОтображениеТаблицы.Дерево;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтчетВИзбранные()
	
	ТекущиеДанные = Элементы.ДеревоОтчетов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		
		Если ЗначениеЗаполнено(ТекущиеДанные.ИмяФайлаШаблона) Тогда 
			Возврат;
		КонецЕсли;
		
		НайденныеОтчеты = РанееСозданныеОтчеты.НайтиСтроки(Новый Структура("ПолноеИмя,Ссылка", ТекущиеДанные.ПолноеИмя, ТекущиеДанные.Ссылка));
		
		Если НайденныеОтчеты.Количество() = 0 Тогда
			
			НоваяСтрока = РанееСозданныеОтчеты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
			
			РанееСозданныеОтчеты.Сортировать("Наименование");
			
		КонецЕсли;
		
		ТекущиеДанные.Пометка = 1;
		
	КонецЕсли;
	
	КоличествоЭлементовВРанееСозданных = РанееСозданныеОтчеты.Количество();
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьОтчетВРанееОткрытыеВидыОтчетов()
	
	ТекущиеДанные = Элементы.ДеревоОтчетов.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено И НЕ ТекущиеДанные.ЭтоГруппа Тогда
		
		РанееОткрытыеВидыОтчетов = ЭтаФорма.ВладелецФормы.РанееОткрытыеВидыОтчетов;
		НайденныеОтчеты = РанееОткрытыеВидыОтчетов.НайтиСтроки(Новый Структура("ПолноеИмя,Ссылка", ТекущиеДанные.ПолноеИмя, ТекущиеДанные.Ссылка));
		
		Если НайденныеОтчеты.Количество() = 0 Тогда
			
			НоваяСтрока = РанееОткрытыеВидыОтчетов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущиеДанные);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УдалитьОтчетИзИзбранных(ИдентификаторЭлементаДерева = Неопределено)
	
	Если ИдентификаторЭлементаДерева <> Неопределено Тогда
		
		ТекущиеДанные = ДеревоОтчетов.НайтиПоИдентификатору(ИдентификаторЭлементаДерева);
		ТекущиеДанные.Пометка = 0;
		НайденныеОтчеты = РанееСозданныеОтчеты.НайтиСтроки(Новый Структура("ПолноеИмя,Ссылка", ТекущиеДанные.ПолноеИмя, ТекущиеДанные.Ссылка));
		Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
			РанееСозданныеОтчеты.Удалить(НайденныйОтчет);
		КонецЦикла;
		
		КоличествоЭлементовВРанееСозданных = РанееСозданныеОтчеты.Количество();
		
		СохранитьНастройки();
		
		Возврат;
		
	КонецЕсли;
	
	СписокОтчетов = Новый СписокЗначений;
	
	ВыделенныеСтроки = Элементы.СписокРанееСозданные.ВыделенныеСтроки;
	Для каждого ТекущийИдентификатор Из ВыделенныеСтроки Цикл
		ТекущиеДанные = РанееСозданныеОтчеты.НайтиПоИдентификатору(ТекущийИдентификатор);
		СписокОтчетов.Добавить(ТекущиеДанные.Ссылка, ТекущиеДанные.ПолноеИмя);
		РанееСозданныеОтчеты.Удалить(ТекущиеДанные);
	КонецЦикла;
	
	СписокОтчетов.СортироватьПоЗначению();
	
	СнятьПометкиВДеревеПоСписку(ДеревоОтчетов, СписокОтчетов);
	
	КоличествоЭлементовВРанееСозданных = РанееСозданныеОтчеты.Количество();
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьПометкиВДеревеПоСписку(Узел, СписокОтчетов)
	
	ЭлементыУзла = Узел.ПолучитьЭлементы();
	
	Для каждого ЭлементУзла Из ЭлементыУзла Цикл
		
		СнятьПометкиВДеревеПоСписку(ЭлементУзла, СписокОтчетов);
		
		Если ЭлементУзла.ЭтоГруппа <> Истина Тогда
			Если СписокОтчетов.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЭлементУзла.Ссылка) Тогда
				НайденныйОтчет = СписокОтчетов.НайтиПоЗначению(ЭлементУзла.Ссылка);
			Иначе
				НайденныйОтчет = Неопределено;
				Для Инд = 0 По СписокОтчетов.Количество() - 1 Цикл
					ЭлементСписка = СписокОтчетов.Получить(Инд);
					Если ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
						Прервать;
					КонецЕсли;
					Если ЭлементСписка.Представление = ЭлементУзла.ПолноеИмя Тогда
						НайденныйОтчет = ЭлементСписка;
						Прервать;
					КонецЕсли;
				КонецЦикла; 
			КонецЕсли;
			Если НайденныйОтчет <> Неопределено Тогда
				ЭлементУзла.Пометка = 0;
				СписокОтчетов.Удалить(НайденныйОтчет);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДеревоВсехИспользуемыхОтчетов()
	
	КоличествоЭлементовВДеревеОтчетов  = 0;
	
	ДеревоВсехИспользуемыхОтчетов = ДеревоИзСправочникаРегламентированныеОтчеты();
	
	// Добавим новые колонки для использования расширенных типов
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("Ссылка");
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("Наименование", Новый ОписаниеТипов("Строка"));
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("ПолноеИмя",    Новый ОписаниеТипов("Строка"));
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("Категория",    Новый ОписаниеТипов("Строка"));
	
	ЗаполнитьНовыеКолонкиДереваОтчетов(ДеревоВсехИспользуемыхОтчетов);
	
	ДеревоВсехИспользуемыхОтчетов.Колонки.Удалить("СсылкаВидОтчета");
	ДеревоВсехИспользуемыхОтчетов.Колонки.Удалить("НаименованиеОтчета");
	ДеревоВсехИспользуемыхОтчетов.Колонки.Удалить("ИсточникОтчета");
	
	РегламентированнаяОтчетность.ДобавитьВДеревоРегламентированныхОтчетовДругиеОтчеты(ДеревоВсехИспользуемыхОтчетов);
	
	// Добавим дополнительные колонки для соответствия данным формы
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("Получатель",     Новый ОписаниеТипов("Строка"));
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("Пометка",        Новый ОписаниеТипов("Число"));
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("ИндексКартинки", Новый ОписаниеТипов("Число"));
	
	ОбработатьДеревоОтчетов(ДеревоВсехИспользуемыхОтчетов);
	ДополнитьДеревоЭлектроннымиПредставлениями(ДеревоВсехИспользуемыхОтчетов);
	ДеревоВсехИспользуемыхОтчетов.Колонки.Удалить("ТипПолучателя");
	
	Для каждого СтрокаПервогоУровня Из ДеревоВсехИспользуемыхОтчетов.Строки Цикл
	
		СтрокаПервогоУровня.Строки.Сортировать("Наименование", Истина);
	
	КонецЦикла;
	
	Возврат ДеревоВсехИспользуемыхОтчетов;
	
КонецФункции

&НаСервере
Функция ДеревоИзСправочникаРегламентированныеОтчеты()
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	РегламентированныеОтчеты.Код КАК КодОтчета,	
	|	РегламентированныеОтчеты.Наименование КАК НаименованиеОтчета,
	|	РегламентированныеОтчеты.Ссылка КАК СсылкаВидОтчета,
	|	РегламентированныеОтчеты.ИсточникОтчета КАК ИсточникОтчета,
	|	ВидыОтправляемыхДокументов.ТипПолучателя КАК ТипПолучателя,
	|	РегламентированныеОтчеты.ЭтоГруппа
	|ИЗ
	|	Справочник.РегламентированныеОтчеты КАК РегламентированныеОтчеты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СкрытыеРегламентированныеОтчеты КАК СкрытыеРегламентированныеОтчеты
	|		ПО (СкрытыеРегламентированныеОтчеты.РегламентированныйОтчет = РегламентированныеОтчеты.Ссылка)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
	|		ПО РегламентированныеОтчеты.ИсточникОтчета = ВидыОтправляемыхДокументов.Источник
	|ГДЕ
	|	СкрытыеРегламентированныеОтчеты.РегламентированныйОтчет ЕСТЬ NULL
	|	И (ВидыОтправляемыхДокументов.ПометкаУдаления ЕСТЬ NULL ИЛИ НЕ ВидыОтправляемыхДокументов.ПометкаУдаления)
	|
	|УПОРЯДОЧИТЬ ПО
	|	КодОтчета ИЕРАРХИЯ");
	
	Возврат Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
КонецФункции

&НаСервере
Процедура ДобавитьРанееСозданныеВИзбранные(Узел, ТаблицаРанееСозданныхОтчетов)
	
	ЭлементыУзла = Узел.ПолучитьЭлементы();
	
	Для каждого ЭлементУзла Из ЭлементыУзла Цикл
		
		ДобавитьРанееСозданныеВИзбранные(ЭлементУзла, ТаблицаРанееСозданныхОтчетов);
		
		Если ЭлементУзла.ЭтоГруппа <> Истина Тогда
			
			Если ТаблицаРанееСозданныхОтчетов.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			НайденныеОтчеты = ТаблицаРанееСозданныхОтчетов.НайтиСтроки(Новый Структура("ПолноеИмя,Наименование", ЭлементУзла.ПолноеИмя, ЭлементУзла.Наименование));
			Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
				НоваяСтрокаТаблицы = РанееСозданныеОтчеты.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицы, ЭлементУзла);
				ЭлементУзла.Пометка = 1;
				ТаблицаРанееСозданныхОтчетов.Удалить(НайденныйОтчет);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОтметитьИзбранноеВОбщемСпискеОтчетов(Узел, ТаблицаРанееСозданныхОтчетов)
	
	ЭлементыУзла = Узел.ПолучитьЭлементы();
	
	Для каждого ЭлементУзла Из ЭлементыУзла Цикл
		
		ОтметитьИзбранноеВОбщемСпискеОтчетов(ЭлементУзла, ТаблицаРанееСозданныхОтчетов);
		
		Если ЭлементУзла.ЭтоГруппа <> Истина Тогда
			
			Если ТаблицаРанееСозданныхОтчетов.Количество() = 0 Тогда
				Прервать;
			КонецЕсли;
			
			НайденныеОтчеты = ТаблицаРанееСозданныхОтчетов.НайтиСтроки(Новый Структура(
			"ПолноеИмя,Наименование", ЭлементУзла.ПолноеИмя, ЭлементУзла.Наименование));
			
			Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
				ЭлементУзла.Пометка = 1;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗаполнитьНовыеКолонкиДереваОтчетов(Узел)
	
	СтрокиУзла = Узел.Строки;
	
	Для каждого СтрокаДерева Из СтрокиУзла Цикл
	
		ЗаполнитьНовыеКолонкиДереваОтчетов(СтрокаДерева);
		
		Если СтрокаДерева.ЭтоГруппа <> Истина Тогда
			
			СтрокаДерева.Ссылка = СтрокаДерева.СсылкаВидОтчета;
			СтрокаДерева.ПолноеИмя = "Документ.РегламентированныйОтчет";
			МетаОтчет = Метаданные.Отчеты.Найти(СтрокаДерева.ИсточникОтчета);
			Если МетаОтчет <> Неопределено И МетаОтчет.ОсновнаяФорма <> Неопределено Тогда
				СтрокаДерева.Наименование = МетаОтчет.ОсновнаяФорма.Синоним;
			Иначе
				СтрокаДерева.Наименование = СтрокаДерева.НаименованиеОтчета;
			КонецЕсли;
			
			// Назначим категорию (имя группы родителя)
			Если ТипЗнч(Узел) <> Тип("ДеревоЗначений") Тогда
				СтрокаДерева.Категория = Узел.НаименованиеОтчета;
			КонецЕсли;
			
		Иначе
			
			СтрокаДерева.Наименование = СтрокаДерева.НаименованиеОтчета;
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьДеревоОтчетов(Узел)
	
	СтрокиДерева = Узел.Строки;
	
	КолСтрок = СтрокиДерева.Количество();
	
	Для Инд = 1 По КолСтрок Цикл
		
		ОбрИндекс = КолСтрок - Инд;
		СтрокаДерева = СтрокиДерева.Получить(ОбрИндекс);
		
		ОбработатьДеревоОтчетов(СтрокаДерева);
		
		// Удаляем пустую группу
		Если СтрокаДерева.ЭтоГруппа И СтрокаДерева.Строки.Количество() = 0 Тогда
			СтрокиДерева.Удалить(СтрокаДерева);
			Продолжить;
		КонецЕсли;
		
		Если СтрокаДерева.ЭтоГруппа <> Истина Тогда
			
			СтрокаДерева.Получатель = Строка(СтрокаДерева.ТипПолучателя);
			
			СтрокаДерева.ИндексКартинки = 1;
			Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
				СтрокаДерева.ИндексКартинки = ?(СтрокаДерева.Ссылка.ПометкаУдаления, 3, 1);
			КонецЕсли;
			
			КоличествоЭлементовВДеревеОтчетов = КоличествоЭлементовВДеревеОтчетов + 1;
			
			СтрокаДерева.Пометка = 0;
		Иначе
			СтрокаДерева.ИндексКартинки = 0;
			Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
				СтрокаДерева.ИндексКартинки = ?(СтрокаДерева.Ссылка.ПометкаУдаления, 2, 0);
			КонецЕсли;
			СтрокаДерева.Пометка = -1;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДеревоОтчетовПоПолучателям(ДеревоВсехИспользуемыхОтчетов)
	
	ТаблицаГруппДляСортировки = Новый ТаблицаЗначений;
	ТаблицаГруппДляСортировки.Колонки.Добавить("Поиск",      Новый ОписаниеТипов("Строка"));
	ТаблицаГруппДляСортировки.Колонки.Добавить("Замена",     Новый ОписаниеТипов("Строка"));
	ТаблицаГруппДляСортировки.Колонки.Добавить("Сортировка", Новый ОписаниеТипов("Строка"));
	ТаблицаГруппДляСортировки.Колонки.Добавить("СтрокаВДереве");
	
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "ФНС";
	СтрокаТаблицы.Замена = "ФНС";
	СтрокаТаблицы.Сортировка = "001.ФНС";
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "ПФР";
	СтрокаТаблицы.Замена = "ПФР";
	СтрокаТаблицы.Сортировка = "002.ПФР";
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "ФСС";
	СтрокаТаблицы.Замена = "ФСС";
	СтрокаТаблицы.Сортировка = "003.ФСС";
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "Росстат";
	СтрокаТаблицы.Замена = "Росстат";
	СтрокаТаблицы.Сортировка = "004.Росстат";
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "Росалкогольрегулирование";
	СтрокаТаблицы.Замена = "ФСРАР";
	СтрокаТаблицы.Сортировка = "005.ФСРАР";
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "БанкРоссии";
	СтрокаТаблицы.Замена = "БанкРоссии";
	СтрокаТаблицы.Сортировка = "006.БанкРоссии";
	СтрокаТаблицы = ТаблицаГруппДляСортировки.Добавить();
	СтрокаТаблицы.Поиск = "яяя";
	СтрокаТаблицы.Замена = "Другие получатели";
	СтрокаТаблицы.Сортировка = "яяя";
	
	ДеревоЗначенийДляСортировки = ДеревоВсехИспользуемыхОтчетов.Скопировать();
	ДеревоЗначенийДляСортировки.Строки.Очистить();
	
	ЗаполнитьДеревоЗначенийПоПолучателям(ДеревоВсехИспользуемыхОтчетов, ДеревоЗначенийДляСортировки);
	
	ВсеГруппыПолучателей = ДеревоЗначенийДляСортировки.Строки.НайтиСтроки(Новый Структура("ЭтоГруппа", Истина), Истина);
	
	Для каждого ГруппаПолучателя Из ВсеГруппыПолучателей Цикл
		НайденныеСтроки = ТаблицаГруппДляСортировки.НайтиСтроки(Новый Структура("Поиск", ГруппаПолучателя.Наименование));
		Если НайденныеСтроки.Количество() > 0 Тогда
			НайденныеСтроки[0].СтрокаВДереве = ГруппаПолучателя;
			ГруппаПолучателя.Наименование = НайденныеСтроки[0].Сортировка;
		КонецЕсли;
	КонецЦикла;
	
	ДеревоЗначенийДляСортировки.Строки.Сортировать("Наименование", Истина);
	
	Для каждого СтрокаТаблицыГруппа Из ТаблицаГруппДляСортировки Цикл
		Если СтрокаТаблицыГруппа.СтрокаВДереве <> Неопределено Тогда
			СтрокаТаблицыГруппа.СтрокаВДереве.Наименование = СтрокаТаблицыГруппа.Замена;
		КонецЕсли;
	КонецЦикла;
	
	Возврат ДеревоЗначенийДляСортировки;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьДеревоЗначенийПоПолучателям(Узел, ДеревоРезультата)
	
	СтрокиУзла = Узел.Строки;
	
	Для каждого СтрокаДерева Из СтрокиУзла Цикл
	
		ЗаполнитьДеревоЗначенийПоПолучателям(СтрокаДерева, ДеревоРезультата);
		
		Если СтрокаДерева.ЭтоГруппа <> Истина Тогда
			
			Получатель = ?(ЗначениеЗаполнено(СтрокаДерева.Получатель), СтрокаДерева.Получатель, "яяя");
			
			НайденныеГруппыПолучателя = ДеревоРезультата.Строки.НайтиСтроки(Новый Структура("Наименование,ЭтоГруппа", Получатель, Истина), Истина);
			Если НайденныеГруппыПолучателя.Количество() > 0 Тогда
				НайденнаяГруппаПолучателя = НайденныеГруппыПолучателя[0];
			Иначе
				НайденнаяГруппаПолучателя = ДеревоРезультата.Строки.Добавить();
				НайденнаяГруппаПолучателя.Наименование = Получатель;
				НайденнаяГруппаПолучателя.ЭтоГруппа = Истина;
				НайденнаяГруппаПолучателя.Пометка = -1;
			КонецЕсли;
			
			НоваяСтрокаДерева = НайденнаяГруппаПолучателя.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДерева, СтрокаДерева);
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДеревоОтчетовБезГруппировки(ДеревоВсехИспользуемыхОтчетов)
	
	ДеревоЗначенийДляСортировки = ДеревоВсехИспользуемыхОтчетов.Скопировать();
	ДеревоЗначенийДляСортировки.Строки.Очистить();
	
	ЗаполнитьДеревоЗначенийБезГруппировки(ДеревоВсехИспользуемыхОтчетов, ДеревоЗначенийДляСортировки);
	
	ДеревоЗначенийДляСортировки.Строки.Сортировать("Наименование, Получатель", Истина);
	
	Возврат ДеревоЗначенийДляСортировки;
	
КонецФункции

&НаСервереБезКонтекста
Процедура ЗаполнитьДеревоЗначенийБезГруппировки(Узел, ДеревоРезультата)
	
	СтрокиУзла = Узел.Строки;
	
	Для каждого СтрокаДерева Из СтрокиУзла Цикл
	
		ЗаполнитьДеревоЗначенийБезГруппировки(СтрокаДерева, ДеревоРезультата);
		
		Если СтрокаДерева.ЭтоГруппа <> Истина Тогда
			
			НоваяСтрокаДерева = ДеревоРезультата.Строки.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрокаДерева, СтрокаДерева);
			
		КонецЕсли;
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьДеревоОтчетовИзДругогоДерева(ИсходноеДеревоОтчетов)
	
	ДеревоОтчетов.ПолучитьЭлементы().Очистить();
	
	КопироватьДанныеФормы(ИсходноеДеревоОтчетов, ДеревоОтчетов);
	
	ПроставитьПометкиИзбранного(ДеревоОтчетов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроставитьПометкиИзбранного(Узел)
	
	ЭлементыУзла = Узел.ПолучитьЭлементы();
	
	Для каждого ЭлементУзла Из ЭлементыУзла Цикл
		
		ПроставитьПометкиИзбранного(ЭлементУзла);
		
		Если ЭлементУзла.ЭтоГруппа <> Истина Тогда
			
			НайденныеОтчеты = РанееСозданныеОтчеты.НайтиСтроки(Новый Структура("ПолноеИмя,Наименование", ЭлементУзла.ПолноеИмя, ЭлементУзла.Наименование));
			Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
				ЭлементУзла.Пометка = 1;
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтчетаНаКлиенте(ДанныеОтчета, ОрганизацияОтчета,
					ДатаНачалаПериодаОтчета, ДатаОкончанияПериодаОтчета,
					ПериодичностьОтчета = Неопределено, ЭтоВыбор = Ложь)
		
	РезультатОткрытияФормыНаСервере = Неопределено;
	ПараметрыОткрытияОтчета = Неопределено;
		
	ФормаОткрытаУспешно = Истина;
		
	ТекДанныеОтчета = Новый Структура("Наименование,ПолноеИмя,Ссылка,ИмяФайлаШаблона");
	ЗаполнитьЗначенияСвойств(ТекДанныеОтчета, ДанныеОтчета);
	НаименованиеОтчета = ТекДанныеОтчета.Наименование;
		
	Если ЗначениеЗаполнено(ТекДанныеОтчета.ИмяФайлаШаблона) Тогда
		РезультатОткрытияФормыНаСервере = ОткрытьФормуУниверсальногоОтчетаНаСервере(ТекДанныеОтчета.ИмяФайлаШаблона, ОрганизацияОтчета);
		
		Если РезультатОткрытияФормыНаСервере = "Недостаточно прав" Тогда
			ПоказатьПредупреждение(, НСтр("ru='Недостаточно прав.'"));
			ФормаОткрытаУспешно = Ложь;
		ИначеЕсли РезультатОткрытияФормыНаСервере = "Отчет не найден" Тогда
			ПоказатьПредупреждение(, НСтр("ru='Отчет не найден.'"));
			ФормаОткрытаУспешно = Ложь;
		ИначеЕсли РезультатОткрытияФормыНаСервере = "Не удалось открыть отчет" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не удалось открыть отчет.'");
			Сообщение.Сообщить();
			
			ФормаОткрытаУспешно = Ложь;
		ИначеЕсли ТипЗнч(РезультатОткрытияФормыНаСервере) = Тип("Структура") Тогда
			ИмяФормыОтчета = "Отчет." + РезультатОткрытияФормыНаСервере.ИсточникОтчета + ".Форма.ОсновнаяФорма";
		КонецЕсли;
		
		Если ФормаОткрытаУспешно Тогда 
			ОткрытьФорму(ИмяФормыОтчета, РезультатОткрытияФормыНаСервере, ЭтаФорма.ВладелецФормы, 
									ИмяФормыОтчета,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			Закрыть();
			Возврат;
		КонецЕсли;
	ИначеЕсли ДанныеОтчета.ПолноеИмя = "Документ.РегламентированныйОтчет" Тогда
		
		ИсточникОтчета = "";		
		РезультатОткрытияФормыНаСервере = ОткрытьФормуОтчетаНаСервере(ТекДанныеОтчета, ОрганизацияОтчета, 
													ДатаНачалаПериодаОтчета, ДатаОкончанияПериодаОтчета, 
													ПериодичностьОтчета, ИсточникОтчета);
		
		Если РезультатОткрытияФормыНаСервере = "Недостаточно прав" Тогда

			ПоказатьПредупреждение(, НСтр("ru='Недостаточно прав.'"));
			ФормаОткрытаУспешно = Ложь;
			
		ИначеЕсли РезультатОткрытияФормыНаСервере = "Отчет не найден" Тогда
			
			ПоказатьПредупреждение(, НСтр("ru='Отчет не найден.'"));
			ФормаОткрытаУспешно = Ложь;
			
		ИначеЕсли РезультатОткрытияФормыНаСервере = "Не удалось открыть отчет" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не удалось открыть отчет.'");
			Сообщение.Сообщить();
			
			ФормаОткрытаУспешно = Ложь;
			
		ИначеЕсли РезультатОткрытияФормыНаСервере = "Открыть внутренний отчет-документ" Тогда
			
			Если ИсточникОтчета = "РегламентированныйОтчетРСВ1" Тогда
				
				ИмяФормыОтчета = "Документ." + ИсточникОтчета + ".Форма." + РегламентированнаяОтчетностьКлиентСерверПереопределяемый.ИмяОсновнойФормыРСВ1();
				
			Иначе
				
				ИмяФормыОтчета = "Документ." + ИсточникОтчета + ".Форма.ОсновнаяФорма";
				
			КонецЕсли;
			
		
		ИначеЕсли ТипЗнч(РезультатОткрытияФормыНаСервере) = Тип("Структура") Тогда
			
			ВариантОткрытия = ?(РезультатОткрытияФормыНаСервере.ВнешнийОтчетИспользовать, "ВнешнийОтчет.", "Отчет.");
			Если ИсточникОтчета = "РегламентированныйОтчетРСВ1" Тогда
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru='Для формирования формы РСВ-1 используйте календарь налогов и отчетности!'");
				Сообщение.Сообщить();
				
				ФормаОткрытаУспешно = Ложь;
				
			Иначе
				
				ИмяФормыОтчета = ВариантОткрытия + РезультатОткрытияФормыНаСервере.ИсточникОтчета + ".Форма.ОсновнаяФорма";
				
			КонецЕсли;
		КонецЕсли;
				
	ИначеЕсли ЗначениеЗаполнено(ДанныеОтчета.Ссылка) Тогда
		
		ПараметрыОткрытияОтчета = СформироватьПараметрыОтчетаНаСервере(ТекДанныеОтчета, ОрганизацияОтчета, 
																	   ДатаНачалаПериодаОтчета, ДатаОкончанияПериодаОтчета);
		ИмяФормыОтчета = ДанныеОтчета.ПолноеИмя + ".ФормаОбъекта";
		
	Иначе
		
		ИмяФормыОтчета = ДанныеОтчета.ПолноеИмя + ".ФормаОбъекта";
		
	КонецЕсли;
	
	Если ФормаОткрытаУспешно Тогда
		
		Если ЭтоВыбор Тогда
			
			ПараметрыВыбора = Новый Структура;
			ПараметрыВыбора.Вставить("Организация", ПараметрыФормы.Организация);
			ПараметрыВыбора.Вставить("ДатаНачалаПериодаОтчета", ПараметрыФормы.ДатаНачалаПериодаОтчета);
			ПараметрыВыбора.Вставить("ДатаКонцаПериодаОтчета", ПараметрыФормы.ДатаКонцаПериодаОтчета);
			ПараметрыВыбора.Вставить("ПредставлениеВидаОтчета",НаименованиеОтчета);
			ПараметрыВыбора.Вставить("ИмяФормы",ИмяФормыОтчета);
			
			ЭтаФорма.ВладелецФормы.ПараметрыВыбораФормыСоздания = ПараметрыВыбора;
			Закрыть();
			
		Иначе
			
			// Сначала попробуем найти его среди открытых стартовых форм.
			// Необходимо для предотвращения
			// открытия нескольких стартовых форм одного отчета.
			НайденоОкно = Ложь;
			РегламентированнаяОтчетностьКлиент.ВебКлиентНайтиАктивизироватьОкно(ИмяФормыОтчета, ЭтаФорма, НайденоОкно);
			
			Если НайденоОкно <> Неопределено Тогда
				Если НайденоОкно Тогда
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если ТипЗнч(РезультатОткрытияФормыНаСервере) = Тип("Структура") Тогда
				ОткрытьФорму(ИмяФормыОтчета, РезультатОткрытияФормыНаСервере, ЭтаФорма.ВладелецФормы, 
						ИмяФормыОтчета,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			ИначеЕсли ТипЗнч(ПараметрыОткрытияОтчета) = Тип("Структура") Тогда
				ОткрытьФорму(ИмяФормыОтчета, ПараметрыОткрытияОтчета, ЭтаФорма.ВладелецФормы, ИмяФормыОтчета);
			Иначе
				ОткрытьФорму(ИмяФормыОтчета, , ЭтаФорма.ВладелецФормы, ИмяФормыОтчета);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ФормаОткрытаУспешно И ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда
		
		ДобавитьОтчетВРанееОткрытыеВидыОтчетов();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОткрытьФормуОтчетаНаСервере(Знач Отчет, ОрганизацияОтчета
											  , ДатаНачалаПериодаОтчета = Неопределено
											  , ДатаОкончанияПериодаОтчета = Неопределено
											  , ПериодичностьОтчета = Неопределено
											  , ИсточникОтчета)
	
	ИсточникОтчета = Отчет.Ссылка.ИсточникОтчета;
	
	ПравоДоступаКОтчету = РегламентированнаяОтчетностьВызовСервера.ПравоДоступаКРегламентированномуОтчету(ИсточникОтчета);
	
	Если ПравоДоступаКОтчету = Ложь Тогда
		Возврат "Недостаточно прав";
	ИначеЕсли ПравоДоступаКОтчету = Неопределено Тогда
		Возврат "Отчет не найден";
	КонецЕсли;
	
	Если Метаданные.Документы.Найти(ИсточникОтчета) <> Неопределено Тогда // это внутренний отчет-документ
		Возврат "Открыть внутренний отчет-документ";
	КонецЕсли;
	
	ТекОтчет = РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета);
	Если ТекОтчет = Неопределено Тогда
		Возврат "Не удалось открыть отчет";
	КонецЕсли;
	
	ТекФорма = РегламентированнаяОтчетность.ФормаРеглОтчета(ИсточникОтчета);
	Если ТекФорма = Неопределено Тогда
		Возврат "Не удалось открыть отчет";
	КонецЕсли;
	
	ПараметрыТекФормы = Новый Структура;
	ПараметрыТекФормы.Вставить("Организация");
	ПараметрыТекФормы.Вставить("мДатаНачалаПериодаОтчета");
	ПараметрыТекФормы.Вставить("мДатаКонцаПериодаОтчета");
	ПараметрыТекФормы.Вставить("мПериодичность");
	
	ПараметрыТекФормы.Организация = ОрганизацияОтчета;
	
	Если ЗначениеЗаполнено(ДатаНачалаПериодаОтчета) ИЛИ ЗначениеЗаполнено(ДатаОкончанияПериодаОтчета) Тогда
		
		ПараметрыТекФормы.мДатаНачалаПериодаОтчета = ?(ТипЗнч(ДатаНачалаПериодаОтчета) = Тип("Дата") И ДатаНачалаПериодаОтчета <> Дата(1,1,1), НачалоДня(ДатаНачалаПериодаОтчета), Неопределено);
		ПараметрыТекФормы.мДатаКонцаПериодаОтчета = ?(ТипЗнч(ДатаОкончанияПериодаОтчета) = Тип("Дата") И ДатаОкончанияПериодаОтчета <> Дата(1,1,1), КонецДня(ДатаОкончанияПериодаОтчета), Неопределено);
		
		Если ПериодичностьОтчета <> Неопределено Тогда
			ПараметрыТекФормы.мПериодичность = ПериодичностьОтчета;
		КонецЕсли;
		
	КонецЕсли;
	
	ПараметрыТекФормы.Вставить("ВнешнийОтчетИспользовать", Отчет.Ссылка.ВнешнийОтчетИспользовать);
	ПараметрыТекФормы.Вставить("ИсточникОтчета", ИсточникОтчета);
	
	Возврат ПараметрыТекФормы;
	
КонецФункции

&НаСервере
Функция СформироватьПараметрыОтчетаНаСервере(Знач Отчет, ОрганизацияОтчета = Неопределено, ДатаНачалаПериодаОтчета = Неопределено, ДатаОкончанияПериодаОтчета = Неопределено)
	
	ПараметрыОткрытия = Новый Структура;
	
	Если Отчет.ПолноеИмя = "Документ.УниверсальныйРегламентированныйОтчет" Тогда
	
		ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Новый Структура);
		
		ПараметрыОткрытия.ЗначенияЗаполнения.Вставить("ВидОтчета", Отчет.Ссылка);
		
		Если ЗначениеЗаполнено(ОрганизацияОтчета) Тогда
			ПараметрыОткрытия.ЗначенияЗаполнения.Вставить("Организация" , ОрганизацияОтчета);
		КонецЕсли;
		
	Иначе
		
		ПараметрыОткрытия.Вставить("Организация" , ОрганизацияОтчета);
		
	КонецЕсли;
	
	РегламентированнаяОтчетностьПереопределяемый.ПереопределитьПараметрыОтчета(Отчет, ОрганизацияОтчета, ДатаНачалаПериодаОтчета, ДатаОкончанияПериодаОтчета, ПараметрыОткрытия);
	
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаСервере
Процедура СохранитьНастройки()
	
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "ФормаОтчетность_ФормаСозданияОтчета_ТекущаяСтраница", Элементы.ГруппаВидыСписков.ТекущаяСтраница.Имя);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "ФормаОтчетность_ФормаСозданияОтчета_ДеревоОтчетовГруппировка", ДеревоОтчетовГруппировка);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты", РеквизитФормыВЗначение("РанееСозданныеОтчеты"));
	
КонецПроцедуры

#КонецОбласти

#Область ВеткаЭлектронныеПредставленияВРосстат

&НаСервереБезКонтекста
Функция ОткрытьФормуУниверсальногоОтчетаНаСервере(ИмяФайлаШаблона, ОрганизацияОтчета = Неопределено)
	
	ИсточникОтчета = "РегламентированныйОтчетСтатистикаПрочиеФормы";
	ПравоДоступаКОтчету = РегламентированнаяОтчетностьВызовСервера.ПравоДоступаКРегламентированномуОтчету(ИсточникОтчета);
	
	Если ПравоДоступаКОтчету = Ложь Тогда
		Возврат "Недостаточно прав";
	ИначеЕсли ПравоДоступаКОтчету = Неопределено Тогда
		Возврат "Отчет не найден";
	КонецЕсли;
	
	Если Метаданные.Документы.Найти(ИсточникОтчета) <> Неопределено Тогда // это внутренний отчет-документ
		Возврат "Открыть внутренний отчет-документ";
	КонецЕсли;
	
	ТекОтчет = РегламентированнаяОтчетность.РеглОтчеты(ИсточникОтчета);
	Если ТекОтчет = Неопределено Тогда
		Возврат "Не удалось открыть отчет";
	КонецЕсли;
	
	ТекФорма = РегламентированнаяОтчетность.ФормаРеглОтчета(ИсточникОтчета);
	Если ТекФорма = Неопределено Тогда
		Возврат "Не удалось открыть отчет";
	КонецЕсли;
	
	ПараметрыТекФормы = Новый Структура;
	ПараметрыТекФормы.Вставить("Организация", ОрганизацияОтчета);
	ПараметрыТекФормы.Вставить("ИмяФайлаШаблона", ИмяФайлаШаблона);
	ПараметрыТекФормы.Вставить("ИсточникОтчета", ИсточникОтчета);
	
	Возврат ПараметрыТекФормы;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТаблицуКодовФСГСНереализованныхОтчетов()
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ОКУД", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(7)));
	Результат.Колонки.Добавить("ЕстьВПрограме", Новый ОписаниеТипов("Булево"));
	
	Макет = Обработки.ДокументооборотСКонтролирующимиОрганами.ПолучитьМакет("ОписаниеФормДляИмпортаФСГС");
	Для Стр = 2 По Макет.ВысотаТаблицы Цикл 
		ОКУД = Макет.Область(Стр, 1, Стр, 1).Текст;
		Если ЗначениеЗаполнено(ОКУД) Тогда
			Отчет = Макет.Область(Стр, 3, Стр, 3).Текст;
			НовСтр = Результат.Добавить();
			НовСтр.ОКУД = ОКУД;
			Если Метаданные.Отчеты.Найти(Отчет) = Неопределено Тогда 
				НовСтр.ЕстьВПрограме = Ложь;
			Иначе
				НовСтр.ЕстьВПрограме = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьТаблицуНереализованныхЭлектронныхПредставлений()
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИсточникОтчета", "РегламентированныйОтчетСтатистикаПрочиеФормы");
	Запрос.УстановитьПараметр("КодыФСГС", ПолучитьТаблицуКодовФСГСНереализованныхОтчетов());
	
	Попытка
		ТекущиеНастройки = ХранилищеСистемныхНастроек.Загрузить("СтатистическаяОтчетность.ОтображатьВДеревеРеализованные", "ОтображатьВДеревеРеализованные");
		ОтображатьВДеревеРеализованные = Ложь;
		
		Если ТипЗнч(ТекущиеНастройки) = Тип("Булево") Тогда 
			ОтображатьВДеревеРеализованные = ТекущиеНастройки;
		КонецЕсли;
	Исключение
		ОтображатьВДеревеРеализованные = Ложь;
	КонецПопытки;
	
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТабКодыФСГС.*
	|ПОМЕСТИТЬ ВТ_КодыФСГС
	|ИЗ
	|	&КодыФСГС КАК ТабКодыФСГС
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ РегламентированныеОтчеты.Ссылка) КАК Количество,
	|	РегламентированныеОтчеты.ИсточникОтчета
	|ПОМЕСТИТЬ ВТ_ЕстьУниверсальныйОтчет
	|ИЗ
	|	Справочник.РегламентированныеОтчеты КАК РегламентированныеОтчеты
	|ГДЕ
	|	РегламентированныеОтчеты.ИсточникОтчета = &ИсточникОтчета
	|
	|СГРУППИРОВАТЬ ПО
	|	РегламентированныеОтчеты.ИсточникОтчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ШаблоныЭВФОтчетовСтатистики.Наименование,
	|	ШаблоныЭВФОтчетовСтатистики.ИмяФайлаШаблона
	|ИЗ
	|	ВТ_КодыФСГС КАК ВТ_КодыФСГС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШаблоныЭВФОтчетовСтатистики КАК ШаблоныЭВФОтчетовСтатистики
	|		ПО (ВТ_КодыФСГС.ОКУД = ШаблоныЭВФОтчетовСтатистики.ОКУД)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ЕстьУниверсальныйОтчет КАК ВТ_ЕстьУниверсальныйОтчет
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ЕСТЬNULL(ВТ_ЕстьУниверсальныйОтчет.Количество, 0) <> 0
	|	И ЕСТЬNULL(ШаблоныЭВФОтчетовСтатистики.ИмяФайлаШаблона, 0) <> 0";
	
	Если Не ОтображатьВДеревеРеализованные Тогда
		Запрос.Текст = Запрос.Текст + " И Не ВТ_КодыФСГС.ЕстьВПрограме"
	КонецЕсли;
	
	Возврат Запрос.Выполнить().Выгрузить();
КонецФункции

&НаСервереБезКонтекста
Процедура ДополнитьДеревоЭлектроннымиПредставлениями(ДеревоВсехИспользуемыхОтчетов)
	ДеревоВсехИспользуемыхОтчетов.Колонки.Добавить("ИмяФайлаШаблона",    Новый ОписаниеТипов("Строка"));
	ТаблицаПредставлений = ПолучитьТаблицуНереализованныхЭлектронныхПредставлений();
	Если ТаблицаПредставлений.Количество() = 0 Тогда 
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаПервогоУровня Из ДеревоВсехИспользуемыхОтчетов.Строки Цикл
		Если СтрокаПервогоУровня.Строки.Найти(Перечисления.ТипыКонтролирующихОрганов.ФСГС, "ТипПолучателя", Ложь) <> Неопределено Тогда 
			ГруппаЭлектронныеПредставления = СтрокаПервогоУровня.Строки.Вставить(0);
			ГруппаЭлектронныеПредставления.Наименование = "Прочие формы";
			ГруппаЭлектронныеПредставления.ЭтоГруппа = Истина;
			ГруппаЭлектронныеПредставления.Пометка = 2;
			Для Каждого Стр Из ТаблицаПредставлений Цикл
				НовСтр = ГруппаЭлектронныеПредставления.Строки.Вставить(0);
				НовСтр.Наименование = "Статистика: " + СокрЛП(Стр.Наименование);
				НовСтр.ИмяФайлаШаблона = Стр.ИмяФайлаШаблона;
				НовСтр.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов.ФСГС;
				НовСтр.Получатель = Перечисления.ТипыКонтролирующихОрганов.ФСГС;
				НовСтр.Категория = СтрокаПервогоУровня.Наименование;
				НовСтр.ИндексКартинки = 1;
				НовСтр.Пометка = 2;
			КонецЦикла;
			
			Прервать;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти