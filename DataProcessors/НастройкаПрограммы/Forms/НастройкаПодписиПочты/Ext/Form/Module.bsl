
#Область ОбработчикиСобытий

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПрочитатьЗначения();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриИзмененииРеквизита(Элемент)
	
	СохранитьЗначениеРеквизита(Элемент.Имя);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПрочитатьЗначения()
	
	ВключатьПодписьДляНовыхСообщений = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиЭлектроннойПочты", "ВключатьПодписьДляНовыхСообщений", Ложь);
	
	ПодписьHTML = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиЭлектроннойПочты", "ПодписьHTML", "");
	КартинкиПодписи = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить("НастройкиЭлектроннойПочты", "КартинкиПодписи", Новый Структура);
	
	Если ПустаяСтрока(ПодписьHTML) Тогда
		ПодписьПоУмолчанию = Символы.ПС + Символы.ПС + "---------------------------------" + Символы.ПС;
		ПодписьHTML = Новый ФорматированнаяСтрока(ПодписьПоУмолчанию);
		ПодписьФорматированныйДокумент.УстановитьФорматированнуюСтроку(ПодписьHTML);
	Иначе
		ПодписьФорматированныйДокумент.УстановитьHTML(ПодписьHTML, КартинкиПодписи);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеРеквизита(ИмяЭлемента)
	
	РеквизитПутьКДанным = Элементы[ИмяЭлемента].ПутьКДанным;
	
	Если РеквизитПутьКДанным = "ВключатьПодписьДляНовыхСообщений" Тогда
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиЭлектроннойПочты", "ВключатьПодписьДляНовыхСообщений", ВключатьПодписьДляНовыхСообщений, , , Истина);
		
	КонецЕсли;
	
	Если РеквизитПутьКДанным = "ПодписьФорматированныйДокумент" Тогда
		
		ПодписьHTML = "";
		КартинкиПодписи = Новый Структура;
		ПодписьФорматированныйДокумент.ПолучитьHTML(ПодписьHTML, КартинкиПодписи);
		
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиЭлектроннойПочты", "ПодписьHTML", ПодписьHTML, , , Истина);
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиЭлектроннойПочты", "КартинкиПодписи", КартинкиПодписи, , , Истина);
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("НастройкиЭлектроннойПочты", "ПодписьПростойТекст", ПодписьФорматированныйДокумент.ПолучитьТекст(), , , Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
