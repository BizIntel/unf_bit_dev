
&НаКлиенте
Перем СтруктураЗаписейПараметровУчета;

&НаКлиенте
Перем ВыводилисьДанныеДляБанка;


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Объект.Организация) Тогда
		
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			Объект.Организация = Параметры.Организация;
		Иначе
			Организация = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
			Если Не ЗначениеЗаполнено(Организация) Тогда
				Организация =УправлениеНебольшойФирмойСервер.ПолучитьПредопределеннуюОрганизацию();
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СобытиеКалендаря) Тогда
		Объект.СобытиеКалендаря = Параметры.СобытиеКалендаря;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.СостояниеСобытия) Тогда
		
		Если ЗначениеЗаполнено(Параметры.СостояниеСобытия) Тогда
			
			Объект.СостояниеСобытия = Параметры.СостояниеСобытия;
			
		Иначе
			
			Объект.СостояниеСобытия = КалендарьОтчетности.ПолучитьСостояниеСобытияКалендаря(
				Объект.Организация,
				Объект.СобытиеКалендаря);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ДатыСобытия = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.СобытиеКалендаря, "ДатаДокументаОбработкиСобытия,ДатаОкончанияСобытия,ДатаНачалаДокументов,ДатаОкончанияДокументов");
	
	ПолучитьДанныеОтчетности();
	СверстатьФорму();
	
	ОбновитьДанныеРегистрацииРасходов();
	
	ПериодЗадачиПредставление = ПредставлениеПериода(
		ДатыСобытия.ДатаНачалаДокументов,
		КонецДня(ДатыСобытия.ДатаОкончанияДокументов),
		"ФП=Истина");
	ПериодЗадачиПредставление = Лев(ПериодЗадачиПредставление, 1)+ Нстр("ru='-й'") + Прав(ПериодЗадачиПредставление, СтрДлина(ПериодЗадачиПредставление)-1);
	
	ИспользуетсяТрудНаемныхРаботниковЗаПериод  = РегламентированнаяОтчетностьПереопределяемый.ИспользуетсяТрудНаемныхРаботниковЗаПериод(Объект.Организация, НачалоКвартала(ДатыСобытия.ДатаНачалаДокументов), КонецДня(ДатыСобытия.ДатаОкончанияДокументов));
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Ознакомиться") Тогда
		
		Элементы.СтраницаЕНВД.ТекущаяСтраница = Элементы.РезультатРасчета;
		
	ИначеЕсли Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Уплатить") Тогда
		
		Элементы.СтраницаЕНВД.ТекущаяСтраница = Элементы.РезультатРасчета;
		
	Иначе
		
		Элементы.СтраницаЕНВД.ТекущаяСтраница = Элементы.ЗадачаВыполнена;
		
	КонецЕсли;
	
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	ВыводилисьДанныеДляБанка = Ложь;
	
	УстановитьВидимостьИДоступностьЭлементов();
	
КонецПроцедуры


&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ИзмененаТекущаяОрганизация" Тогда
		Если Окно.Основное Тогда
			ПерейтиПоНавигационнойСсылке("e1cib/navigationpoint/НалогиИОтчетность");
		Иначе
			Закрыть();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "ИзменениеСостоянияСобытияКалендаря" Тогда
		Если Параметр = Объект.СобытиеКалендаря
				И Источник <> ЭтаФорма
				И Окно<> Неопределено
				И Не Окно.Основное Тогда
			Закрыть();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ


&НаКлиенте
Процедура ВсегоНалогаКУплатеНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	ПоказатьПолнуюТаблицу();
КонецПроцедуры

&НаКлиенте
Процедура УчтеноВзносовНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПериодПараметр = КалендарьОтчетностиПовтИсп.ПолучитьСтандартныйПериодДокументовДляСобытия(Объект.СобытиеКалендаря);
	
	СпсВидов = Новый СписокЗначений;
	
	Если ИспользуетсяТрудНаемныхРаботниковЗаПериод Тогда
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРНакопительнаяСотрудники"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРСтраховаяСотрудники"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ФСССотрудники"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ФССТравматизмСотрудники"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ФОМССотрудники"));
	Иначе
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРНакопительная"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРСтраховая"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ПФРСвыше300тр"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ТФОМС"));
		СпсВидов.Добавить(ПредопределенноеЗначение("Справочник.ВидыНалогов.ФФОМС"));
	КонецЕсли;
	
	ОткрытьФорму(
		"Отчет.УплатыНалоговИВзносов.ФормаОбъекта",
		Новый Структура(
			"Организация, СформироватьПриОткрытии, Период,ВидВзаиморасчетов",
			Объект.Организация,
			Истина,
			ПериодПараметр,
			СпсВидов));

КонецПроцедуры

&НаКлиенте
Процедура ОтразитьРасходыВПрограмме(Команда)
	
	ДатаУплаты = ТекущаяДата();
	
	Если ТаблицаЕНВДПоОкато.Количество() > 1 Тогда
		
		Список = Новый СписокЗначений;
		Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
			Список.Добавить(Строка.ДокументВзаиморасчетовСБюджетом,НСтр("ru='ОКАТО: '")+Строка.ОКАТО);
		КонецЦикла;
		
		ОткрытьФорму(
			"Обработка.ОбработкиНалоговИОтчетности.Форма.ГрупповаяРегистрацияРасходов", 
			Новый Структура(
				"Организация,СписокКПоиску",
				Объект.Организация,
				Список));
		
	Иначе
		
		Если НЕ ТаблицаЕНВДПоОкато[0].НаличнаяУплата.Пустая() Тогда
			ПоказатьЗначение(,ТаблицаЕНВДПоОкато[0].НаличнаяУплата);
			Возврат;
		КонецЕсли;
		
		оп = Новый ОписаниеОповещения("ОповещениеВыбораДаты", ЭтотОбъект);
		
		ПоказатьВводДаты(оп, ДатаУплаты, НСтр("ru='Когда был уплачен ЕНВД?'"));
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеВыбораДаты(ВыбраннаяДата, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ВыбраннаяДата) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаЕНВДПоОкато[0].НаличнаяУплата = РегламентированнаяОтчетностьУСН.ЗарегистрироватьРасходыПоНалогамИВзносамНаличные(
		Объект.Организация,
		ТаблицаЕНВДПоОкато[0].ДокументВзаиморасчетовСБюджетом,
		ВыбраннаяДата);
	
	ПоказатьОповещениеПользователя(
		НСтр("ru='Расходы зарегистрированы'"),
		ПолучитьНавигационнуюСсылку(ТаблицаЕНВДПоОкато[0].НаличнаяУплата),
		НСтр("ru='Документ регистрации расходов на налоги'"),
		БиблиотекаКартинок.Информация32);
	ОбновитьДанныеРегистрацииРасходов();
	
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьПлатежноеПоручение(Команда)
	
	Если ВсегоНалогаКУплате = "0,00 р." Тогда
		ПоказатьПредупреждение(,НСтр("ru='Сумма нулевая. Уплата не требуется.'"));
		Возврат;
	КонецЕсли;

	
	ВыводилисьДанныеДляБанка = Истина;
	
	оп = Новый ОписаниеОповещения("ОповещениеПолученияСпискаПлатежек", ЭтотОбъект, Новый Структура("Файл", Ложь));
	ПолучитьСписокПлатежек(оп);
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеПолученияСпискаПлатежек(БанкСчетСтруктура, Параметры) Экспорт
	
	Если БанкСчетСтруктура <> Неопределено Тогда
		Если Параметры.Файл Тогда
			Если ПроверитьНаличиеГосОргана("ПолучитьФайлКлиентБанка") Тогда
				ПолучитьСписокПлатежекНаСервере();
				СписокПлатежек = Новый СписокЗначений;
				Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
					СписокПлатежек.Добавить(Строка.БезналичнаяУплата);
				КонецЦикла;
				
				БанкСчетСтруктура.Вставить("СписокПлатежек", СписокПлатежек);
				ОткрытьФорму(
					"Обработка.КлиентБанк.Форма.СохранениеПлатежек",
					БанкСчетСтруктура);
			КонецЕсли;
		Иначе
			Если ПроверитьНаличиеГосОргана("РаспечататьПлатежноеПоручениеЗавершение") Тогда
				РаспечататьПлатежноеПоручениеЗавершение();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ФайлВыгрузкиДляКлиентБанка(Команда)
	
	Если ВсегоНалогаКУплате = "0,00 р." Тогда
		ПоказатьПредупреждение(,НСтр("ru='Сумма нулевая. Уплата не требуется.'"));
		Возврат;
	КонецЕсли;
	
	ВыводилисьДанныеДляБанка = Истина;
	оп = Новый ОписаниеОповещения("ОповещениеПолученияСпискаПлатежек", ЭтотОбъект, Новый Структура("Файл", Истина));
	ПолучитьСписокПлатежек(оп);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьКвитанциюДляСбербанка(Команда)
	
	Если ПроверитьНаличиеГосОргана("РаспечататьКвитанциюЗавершение") Тогда
		РаспечататьКвитанциюЗавершение();
	КонецЕсли;
	
КонецПроцедуры


//-----------------------------------------------------------------------------
// События переходов


&НаКлиенте
Процедура ПереходЗаполнение(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Заполнить");
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
		Объект.Организация,
		Объект.СобытиеКалендаря,
		Объект.СостояниеСобытия,
		"");
	
	ПараметрыФормы = Новый Структура("Организация,СобытиеКалендаря", Объект.Организация,Объект.СобытиеКалендаря);
	
	КалендарьОтчетностиКлиент.ОткрытьФормуНачалаЗаполнения(ЭтаФорма,ПараметрыФормы);
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходУплата(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Уплатить");
	
	ЗафиксироватьПереходкУплате(Объект.Организация, Объект.СобытиеКалендаря, Объект.СостояниеСобытия, ВсегоНалогаКУплате);
	
	Элементы.СтраницаЕНВД.ТекущаяСтраница = Элементы.РезультатРасчета;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ЗафиксироватьПереходкУплате(Организация, Событие, Состояние, ВсегоКУплате)
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
		Организация,
		Событие,
		Состояние,
		ВсегоКУплате);
	
	
	ЗаписьКалендаря = Справочники.ЗаписиКалендаряПодготовкиОтчетности.ПолучитьЗаписьКалендаря(Организация, Событие);
	Если ЗаписьКалендаря <> Неопределено Тогда
		ОбъектЗаписьКалендаря = ЗаписьКалендаря.ПолучитьОбъект();
		ОбъектЗаписьКалендаря.Завершено = Ложь;
		ОбъектЗаписьКалендаря.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходРезультатРасчета1Нажатие(Элемент)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Ознакомиться");
	
	КалендарьОтчетности.ЗаписатьСостояниеСобытияКалендаря(
		Объект.Организация,
		Объект.СобытиеКалендаря,
		Объект.СостояниеСобытия,
		0);
	
	Элементы.СтраницаЕНВД.ТекущаяСтраница = Элементы.РезультатРасчета;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	Оповестить("ИзменениеСостоянияСобытияКалендаря", Объект.СобытиеКалендаря, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереходВыполнил(Команда)
	
	Объект.СостояниеСобытия = ПредопределенноеЗначение("Перечисление.СостоянияСобытийКалендаря.Завершено");
	
	КалендарьОтчетности.ЗавершитьСобытиеКалендаряОтчетности(
		Объект.Организация,
		Объект.СобытиеКалендаря,
		"");
	
	Элементы.СтраницаЕНВД.ТекущаяСтраница = Элементы.ЗадачаВыполнена;
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
	ОповеститьОбИзменении(Тип("СправочникСсылка.ЗаписиКалендаряПодготовкиОтчетности"));
	Оповестить("Запись_ЗаписиКалендаряПодготовкиОтчетности");
	
КонецПроцедуры


// Конец событий переходов
//-----------------------------------------------------------------------------

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


// Процедура заполняет данные формы по данным ранее сформированной отчетности
//
&НаСервере
Процедура ПолучитьДанныеОтчетности()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ЗначенияПоказателейОтчетности.ЗначениеПоказателя КАК ЗначениеПоказателя,
	|	ЗначенияПоказателейОтчетности.ПоказательОтчетности КАК ПоказательОтчетности
	|ИЗ
	|	РегистрСведений.ЗначенияПоказателейОтчетности КАК ЗначенияПоказателейОтчетности
	|ГДЕ
	|	ЗначенияПоказателейОтчетности.Организация = &Организация
	|	И ЗначенияПоказателейОтчетности.ПериодОтчетности = &ПериодОтчетности
	|	И ЗначенияПоказателейОтчетности.ПоказательОтчетности В ИЕРАРХИИ(&ГруппаПоказателя)");
	
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	Запрос.УстановитьПараметр("ПериодОтчетности", Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия);
	
	Запрос.УстановитьПараметр("ГруппаПоказателя", ПланыВидовХарактеристик.ПоказателиОтчетности.ЕНВД);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.К1ЕНВД Тогда
			К1 = Выборка.ЗначениеПоказателя;
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.УплаченоСтраховыхВзносовЕНВД Тогда
			СуммаСтраховыхВзносовЕНВД = ""+ Формат(Выборка.ЗначениеПоказателя, " ЧН=") + " р.";
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.хзТаблицаЕНВД Тогда
			
			Табличка = РегламентированнаяОтчетностьУСН.ПолучитьЗначениеПроизвольногоПоказателя(
				Объект.Организация,
				Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия,
				ПланыВидовХарактеристик.ПоказателиОтчетности.хзТаблицаЕНВД);
			
			Если Табличка = Неопределено Тогда
				ЗаписьЖурналаРегистрации(
					НСтр("ru='ЕНВД.Заполнение задачи'"),
					УровеньЖурналаРегистрации.Ошибка,
					,
					,
					НСтр("ru='Не удалось получить данные для ТаблицаЕНВД'"));
			Иначе
				ТаблицаЕНВД.Загрузить(Табличка.Получить());
				АдресТаблицаЕНВД = ПоместитьВоВременноеХранилище(Табличка,УникальныйИдентификатор);
			КонецЕсли;
			
		ИначеЕсли Выборка.ПоказательОтчетности = ПланыВидовХарактеристик.ПоказателиОтчетности.хзТаблицаЕНВДПоОкато Тогда
			
			Табличка = РегламентированнаяОтчетностьУСН.ПолучитьЗначениеПроизвольногоПоказателя(
				Объект.Организация,
				Объект.СобытиеКалендаря.ДатаДокументаОбработкиСобытия,
				ПланыВидовХарактеристик.ПоказателиОтчетности.хзТаблицаЕНВДПоОкато);
			
			Если Табличка = Неопределено Тогда
				ЗаписьЖурналаРегистрации(
					НСтр("ru='ЕНВД.Заполнение задачи'"),
					УровеньЖурналаРегистрации.Ошибка,
					,
					,
					НСтр("ru='Не удалось получить данные для ТаблицаЕНВДПоОкато'"));
			Иначе
				ТаблицаЕНВДПоОкато.Загрузить(Табличка.Получить());
				АдресТаблицаЕНВДПоОкато = ПоместитьВоВременноеХранилище(Табличка,УникальныйИдентификатор);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ВсегоНалогаКУплате = ""+ТаблицаЕНВДПоОкато.Итог("СуммаНалога") + " р.";
	Если ТаблицаЕНВДПоОкато.Количество() = 1 Тогда
		Элементы.ВсегоНалогаКУплате.Видимость= Ложь;
	Иначе
		Элементы.ВсегоНалогаКУплате.Видимость= Истина;
	КонецЕсли;
	
	
КонецПроцедуры

// Процедура устанавливает видимость и доступность элементов управления
//
&НаКлиенте
Процедура УстановитьВидимостьИДоступностьЭлементов()
	
	НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок();
	
КонецПроцедуры

&НаКлиенте
Процедура НазначитьКнопкуПоУмолчаниюИУстановитьЗаголовок()
	
	ИмяКнопки = ПолучитьИмяЭлементаКнопкиПоУмолчанию(Элементы.СтраницаЕНВД.ТекущаяСтраница.Имя);
	
	Если НЕ ПустаяСтрока(ИмяКнопки) Тогда
		Элементы[ИмяКнопки].КнопкаПоУмолчанию = Истина;
	КонецЕсли;
	
	Попытка
		Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Элементы.СтраницаЕНВД.ТекущаяСтраница.Заголовок, ПериодЗадачиПредставление);
		РегламентированнаяОтчетностьУСНКлиентСервер.УстановитьЗаголовокФормыЗадачи(ЭтаФорма, Объект.Организация);
	Исключение
	КонецПопытки;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяЭлементаКнопкиПоУмолчанию(ИмяСтраницы)
	
	Если ИмяСтраницы = "РезультатРасчета" Тогда
		Возврат "ПереходВыполнил";
	ИначеЕсли ИмяСтраницы = "ЗадачаВыполнена" Тогда
		Возврат "";
	Иначе
		ВызватьИсключение НСтр("ru='Неизвестное имя текущей страницы'");
	КонецЕсли;
	
КонецФункции


&НаСервере
Процедура СверстатьФорму()
	
	Если ТаблицаЕНВДПоОкато.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	к = 0;
	
	// добавим реквизиты
	Массив = Новый Массив(3*ТаблицаЕНВДПоОкато.Количество());
	
	Для м = 0 по ТаблицаЕНВДПоОкато.Количество()-1 Цикл
		
		реквизит = Новый РеквизитФормы(
			"Окато"+м,
			Новый ОписаниеТипов("Строка"),
			,
			НСтр("ru='ОКАТО'"));
		Массив[3*м] = реквизит;
		
		реквизит = Новый РеквизитФормы(
			"СуммаНалога"+м,
			Новый ОписаниеТипов("Строка"),
			,
			НСтр("ru='Сумма налога'"));
		Массив[3*м+1] = реквизит;
		
		реквизит = Новый РеквизитФормы(
			"СтраховыеВзносы"+м,
			Новый ОписаниеТипов("Строка"),
			,
			НСтр("ru='Страховые взносы'"));
		
		Массив[3*м+2] = реквизит;

		
	КонецЦикла;
	
	ЭтаФорма.ИзменитьРеквизиты(Массив);
	
	
	Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
		
		ЭтаФорма["Окато"+к] = Строка.Окато;
		ЭтаФорма["СуммаНалога"+к] = "" + Строка.СуммаНалога + " р.";
		ЭтаФорма["СтраховыеВзносы"+к] = "" + Формат(Число(Строка.СуммаНалогаДоВычетаВзносов-Строка.СуммаНалога ),"ЧДЦ=2; ЧН=")+ " р.";
		
		грОкато = Элементы.Добавить("ГруппаОкато"+к,Тип("ГруппаФормы"), Элементы.ГруппаДеталиСсылкиЛевая);
		грОкато.Вид = ВидГруппыФормы.ОбычнаяГруппа;
		грОкато.Отображение = ОтображениеОбычнойГруппы.Нет;
		грОкато.ОтображатьЗаголовок = Ложь;
		грОкато.Группировка = ГруппировкаПодчиненныхЭлементовФормы.Вертикальная;
		
		элОкато = Элементы.Добавить("элОкатоТекст"+к, Тип("ПолеФормы"), грОкато);
		элОкато.Вид = ВидПоляФормы.ПолеНадписи;
		элОкато.ПутьКДанным = "Окато"+к;
		элОкато.Ширина = 24;
		элОкато.ШрифтЗаголовка = Новый Шрифт(,12);
		элОкато.Шрифт = Новый Шрифт(,12);
		элОкато.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветПояснящегоТекстаПротоколовРасчета;
		элОкато.ЦветТекста = ЦветаСтиля.ЦветПояснящегоТекстаПротоколовРасчета;
		элОкато.РастягиватьПоГоризонтали = Ложь;
		элОкато.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		
		Если ТаблицаЕНВДПоОкато.Количество() > 1 Тогда
			элСтраховыеВзносы = Элементы.Добавить("элСтраховыеВзносы"+к, Тип("ПолеФормы"), грОкато);
			элСтраховыеВзносы.Вид = ВидПоляФормы.ПолеНадписи;
			элСтраховыеВзносы.ПутьКДанным = "СтраховыеВзносы"+к;
			элСтраховыеВзносы.Ширина = 24;
			элСтраховыеВзносы.ШрифтЗаголовка = Новый Шрифт(,12);
			элСтраховыеВзносы.Шрифт = Новый Шрифт(,12);
			элСтраховыеВзносы.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветПояснящегоТекстаПротоколовРасчета;
			элСтраховыеВзносы.ЦветТекста = ЦветаСтиля.ЦветПояснящегоТекстаПротоколовРасчета;
			элСтраховыеВзносы.РастягиватьПоГоризонтали = Ложь;
			элСтраховыеВзносы.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		КонецЕсли;
		
		элСуммаНалога = Элементы.Добавить("элСуммаНалога"+к, Тип("ПолеФормы"), грОкато);
		элСуммаНалога.Вид = ВидПоляФормы.ПолеНадписи;
		элСуммаНалога.ПутьКДанным = "СуммаНалога"+к;
		элСуммаНалога.Ширина = 24;
		элСуммаНалога.ШрифтЗаголовка = Новый Шрифт(,12);
		элСуммаНалога.Шрифт = Новый Шрифт(,12);
		элСуммаНалога.ЦветТекста = Новый Цвет(0,0,0);
		элСуммаНалога.Гиперссылка = Истина;
		элСуммаНалога.ЦветТекстаЗаголовка = ЦветаСтиля.ЦветПояснящегоТекстаПротоколовРасчета;
		элСуммаНалога.РастягиватьПоГоризонтали = Ложь;
		элСуммаНалога.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Право;
		элСуммаНалога.УстановитьДействие("Нажатие", "ПриНажатииСуммыНалога");
		
		элОтступ = Элементы.Добавить("элОтступ"+к, Тип("ДекорацияФормы"), грОкато);
		элОтступ.Вид = ВидДекорацииФормы.Надпись;
		
		Если ТаблицаЕНВДПоОкато.Количество() =  1 Тогда
			элОкато.Ширина = 29;
			элСуммаНалога.Ширина = 29;
		КонецЕсли;
		
		к = к + 1;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриНажатииСуммыНалога(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НомерСтроки = Сред(Элемент.Имя,СтрДлина("элСуммаНалога")+1);
	ПоказатьПолнуюТаблицу(НомерСтроки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПолнуюТаблицу(НомерСтрокиТаблицы = Неопределено)
	
	ОткрытьФорму(
		"Обработка.ОбработкиНалоговИОтчетности.Форма.ТаблицаЕНВДРасшифровка",
		Новый Структура(
			"АдресОбщейТаблицы,АдресТаблицыПоОкато,ПолнаяТаблица,НомерСтроки",
			АдресТаблицаЕНВД,
			АдресТаблицаЕНВДПоОкато,
			НомерСтрокиТаблицы = Неопределено,
			НомерСтрокиТаблицы));
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСписокПлатежек(ОповещениеВыбораПлатежек)
	
	Если Не ЗначениеЗаполнено(БанковскийСчетПоУмолчанию) Тогда
		оп = Новый ОписаниеОповещения("ПолучениеБанкСчета", ЭтотОбъект, Новый Структура("ОповещениеВыбораПлатежек", ОповещениеВыбораПлатежек));
		РегламентированнаяОтчетностьУСНКлиент.ПолучитьБанковскийСчетДляУплатыНалога(оп, Объект.Организация);
		Возврат;
	КонецЕсли;
	
	Возвращаем = Новый Структура ("БанковскийСчет", БанковскийСчетПоУмолчанию);
	ВыполнитьОбработкуОповещения(ОповещениеВыбораПлатежек, Возвращаем);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеБанкСчета(Счет, Параметры) Экспорт
	Если ЗначениеЗаполнено(Счет) Тогда
		БанковскийСчетПоУмолчанию = Счет;
		Возвращаем = Новый Структура ("БанковскийСчет", Счет);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеВыбораПлатежек, Возвращаем);
	КонецЕсли;
КонецПроцедуры

&наСервере
Функция ПолучитьСписокПлатежекНаСервере()
	
	Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
		Ссылка = РегламентированнаяОтчетностьУСН.СоздатьБезналичноеСписаниеПоВзаиморасчетамСБюджетом(Строка.ДокументВзаиморасчетовСБюджетом, БанковскийСчетПоУмолчанию);
		Строка.БезналичнаяУплата = Ссылка;
	КонецЦикла;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеРегистрацииРасходов()
	Если ТаблицаЕНВДПоОкато.Количество() > 1 Тогда
		Элементы.ОтразитьРасходыВПрограмме.Заголовок = НСтр("ru='Зарегистрировать расходы в программе'");
	ИначеЕсли ТаблицаЕНВДПоОкато.Количество() = 0 Тогда
	Иначе
		Элементы.ОтразитьРасходыВПрограмме.Заголовок = ТаблицаЕНВДПоОкато[0].НаличнаяУплата;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьКвитанциюЗавершение() Экспорт
	
	СписокДокументов = Новый Массив;
	
	Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
		СписокДокументов.Добавить(Строка.ДокументВзаиморасчетовСБюджетом);
	КонецЦикла;
	
	СтруктураПечати = ВыгрузитьДокументВФайл(СписокДокументов, "Квитанция");
	Если СтруктураПечати.АдресФайла = Неопределено Тогда
		
		Сообщить(Нстр("ru='Произошла ошибка при выгрузке документа '")+Объект);
		Возврат;
		
	КонецЕсли;
	
	ПолучитьФайл(СтруктураПечати.АдресФайла, СтруктураПечати.НаименованиеФайла, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура РаспечататьПлатежноеПоручениеЗавершение() Экспорт
	
	ПолучитьСписокПлатежекНаСервере();
	СписокДокументов = Новый Массив;
	
	Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
		СписокДокументов.Добавить(Строка.БезналичнаяУплата);
	КонецЦикла;
	
	СтруктураПечати = ВыгрузитьДокументВФайл(СписокДокументов, "ПлатежноеПоручение");
	Если СтруктураПечати.АдресФайла = Неопределено Тогда
		
		Сообщить(Нстр("ru='Произошла ошибка при выгрузке документа '")+Объект);
		Возврат;
		
	КонецЕсли;
	
	ПолучитьФайл(СтруктураПечати.АдресФайла, СтруктураПечати.НаименованиеФайла, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьФайлКлиентБанка() Экспорт
	
	БанкСчетСтруктура = Новый Структура("БанковскийСчет", БанковскийСчетПоУмолчанию);
	
	ПолучитьСписокПлатежекНаСервере();
	СписокПлатежек = Новый СписокЗначений;
	СписокПлатежек = Новый СписокЗначений;
	Для Каждого Строка Из ТаблицаЕНВДПоОкато Цикл
		СписокПлатежек.Добавить(Строка.БезналичнаяУплата);
	КонецЦикла;
	
	БанкСчетСтруктура.Вставить("СписокПлатежек", СписокПлатежек);
	
	ОткрытьФорму(
		"Обработка.КлиентБанк.Форма.СохранениеПлатежек",
		БанкСчетСтруктура);
	
КонецПроцедуры


&НаКлиенте
Функция ПроверитьНаличиеГосОргана(ИмяПроцедурыПоЗавершениюСозданияГосОргана)
	КодГосОргана = "";
	
	Если ГосОрганСуществует( КодГосОргана) Тогда
		Возврат Истина;
	Иначе
		ДопПараметры = Новый Структура("ИмяПроцедурыПоЗавершениюСозданияГосОргана, КодГосОргана", ИмяПроцедурыПоЗавершениюСозданияГосОргана, КодГосОргана);
		ТекстВопроса = НСтр("ru='В справочнике «Контрагенты» не задан налоговый орган для уплаты налога.
			|Создать его автоматически?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПриОтветеНаВопросОСозданииГосОргана",
			ЭтаФорма, ДопПараметры);
		Кнопки = Новый СписокЗначений;
		Кнопки.Добавить(КодВозвратаДиалога.Да, НСтр("ru='Создать автоматически'"));
		Кнопки.Добавить(КодВозвратаДиалога.Нет, НСтр("ru='Создать вручную'"));
		Кнопки.Добавить(КодВозвратаДиалога.Отмена, НСтр("ru='Отмена'"));
		
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, Кнопки,,КодВозвратаДиалога.Да, НСтр("ru='Отсутствует налоговый орган'"));
		
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПриОтветеНаВопросОСозданииГосОргана(РезультатВопроса, ДопПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		ВыполнитьЗаполнениеСведенийОНалоговойИнспекции(ДопПараметры);
	ИначеЕсли РезультатВопроса = КодВозвратаДиалога.Нет Тогда
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидГосударственногоОргана", ПредопределенноеЗначение("Перечисление.ВидыГосударственныхОрганов.НалоговыйОрган"));
		ПараметрыФормы.Вставить("КодГосударственногоОргана", ДопПараметры.КодГосОргана);
		ПараметрыФормы.Вставить("ИмяПроцедурыПоЗавершениюСозданияГосОргана",ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана);
		ПараметрыФормы.Вставить("ЗапретРедактированияКода", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьИзменениеПлатежныхРеквизитовФНС", ЭтотОбъект, ПараметрыФормы);
		
		ОткрытьФорму("Справочник.Контрагенты.Форма.РеквизитыГосударственныхОрганов", ПараметрыФормы, ЭтотОбъект, ЭтотОбъект, , , ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьЗаполнениеСведенийОНалоговойИнспекции(ДопПараметры)
	
	ОписаниеОшибки = "";
	ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ОписаниеОшибки, ДопПараметры.КодГосОргана);
	
	// Обработка ошибок
	Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
		Если ОписаниеОшибки = "НеУказаныПараметрыАутентификации" Тогда
			
			ТекстВопроса = НСтр("ru='Для автоматического создания налогового органа
				|в справочнике «Контрагенты» необходимо подключиться к Интернет-поддержке
				|пользователей. Подключиться сейчас?'");
				
			ПараметрыВопроса = Новый Структура("ВызовПослеПодключения,ИмяПроцедурыПоЗавершениюСозданияГосОргана", "ЗаполнитьСведенияОНалоговойИнспекцииПоКоду, КодГосОргана", ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана, ДопПараметры.КодГосОргана);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержку", ЭтотОбъект, ПараметрыВопроса);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		Иначе
			ПоказатьПредупреждение(, ОписаниеОшибки);
		КонецЕсли;
	Иначе
		Если ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
			РаспечататьКвитанциюЗавершение();
		ИначеЕсли ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьПлатежноеПоручениеЗавершение" Тогда
			РаспечататьПлатежноеПоручениеЗавершение();
		ИначеЕсли ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "ПолучитьФайлКлиентБанка" Тогда
			ПолучитьФайлКлиентБанка();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержку(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Оповещение = Новый ОписаниеОповещения("ПодключитьИнтернетПоддержкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(Оповещение, ЭтотОбъект);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПодключитьИнтернетПоддержкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		Если ДополнительныеПараметры.Свойство("ВызовПослеПодключения") Тогда
			
			Если ДополнительныеПараметры.ВызовПослеПодключения = "ЗаполнитьСведенияОНалоговойИнспекцииПоКоду" Тогда
				
				ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(,ДополнительныеПараметры.КодГосОргана);
				
				Если ДополнительныеПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
					РаспечататьКвитанциюЗавершение();
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры


&НаСервере
Процедура ЗаполнитьСведенияОНалоговойИнспекцииПоКоду(ОписаниеОшибки = "", КодНалоговогоОрганаПолучателя)
	
	РеквизитыНалоговогоОргана = ДанныеГосударственныхОрганов.РеквизитыНалоговогоОрганаПоКоду(КодНалоговогоОрганаПолучателя);
	
	Если ЗначениеЗаполнено(РеквизитыНалоговогоОргана.ОписаниеОшибки) Тогда
		ОписаниеОшибки = РеквизитыНалоговогоОргана.ОписаниеОшибки;
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(РеквизитыНалоговогоОргана.Ссылка) Тогда
		ДанныеГосударственныхОрганов.ОбновитьДанныеГосударственногоОргана(РеквизитыНалоговогоОргана);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьИзменениеПлатежныхРеквизитовФНС(Ответ, ДопПараметры) Экспорт
	Если ТипЗнч(Ответ) = Тип("Структура") Тогда
		Если ДопПараметры.ИмяПроцедурыПоЗавершениюСозданияГосОргана = "РаспечататьКвитанциюЗавершение" Тогда
			РаспечататьКвитанциюЗавершение();
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция ГосОрганСуществует( КодГосОргана)
	
	ТаблицаДокументов = ТаблицаЕНВДПоОкато.Выгрузить(,"ДокументВзаиморасчетовСБюджетом");
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НачислениеНалоговНалоги.КодИФНС КАК КодИФНС
	|ИЗ
	|	Документ.НачислениеНалогов.Налоги КАК НачислениеНалоговНалоги
	|ГДЕ
	|	НачислениеНалоговНалоги.Ссылка В(&МассивДокументов)
	|	И НачислениеНалоговНалоги.НомерСтроки = 1
	|	И НачислениеНалоговНалоги.ВидНалога = &ВидНалога");
	
	МассивДокументов = Новый Массив;
	Для Каждого СтрокаДокументаВзаиморасчетов Из ТаблицаДокументов Цикл
		МассивДокументов.Добавить( СтрокаДокументаВзаиморасчетов.ДокументВзаиморасчетовСБюджетом);
	КонецЦикла;
	
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	Запрос.УстановитьПараметр("ВидНалога", Справочники.ВидыНалогов.ЕНВД);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		КодГосОргана = Выборка.КодИФНС;
		ГосОрган = ДанныеГосударственныхОрганов.ГосударственныйОрган(Перечисления.ВидыГосударственныхОрганов.НалоговыйОрган, КодГосОргана);
		Если Не ЗначениеЗаполнено(ГосОрган.Ссылка) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

&НаСервереБезКонтекста
Функция ВыгрузитьДокументВФайл(МассивОбъектов, ВидФайла)
	
	ТабДок = Новый ТабличныйДокумент;
	
	Если ВидФайла = "Квитанция" Тогда
		Для Каждого Объект Из МассивОбъектов Цикл
			ТабДокОбъекта = Документы.НачислениеНалогов.СформироватьКвитанцию(Объект);
			ТабДокОбъекта.Область().СоздатьФорматСтрок();
			Если ТабДок = Неопределено Тогда
				ТабДок = ТабДокОбъекта;
			Иначе
				ТабДок.Вывести(ТабДокОбъекта);
			КонецЕсли;
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЦикла;
		
	ИначеЕсли ВидФайла = "ПлатежноеПоручение" Тогда
		ОбъектыПечати = Новый СписокЗначений;
		ТабДок = Документы.ПлатежноеПоручение.ПечатнаяФорма(МассивОбъектов, ОбъектыПечати);
	КонецЕсли;
	
	СтруктураПечати = Новый Структура ("АдресФайла, НаименованиеФайла");
	
	Если ТабДок = Неопределено Тогда
		Возврат СтруктураПечати;
	Конецесли;
	
	Каталог = КаталогВременныхФайлов()+ОбщегоНазначения.ЗначениеРазделителяСеанса()+"\";
	
	ПроверкаКаталога = Новый Файл(Каталог);
	Если НЕ ПроверкаКаталога.Существует() Тогда
		СоздатьКаталог(Каталог);
	КонецЕсли;
	ПризнакОшибки = Ложь;
	РасширениеФайла = ".pdf" ;
	НаименованиеФайла = Строка(?(МассивОбъектов.Количество() > 0,МассивОбъектов[0], НСтр("ru='Результат'")));
	НаименованиеФайла = ?(СтрДлина(НаименованиеФайла)+СтрДлина(РасширениеФайла)>64, Лев(НаименованиеФайла,64 - СтрДлина(РасширениеФайла)),НаименованиеФайла);
	НаименованиеФайла = СтрЗаменить(НаименованиеФайла, ":","_");
	
	ПолноеИмяФайла = Каталог + НаименованиеФайла + РасширениеФайла;
	ПолноеИмяФайла = СтрЗаменить(ПолноеИмяФайла, """","");
	
	Попытка
		ТабДок.Записать(ПолноеИмяФайла, ТипФайлаТабличногоДокумента.PDF);
		
	Исключение
		ПризнакОшибки = Истина;
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Ошибка сохранения файла на сервере'"),
			УровеньЖурналаРегистрации.Ошибка,
			,
			,
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru= 'При сохранении файла на сервере в области данных %1 произошла ошибка:
						|%2'"),
				ОбщегоНазначения.ЗначениеРазделителяСеанса(),
				ОписаниеОшибки()),
			РежимТранзакцииЗаписиЖурналаРегистрации.Независимая);
		
		Возврат СтруктураПечати;
		
	Конецпопытки;
	ДанныеФайла = Новый ДвоичныеДанные(ПолноеИмяФайла);
	АдресФайла = ПоместитьВоВременноеХранилище(ДанныеФайла, Новый УникальныйИдентификатор);
	УдалитьФайлы(ПолноеИмяФайла);
	УдалитьФайлы(Каталог);
	СтруктураПечати.АдресФайла = АдресФайла;
	СтруктураПечати.НаименованиеФайла = НаименованиеФайла+РасширениеФайла;
	
	Возврат СтруктураПечати;
	
КонецФункции


