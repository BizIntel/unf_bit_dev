&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Организация = Параметры.Организация;
	ДатаНачала = НачалоГода(Параметры.НачалоПериода);
	ДатаОкончания = КонецДня(Параметры.ОкончаниеПериода);
	
	ЗаполнитьТаблицуПатентов();
	
	Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Заголовок,
		?(ЗначениеЗаполнено(Параметры.НачалоПериода),Формат(Параметры.НачалоПериода, "ДФ=dd.MM.yyyy"), "" ),
		?(ЗначениеЗаполнено(Параметры.ОкончаниеПериода),Формат(Параметры.ОкончаниеПериода, "ДФ=dd.MM.yyyy"), "" ));
КонецПроцедуры


&НаКлиенте
Процедура ТаблицаПатентВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	Если Поле.Имя = "ТаблицаПатентПатент" Тогда
		ПоказатьЗначение(,Элементы.ТаблицаПатент.ТекущиеДанные.Патент);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры


Процедура ЗаполнитьТаблицуПатентов()
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Патенты.Ссылка КАК Патент,
	|	Патенты.ДатаНачала,
	|	Патенты.ДатаОкончания,
	|	Патенты.ПотенциальноВозможныйГодовойДоход
	|ИЗ
	|	Справочник.Патенты КАК Патенты
	|ГДЕ
	|	НЕ Патенты.ПометкаУдаления
	|	И Патенты.Владелец = &Организация
	|	И (Патенты.ДатаОкончания МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|			ИЛИ Патенты.ДатаНачала МЕЖДУ &НачалоПериода И &ОкончаниеПериода
	|			ИЛИ &НачалоПериода МЕЖДУ Патенты.ДатаНачала И Патенты.ДатаОкончания
	|			ИЛИ &ОкончаниеПериода МЕЖДУ Патенты.ДатаНачала И Патенты.ДатаОкончания)");
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачала);
	Запрос.УстановитьПараметр("ОкончаниеПериода", ДатаОкончания);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаПатент.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.ПотенциальныйДоход = РегламентированнаяОтчетностьУСНКлиентСервер.РассчитатьПотенциальноВозможныйДоход(
				Выборка.ПотенциальноВозможныйГодовойДоход, ДатаНачала, ДатаОкончания);
		
	КонецЦикла;
	
КонецПроцедуры