
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
// Функция позвращает массив значений состоящий из элементов табличной части
//
Функция ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте(ИмяТабличнойЧасти)
	
	МассивЗначений = Новый Массив;
	
	Для каждого СтрокаТаблицы Из Объект[ИмяТабличнойЧасти] Цикл
		
		МассивЗначений.Добавить(СтрокаТаблицы.Ссылка);
		
	КонецЦикла;
	
	Возврат МассивЗначений;
	
КонецФункции // ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте()

&НаКлиенте
// Добавляет элементы массива в табличную часть.
// Предварительно проверяем на наличие данного элемента в табличной часте.
//
Процедура ДобавитьЭлементыВТабличнуюЧасть(МассивЭлементов)
	
	Если НЕ ТипЗнч(МассивЭлементов) = Тип("Массив") 
		ИЛИ НЕ МассивЭлементов.Количество() > 0 Тогда 
		
		Возврат;
		
	КонецЕсли;
	
	Для каждого ЭлементМассива Из МассивЭлементов Цикл
		
		Если Объект.ВидыЦенКонтрагента.НайтиСтроки(Новый Структура("Ссылка", ЭлементМассива)).Количество() > 0 Тогда
			
			ТекстСообщения = Нстр("ru = 'Элемент [" + ЭлементМассива + "] присутствует в отборе.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Продолжить;
			
		КонецЕсли;
		
		НоваяСтрока 		= Объект.ВидыЦенКонтрагента.Добавить();
		НоваяСтрока.Ссылка	= ЭлементМассива;
		
	КонецЦикла;
	
КонецПроцедуры //ДобавитьЭлементыВТабличнуюЧасть()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("МассивВидыЦенКонтрагента") Тогда
		
		Для каждого ЭлементаМассива Из Параметры.МассивВидыЦенКонтрагента Цикл
				
			НоваяСтрока = Объект.ВидыЦенКонтрагента.Добавить();
			НоваяСтрока.Ссылка = ЭлементаМассива.Ссылка;
				
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры //ПриСозданииНаСервере()

&НаКлиенте
// Процедура - обработчик события ОбработкаВыбора формы.
//
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если НЕ ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		ЗначениеВыбора 		= ВыбранноеЗначение;
		ВыбранноеЗначение	= Новый Массив;
		ВыбранноеЗначение.Добавить(ЗначениеВыбора);
		
	КонецЕсли;
	
	ДобавитьЭлементыВТабличнуюЧасть(ВыбранноеЗначение);
	
КонецПроцедуры //ОбработкаВыбора()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ КОМАНД

&НаКлиенте
// Процедура - обработчик команды ОК.
//
Процедура ОК(Команда)
	
	ОповеститьОВыборе(ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("ВидыЦенКонтрагента"));
	
КонецПроцедуры //ОК()

&НаКлиенте
// Процедура - обработчик команды Подбор.
//
Процедура Подбор(Команда)
	
	ОткрытьФорму("Справочник.ВидыЦенКонтрагентов.Форма.ФормаБыстрогоВыбора", Новый Структура("МножественныйВыбор, РежимВыбора, ЗакрыватьПриВыборе", Истина, Истина, Ложь), ЭтаФорма);
	
КонецПроцедуры //Подбор()
