
////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
Процедура ПереключитьРежимОтбора(ИмяТабличнойЧасти)
	
	Если Элементы[ИмяТабличнойЧасти + "Список"].Пометка Тогда
		
		Элементы["ДекорацияМножетвенныйОтбор" + ИмяТабличнойЧасти].Заголовок = ПолучитьСодержаниеЗаголовкаДекорации(ИмяТабличнойЧасти);
		
	Иначе
		
		Если Объект[ИмяТабличнойЧасти].Количество() > 0 Тогда
		
			ТекстВопроса = Нстр("ru = 'Множественный отбор будет очищен. Продолжить?'");
			ПоказатьВопрос(Новый ОписаниеОповещения("ПереключитьРежимОтбораЗавершение", ЭтотОбъект, Новый Структура("ИмяТабличнойЧасти", ИмяТабличнойЧасти)), ТекстВопроса, РежимДиалогаВопрос.ДаНет);
            Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ПереключитьРежимОтбораФрагмент(ИмяТабличнойЧасти);
КонецПроцедуры

&НаКлиенте
Процедура ПереключитьРежимОтбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    ИмяТабличнойЧасти = ДополнительныеПараметры.ИмяТабличнойЧасти;
    
    
    Если Результат = КодВозвратаДиалога.Да Тогда
        
        Объект[ИмяТабличнойЧасти].Очистить();
        
    Иначе
        
        Элементы[ИмяТабличнойЧасти + "Список"].Пометка = НЕ Элементы[ИмяТабличнойЧасти + "Список"].Пометка;
        
    КонецЕсли;
    
    
    ПереключитьРежимОтбораФрагмент(ИмяТабличнойЧасти);

КонецПроцедуры

&НаКлиенте
Процедура ПереключитьРежимОтбораФрагмент(Знач ИмяТабличнойЧасти)
    
    ИзменитьСтраницуОтбора(ИмяТабличнойЧасти, Элементы[ИмяТабличнойЧасти + "Список"].Пометка);

КонецПроцедуры

&НаКлиенте
Функция ПолучитьСодержаниеЗаголовкаДекорации(ИмяТабличнойЧасти) 
	
	Если Объект[ИмяТабличнойЧасти].Количество() < 1 Тогда
		
		ЗаголовокДекорации = "Множественный отбор не заполнен";
		
	ИначеЕсли Объект[ИмяТабличнойЧасти].Количество() > 1 Тогда
		
		ЗаголовокДекорации = "Выбранные элементы: " + Строка(Объект[ИмяТабличнойЧасти][0].Ссылка) + "; " + Строка(Объект[ИмяТабличнойЧасти][1].Ссылка) + "...";
		
	Иначе
		
		ЗаголовокДекорации = "Выбранный элемент: " + Строка(Объект[ИмяТабличнойЧасти][0].Ссылка);
		
	КонецЕсли;
	
	Возврат ЗаголовокДекорации;
	
КонецФункции

&НаКлиенте
Процедура ИзменитьСтраницуОтбора(ИмяТабличнойЧасти, Списком)
	
	ГруппаСтраницы = Элементы["СтраницыОтбора" + ИмяТабличнойЧасти];
	
	УстановитьТекущейСтраницей = Неопределено;
	
	Для каждого СтраницаГруппы из ГруппаСтраницы.ПодчиненныеЭлементы Цикл
		
		Если Списком Тогда
			
			Если СтрНайти(СтраницаГруппы.Имя, "МножественныйОтбор") > 0 Тогда
			
				УстановитьТекущейСтраницей = СтраницаГруппы;
				Прервать;
			
			КонецЕсли;
			
		Иначе
			
			Если СтрНайти(СтраницаГруппы.Имя, "БыстрыйОтбор") > 0 Тогда
			
				УстановитьТекущейСтраницей = СтраницаГруппы;
				Прервать;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Элементы["ДекорацияМножетвенныйОтбор" + ИмяТабличнойЧасти].Заголовок = ПолучитьСодержаниеЗаголовкаДекорации(ИмяТабличнойЧасти);
	
	ГруппаСтраницы.ТекущаяСтраница = УстановитьТекущейСтраницей;
	
КонецПроцедуры

&НаКлиенте
Функция ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте(ИмяТабличнойЧасти)
	
	МассивЗначений = Новый Массив;
	
	Для каждого СтрокаТаблицы Из Объект[ИмяТабличнойЧасти] Цикл
		
		МассивЗначений.Добавить(СтрокаТаблицы.Ссылка);
		
	КонецЦикла;
	
	Возврат МассивЗначений;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаКлиенте(ИмяТабличнойЧасти, МассивЭлементов, ОчиститьТаблицу)
	
	Если ОчиститьТаблицу Тогда
		
		Объект[ИмяТабличнойЧасти].Очистить();
		
	КонецЕсли;
	
	Для каждого ЭлементМассива Из МассивЭлементов Цикл
		
		НоваяСтрока 		= Объект[ИмяТабличнойЧасти].Добавить();
		НоваяСтрока.Ссылка	= ЭлементМассива;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаСервере(ИмяТабличнойЧасти, МассивЭлементов, ОчиститьТаблицу = Истина)
	
	Если ОчиститьТаблицу Тогда
		
		Объект[ИмяТабличнойЧасти].Очистить();
		
	КонецЕсли;
	
	Для каждого ЭлементМассива Из МассивЭлементов Цикл
		
		НоваяСтрока 		= Объект[ИмяТабличнойЧасти].Добавить();
		НоваяСтрока.Ссылка	= ЭлементМассива;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура АнализироватьВыбор(ИмяТабличнойЧасти)
	
	КоличествоЭлементов = Объект[ИмяТабличнойЧасти].Количество();
	
	Элементы[ИмяТабличнойЧасти + "Список"].Пометка = (КоличествоЭлементов > 0);
	
	ИзменитьСтраницуОтбора(ИмяТабличнойЧасти, Элементы[ИмяТабличнойЧасти + "Список"].Пометка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	НаДату 					= Параметры.НаДату;
	Актуальность			= Параметры.Актуальность;
	ПолноеНаименование		= Параметры.ПолноеНаименование;
	
	Если ТипЗнч(Параметры.ВидЦенКонтрагента) = Тип("Массив") Тогда
		
		ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаСервере("ВидыЦенКонтрагента", Параметры.ВидЦенКонтрагента, Истина);
		
	Иначе
		
		ВидЦеныКонтрагента = Параметры.ВидЦенКонтрагента;
		
	КонецЕсли;
	
	Если ТипЗнч(Параметры.ЦеноваяГруппа) = Тип("Массив") Тогда
		
		ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаСервере("ЦеновыеГруппы", Параметры.ЦеноваяГруппа, Истина);
		
	Иначе
		
		ЦеноваяГруппа = Параметры.ЦеноваяГруппа;
		
	КонецЕсли;
	
	Если ТипЗнч(Параметры.Номенклатура) = Тип("Массив") Тогда
		
		ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаСервере("Номенклатура", Параметры.Номенклатура, Истина);
		
	Иначе
		
		Номенклатура = Параметры.Номенклатура;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.ВидыЦенКонтрагентаСписок.Пометка = (Объект.ВидыЦенКонтрагента.Количество() > 0);
	ИзменитьСтраницуОтбора("ВидыЦенКонтрагента", Элементы.ВидыЦенКонтрагентаСписок.Пометка);
	
	Элементы.ЦеновыеГруппыСписок.Пометка = (Объект.ЦеновыеГруппы.Количество() > 0);
	ИзменитьСтраницуОтбора("ЦеновыеГруппы", Элементы.ЦеновыеГруппыСписок.Пометка);
	
	Элементы.НоменклатураСписок.Пометка = (Объект.Номенклатура.Количество() > 0);
	ИзменитьСтраницуОтбора("Номенклатура", Элементы.НоменклатураСписок.Пометка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("Массив") Тогда
		
		ОчиститьТаблицу = (СтрНайти(ИсточникВыбора.ИмяФормы, "Обработка.ПрайсЛистыКонтрагентов") > 0);
		
		Если ИсточникВыбора.ИмяФормы = "Справочник.ВидыЦенКонтрагентов.Форма.ФормаБыстрогоВыбора" 
			ИЛИ ИсточникВыбора.ИмяФормы = "Обработка.ПрайсЛистыКонтрагентов.Форма.ФормаРедактированияВидовЦенКонтрагента" Тогда
			
			ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаКлиенте("ВидыЦенКонтрагента", ВыбранноеЗначение, ОчиститьТаблицу);
			АнализироватьВыбор("ВидыЦенКонтрагента");
			
		ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.ЦеновыеГруппы.Форма.ФормаВыбора" 
			ИЛИ ИсточникВыбора.ИмяФормы = "Обработка.ПрайсЛистыКонтрагентов.Форма.ФормаРедактированияЦеновыхГрупп" Тогда
			
			ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаКлиенте("ЦеновыеГруппы", ВыбранноеЗначение, ОчиститьТаблицу);
			АнализироватьВыбор("ЦеновыеГруппы");
			
		ИначеЕсли ИсточникВыбора.ИмяФормы = "Справочник.Номенклатура.ФормаВыбора" 
			ИЛИ ИсточникВыбора.ИмяФормы = "Справочник.Номенклатура.Форма.ФормаСписка"
			ИЛИ ИсточникВыбора.ИмяФормы = "Обработка.ПрайсЛистыКонтрагентов.Форма.ФормаРедактированияНоменклатуры" Тогда
			
			ЗаполнитьТабличнуюЧастьИзЭлементовМассиваНаКлиенте("Номенклатура", ВыбранноеЗначение, ОчиститьТаблицу);
			АнализироватьВыбор("Номенклатура");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ КОМАНД

&НаКлиенте
Процедура ВидЦенСписок(Команда)
	
	Элементы.ВидыЦенКонтрагентаСписок.Пометка = НЕ Элементы.ВидыЦенКонтрагентаСписок.Пометка;
	
	Если ЗначениеЗаполнено(ВидЦеныКонтрагента) 
		И Элементы.ВидыЦенКонтрагентаСписок.Пометка Тогда
		
		НоваяСтрока 		= Объект.ВидыЦенКонтрагента.Добавить();
		НоваяСтрока.ССылка	= ВидЦеныКонтрагента;
		
		ВидЦеныКонтрагента	= Неопределено;
		
	КонецЕсли;
	
	ПереключитьРежимОтбора("ВидыЦенКонтрагента");
	
КонецПроцедуры

&НаКлиенте
Процедура ЦеновыеГруппыСписок(Команда)
	
	Элементы.ЦеновыеГруппыСписок.Пометка = НЕ Элементы.ЦеновыеГруппыСписок.Пометка;
	
	Если ЗначениеЗаполнено(ЦеноваяГруппа) 
		И Элементы.ЦеновыеГруппыСписок.Пометка Тогда
		
		НоваяСтрока 		= Объект.ЦеновыеГруппы.Добавить();
		НоваяСтрока.ССылка	= ЦеноваяГруппа;
		
		ЦеноваяГруппа		= Неопределено;
		
	КонецЕсли;
	
	ПереключитьРежимОтбора("ЦеновыеГруппы");
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураСписок(Команда)
	
	Элементы.НоменклатураСписок.Пометка = НЕ Элементы.НоменклатураСписок.Пометка;
	
	Если ЗначениеЗаполнено(Номенклатура) 
		И Элементы.НоменклатураСписок.Пометка Тогда
		
		НоваяСтрока 		= Объект.Номенклатура.Добавить();
		НоваяСтрока.ССылка	= Номенклатура;
		
		Номенклатура		= Неопределено;
		
	КонецЕсли;
	
	ПереключитьРежимОтбора("Номенклатура");
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМножетвенныйОтборВидыЦенКонтрагентаНажатие(Элемент)
	
	Если Объект.ВидыЦенКонтрагента.Количество() > 0 Тогда
		
		ОткрытьФорму("Обработка.ПрайсЛистыКонтрагентов.Форма.ФормаРедактированияВидовЦенКонтрагента", Новый Структура("МассивВидыЦенКонтрагента", ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("ВидыЦенКонтрагента")), ЭтаФорма);
		
	Иначе
		
		ОткрытьФорму("Справочник.ВидыЦенКонтрагентов.Форма.ФормаБыстрогоВыбора", Новый Структура("МножественныйВыбор", Истина), ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМножетвенныйОтборЦеновыеГруппыНажатие(Элемент)
	
	Если Объект.ЦеновыеГруппы.Количество() > 0 Тогда
		
		ОткрытьФорму("Обработка.ПрайсЛистыКонтрагентов.Форма.ФормаРедактированияЦеновыхГрупп", Новый Структура("МассивЦеновыеГруппы", ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("ЦеновыеГруппы")), ЭтаФорма);
		
	Иначе
		
		ОткрытьФорму("Справочник.ЦеновыеГруппы.Форма.ФормаВыбора", Новый Структура("МножественныйВыбор", Истина), ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияМножетвенныйОтборНоменклатураНажатие(Элемент)
	
	Если Объект.Номенклатура.Количество() > 0 Тогда
		
		ОткрытьФорму("Обработка.ПрайсЛистыКонтрагентов.Форма.ФормаРедактированияНоменклатуры", Новый Структура("МассивНоменклатуры", ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("Номенклатура")), ЭтаФорма);
		
	Иначе
		
		ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", Новый Структура("МножественныйВыбор, ВыборГруппИЭлементов, РежимВыбора", Истина, ИспользованиеГруппИЭлементов.ГруппыИЭлементы, Истина), ЭтаФорма, , , , , );
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	// Передадим заполненные отборы
	ПараметрыФормы.Вставить("НаДату", 				НаДату);
	ПараметрыФормы.Вставить("Актуальность",			Актуальность);
	ПараметрыФормы.Вставить("ПолноеНаименование",	ПолноеНаименование);
	
	ЗначениеПараметра = ?(Элементы.ВидыЦенКонтрагентаСписок.Пометка, ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("ВидыЦенКонтрагента"), ВидЦеныКонтрагента);
	ПараметрыФормы.Вставить("ВидЦенКонтрагента", ЗначениеПараметра);
	
	ЗначениеПараметра = ?(Элементы.ЦеновыеГруппыСписок.Пометка, ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("ЦеновыеГруппы"), ЦеноваяГруппа);
	ПараметрыФормы.Вставить("ЦеноваяГруппа", ЗначениеПараметра);
	
	ЗначениеПараметра = ?(Элементы.НоменклатураСписок.Пометка, ЗаполнитьМассивПоТаблицнойЧастиНаКлиенте("Номенклатура"), Номенклатура);
	ПараметрыФормы.Вставить("Номенклатура", ЗначениеПараметра);
	
	Оповестить("МножественныеОтборыПрайсЛистыКонтрагентов", ПараметрыФормы);
	Закрыть();
	
КонецПроцедуры

