
#Область ПеременныеФормы

&НаКлиенте
Перем ТекущийКонтрагент; // Для проверки в событии "ПриАктивизацииСтроки", что текущая строка была изменена

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ОбработатьАктивизациюСтрокиСписка()
	
	ТекущаяСтрока = Элементы.БанковскиеВыписки.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено
		И ТекущаяСтрока.Свойство("Дата")
		И ТекущаяСтрока.Свойство("СчетКасса") Тогда
		
		СтруктураДанные = ПолучитьДанныеПоБанковскомуСчету(ТекущаяСтрока.Дата, ТекущаяСтрока.СчетКасса);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, СтруктураДанные);
		Дата = НСтр("ru = 'Ведомость за '") + Формат(ТекущаяСтрока.Дата, "ДЛФ=D");
		НомерБанковскогоСчета = СтруктураДанные.НомерБанковскогоСчета;
		Если СтруктураДанные.УчетВалютныхОпераций Тогда
			НомерБанковскогоСчета = НомерБанковскогоСчета + " (" + ?(ЗначениеЗаполнено(ТекущаяСтрока.Валюта), ТекущаяСтрока.Валюта, НСтр("ru = '<валюта пустая>'")) + ")";
		КонецЕсли;
		
		ТаблицаИтогов[0].Значение = ИнформацияСуммаВалНачальныйОстаток;
		ТаблицаИтогов[1].Значение = ИнформацияСуммаВалПриход;
		ТаблицаИтогов[2].Значение = ИнформацияСуммаВалРасход;
		ТаблицаИтогов[3].Значение = ИнформацияСуммаВалКонечныйОстаток;
		
	Иначе
		ИнформацияСуммаВалКонечныйОстаток = 0;
		ИнформацияСуммаВалНачальныйОстаток = 0;
		ИнформацияСуммаВалПриход = 0;
		ИнформацияСуммаВалРасход = 0;
		Дата = "<Не выбран документ>";
		НомерБанковскогоСчета = "";
	Конецесли;
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоБанковскомуСчету(Период, БанковскийСчет)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалНачальныйОстаток КАК ИнформацияСуммаВалНачальныйОстаток,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалПриход КАК ИнформацияСуммаВалПриход,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалРасход КАК ИнформацияСуммаВалРасход,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалКонечныйОстаток КАК ИнформацияСуммаВалКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, День, , БанковскийСчетКасса = &БанковскийСчет) КАК ДенежныеСредстваОстаткиИОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период));
	Запрос.УстановитьПараметр("КонецПериода", КонецДня(Период));
	Запрос.УстановитьПараметр("БанковскийСчет", БанковскийСчет);
	ВыборкаРезультата = Запрос.Выполнить().Выбрать();
	
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда
		НомерБанковскогоСчета = ?(ТипЗнч(БанковскийСчет) = Тип("СправочникСсылка.БанковскиеСчета"), БанковскийСчет.НомерСчета, БанковскийСчет);
	Иначе
		НомерБанковскогоСчета = "";
	КонецЕсли;
	
	Если ВыборкаРезультата.Следующий() Тогда
		СтруктураВозврата = Новый Структура("ИнформацияСуммаВалНачальныйОстаток, ИнформацияСуммаВалКонечныйОстаток, ИнформацияСуммаВалПриход, ИнформацияСуммаВалРасход, УчетВалютныхОпераций, НомерБанковскогоСчета");
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, ВыборкаРезультата);
		СтруктураВозврата.УчетВалютныхОпераций = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		СтруктураВозврата.НомерБанковскогоСчета = НомерБанковскогоСчета;
		Возврат СтруктураВозврата;
	Иначе
		Возврат Новый Структура(
			"ИнформацияСуммаВалКонечныйОстаток, ИнформацияСуммаВалНачальныйОстаток, ИнформацияСуммаВалПриход, ИнформацияСуммаВалРасход, УчетВалютныхОпераций, НомерБанковскогоСчета",
			0,0,0,0,Ложь,НомерБанковскогоСчета
		);
	КонецЕсли;
	
КонецФункции // ПолучитьДанныеПобанковскомуСчету()

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокЗначений = Новый СписокЗначений;
	ВыборкаОрганизаций = Справочники.Организации.Выбрать();
	Пока ВыборкаОрганизаций.Следующий() Цикл
		СписокЗначений.Добавить(ВыборкаОрганизаций.Ссылка);
	КонецЦикла;
	СписокОрганизаций = СписокЗначений;
	
	РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям = ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям");
	ВестиУчетРасчетовСБанкомЭквайрером = ПолучитьФункциональнуюОпцию("ВестиУчетРасчетовСБанкомЭквайрером");
	
	// Отборы
	// Банк и касса
	ПрочитатьРасчетныеСчетаИКассы();
	// Конец Отборы
	
	// Итоги
	НеОтображатьИтоги = Перечисления.ВариантыОтображенияИтоговБанкИКасса.НеОтображатьИтоги;
	ВедомостьЗаДень = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень;
	ВедомостьЗаПериод = Перечисления.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод;
	
	Если НЕ ЗначениеЗаполнено(ВариантОтображенияИтогов) ИЛИ НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
		ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень");
		Если НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
			Элементы.ДекорацияОткрытьФормуНастройкиИтогов.Видимость = Ложь;
			Элементы.ДекорацияОткрытьФормуНастройкиИтогов2.Видимость = Ложь;
			Элементы.ДекорацияОткрытьФормуНастройкиИтогов3.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ЗаполнитьТаблицуИтогов();
	// Конец Итоги
	
	// Установим формат для текущей даты: ДФ=Ч:мм
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(СписокПлатежныхПоручений);
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(БанковскиеВыписки);
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(СписокОперацииПоПлатежнымКартам);
	
	//УНФ.ОтборыСписка
	//установим пустые значения параметров запроса списка
	СписокКассИСчетов = Новый Массив;
	БанковскиеВыписки.Параметры.УстановитьЗначениеПараметра("БезОтбора", Истина);
	БанковскиеВыписки.Параметры.УстановитьЗначениеПараметра("СписокКассИСчетов", СписокКассИСчетов);
	
	СтруктураИменЭлементовПП = Новый Структура;
	СтруктураИменЭлементовПП.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
	СтруктураИменЭлементовПП.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
	СтруктураИменЭлементовПП.Вставить("ПраваяПанель", "ПраваяПанельПП");
	СтруктураИменЭлементовПП.Вставить("ОтборПериод", "ОтборПериодПП");
	СтруктураИменЭлементовПП.Вставить("ПредставлениеПериода", "ПредставлениеПериодаПП");
	
	СтруктураИменЭлементовЭ = Новый Структура;
	СтруктураИменЭлементовЭ.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
	СтруктураИменЭлементовЭ.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
	СтруктураИменЭлементовЭ.Вставить("ПраваяПанель", "ПраваяПанельЭ");
	СтруктураИменЭлементовЭ.Вставить("ОтборПериод", "ОтборПериодЭ");
	СтруктураИменЭлементовЭ.Вставить("ПредставлениеПериода", "ПредставлениеПериодаЭ");
	
	Если Параметры.Свойство("ЭтоНачальнаяСтраница") Тогда
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, БанковскиеВыписки, "БанковскиеВыписки");
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, СписокПлатежныхПоручений, "СписокПлатежныхПоручений", СтруктураИменЭлементовПП,,,, "ДанныеМетокПП");
		РаботаСОтборами.ВосстановитьНастройкиОтборов(ЭтотОбъект, СписокОперацииПоПлатежнымКартам, "СписокОперацииПоПлатежнымКартам", СтруктураИменЭлементовЭ,,,, "ДанныеМетокОплатаКартами");
		
		
		ЭтоНачальнаяСтраница = Ложь;
	Иначе
		РаботаСОтборами.СвернутьРазвернутьОтборыНаСервере(ЭтотОбъект, Ложь);
		РаботаСОтборами.СвернутьРазвернутьОтборыНаСервере(ЭтотОбъект, Ложь, СтруктураИменЭлементовЭ);
		РаботаСОтборами.СвернутьРазвернутьОтборыНаСервере(ЭтотОбъект, Ложь, СтруктураИменЭлементовПП);
		ПредставлениеПериода	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Неопределено);
		ПредставлениеПериодаПП	= РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Неопределено);
		ПредставлениеПериодаЭ   = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Неопределено);
		
		ЭтоНачальнаяСтраница = Истина;
	КонецЕсли;
	//Конец УНФ.ОтборыСписка
	
	// ИнтернетПоддержкаПользователей.Новости
	ОбработкаНовостей.КонтекстныеНовости_ПриСозданииНаСервере(
		ЭтотОбъект,
		"УНФ.Обработка.БанкИКасса",
		"ФормаСписка",
		Неопределено,
		НСтр("ru='Новости: Банк и касса'"),
		Ложь,
		Новый Структура("ПолучатьНовостиНаСервере, ХранитьМассивНовостейТолькоНаСервере", Истина, Истина),
		"ПриОткрытии"
	);
	// Конец ИнтернетПоддержкаПользователей.Новости
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтотОбъект, "ФормаСписка");
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтотОбъект, Элементы.ПодменюПечать);
	// Конец СтандартныеПодсистемы.Печать
	УправлениеНебольшойФирмойСервер.УстановитьОтображаниеПодменюПечати(Элементы.ПодменюПечать);
	
	ИспользуютсяПереводыВПути = РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути();
	Если ИспользуютсяПереводыВПути Тогда
		Элементы.ПодменюПеремещение.Видимость = Истина;
		Элементы.ПодменюПеремещениеБез57Счета.Видимость = Ложь;
	Иначе
		Элементы.ПодменюПеремещение.Видимость = Ложь;
		Элементы.ПодменюПеремещениеБез57Счета.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// ИнтернетПоддержкаПользователей.Новости
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтотОбъект);
	// Конец ИнтернетПоддержкаПользователей.Новости
	
КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	// Флаги над списокм на закладке "Поступление и расход".
	ОтборБанк			= Настройки.Получить("ОтборБанк");
	ОтборКасса			= Настройки.Получить("ОтборКасса");
	НастроитьОтборДинамическогоСпискаБанкКасса();
	
	ОтборПоступления	= Настройки.Получить("ОтборПоступления");
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПоступление", ОтборПоступления);
	
	ОтборРасход			= Настройки.Получить("ОтборРасход");
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоРасход", ОтборРасход);
	
	ОтборПеремещения	= Настройки.Получить("ОтборПеремещения");
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПеремещение", ОтборПеремещения);
	
	// Вариант отображения итогов.
	Если НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
		ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень");
	Иначе
		ТекущийВариантОтображенияИтогов = Настройки.Получить("ВариантОтображенияИтогов");
		Если ЗначениеЗаполнено(ТекущийВариантОтображенияИтогов) Тогда
			ВариантОтображенияИтогов = ТекущийВариантОтображенияИтогов;
			ОтборВалютаВедомости = Настройки.Получить("ОтборВалютаВедомости");
			ОтборВалютаОстатков = Настройки.Получить("ОтборВалютаОстатков");
		КонецЕсли;
	КонецЕсли;
	НастройкаИтоговЗавершениеНаСервере();
	
КонецПроцедуры // ПриЗагрузкеДанныхИзНастроекНаСервере()

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	Если НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
		ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка.БанковскиеСчета") Тогда
		ЗагрузитьИзФайлаФрагмент(ВыбранноеЗначение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

&НаКлиенте
Процедура БанковскиеВыпискиПриАктивизацииСтроки(Элемент)
	
	Если ВариантОтображенияИтогов = ВедомостьЗаДень Тогда
		ПодключитьОбработчикОжидания("Подключаемый_ОбработатьАктивизациюСтрокиСписка", 0.2, Истина);
	КонецЕсли;
	
КонецПроцедуры // БанковскиеВыпискиПриАктивизацииСтроки()

&НаКлиенте
Процедура БанковскиеВыпискиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Копирование Тогда
		возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Параметр) Тогда
		возврат;
	КонецЕсли;
	
	Отказ = Истина;
	
	ИмяДокумента = РаботаСФормойДокументаКлиент.ПолучитьИмяДокументаПоТипу(Параметр);
	Если ИмяДокумента = "ПеремещениеДС" Тогда
		ДвиженияДенежныхСредствКлиент.СоздатьДокумент(ИмяДокумента, "", ДанныеМеток, "СписокПлатежныхПоручений", ПолучитьПараметрыПеремещенияДС("ПеремещениеИзКассыНаСчет"));
	ИначеЕсли ИмяДокумента = "ОперацияПоПлатежнымКартам" Тогда
		ДвиженияДенежныхСредствКлиент.СоздатьДокумент(ИмяДокумента, "", ДанныеМеток, "СписокОперацииПоПлатежнымКартам");
	Иначе
		ДвиженияДенежныхСредствКлиент.СоздатьДокумент(ИмяДокумента, "", ДанныеМеток, "БанковскиеВыписки");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОповещениеОбИзмененииДолга" Тогда
		Если ВариантОтображенияИтогов = ВедомостьЗаДень Тогда
			Подключаемый_ОбработатьАктивизациюСтрокиСписка();
		ИначеЕсли ВариантОтображенияИтогов <> НеОтображатьИтоги Тогда // Итоги по валюте (за период) и остаток в валюте.
			НастройкаИтоговЗавершениеНаСервере();
		КонецЕсли;
	ИначеЕсли ИмяСобытия = "Запись_ПеремещениеДС" ИЛИ
		ИмяСобытия = "Запись_ПоступлениеВКассу" ИЛИ
		ИмяСобытия = "Запись_ПоступлениеНаСчет" ИЛИ
		ИмяСобытия = "Запись_РасходИзКассы" ИЛИ
		ИмяСобытия = "Запись_РасходСоСчета" Тогда
		Если Параметр.Свойство("УдалениеПомеченных") И Параметр.УдалениеПомеченных Тогда
			Возврат;
		КонецЕсли;
		Элементы.БанковскиеВыписки.ТекущаяСтрока = Параметр.Ссылка;
	ИначеЕсли ИмяСобытия = "Запись_ПлатежноеПоручение" Тогда
		Элементы.СписокПлатежныхПоручений.ТекущаяСтрока = Параметр.Ссылка;
	ИначеЕсли ИмяСобытия = "Запись_ОперацияПоПлатежнымКартам" Тогда
		Элементы.СписокОперацииПоПлатежнымКартам.ТекущаяСтрока = Параметр.Ссылка;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаОповещения()

&НаКлиенте
Процедура ВедомостьПерейти(Команда)
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии", "Ведомость", Истина));
	
КонецПроцедуры // ВедомостьПерейти()

&НаКлиенте
Процедура ВедомостьПерейтиКОтчетуПоЭквайрингу(Команда)
	
	Если ВестиУчетРасчетовСБанкомЭквайрером Тогда
		Если РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям Тогда
			ОткрытьФорму("Отчет.КонтрольОперацийЭквайринга.Форма", Новый Структура("СформироватьПриОткрытии", Истина));
		Иначе
			ОткрытьФорму("Отчет.РасчетыПоЭквайрингу.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии", "ОсновнойБезРазнесенияПоОперациямОплаты", Истина));
		КонецЕсли;
	Иначе
		ОткрытьФорму("Отчет.ОплатаПлатежнымиКартами.Форма", Новый Структура("СформироватьПриОткрытии", Истина));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область НастройкаВидимостьЭлементовФормы



#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды ВыгрузитьВКлиентБанк.
//
&НаКлиенте
Процедура ВыгрузитьВКлиентБанк(Команда)
	
	ПараметрыОткрытия = Новый Структура("НачПериода, КонПериода, БанковскийСчетОрганизации", 
		ОтборПериодПП.ДатаНачала, ОтборПериодПП.ДатаОкончания, );
	ТекущиеДанные = Элементы.СписокПлатежныхПоручений.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И ЗначениеЗаполнено(ТекущиеДанные.БанковскийСчет) Тогда
		БанковскийСчетОрганизации = ТекущиеДанные.БанковскийСчет;
		ПараметрыОткрытия.Вставить("БанковскийСчетОрганизации", БанковскийСчетОрганизации);
	КонецЕсли;
	Если ТекущиеДанные <> Неопределено И ОтборПериодПП.ДатаНачала = '00010101' И ОтборПериодПП.ДатаОкончания = '00010101' Тогда
		ПараметрыОткрытия.Вставить("НачПериода", НачалоДня(ТекущиеДанные.Дата));
		ПараметрыОткрытия.Вставить("КонПериода", КонецДня(ТекущиеДанные.Дата));
	КонецЕсли;
		
	ОткрытьФорму(
		"Обработка.КлиентБанк.Форма.ФормаВыгрузка", 
		ПараметрыОткрытия
	);
	
КонецПроцедуры // ВыгрузитьВКлиентБанк()

&НаКлиенте
Процедура СоздатьПоШаблону(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(СписокПлатежныхПоручений, Элементы.СписокПлатежныхПоручений.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоШаблонуОперациюПоПлатежнойКарте(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(СписокОперацииПоПлатежнымКартам, Элементы.СписокОперацииПоПлатежнымКартам.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	ЗагрузитьИзФайлаФрагмент();
	
КонецПроцедуры // ЗагрузитьИзФайла()

&НаКлиенте
Процедура ЗагрузитьИзФайлаФрагмент(ПараметрБанковскийСчет = Неопределено)
	
	Если ПараметрБанковскийСчет = Неопределено Тогда
		СтруктураПрямогоОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруПрямогоОбмена();
		Если СтруктураПрямогоОбмена.БанковскийСчет = Неопределено Тогда // Есть несколько счетов, нужно выбирать.
			Отбор = Новый Структура("ЭтоСчетОрганизации", Истина);
			ОткрытьФорму("Справочник.БанковскиеСчета.Форма.ФормаВыбораБезВладельца", Новый Структура("ТекущаяСтрока, Отбор", СтруктураПрямогоОбмена.БанковскийСчет, Отбор), ЭтотОбъект);
		ИначеЕсли СтруктураПрямогоОбмена.БанковскийСчет.Пустая() Тогда  // Нет ни одной настройки обмена с банками, можно сразу выбирать файл загрузки.
			УправлениеНебольшойФирмойКлиент.ЗагрузитьДанныеИзФайлаВыписки(УникальныйИдентификатор);
		Иначе                                                           // Один счет, можно сразу подставить его.
			ОткрытьФорму(
				"Обработка.КлиентБанк.Форма.ФормаЗагрузка",
				СтруктураПрямогоОбмена
			);
		КонецЕсли;
	Иначе
		СтруктураОбмена = КлиентБанкВызовСервера.ПолучитьСтруктуруОбмена(ПараметрБанковскийСчет);
		Если НЕ СтруктураОбмена.ПрямойОбмен Тогда
			КлиентБанкКлиент.ЗагрузитьДанныеИзФайлаВыписки(УникальныйИдентификатор, СтруктураОбмена);
		Иначе
			ОткрытьФорму(
				"Обработка.КлиентБанк.Форма.ФормаЗагрузка",
				СтруктураОбмена
			);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПлатежныхПорученийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	КлючеваяОперация = "СозданиеФормыПлатежноеПоручение";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	КлючеваяОперация = "ОткрытиеФормыПлатежноеПоручение";
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, КлючеваяОперация);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// ИнтернетПоддержкаПользователей.Новости
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()
	
	ОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтотОбъект, "ПриОткрытии");
	
КонецПроцедуры
// Конец ИнтернетПоддержкаПользователей.Новости

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.СписокПлатежныхПоручений);
КонецПроцедуры

#КонецОбласти
// Конец СтандартныеПодсистемы.Печать

#Область Отборы

&НаКлиенте
Процедура ОтборПоступленияПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПоступление", ОтборПоступления);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСписанияПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоРасход", ОтборРасход);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПеремещенияПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаПоВидуДвижения("БанковскиеВыписки", "ЭтоПеремещение", ОтборПеремещения);
	
КонецПроцедуры

// Процедура установки отбора в динамическом списке.
//
&НаСервере
Процедура НастроитьОтборДинамическогоСпискаПоВидуДвижения(ИмяСписка, ИмяПоля, Использование)
	
	ЧислоИзмененных = ОбщегоНазначенияКлиентСервер.ИзменитьЭлементыОтбора(
		ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
		ИмяПоля,
		,
		Истина,
		ВидСравненияКомпоновкиДанных.Равно,
		Использование,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
		
	Если ЧислоИзмененных = 0 Тогда
		
		ГруппаДеловыеОтношения = ОбщегоНазначенияКлиентСервер.НайтиЭлементОтбораПоПредставлению(
			ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы, ИмяСписка);
		
		Если ГруппаДеловыеОтношения = Неопределено Тогда
			ГруппаДеловыеОтношения = ОбщегоНазначенияКлиентСервер.СоздатьГруппуЭлементовОтбора(
				ЭтотОбъект[ИмяСписка].КомпоновщикНастроек.ФиксированныеНастройки.Отбор.Элементы,
				ИмяСписка,
				ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИЛИ);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.ДобавитьЭлементКомпоновки(
			ГруппаДеловыеОтношения,
			ИмяПоля,
			ВидСравненияКомпоновкиДанных.Равно,
			Истина,
			,
			Использование,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Недоступный);
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаБанкКасса();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКассаПриИзменении(Элемент)
	
	НастроитьОтборДинамическогоСпискаБанкКасса();
	
КонецПроцедуры

// Процедура установки отбора в динамическом списке.
//
&НаСервере
Процедура НастроитьОтборДинамическогоСпискаБанкКасса()
	
	Если ОтборБанк И Не ОтборКасса Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоБанк",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина
		);
	ИначеЕсли Не ОтборБанк И ОтборКасса Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоКасса",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Истина
		);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоБанк",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Ложь
		);
		
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			БанковскиеВыписки,
			"ЭтоКасса",
			Истина,
			ВидСравненияКомпоновкиДанных.Равно,
			,
			Ложь
		);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьРасчетныеСчетаИКассы()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Кассы.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(Кассы.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.Кассы КАК Кассы
		|
		|УПОРЯДОЧИТЬ ПО
		|	Кассы.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	БанковскиеСчета.Ссылка,
		|	ПРЕДСТАВЛЕНИЕ(БанковскиеСчета.Ссылка) КАК Представление
		|ИЗ
		|	Справочник.БанковскиеСчета КАК БанковскиеСчета
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
		|		ПО БанковскиеСчета.Владелец = Организации.Ссылка";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаКасс = МассивРезультатов[0].Выбрать();
	ВыборкаСчетов = МассивРезультатов[1].Выбрать();
	
	Элементы.ОтборСчетИКасса.СписокВыбора.Очистить();
	Элементы.ОтборСчетПП.СписокВыбора.Очистить();
	
	Пока ВыборкаСчетов.Следующий() Цикл
		Элементы.ОтборСчетИКасса.СписокВыбора.Добавить(ВыборкаСчетов.Ссылка,,, БиблиотекаКартинок.Банк);
	КонецЦикла;
	ВыборкаСчетов.Сбросить();
	Пока ВыборкаСчетов.Следующий() Цикл
		Элементы.ОтборСчетПП.СписокВыбора.Добавить(ВыборкаСчетов.Ссылка,,, БиблиотекаКартинок.Банк);
	КонецЦикла;
	Пока ВыборкаКасс.Следующий() Цикл
		Элементы.ОтборСчетИКасса.СписокВыбора.Добавить(ВыборкаКасс.Ссылка,,, БиблиотекаКартинок.Касса);
	КонецЦикла;
	
КонецПроцедуры

#Область ОтборПоПериоду

&НаКлиенте
Процедура НастройкаИнтервала(Команда)
	
	УстановитьПериодНаКлиенте("БанковскиеВыписки");
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

// Процедура настривает период динамического списка.
//
&НаКлиенте
Процедура УстановитьПериодНаКлиенте(ИмяСписка, ПараметрСтандартныйПериод = Неопределено)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("УстановитьПериодЗавершение", ЭтотОбъект, Новый Структура("ИмяСписка", ИмяСписка));
	Диалог = Новый ДиалогРедактированияСтандартногоПериода();
	Диалог.Период = ЭтотОбъект.Элементы[ИмяСписка].Период;
	Диалог.Показать(ОписаниеОповещения);
	
КонецПроцедуры

// Процедура настривает период динамического списка (если требуется интерактивный выбор периода).
//
&НаКлиенте
Процедура УстановитьПериодЗавершение(Результат, Параметры) Экспорт
	
	УстановитьПериодЗавершениеНаСервере(Результат, Параметры);
	
КонецПроцедуры

// Процедура настривает период динамического списка на сервере (если требуется интерактивный выбор периода).
//
&НаСервере
Процедура УстановитьПериодЗавершениеНаСервере(Результат, Параметры)
	
	Если Результат <> Неопределено Тогда
		
		Элементы[Параметры.ИмяСписка].Период.Вариант = Результат.Вариант;
		Элементы[Параметры.ИмяСписка].Период.ДатаНачала = Результат.ДатаНачала;
		Элементы[Параметры.ИмяСписка].Период.ДатаОкончания = Результат.ДатаОкончания;
		Элементы[Параметры.ИмяСписка].Обновить();
		
		ВидПериодаЖурнала = ПолучитьПредставлениеПериода(Результат, " - ");
		
	КонецЕсли;
	
КонецПроцедуры

// Функция возвращает представление стандартного периода.
//
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПредставлениеПериода(СтандартныйПериод, Разделитель = " по ")
	
	ДатаНачала = СтандартныйПериод.ДатаНачала;
	ДатаОкончания = СтандартныйПериод.ДатаОкончания;
	Если Не ЗначениеЗаполнено(ДатаНачала) И Не ЗначениеЗаполнено(ДатаОкончания) Тогда
		Возврат "Интервал: <За все время>";
	ИначеЕсли СтандартныйПериод.Вариант <> ВариантСтандартногоПериода.ПроизвольныйПериод Тогда
		Возврат "Интервал: <" + СтандартныйПериод.Вариант + ">";
	ИначеЕсли Год(ДатаНачала) = Год(ДатаОкончания) И Месяц(ДатаНачала) = Месяц(ДатаОкончания) Тогда
		Возврат "Интервал: " + Формат(ДатаНачала, "ДФ=dd")+Разделитель+Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	ИначеЕсли Год(ДатаНачала) = Год(ДатаОкончания) Тогда
		Возврат "Интервал: " + Формат(ДатаНачала, "ДФ=dd.MM")+Разделитель+Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	Иначе
		Возврат "Интервал: " + Формат(ДатаНачала, "ДФ=dd.MM.yyyy")+Разделитель+Формат(ДатаОкончания, "ДФ=dd.MM.yyyy");
	КонецЕсли;
	
КонецФункции

#КонецОбласти

#КонецОбласти

&НаКлиенте
Процедура НастройкаИтогов(Команда)
	
	СтруктураПараметров = Новый Структура();
	
	СтруктураПараметров.Вставить("ВариантОтображенияИтогов", ВариантОтображенияИтогов);
	СтруктураПараметров.Вставить("ВалютаВедомости", ОтборВалютаВедомости);
	СтруктураПараметров.Вставить("ВалютаОстатков", ОтборВалютаОстатков);
	
	ОткрытьФорму("ОбщаяФорма.ФормаНастройкиИтоговБанкИКасса", 
		СтруктураПараметров, 
		ЭтотОбъект, 
		УникальныйИдентификатор,,,
		Новый ОписаниеОповещения("НастройкаИтоговЗавершение", ЭтотОбъект), 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаИтоговЗавершение(РезультатЗавершения, ПараметрыЗавершения) Экспорт
	
	Если ТипЗнч(РезультатЗавершения) = Тип("Структура") Тогда
		
		ВариантОтображенияИтогов = РезультатЗавершения.ВариантОтображенияИтогов;
		ОтборВалютаВедомости = РезультатЗавершения.ВалютаВедомости;
		ОтборВалютаОстатков = РезультатЗавершения.ВалютаОстатков;
		
		НастройкаИтоговЗавершениеНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкаИтоговЗавершениеНаСервере() Экспорт
	
	Если ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаДень") Тогда
		
		Элементы.ИнформацияСуммаВалНачальныйОстаток.Видимость = Истина;
		Элементы.ИнформацияСуммаВалПриход.Видимость = Истина;
		Элементы.ИнформацияСуммаВалРасход.Видимость = Истина;
		
		Элементы.ИнформацияСуммаВалНачальныйОстаток.Заголовок = "Начало дня";
		Элементы.ИнформацияСуммаВалКонечныйОстаток.Заголовок = "Конец дня";
		Элементы.ИнформацияСуммаВалКонечныйОстаток.Шрифт = Новый Шрифт(Элементы.ИнформацияСуммаВалКонечныйОстаток.Шрифт,,, Ложь);
		
		ТаблицаИтогов[0].Показатель = НСтр("ru = 'Начало дня'");
		ТаблицаИтогов[3].Показатель = НСтр("ru = 'Конец дня'");
		
		Элементы.СтраницаЗаДень.Видимость = Истина;
		Элементы.СтраницаЗаПериод.Видимость = Ложь;
		Элементы.СтраницаВВалюте.Видимость = Ложь;
		Элементы.СтраницаНеОтображатьИтоги.Видимость = Ложь;
		
		Элементы.Группа5.Видимость = Ложь;
		Элементы.ТаблицаИтогов.Видимость = Истина;
		
		Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаЗаДень;
		
	ИначеЕсли ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ВедомостьЗаПериод") Тогда
		
		ЗаполнитьИнформациюОбИтогахПоВалюте();
		
		//Элементы.ДекорацияВедомостьПоВалюте.Заголовок = НСтр("ru = 'Ведомость по валюте '")+""""+ОтборВалютаВедомости + """";
		Элементы.ДекорацияВедомостьПоВалюте.Заголовок = НСтр("ru = 'Ведомость по валюте: '")+ОтборВалютаВедомости;
		
		Элементы.ИнформацияСуммаВалНачальныйОстаток.Видимость = Истина;
		Элементы.ИнформацияСуммаВалПриход.Видимость = Истина;
		Элементы.ИнформацияСуммаВалРасход.Видимость = Истина;
		
		Элементы.ИнформацияСуммаВалНачальныйОстаток.Заголовок = "Начало периода";
		Элементы.ИнформацияСуммаВалКонечныйОстаток.Заголовок = "Конец периода";
		Элементы.ИнформацияСуммаВалКонечныйОстаток.Шрифт = Новый Шрифт(Элементы.ИнформацияСуммаВалКонечныйОстаток.Шрифт,,, Ложь);
		
		Если НЕ ЗначениеЗаполнено(ОтборПериод.ДатаНачала) И НЕ ЗначениеЗаполнено(ОтборПериод.ДатаОкончания) Тогда
			ТаблицаИтогов[0].Показатель = НСтр("ru = 'За все время'");
			ТаблицаИтогов[3].Показатель = НСтр("ru = 'Остаток'");
		Иначе
			ТаблицаИтогов[0].Показатель = НСтр("ru = 'С '")+Формат(ОтборПериод.ДатаНачала, "ДФ=dd.MM.yyyy");
			ТаблицаИтогов[3].Показатель = НСтр("ru = 'По '")+Формат(ОтборПериод.ДатаОкончания, "ДФ=dd.MM.yyyy");
		КонецЕсли;
		
		Элементы.СтраницаЗаДень.Видимость = Ложь;
		Элементы.СтраницаЗаПериод.Видимость = Истина;
		Элементы.СтраницаВВалюте.Видимость = Ложь;
		Элементы.СтраницаНеОтображатьИтоги.Видимость = Ложь;
		
		Элементы.Группа5.Видимость = Ложь;
		Элементы.ТаблицаИтогов.Видимость = Истина;
		
		Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаЗаПериод;
		
	ИначеЕсли ВариантОтображенияИтогов = ПредопределенноеЗначение("Перечисление.ВариантыОтображенияИтоговБанкИКасса.ОстаткиПересчитанныеВВалюту") Тогда
		
		СтруктураДанных = ПолучитьОстатокВВалюте(ОтборВалютаОстатков);
		Элементы.ОтборКурс.Заголовок = НСтр("ru = 'Курс на '")+Формат(ТекущаяДатаСеанса(), "ДФ=dd.MM.yyyy")+"";
		ОтборКурс = ""+СтруктураДанных.Курс;
		ИнформацияСуммаВалКонечныйОстаток = СтруктураДанных.Остаток;
		Элементы.ДекорацияОстатокВВалюте.Заголовок = НСтр("ru = 'Остатки в валюте: '")+ОтборВалютаОстатков;
		
		Элементы.ИнформацияСуммаВалНачальныйОстаток.Видимость = Ложь;
		Элементы.ИнформацияСуммаВалПриход.Видимость = Ложь;
		Элементы.ИнформацияСуммаВалРасход.Видимость = Ложь;
		
		Элементы.ИнформацияСуммаВалКонечныйОстаток.Заголовок = НСтр("ru = 'Остаток'");
		
		Элементы.СтраницаЗаДень.Видимость = Ложь;
		Элементы.СтраницаЗаПериод.Видимость = Ложь;
		Элементы.СтраницаВВалюте.Видимость = Истина;
		Элементы.СтраницаНеОтображатьИтоги.Видимость = Ложь;
		
		Элементы.Группа5.Видимость = Истина;
		Элементы.ТаблицаИтогов.Видимость = Ложь;
		
		Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаВВалюте;
		
	Иначе
		
		Элементы.СтраницаЗаДень.Видимость = Ложь;
		Элементы.СтраницаЗаПериод.Видимость = Ложь;
		Элементы.СтраницаВВалюте.Видимость = Ложь;
		Элементы.СтраницаНеОтображатьИтоги.Видимость = Истина;
		
		Элементы.Группа5.Видимость = Ложь;
		Элементы.ТаблицаИтогов.Видимость = Ложь;
		
		Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаНеОтображатьИтоги;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКВалютам(Команда)
	
	ОткрытьФорму("Справочник.Валюты.ФормаСписка");
	
КонецПроцедуры

#Область ОбработчикиСобытийЭлементовФормы

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ДИНАМИЧЕСКОГО СПИСКА

// Выбор значения отбора в поле отбора
&НаКлиенте
Процедура ОтборКонтрагентОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "ОрганизацияДляОтбора", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетИКассаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "СчетКассаДляОтбора", Элемент.Родитель.Имя, ВыбранноеЗначение,, "СписокКассИСчетов");
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборАвторЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Автор", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

#КонецОбласти

#Область МеткиОтборов

&НаСервере
Процедура УстановитьМеткуИОтборСписка(СписокДляОтбора, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения="", ИмяПараметраЗапроса="")
	
	Если ПредставлениеЗначения="" Тогда
		ПредставлениеЗначения=Строка(ВыбранноеЗначение);
	КонецЕсли; 
	
	Если СписокДляОтбора = "СписокПлатежныхПоручений" Тогда
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
	ИначеЕсли СписокДляОтбора = "СписокОперацииПоПлатежнымКартам" Тогда
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
	Иначе
		ИмяТЧДанныеМеток = "ДанныеМеток";
	КонецЕсли;
	
	РаботаСОтборами.ПрикрепитьМеткуОтбора(ЭтотОбъект, ИмяПоляОтбораСписка, ГруппаРодительМетки, ВыбранноеЗначение, ПредставлениеЗначения, СписокДляОтбора, ИмяПараметраЗапроса, ИмяТЧДанныеМеток);
	
	Если ИмяПараметраЗапроса="" Тогда
		
		РаботаСОтборами.УстановитьОтборСписка(ЭтотОбъект, ЭтотОбъект[СписокДляОтбора], ИмяПоляОтбораСписка, СписокДляОтбора,, ИмяТЧДанныеМеток);
		
	Иначе
		
		СтруктураОтбораМеток = Новый Структура("ИмяПараметраЗапроса", ИмяПараметраЗапроса);
		НайденныеСтроки = ЭтотОбъект[ИмяТЧДанныеМеток].НайтиСтроки(СтруктураОтбораМеток);
		МассивОтбора = Новый Массив;
		Для каждого стр Из НайденныеСтроки Цикл
			МассивОтбора.Добавить(стр.Метка);
		КонецЦикла;
		ЭтотОбъект[СписокДляОтбора].Параметры.УстановитьЗначениеПараметра("БезОтбора", НайденныеСтроки.Количество()=0);
		ЭтотОбъект[СписокДляОтбора].Параметры.УстановитьЗначениеПараметра(ИмяПараметраЗапроса, МассивОтбора);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_МеткаОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ИмяГруппыРодителя = СокрЛП(Элемент.Родитель.Имя);
	Если Прав(ИмяГруппыРодителя, 2) = "ПП" Тогда
		ИмяРеквизитаСписка = "СписокПлатежныхПоручений";
		ИмяТЧДанныеМеток = "ДанныеМетокПП";
		НачалоИмениМетки = "МеткаПП_";
	ИначеЕсли Прав(ИмяГруппыРодителя, 1) = "Э" Тогда
		ИмяРеквизитаСписка = "СписокОперацииПоПлатежнымКартам";
		ИмяТЧДанныеМеток = "ДанныеМетокОплатаКартами";
		НачалоИмениМетки = "МеткаОплатаКартами_";
	Иначе
		ИмяРеквизитаСписка = "БанковскиеВыписки";
		ИмяТЧДанныеМеток = "ДанныеМеток";
		НачалоИмениМетки = "Метка_";
	КонецЕсли;
	
	МеткаИД = Сред(Элемент.Имя, СтрДлина(НачалоИмениМетки)+1);
	
	УдалитьМеткуОтбора(МеткаИД, ИмяРеквизитаСписка, ИмяТЧДанныеМеток);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьМеткуОтбора(МеткаИД, ИмяРеквизитаСписка, ИмяТЧДанныеМеток)
	
	РаботаСОтборами.УдалитьМеткуОтбораСервер(ЭтотОбъект, ЭтотОбъект[ИмяРеквизитаСписка], МеткаИД, ИмяРеквизитаСписка, ИмяТЧДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаНажатие(Элемент, СтандартнаяОбработка)
	
	СтруктураИменЭлементов = Новый Структура("ОтборПериод, ПредставлениеПериода, СобытиеОповещения", "ОтборПериод", "ПредставлениеПериода", "ОповещениеОбИзмененииДолга");
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "БанковскиеВыписки", "Дата", СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаППНажатие(Элемент, СтандартнаяОбработка)
	
	СтруктураИменЭлементов = Новый Структура("ОтборПериод, ПредставлениеПериода", "ОтборПериодПП", "ПредставлениеПериодаПП");
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "СписокПлатежныхПоручений", "Дата", СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПериодаЭНажатие(Элемент, СтандартнаяОбработка)
	
	СтруктураИменЭлементов = Новый Структура("ОтборПериод, ПредставлениеПериода", "ОтборПериодЭ", "ПредставлениеПериодаЭ");
	
	СтандартнаяОбработка = Ложь;
	РаботаСОтборамиКлиент.ПредставлениеПериодаВыбратьПериод(ЭтотОбъект, "СписокОперацииПоПлатежнымКартам", "Дата", СтруктураИменЭлементов);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройкиОтборов()
	
	Если НЕ ЭтоНачальнаяСтраница Тогда
	
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, "БанковскиеВыписки");
		
		СтруктураИменЭлементовПП = Новый Структура;
		СтруктураИменЭлементовПП.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
		СтруктураИменЭлементовПП.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
		СтруктураИменЭлементовПП.Вставить("ПраваяПанель", "ПраваяПанельПП");
		СтруктураИменЭлементовПП.Вставить("ОтборПериод", "ОтборПериодПП");
		
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, "СписокПлатежныхПоручений", СтруктураИменЭлементовПП,,, "ДанныеМетокПП");
		
		СтруктураИменЭлементовЭ = Новый Структура;
		СтруктураИменЭлементовЭ.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
		СтруктураИменЭлементовЭ.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
		СтруктураИменЭлементовЭ.Вставить("ПраваяПанель", "ПраваяПанельЭ");
		СтруктураИменЭлементовЭ.Вставить("ОтборПериод", "ОтборПериодЭ");
		
		РаботаСОтборами.СохранитьНастройкиОтборов(ЭтотОбъект, "СписокОперацииПоПлатежнымКартам", СтруктураИменЭлементовЭ,,, "ДанныеМетокОплатаКартами");
	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьРазвернутьПанельОтборов(Элемент)
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфо.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьОтборыППНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельПП");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоПП.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура СвернутьОтборыЭНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельЭ");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоЭ.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазвернутьОтборыППНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоПП");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыПП");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельПП");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоПП.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияРазвернутьОтборыЭНажатие(Элемент)
	
	СтруктураИменЭлементов = Новый Структура;
	СтруктураИменЭлементов.Вставить("ФильтрыНастройкиИДопИнфо", "ФильтрыНастройкиИДопИнфоЭ");
	СтруктураИменЭлементов.Вставить("ДекорацияРазвернутьОтборы", "ДекорацияРазвернутьОтборыЭ");
	СтруктураИменЭлементов.Вставить("ПраваяПанель", "ПраваяПанельЭ");
	
	НовоеЗначениеВидимость = НЕ Элементы.ФильтрыНастройкиИДопИнфоЭ.Видимость;
	РаботаСОтборамиКлиент.СвернутьРазвернутьПанельОтборов(ЭтотОбъект, НовоеЗначениеВидимость, СтруктураИменЭлементов);
	
КонецПроцедуры

#КонецОбласти

&НаКлиенте
Процедура Дата1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если Элементы.БанковскиеВыписки.ТекущиеДанные <> Неопределено Тогда
		ПериодОтчета = Новый СтандартныйПериод;
		ПериодОтчета.ДатаНачала = Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
		ПериодОтчета.ДатаОкончания = Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
		ПараметрыОтчета = Новый Структура("СтПериод", ПериодОтчета);
	Иначе
		ПериодОтчета = Новый СтандартныйПериод;
		ПериодОтчета.ДатаНачала = '00010101';
		ПериодОтчета.ДатаОкончания = '00010101';
		ПараметрыОтчета = Новый Структура("СтПериод", ПериодОтчета);
	КонецЕсли;
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ВедомостьВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалюта1Нажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияНастройкаВыводаИтоговНажатие(Элемент)
	
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияОстатокВВалютеНажатие(Элемент)
	
	//НастройкаИтогов(Неопределено);
	ПериодОтчета = Новый СтандартныйПериод;
	ПериодОтчета.ДатаНачала = ТекущаяДата(); //Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
	ПериодОтчета.ДатаОкончания = ТекущаяДата(); //Элементы.БанковскиеВыписки.ТекущиеДанные.Дата;
	ПараметрыОтчета = Новый Структура("СтПериод, Валюта", ПериодОтчета, ОтборВалютаОстатков);
	
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ОстаткиВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияВедомостьПоВалютеНажатие(Элемент)
	
	//НастройкаИтогов(Неопределено);
	ПараметрыОтчета = Новый Структура("СтПериод, Валюта", ОтборПериод, ОтборВалютаВедомости);
	ОткрытьФорму("Отчет.ДенежныеСредства.Форма", Новый Структура("КлючВарианта, СформироватьПриОткрытии, Отбор", "ВедомостьВВалюте", Истина, ПараметрыОтчета));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("БанковскиеВыписки", "Валюта", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеИзДругойКассы(Команда)
	
	СоздатьНовыйДокумент("ПеремещениеДС", "ИзДругойКассы");
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйДокумент(ИмяДокумента, ВидОперации)
	
	Если ИмяДокумента = "ПеремещениеДС" Тогда
		Если ВидОперации = "ИзДругойКассы" Тогда
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ТипДенежныхСредств", ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные"));
		ИначеЕсли ВидОперации = "СРасчетногоСчета" Тогда
			СтруктураПараметров = Новый Структура;
			СтруктураПараметров.Вставить("ТипДенежныхСредств", ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные"));
		КонецЕсли;
		
		// Нужно доработать обработку выбора элементов из различных подменю: "Банк" или "Касса".
		Если Ложь Тогда
			СтруктураПараметров.Вставить("ТипДенежныхСредствПолучатель", ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные"));
		Иначе
			СтруктураПараметров.Вставить("ТипДенежныхСредствПолучатель", ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные"));
		КонецЕсли;
		
		ОткрытьФорму("Документ.ПеремещениеДС.ФормаОбъекта", СтруктураПараметров);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуИтогов()
	
	ТаблицаИтогов.Очистить();
	
	// Начальный остаток
	СтрокаИтогов = ТаблицаИтогов.Добавить();
	СтрокаИтогов.Показатель = "Начало дня";
	СтрокаИтогов.Значение = 0;
	
	// Начальный остаток
	СтрокаИтогов = ТаблицаИтогов.Добавить();
	СтрокаИтогов.Показатель = "Поступило";
	СтрокаИтогов.Значение = 0;
	
	// Начальный остаток
	СтрокаИтогов = ТаблицаИтогов.Добавить();
	СтрокаИтогов.Показатель = "Списано";
	СтрокаИтогов.Значение = 0;
	
	// Начальный остаток
	СтрокаИтогов = ТаблицаИтогов.Добавить();
	СтрокаИтогов.Показатель = "Конец дня";
	СтрокаИтогов.Значение = 0;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьИнформациюОбИтогахПоВалюте()
	
	Если НЕ ОтборВалютаВедомости.Пустая() Тогда
		
		СтруктураДанные = ПолучитьДанныеПоВалюте(ОтборПериод, ОтборВалютаВедомости);
		
		ТаблицаИтогов[0].Значение = СтруктураДанные.ИнформацияСуммаВалНачальныйОстаток;
		ТаблицаИтогов[1].Значение = СтруктураДанные.ИнформацияСуммаВалПриход;
		ТаблицаИтогов[2].Значение = СтруктураДанные.ИнформацияСуммаВалРасход;
		ТаблицаИтогов[3].Значение = СтруктураДанные.ИнформацияСуммаВалКонечныйОстаток;
		
	Иначе
		ИнформацияСуммаВалКонечныйОстаток = 0;
		ИнформацияСуммаВалНачальныйОстаток = 0;
		ИнформацияСуммаВалПриход = 0;
		ИнформацияСуммаВалРасход = 0;
		Дата = "";
	Конецесли;
	
КонецПроцедуры // ОбработатьАктивизациюСтрокиСписка()

&НаСервереБезКонтекста
Функция ПолучитьДанныеПоВалюте(Период, Валюта)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалНачальныйОстаток КАК ИнформацияСуммаВалНачальныйОстаток,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалПриход КАК ИнформацияСуммаВалПриход,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалРасход КАК ИнформацияСуммаВалРасход,
	|	ДенежныеСредстваОстаткиИОбороты.СуммаВалКонечныйОстаток КАК ИнформацияСуммаВалКонечныйОстаток
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.ОстаткиИОбороты(&НачалоПериода, &КонецПериода, , , Валюта = &Валюта) КАК ДенежныеСредстваОстаткиИОбороты";
	
	Запрос.УстановитьПараметр("НачалоПериода", НачалоДня(Период.ДатаНачала));
	Запрос.УстановитьПараметр("КонецПериода", ?(Период.ДатаОкончания = '00010101', '39991231', КонецДня(Период.ДатаОкончания)));
	Запрос.УстановитьПараметр("Валюта", Валюта);
	ВыборкаРезультата = Запрос.Выполнить().Выбрать();
	
	Если ВыборкаРезультата.Следующий() Тогда
		СтруктураВозврата = Новый Структура("ИнформацияСуммаВалНачальныйОстаток, ИнформацияСуммаВалКонечныйОстаток, ИнформацияСуммаВалПриход, ИнформацияСуммаВалРасход, УчетВалютныхОпераций, НомерБанковскогоСчета");
		ЗаполнитьЗначенияСвойств(СтруктураВозврата, ВыборкаРезультата);
		СтруктураВозврата.УчетВалютныхОпераций = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		Возврат СтруктураВозврата;
	Иначе
		Возврат Новый Структура(
			"ИнформацияСуммаВалКонечныйОстаток, ИнформацияСуммаВалНачальныйОстаток, ИнформацияСуммаВалПриход, ИнформацияСуммаВалРасход, УчетВалютныхОпераций, НомерБанковскогоСчета",
			0,0,0,0,Ложь
		);
	КонецЕсли;
	
КонецФункции // ПолучитьДанныеПобанковскомуСчету()

&НаСервереБезКонтекста
Функция ПолучитьОстатокВВалюте(Валюта)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
		|	ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) КАК Кратность
		|ИЗ
		|	РегистрСведений.КурсыВалют.СрезПоследних(, Валюта = &Валюта) КАК КурсыВалютСрезПоследних";
	
	Запрос.УстановитьПараметр("Валюта", Валюта);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаПоВалюте = РезультатЗапроса.Выбрать();
	ВыборкаПоВалюте.Следующий();
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	СУММА(ДенежныеСредстваОстатки.СуммаВалОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) * &Кратность / (ЕСТЬNULL(КурсыВалютСрезПоследних.Кратность, 1) * &Курс)) КАК ОстатокВВалюте
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства.Остатки(ДатаВремя(3999, 12, 31), ) КАК ДенежныеСредстваОстатки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(, ) КАК КурсыВалютСрезПоследних
		|		ПО ДенежныеСредстваОстатки.Валюта = КурсыВалютСрезПоследних.Валюта";
	
	Запрос.УстановитьПараметр("Валюта", Валюта);
	Запрос.УстановитьПараметр("Кратность", ВыборкаПоВалюте.Кратность);
	Запрос.УстановитьПараметр("Курс", ВыборкаПоВалюте.Курс);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	СтруктураВозврата = Новый Структура("Курс, Остаток", ВыборкаПоВалюте.Курс, 0);
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СтруктураВозврата.Вставить("Остаток", ВыборкаДетальныеЗаписи.ОстатокВВалюте);
	КонецЕсли;
	
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ДекорацияОткрытьФормуНастройкиИтоговНажатие(Элемент)
	
	НастройкаИтогов(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПоШаблонуДокументДвиженияДС(Команда)
	
	ЗаполнениеОбъектовУНФКлиент.ПоказатьВыборШаблонаДляСозданияДокументаИзСписка(БанковскиеВыписки, Элементы.БанковскиеВыписки.ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозОперацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ОткрытьФорму("Обработка.БанкИКасса.Форма.ФормаВыбораВидаОперации",,,,,,
		Новый ОписаниеОповещения("ОтборХозОперацияНачалоВыбораЗавершение", ЭтотОбъект),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборХозОперацияНачалоВыбораЗавершение(РезультатЗавершения, ПараметрыЗавершения) Экспорт
	
	Если ЗначениеЗаполнено(РезультатЗавершения) Тогда
		Если ТипЗнч(РезультатЗавершения) = Тип("Строка") Тогда
			СтандартнаяОбработка = Ложь;
			УстановитьМеткуИОтборСписка("БанковскиеВыписки", "ХозяйственнаяОперация", "ГруппаОтборХозОперация", "Перемещение. "+РезультатЗавершения);
		Иначе
			СтандартнаяОбработка = Ложь;
			УстановитьМеткуИОтборСписка("БанковскиеВыписки", "ХозяйственнаяОперация", "ГруппаОтборХозОперация", РезультатЗавершения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОперацииППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "ВидОперации", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОперацииЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "ВидОперации", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборБанкЭквайрерОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Эквайрер", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборКонтрагентЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Контрагент", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСчетППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "БанковскийСчет", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВалютаППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "ВалютаДокумента", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияППОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокПлатежныхПоручений", "Организация", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияЭОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "Организация", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборЭквайринговыйТерминалОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Если Не ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьМеткуИОтборСписка("СписокОперацииПоПлатежнымКартам", "ЭквайринговыйТерминал", Элемент.Родитель.Имя, ВыбранноеЗначение);
	ВыбранноеЗначение = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьВКлиентБанкаТекущийДокумент(Команда)
	
	ТекущиеДанные = Элементы.СписокПлатежныхПоручений.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, "Необходимо выделить документ, который хотите выгрузить в клиент-банка.");
		Возврат;
	КонецЕсли;
	
	СписокВыгрузки = Новый СписокЗначений;
	СписокВыгрузки.Добавить(ТекущиеДанные.Ссылка);
	ПараметрыОткрытия = Новый Структура("СписокПлатежек, БанковскийСчет", СписокВыгрузки, ТекущиеДанные.БанковскийСчет);
	ОткрытьФорму("Обработка.КлиентБанк.Форма.СохранениеПлатежек", ПараметрыОткрытия,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если НЕ ЗавершениеРаботы Тогда
		//УНФ.ОтборыСписка
		СохранитьНастройкиОтборов();
		//Конец УНФ.ОтборыСписка
	КонецЕсли;
	
КонецПроцедуры

#Область РасходСоСчета

// Обработчик команды "РасходПоставщикуБанк" формы.
//
&НаКлиенте
Процедура РасходПоставщикуБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "Поставщику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходЗарплатаПоВедомостиБанк" формы.
//
&НаКлиенте
Процедура РасходЗарплатаПоВедомостиБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "Зарплата", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходПокупателюБанк" формы.
//
&НаКлиенте
Процедура РасходПокупателюБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "Покупателю", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходВозвратКредитаБанк" формы.
//
&НаКлиенте
Процедура РасходВозвратКредитаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "РасчетыПоКредитам", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходВыдачаЗаймаБанк" формы.
//
&НаКлиенте
Процедура РасходВыдачаЗаймаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "ВыдачаЗаймаСотруднику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходПрочееБанк" формы.
//
&НаКлиенте
Процедура РасходПрочееБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "Прочее", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходПодотчетникуБанк" формы.
//
&НаКлиенте
Процедура РасходПодотчетникуБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "Подотчетнику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходОтчетЭквайрераБанк" формы.
//
&НаКлиенте
Процедура РасходОтчетЭквайрераБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "ВозвратОплатыНаПлатежныеКарты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходНалогиБанк" формы.
//
&НаКлиенте
Процедура РасходНалогиБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "Налоги", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходНаРасходыБанк" формы.
//
&НаКлиенте
Процедура РасходНаРасходыБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "НаРасходы", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

// Обработчик команды "РасходПрочиеРачетыБанк" формы.
//
&НаКлиенте
Процедура РасходПрочиеРачетыБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "ПрочиеРасчеты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

#КонецОбласти

#Область ПоступлениеНаСчет

&НаКлиенте
Процедура ПоступлениеОтПокупателяБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ОтПокупателя", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеПрочееБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "Прочее", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеОтПоставщикаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ОтПоставщика", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеПолучениеКредитаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "РасчетыПоКредитам", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеВозвратЗаймаСотрудникомБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ВозвратЗаймаСотрудником", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеПрочиеРасчетыБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ПрочиеРасчеты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеОтПодотчетникаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ОтПодотчетника", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеОтчетЭквайрераБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ПоступлениеОплатыПоКартам", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры


&НаКлиенте
Процедура ПоступлениеНалогиБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "Налоги", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПокупкаВалютыБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ПокупкаВалюты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

#КонецОбласти

#Область ПоступлениеВКассу

&НаКлиенте
Процедура ПоступлениеОтПокупателяКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ОтПокупателя", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПрочееКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "Прочее", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеОтПоставщикаКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ОтПоставщика", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПолучениеКредитаКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "РасчетыПоКредитам", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеВозвратЗаймаСотрудникомКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ВозвратЗаймаСотрудником", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПрочиеРасчетыКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ПрочиеРасчеты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеОтПодотчетникаКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ОтПодотчетника", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеРозничнаяВыручкаКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "РозничнаяВыручка", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеРозничнаяВыручкаСуммовойУчетКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "РозничнаяВыручкаСуммовойУчет", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПокупкаВалютыКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ПокупкаВалюты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

#КонецОбласти

#Область РасходИзКассы

// Обработчик команды "РасходПрочиеРачетыКасса" формы.
//
&НаКлиенте
Процедура РасходПрочиеРачетыКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "ПрочиеРасчеты", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходНаРасходыКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "НаРасходы", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходПоставщикуКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "Поставщику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходЗарплатаПоВедомостиКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "Зарплата", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходЗарплатаСотрудникуКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "ЗарплатаСотруднику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходПокупателюКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "Покупателю", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходВозвратКредитаКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "РасчетыПоКредитам", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходВыдачаЗаймаКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "ВыдачаЗаймаСотруднику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходПрочееКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "Прочее", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходПодотчетникуКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "Подотчетнику", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходПеремещениеВКассуККМКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "ПеремещениеВКассуККМ", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

&НаКлиенте
Процедура РасходНалогиКасса(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "Налоги", ДанныеМеток, "БанковскиеВыписки");
	
КонецПроцедуры

#КонецОбласти

#Область ПеремещениеДС

&НаСервереБезКонтекста
Функция ПолучитьПараметрыПеремещенияДС(ИмяКоманды)
	
	Если ИмяКоманды = "ПеремещениеИзКассыНаСчет" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Безналичные;
	ИначеЕсли ИмяКоманды = "ПеремещениеСоСчетаВКассу" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Безналичные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Наличные;
	ИначеЕсли ИмяКоманды = "ПеремещениеМеждуСчетами" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Безналичные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Безналичные;
	ИначеЕсли ИмяКоманды = "ПеремещениеМеждуКассами" Тогда
		ТипДенежныхСредств = Перечисления.ТипыДенежныхСредств.Наличные;
		ТипДенежныхСредствПолучатель = Перечисления.ТипыДенежныхСредств.Наличные;
	КонецЕсли;
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗначенияЗаполнения", Новый Структура("ТипДенежныхСредств, ТипДенежныхСредствПолучатель", 
		ТипДенежныхСредств, ТипДенежныхСредствПолучатель));
		
	Возврат ПараметрыОткрытия;
	
КонецФункции

&НаКлиенте
Процедура ПеремещениеИзКассыНаСчет(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходИзКассы", "ВзносНаличнымиВБанк", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещениеСоСчетаВКассу(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеВКассу", "ПолучениеНаличныхВБанке", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещениеМеждуСчетами(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "ПереводНаДругойСчет", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеремещениеМеждуКассами(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПеремещениеДС", "ПеремещениеДС", ДанныеМеток, "БанковскиеВыписки", ПолучитьПараметрыПеремещенияДС(Команда.Имя));
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеВзносНаличнымиБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ВзносНаличными", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура РасходСнятиеНаличныхБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("РасходСоСчета", "СнятиеНаличных", ДанныеМеток);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоступлениеПереводСДругогоСчетаБанк(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПоступлениеНаСчет", "ПереводСДругогоСчета", ДанныеМеток);
	
КонецПроцедуры

#КонецОбласти

#Область ПлатежноеПоручение

&НаКлиенте
Процедура ПлатежноеПоручениеНаОплату(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПлатежноеПоручение", "Оплата", ДанныеМетокПП, "СписокПлатежныхПоручений");
	
КонецПроцедуры

&НаКлиенте
Процедура ПлатежноеПоручениеНаПеречислениеВБюджет(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ПлатежноеПоручение", "ПеречислениеНалога", ДанныеМетокПП, "СписокПлатежныхПоручений");
	
КонецПроцедуры

#КонецОбласти

#Область ОперацияПоПлатежнымКартам

&НаКлиенте
Процедура ОплатаКартойПоступлениеОплатыОтПокупателя(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ОперацияПоПлатежнымКартам", "ПоступлениеОплатыОтПокупателя", ДанныеМетокОплатаКартами, "СписокОперацииПоПлатежнымКартам");
	
КонецПроцедуры

&НаКлиенте
Процедура ОплатаКартойВозвратОплатыПокупателю(Команда)
	
	ДвиженияДенежныхСредствКлиент.СоздатьДокумент("ОперацияПоПлатежнымКартам", "ВозвратОплатыПокупателю", ДанныеМетокОплатаКартами, "СписокОперацииПоПлатежнымКартам");
	
КонецПроцедуры

#КонецОбласти
