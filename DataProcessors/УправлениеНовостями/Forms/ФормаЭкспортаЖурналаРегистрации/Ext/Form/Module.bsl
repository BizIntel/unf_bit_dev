#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	// Текст сообщения пустой, вывести его на передний план.
	Элементы.СтраницыПрогресса.ТекущаяСтраница = Элементы.СтраницаГотово;

	лкКаталогВременныхФайлов = КаталогВременныхФайлов();
	Если (Прав(лкКаталогВременныхФайлов, 1) <> ПолучитьРазделительПутиСервера())
			И (Прав(лкКаталогВременныхФайлов, 1) <> ПолучитьРазделительПутиСервера()) Тогда
		лкКаталогВременныхФайлов = лкКаталогВременныхФайлов + ПолучитьРазделительПутиСервера();
	КонецЕсли;
	ЭтаФорма.ИмяКаталогаЭкспорта = лкКаталогВременныхФайлов;

	ЭтаФорма.ПериодДень = ТекущаяДата();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЭкспорт(Команда)

	Если ПроверитьЗаполнение() Тогда

		Элементы.СтраницыПрогресса.ТекущаяСтраница = Элементы.СтраницаПрогресс;
		ЭтаФорма.ОбновитьОтображениеДанных();

		ДатаСтрокой = Формат(ЭтаФорма.ПериодДень,"ДФ=yyyy-MM-dd");

		ВсеСобытия = ОбработкаНовостейКлиентСервер.ПолучитьСписокВсехСобытийЖурналаРегистрации();
		Результат = ИнтернетПоддержкаПользователейВызовСервера.ВыгрузитьВсеСобытияЖурналаРегистрации(
			Новый Структура("ДатаНачала, ДатаОкончания, Событие",
				НачалоДня(ЭтаФорма.ПериодДень),
				КонецДня(ЭтаФорма.ПериодДень),
				ВсеСобытия),
			Новый Структура("Архивировать", Истина));

		Если ПустаяСтрока(Результат.ТекстОшибки) Тогда
			Если ПустаяСтрока(Результат.АдресВременногоХранилищаФайла) Тогда
				ТекстСообщения = НСтр("ru='Не удалось экспортировать события журнала регистрации в файл.
					|Данные не удалось поместить во временное хранилище.'");
				ЭтаФорма.РезультатЭкспорта = ТекстСообщения;
			Иначе
				// Попробовать записать файл.
				ДвоичныеДанныеФайла = ПолучитьИзВременногоХранилища(Результат.АдресВременногоХранилищаФайла);
				Если ТипЗнч(ДвоичныеДанныеФайла) = Тип("ДвоичныеДанные") Тогда
					лкИмяКаталогаЭкспорта = ИнтернетПоддержкаПользователейКлиентСервер.УдалитьПоследнийСимвол(
						ЭтаФорма.ИмяКаталогаЭкспорта, "\/")
						+ ПолучитьРазделительПути();
					Если ЭтаФорма.СжиматьФайл Тогда
						ИмяФайла = лкИмяКаталогаЭкспорта + ДатаСтрокой + ".zip";
					Иначе
						ИмяФайла = лкИмяКаталогаЭкспорта + ДатаСтрокой + ".xml";
					КонецЕсли;
					Попытка
						ДвоичныеДанныеФайла.Записать(ИмяФайла);
						ТекстСообщения = СтрШаблон(
							НСтр("ru='Журнал событий успешно экспортирован в файл с именем
								|%1'"),
							ИмяФайла);
						ЭтаФорма.РезультатЭкспорта = ТекстСообщения;
					Исключение
						ТекстСообщения = СтрШаблон(
							НСтр("ru='Не удалось записать файл по причине:
								|%1'"),
							ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
						ЭтаФорма.РезультатЭкспорта = ТекстСообщения;
					КонецПопытки;
				Иначе
					ТекстСообщения = СтрШаблон(
						НСтр("ru='Не удалось экспортировать события журнала регистрации в файл.
							|Во временное хранилище помещены данные неправильного типа %1.'"),
						?(ДвоичныеДанныеФайла = Неопределено, "Неопределено", ТипЗнч(ДвоичныеДанныеФайла)));
					ЭтаФорма.РезультатЭкспорта = ТекстСообщения;
				КонецЕсли;
			КонецЕсли;
		Иначе
			ЭтаФорма.РезультатЭкспорта = Результат.ТекстОшибки;
		КонецЕсли;

		Элементы.СтраницыПрогресса.ТекущаяСтраница = Элементы.СтраницаГотово;

	КонецЕсли;

КонецПроцедуры

#КонецОбласти
