#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("УчетнаяЗапись") И ЗначениеЗаполнено(Параметры.УчетнаяЗапись) Тогда
		УчетнаяЗапись = ЭлектроннаяПодписьВМоделиСервисаБРОВызовСервера.УчетнаяЗапись(Параметры.УчетнаяЗапись);
		РеквизитыУчетнойЗаписи = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
			УчетнаяЗапись, 
			"ИдентификаторДокументооборота,ТелефонМобильныйДляАвторизации");
		
		Идентификатор    = РеквизитыУчетнойЗаписи.ИдентификаторДокументооборота;
		ТелефонМобильный = РеквизитыУчетнойЗаписи.ТелефонМобильныйДляАвторизации;
	Иначе
		Идентификатор    = Параметры.ИдентификаторДокументооборота;
		ТелефонМобильный = Параметры.ТелефонМобильныйДляАвторизации;	
	КонецЕсли;
	
	СпособДоставкиПаролей = "phone";
	
	СпособыДоставкиПаролей = КриптосервисВМоделиСервиса.ПолучитьСпособыДоставкиПаролей(Идентификатор);
	НомерТелефона = СпособыДоставкиПаролей.НомерТелефона;
	АдресЭлектроннойПочты = СпособыДоставкиПаролей.АдресЭлектроннойПочты;
		
	ПарольОтправлен = ПолучитьПарольНаСервере(Идентификатор, Ложь, СпособДоставкиПаролей);
	
	Элементы.ИзменениеСпособаДоставкиПаролей.Видимость = Ложь;
	Элементы.ИзменениеСпособаДоставкиПаролей.Доступность = Ложь;	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПарольОтправлен Тогда	
		ЗапуститьТаймерПовторнойОтправки();
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Сервис отправки SMS-сообщений временно недоступен. Повторите попытку позже.'"));
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Подтвердить(Команда)

	ОчиститьСообщения();
	ТекстОшибки = "";
	Пароль = СокрЛП(ВременныйПароль);
	Если ЗначениеЗаполнено(Пароль) И СтрДлина(Пароль) = 6 Тогда
		Попытка
			ПодтвердитьНаСервере();
			Закрыть(КодВозвратаДиалога.ОК);
		Исключение
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Если СтрНайти(ПодробноеПредставлениеОшибки(ИнформацияОбОшибке), КриптосервисВМоделиСервисаКлиентСервер.ТекстИсключенияУказанНеВерныйВременныйПароль()) > 0 Тогда
				ТекстОшибки = НСтр("ru = 'Указан неверный временный пароль.'");
			Иначе 
				ПоказатьПредупреждение(, НСтр("ru = 'Выполнение операции временно невозможно.'"));
			КонецЕсли;
		КонецПопытки;		
	ИначеЕсли ЗначениеЗаполнено(Пароль) И СтрДлина(Пароль) <> 6 Тогда
		ТекстОшибки = НСтр("ru = 'Временный пароль должен состоять из 6 цифр.'");
	Иначе
		ТекстОшибки = НСтр("ru = 'Временный пароль не указан.'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПарольЕщеРаз(Команда)
	
	ПроверочныйКод = Неопределено;
	КодОтправлен = ПолучитьПарольНаСервере(Идентификатор, Истина, СпособДоставкиПаролей);
	
	ЗапуститьТаймерПовторнойОтправки();	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Если Форма.СпособДоставкиПаролей = "phone" Тогда
		Пояснение = НСтр("ru = 'На номер %1 отправлено SMS-сообщение с временным паролем.'");
		
		Пояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Пояснение, Форма.НомерТелефона);
			
		ТекстКоманды = НСтр("ru = 'Не получили SMS? Попробуйте отправить пароль на %1'");
		ТекстКоманды = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстКоманды, Форма.АдресЭлектроннойПочты);
		Элементы.ИзменениеСпособаДоставкиПаролей.Заголовок = ТекстКоманды;	
	ИначеЕсли Форма.СпособДоставкиПаролей = "email" Тогда
		Пояснение = НСтр("ru = 'На адрес %1 отправлено письмо с временным паролем.'");
		
		Пояснение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			Пояснение, Форма.АдресЭлектроннойПочты);
			
		ТекстКоманды = НСтр("ru = 'Не получили письмо? Попробуйте отправить пароль на %1'");
		ТекстКоманды = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстКоманды, Форма.НомерТелефона);
		Элементы.ИзменениеСпособаДоставкиПаролей.Заголовок = ТекстКоманды;
	КонецЕсли;
	Элементы.Пояснение.Заголовок = Пояснение;
	
КонецПроцедуры

&НаСервере
Функция ПодтвердитьНаСервере()
	
	Возврат КриптосервисВМоделиСервиса.ПолучитьСеансовыйКлюч(Идентификатор, ВременныйПароль);
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьТаймерПовторнойОтправки()
	
	Таймер = 60;
	ПодключитьОбработчикОжидания("ОбновитьНадписьОбработногоОтчета", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНадписьОбработногоОтчета()
	
	Если Таймер > 0 Тогда
		Таймер = Таймер - 1;
		Элементы.ВыслатьПарольЕщеРаз.Доступность = Ложь;
		Элементы.ИзменениеСпособаДоставкиПаролей.Доступность = Ложь;
		НадписьПовторнойОтправкиСМС = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = '(через %1 сек...)'"), Таймер);
		ПодключитьОбработчикОжидания("ОбновитьНадписьОбработногоОтчета", 1, Истина);
	Иначе
		Элементы.ВыслатьПарольЕщеРаз.Доступность = Истина;
		НадписьПовторнойОтправкиСМС = "";
		Элементы.ИзменениеСпособаДоставкиПаролей.Видимость = 
			СообщениеСПаролемДоставлено(Идентификатор) И ЗначениеЗаполнено(АдресЭлектроннойПочты) 
			И СпособДоставкиПаролей = "phone"
			ИЛИ ИзменениеСпособаДоставкиПаролейПоказано;
			
		ИзменениеСпособаДоставкиПаролейПоказано = Макс(ИзменениеСпособаДоставкиПаролейПоказано, Элементы.ИзменениеСпособаДоставкиПаролей.Видимость);
		Элементы.ИзменениеСпособаДоставкиПаролей.Доступность = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СообщениеСПаролемДоставлено(Идентификатор)
	
	Попытка
		Возврат КриптосервисВМоделиСервиса.СообщениеСПаролемДоставлено(Идентификатор);
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьПарольНаСервере(Идентификатор, ПовторнаяОтправка, СпособДоставкиПаролей)
	
	Возврат КриптосервисВМоделиСервиса.ПолучитьВременныйПароль(Идентификатор, ПовторнаяОтправка, СпособДоставкиПаролей);
	
КонецФункции

&НаКлиенте
Процедура ИзменениеСпособаДоставкиПаролей(Команда)
	
	Если СпособДоставкиПаролей = "phone" Тогда
		СпособДоставкиПаролей = "email";
	Иначе
		СпособДоставкиПаролей = "phone";
	КонецЕсли;
	
	ОтправитьПарольЕщеРаз(Неопределено);
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти