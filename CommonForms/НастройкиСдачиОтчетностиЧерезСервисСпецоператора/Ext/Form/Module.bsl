#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КонстантыНабор = Константы.СоздатьНабор("АдресСервераСдачиОтчетности, АдресСервисаНаправленийСдачиОтчетности, РеквизитыДоверенности");
	КонстантыНабор.Прочитать();
	РеквизитыДоверенности = КонстантыНабор.РеквизитыДоверенности.Получить();
	АдресСервераСдачиОтчетности = КонстантыНабор.АдресСервераСдачиОтчетности;
	АдресСервисаНаправленийСдачиОтчетности = КонстантыНабор.АдресСервисаНаправленийСдачиОтчетности;
	
	СсылкаШаблонДоверенности = "Открыть шаблон";
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПолеШаблонДоверенностиФНСНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПолеШаблонДоверенностиФНСНажатиеЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ШаблонДоверенностиНалогоплательщика", Новый Структура("СтруктураРеквизитов",РеквизитыДоверенности),,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	ЗаписатьНаСервере();
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура Записать(Команда)
	
	ЗаписатьНаСервере();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолеШаблонДоверенностиФНСНажатиеЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		РеквизитыДоверенности = Результат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНаСервере()
	
	КонстантыНабор = Константы.СоздатьНабор("АдресСервераСдачиОтчетности, АдресСервисаНаправленийСдачиОтчетности, РеквизитыДоверенности");
	КонстантыНабор.АдресСервераСдачиОтчетности = АдресСервераСдачиОтчетности;
	КонстантыНабор.АдресСервисаНаправленийСдачиОтчетности = АдресСервисаНаправленийСдачиОтчетности;
	КонстантыНабор.РеквизитыДоверенности = Новый ХранилищеЗначения(РеквизитыДоверенности);
	КонстантыНабор.Записать();
	
КонецПроцедуры

#КонецОбласти