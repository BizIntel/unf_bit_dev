
&НаКлиенте
Перем ФормаДлительнойОперации;

&НаКлиенте
// Переменная используется извне формы механизмом длительных операций алкоотчетности.
Перем ПараметрыОбработчикаОжидания Экспорт;

&НаКлиенте
Перем ПараметрыОбработчикаОжиданияМОС;

&НаКлиенте
Перем КонтекстЭДОКлиент;

&НаКлиенте
Перем ФормаОтчета; // Обход ошибки в платформе.

&НаКлиенте
// Переменная используется механизмом длительных операций алкоотчетности.
Перем СтруктураВспомДанныхДлитОперацииАЛКО;


#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Элементы.ЭДОСБанкомРоссии.Видимость = 
		ОбщегоНазначения.ПодсистемаСуществует("РегламентированнаяОтчетность.ЭлектронныйДокументооборотСКонтролирующимиОрганами.СдачаОтчетностиВБанкРоссии");
	
	ОпределитьДоступностьЭДО();
	
	ПоказатьПредложениеОформитьЗаявлениеНаПодключение = НЕ ЭлектронныйДокументооборотДоступен
		И ХранилищеОбщихНастроек.Загрузить("Обработка.ОбщиеОбъектыРеглОтчетности", "НеПоказыватьПредложениеОформитьЗаявлениеНаПодключение") <> Истина;
	
	// Инициализируем цвета
	Желтый	= Новый Цвет(251, 225, 81);
	Серый	= Новый Цвет(242, 242, 242);
	Черный	= Новый Цвет(0, 0, 0);
	
	// Определяем организацию по-умолчанию в случае использования одной организации
	Организация = Неопределено;
	
	Если Параметры.Свойство("Организация") Тогда
		Организация = Параметры.Организация;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
			Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
			Организация = Модуль.ОрганизацияПоУмолчанию();
		КонецЕсли;
	КонецЕсли;
	
	// Если организаций больше одной, то используем основную организацию
	Если НЕ ЗначениеЗаполнено(Организация) Тогда
		Организация = РегламентированнаяОтчетность.ПолучитьОрганизациюПоУмолчанию();
	КонецЕсли;
	
	СохраненныйСпособОтбораПериода = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_ТолькоУказанныйПериод");
	Если СохраненныйСпособОтбораПериода <> Неопределено Тогда
		ТолькоУказанныйПериод = СохраненныйСпособОтбораПериода;
	КонецЕсли;
	СохраненныйПериодОтчета = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_ПериодОтчета");
	Если СохраненныйПериодОтчета <> Неопределено Тогда
		ПериодОтчета = СохраненныйПериодОтчета;
	КонецЕсли;
	СохраненныйНачалоПериода = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_НачалоПериода");
	Если СохраненныйНачалоПериода <> Неопределено Тогда
		НачалоПериода = СохраненныйНачалоПериода;
	КонецЕсли;
	СохраненныйКонецПериода = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_КонецПериода");
	Если СохраненныйКонецПериода <> Неопределено Тогда
		КонецПериода = СохраненныйКонецПериода;
	КонецЕсли;
	
	// Заполняем признак использования внешней программы для ведения персонифицированного учета
	ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме = РегламентированнаяОтчетность.ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме();
	
	// Заполняем список выбора для видов сверок
	СписокВыбораВидаСверки = Элементы.ОтборВидСверки.СписокВыбора;
	
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОС.СверкаФИОиСНИЛС));
	СписокВыбораВидаСверки.Добавить(Строка(Перечисления.ВидыУслугПриИОС.СправкаОСостоянииРасчетов));
	
	ОткрытьНаНужнойСтранице(Параметры);
	
	НазначитьВидУведомленияИзПараметра(Параметры);
	НазначитьВидОтчетаИзПараметра(Параметры);

	ДействияПриИзмененииОтборов(ЭтаФорма);
	
	СделатьВсеГиперссылкиНеактивными();
	Для каждого Раздел Из Разделы(ЭтотОбъект, Истина) Цикл
		ЭтотОбъект["Гиперссылка" + Раздел] = Раздел;
	КонецЦикла; 
	
	УправлениеЭУ(ЭтаФорма, Истина);
	
	УправлениеЭУВРежимеСервиса();
	
	РегламентированнаяОтчетность.ФормаРегламентированнойОтчетности_ПриСозданииНаСервере(ЭтаФорма);
	
	// Запуск фонового задания для поиска объектов, не записанных в регистры
	РезультатПроверкиКоличестваНезаписанныхОбъектов = ПроверитьКоличествоОбъектовНеЗаписанныхВРегистр();
			
	// Скрываем некоторые элементы при отключенной 1С-Отчетности
	УправлениеВидимостьюЭлементовПриОтключенномЭДО();
	
	УправлениеВидимостьюЛичныхКабинетов();
	
	ЗаполнитьДеревоНовое(Истина);
	
	ЭтоТакси = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЭтоВариантИнтерфейсаТакси();
		
	// ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	Если ОбщегоНазначения.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
		
		МодульОбработкаНовостей = ОбщегоНазначения.ОбщийМодуль("ОбработкаНовостей");
		
		МодульОбработкаНовостей.КонтекстныеНовости_ПриСозданииНаСервере(
			ЭтаФорма,
			"БП.ОбщаяФорма.РегламентированнаяОтчетность",
			"ОбщаяФорма",
			,
			НСтр("ru='Новости: Регламентированная отчетность'"),
			Ложь,
			Новый Структура("ПолучатьНовостиНаСервере, ХранитьМассивНовостейТолькоНаСервере", Истина, Ложь),
			ИдентификаторыСобытийПриОткрытии);
		
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.КонтекстныеНовости_ПриСозданииНаСервере
	
	ТекстОшибкиИнициализацииКонтекстаЭДО = НСтр("ru = 'Подождите, пожалуйста. Выполняется обновление модуля 1С-Отчетности...'");
		
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии()
	
	Если Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаНастройки Тогда
		УправлениеЭУГруппаНастройки();
	КонецЕсли;
	
	#Если ВебКлиент Тогда 
		ЭтоВебКлиент = Истина;
 	#КонецЕсли
	
	// Обрабатываем результат запуска фонового задания по проверке наличия объектов, не записанных в регистры
	Если РезультатПроверкиКоличестваНезаписанныхОбъектов <> Неопределено Тогда
		// Результат получен и уже обработан на стороне сервера
		ПоказатьРезультатПроверки();
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеПодсчетаКоличестваНезаписанныхОбъектов", 1, Истина);
	КонецЕсли;
	
	// Из-за ошибки платформы скрываем отображение значка критических ошибок в вебе 
	Если ЭтоВебКлиент Тогда
		Элементы.ОтчетыЕстьКритическиеОшибкиОтправки.Видимость 		 = Ложь;
		Элементы.УведомленияЕстьКритическиеОшибкиОтправки.Видимость  = Ложь;
		Элементы.ПисьмаЕстьКритическиеОшибкиОтправки.Видимость 		 = Ложь;
		Элементы.СверкиЕстьКритическиеОшибкиОтправки.Видимость 		 = Ложь;
		Элементы.ЕГРЮЛЕстьКритическиеОшибкиОтправки.Видимость 		 = Ложь;
		Элементы.ВходящиеЕстьКритическиеОшибкиОтправки.Видимость 	 = Ложь;
		Элементы.ТаблицаНовоеЕстьКритическиеОшибкиОтправки.Видимость = Ложь;
	КонецЕсли;
	
	ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияМОС);
	
	РегламентированнаяОтчетностьКлиент.ПередОткрытиемОсновнойФормыРаботыСОтчетностью(ЭтаФорма);
	
	// ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ПриОткрытии(ЭтаФорма);
		
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.ПриОткрытии
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	// В процедуре ниже ничего не писать - это асинхронный метод!
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	ТекстОшибкиИнициализацииКонтекстаЭДО = Результат.ТекстОшибки;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроизвестиДействияПослеПоказаФормы", 0.1, Истина);
	ПодключитьОбработчикОжидания("Подключаемый_ОбновитьНадписьВремяОбновления", 900);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ТипЗнч(Источник) = Тип("УправляемаяФорма") Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяСобытия = "Открытие формы 1С-Отчетность" Тогда
		
		Раздел = Параметр.Раздел;
		Если Элементы.Разделы.ТекущаяСтраница <> Элементы["Страница" + ИмяСтраницы(Раздел)] Тогда
			ОткрытьНаНужнойСтранице(Параметр);
			СделатьВсеГиперссылкиНеактивными();
			УправлениеЭУ(ЭтаФорма, Истина);
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ИзменениеНастроекЭДООрганизации" ИЛИ ИмяСобытия = "ОбновитьУчетнуюЗапись" Тогда
		
		ОпределитьДоступностьЭДО();
		УправлениеВидимостьюЭлементовПриОтключенномЭДО();
		
		Если Параметр = Организация Тогда
			УправлениеЭУГруппаНастройки();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Завершение индексирования объектов журнала Отчетность"
		ИЛИ ИмяСобытия = "Завершение обновления" Тогда
		
		ОбновитьТекущуюТаблицу();
		Если ЗаполнитьДеревоНовое() Тогда
			ПриПереключенииРаздела(Элементы.ГиперссылкаНовое);
			Оповестить("Получены новые сообщения 1С-Отчетности",, ЭтаФорма);
		КонецЕсли;
		
		ОбновитьКоличествоНепрочтенныхОбъектов(ЭтаФорма);

	ИначеЕсли СтрНайти(ИмяСобытия, "Запись_") > 0 
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФСС" ИЛИ ИмяСобытия = "Запись_ОтправкиФСС" ИЛИ ИмяСобытия = "Редактирование_ОтправкиФСС" 
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФСРАР" ИЛИ ИмяСобытия = "Запись_ОтправкиФСРАР" ИЛИ ИмяСобытия = "Редактирование_ОтправкиФСРАР"
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером РПН" ИЛИ ИмяСобытия = "Запись_ОтправкиРПН" ИЛИ ИмяСобытия = "Редактирование_ОтправкиРПН"
		ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФТС" ИЛИ ИмяСобытия = "Запись_ОтправкиФТС" ИЛИ ИмяСобытия = "Редактирование_ОтправкиФТС"
		ИЛИ ИмяСобытия = "Завершение отправки в контролирующий орган"
		ИЛИ ИмяСобытия = "Завершение отправки"
		ИЛИ ИмяСобытия = "Изменение пометки удаления объекта"
		ИЛИ ИмяСобытия = "Скрытие/восстановление объекта"
		ИЛИ ИмяСобытия = "Позиционирование в списке отчетов"
		ИЛИ ИмяСобытия = "Пометка прочтенным"
		ИЛИ ИмяСобытия = "Пометка непрочтенным"
		ИЛИ ИмяСобытия = "Актуализация состояния отправки" Тогда
		
		ОрганизацияВКлюче = Неопределено;
		СсылкаВКлюче = Неопределено;
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("Организация") И Параметр.Свойство("Ссылка") Тогда
			ОрганизацияВКлюче 	= Параметр.Организация;
			СсылкаВКлюче 		= Параметр.Ссылка;
		Иначе
			Если КонтекстЭДОКлиент <> Неопределено И Источник <> Неопределено Тогда
				ОрганизацияВКлюче 	= КонтекстЭДОКлиент.СведенияПоОтправляемымОбъектам(Источник).Организация;
				СсылкаВКлюче 		= Источник;
			КонецЕсли;
		КонецЕсли;
			
		// Обновляем текущую таблицу
		Если Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты Тогда
			
			Если ОрганизацияВКлюче <> Неопределено Тогда
				
				Отборы = Новый Массив;
				Отборы.Добавить(Новый Структура("Организация, Ссылка", ОрганизацияВКлюче, СсылкаВКлюче));
				
				КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.ЖурналОтчетовСтатусы"), Отборы);
				
				Если НЕ КлючЗаписи.Пустой() Тогда
					
					ОповеститьОбИзменении(КлючЗаписи);
					Если ИмяСобытия <> "Пометка прочтенным"
						И ИмяСобытия <> "Пометка непрочтенным"
						И ИмяСобытия <> "Скрытие/восстановление объекта" Тогда
						ДобавитьВидОтчетаВИзбранные(Отборы[0]);
						Элементы.Отчеты.ТекущаяСтрока = КлючЗаписи;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		Иначе
			
			// Определем текущую страницу
			ИмяРаздела 		= СтрЗаменить(Элементы.Разделы.ТекущаяСтраница.Имя, "Страница", "");
			СтраницаЖурнала = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность." + ИмяРаздела);
			
			// Вызываем обновление только одной строки только данной страницы
			Если СтраницаЖурнала <> Неопределено Тогда
			
				Отборы = Новый Массив;
				Отборы.Добавить(Новый Структура("СтраницаЖурнала, Организация, Ссылка", СтраницаЖурнала, ОрганизацияВКлюче, СсылкаВКлюче));
				
				КлючЗаписи = Новый(Тип("РегистрСведенийКлючЗаписи.ЖурналОтправокВКонтролирующиеОрганы"), Отборы);
				
				Если НЕ КлючЗаписи.Пустой() Тогда
					ОповеститьОбИзменении(КлючЗаписи);
					
					СтраницыБезСписков = СтрРазделить("Настройки,Сервисы,Новое", ",");
					// При записи объекта позиционируемся на нем в таблице
					Если СтрНайти(ИмяСобытия, "Запись_") > 0 И СтраницыБезСписков.Найти(ИмяРаздела) = Неопределено Тогда
						Элементы[ИмяРаздела].ТекущаяСтрока = КлючЗаписи;
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если ИмяСобытия = "Пометка прочтенным"
			ИЛИ ИмяСобытия = "Пометка непрочтенным"
			ИЛИ ИмяСобытия = "Скрытие/восстановление объекта" Тогда
			
			// Обновление количества непрочтенных.
			ОбновитьКоличествоНепрочтенныхОбъектов(ЭтаФорма);
			
			// Закладка Новое
			Если ИмяСобытия = "Пометка непрочтенным" ИЛИ ИмяСобытия = "Скрытие/восстановление объекта" Тогда
				ЗаполнитьДеревоНовое(Истина);
			Иначе
				СтрокиДерева = Новое.ПолучитьЭлементы();
				Для Каждого СтрокаДерева Из СтрокиДерева Цикл
					Если СтрокаДерева.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.НезавершенныеОтправки") Тогда
						Прервать;				
					КонецЕсли;
					
					Если СтрокаДерева.Ссылка = Источник Тогда
						СтрокаДерева.НеПрочитано = Ложь;
						СписокСсылок = Новый Массив;
						СписокСсылок.Добавить(Источник);
						ОбновитьКоличествоНовых(ЭтаФорма);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
		
		Если ИмяСобытия = "Завершение автообмена с сервером ФСС" ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФСРАР"
			ИЛИ ИмяСобытия = "Завершение автообмена с сервером РПН" ИЛИ ИмяСобытия = "Завершение автообмена с сервером ФТС"
			ИЛИ ИмяСобытия = "Завершение отправки в контролирующий орган" ИЛИ ИмяСобытия = "Завершение отправки"  Тогда
			
			ЗаполнитьДеревоНовое();
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "ПриЗакрытии_ЭлектронныеПредставленияРегламентированныхОтчетов" Тогда	
		Если ТипЗнч(Параметр) = Тип("Структура") И Параметр.Свойство("ЭтоУведомление") Тогда
			ШаблонПредупреждения = НСтр("ru = '""%1"" отображается в разделе ""%2""'");
			Если Параметр.ЭтоУведомление И Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредупреждения, Строка(Источник), НСтр("ru = 'Уведомления'"));
				ПоказатьПредупреждение(, ТекстПредупреждения);
			ИначеЕсли Не Параметр.ЭтоУведомление И Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаУведомления Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонПредупреждения, Строка(Источник), НСтр("ru = 'Отчеты'"));
				ПоказатьПредупреждение(, ТекстПредупреждения);	
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли ИмяСобытия = "Получены новые сообщения 1С-Отчетности" И Источник <> ЭтаФорма Тогда
		Оповестить("Закрыть форму новых сообщений 1С-Отчетности",, ЭтаФорма);
		ЗаполнитьДеревоНовое(Истина);
		// Обновление количества непрочтенных.
		ОбновитьКоличествоНепрочтенныхОбъектов(ЭтаФорма);
		ПриПереключенииРаздела(Элементы.ГиперссылкаНовое);
	ИначеЕсли ИмяСобытия = "РеализованаНестандартнаяОбработкаОткрытияУведомления" Тогда
		Попытка
			РегламентированнаяОтчетностьКлиентПереопределяемый.ОбработчикСозданияУведомления(ЭтотОбъект, Параметр);
		Исключение
		КонецПопытки;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ФормаРегламентированнойОтчетности_ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтаФорма);
	
	// ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаОповещения(ЭтаФорма, ИмяСобытия, Параметр, Источник);
		
	КонецЕсли;
	// Конец ИнтернетПоддержкаПользователей.Новости.ОбработкаОповещения
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьВидОтчетаВИзбранные(Отбор)
	
	МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
	Элементы.ОтборВидОтчета.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
	
	МенеджерЗаписиОтчета = РегистрыСведений.ЖурналОтчетовСтатусы.СоздатьМенеджерЗаписи();
	МенеджерЗаписиОтчета.Организация = Отбор.Организация;
	МенеджерЗаписиОтчета.Ссылка      = Отбор.Ссылка;
	МенеджерЗаписиОтчета.Прочитать();
	
	ДанныеНайденногоВидаОтчета = Неопределено;
	
	Если МенеджерЗаписиОтчета.Выбран() Тогда
		Если ЗначениеЗаполнено(МенеджерЗаписиОтчета.НаименованиеОтчета) Тогда
			НайденныеОтчеты = РанееОткрытыеВидыОтчетов.НайтиСтроки(Новый Структура("Наименование", МенеджерЗаписиОтчета.НаименованиеОтчета));
			Для каждого НайденныйОтчет Из НайденныеОтчеты Цикл
				Если НайденныйОтчет.ЭтоГруппа <> Истина Тогда
					ДанныеНайденногоВидаОтчета = Новый Структура("Наименование,ПолноеИмя,Ссылка");
					ЗаполнитьЗначенияСвойств(ДанныеНайденногоВидаОтчета, НайденныйОтчет);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если ДанныеНайденногоВидаОтчета <> Неопределено Тогда
		
		ТаблицаРанееСозданныхОтчетов = ХранилищеНастроекДанныхФорм.Загрузить(
			"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаСозданияОтчета",
			"ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты");
		
		Если ТипЗнч(ТаблицаРанееСозданныхОтчетов) = Тип("ТаблицаЗначений") Тогда
			НайденныеОтчеты = ТаблицаРанееСозданныхОтчетов.НайтиСтроки(
				Новый Структура("ПолноеИмя,Ссылка", ДанныеНайденногоВидаОтчета.ПолноеИмя, ДанныеНайденногоВидаОтчета.Ссылка));
			Если НайденныеОтчеты.Количество() = 0 Тогда
				НоваяСтрока = ТаблицаРанееСозданныхОтчетов.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ДанныеНайденногоВидаОтчета);
				
				ХранилищеНастроекДанныхФорм.Сохранить(
					"Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаСозданияОтчета",
					"ФормаОтчетность_ФормаСозданияОтчета_РанееСозданныеОтчеты", ТаблицаРанееСозданныхОтчетов);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПриПереключенииРаздела(Элемент)
	
	ПредыдущаяГиперссылка 	= ТекущийРазделФормы(ЭтаФорма);
	ТекущаяГиперссылка 		= Элемент;
	
	ПриПереключенииРазделаСервер(ПредыдущаяГиперссылка.Имя, ТекущаяГиперссылка.Имя);
	
	ВывестиТаблицуРаздела(ЭтаФорма, Элемент.Имя);
	
	Если Элемент.Имя = "ГиперссылкаНастройки" Тогда
		УправлениеЭУГруппаНастройки();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриПереключенииРазделаСервер(ПредыдущаяГиперссылкаИмя, ТекущаяГиперссылкаИмя)
	
	ПредыдущаяГиперссылка 	= Элементы[ПредыдущаяГиперссылкаИмя];
	ТекущаяГиперссылка 		= Элементы[ТекущаяГиперссылкаИмя];
	
	// Делаем серой предыдущую гиперссылку.
	ПредыдущаяГиперссылка.ЦветФона		= Серый;
	ПредыдущаяГиперссылка.Гиперссылка	= Истина;

	// Делаем желтой текущую гиперссылку.
	ТекущаяГиперссылка.ЦветФона		= Желтый;
	ТекущаяГиперссылка.Гиперссылка	= Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура ГиперссылкаНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПриПереключенииРаздела(Элемент);
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеЭУ(Форма, ВыполнитьУстановкуСтраницы = Ложь)

	Форма.Элементы.ГруппаСсылокРазделы.ЦветФона = Форма.Серый;
		
	// Кнопку текущего раздела делаем желтой
	Если ВыполнитьУстановкуСтраницы Тогда
		
		// Кнопку текущего раздела делаем желтой
		ТекущийРаздел = Форма.Элементы["Гиперссылка" + СтрЗаменить(Форма.Элементы.Разделы.ТекущаяСтраница.Имя, "Страница", "")];
		ОформитьКнопку(Форма, ТекущийРаздел, ТекущийРаздел);
		
	Иначе
		ТекущийРаздел = ТекущийРазделФормы(Форма);
	КонецЕсли;
	
	// Количество в разделе "Новое"
	Если Форма.КоличествоНовых > 0 Тогда
		Форма.Элементы.ГиперссылкаНовое.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Центр;
		Форма.ГиперссылкаНовое = СтрШаблон(НСтр("ru = 'Новое (%1)'"), ?(Форма.КоличествоНовых > 99, "99+", Форма.КоличествоНовых));
	КонецЕсли;
	
	// Раздел "Входящие"
	ОбновитьКоличествоНепрочтенныхОбъектов(Форма);
	
	// Выводим таблицу, соотвествующую разделу
	Если ТекущийРаздел <> Неопределено Тогда 
		ВывестиТаблицуРаздела(Форма, ТекущийРаздел.Имя);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОформитьКнопку(Форма, Гиперссылка, ТекущаяГиперссылка)
	
	АктивнаКнопка = (Гиперссылка = ТекущаяГиперссылка);
	
	Гиперссылка.ЦветФона	= ?(АктивнаКнопка, Форма.Желтый, Форма.Серый);
	Гиперссылка.Гиперссылка	= НЕ АктивнаКнопка;
	Гиперссылка.ЦветТекста	= Форма.Черный;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	ДействияПриИзмененииОтборов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияНастройкиПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтаФорма);
	УправлениеЭУГруппаНастройки();
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОтчетаПриИзменении(Элемент)
	
	УстановитьОтборы(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодОтчетаПриИзменении(Элемент)
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОтчетаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтборВидОтчетаНачалоВыбора(МассивВидовОтчетов);
	
	Элемент.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
	Элемент.СписокВыбора.СортироватьПоЗначению();
		
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидОтчетаОчистка(Элемент, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущаяДатаСеанса = Неопределено;
	
	НачалоПериодаВыбор = НачалоПериода;
	КонецПериодаВыбор  = КонецПериода;
	
	Если НЕ ЗначениеЗаполнено(НачалоПериодаВыбор) Тогда
		ТекущаяДатаСеанса = ТекущаяДатаНаСервере();
		НачалоПериодаВыбор = НачалоКвартала(ТекущаяДатаСеанса);
	КонецЕсли;
	Если НЕ ЗначениеЗаполнено(КонецПериодаВыбор) Тогда
		Если НЕ ЗначениеЗаполнено(ТекущаяДатаСеанса) Тогда
			ТекущаяДатаСеанса = ТекущаяДатаНаСервере();
		КонецЕсли;
		КонецПериодаВыбор = КонецКвартала(ТекущаяДатаСеанса);
	КонецЕсли;
	
	ПараметрыВыбора = Новый Структура("НачалоПериода, КонецПериода, ВидПериода, ТолькоУказанныйПериод"
									 , НачалоПериодаВыбор, КонецПериодаВыбор, Неопределено, ТолькоУказанныйПериод);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтборВыбратьПериодЗавершение", ЭтаФорма);
	
	ПолноеИмяФормыВыбораПериода = "Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ВыборСтандартногоПериодаГодКвартал";
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ФормаРегламентированнойОтчетности_ИмяФормыВыбораПериода(ПолноеИмяФормыВыбораПериода);
	
	ОткрытьФорму(ПолноеИмяФормыВыбораПериода, ПараметрыВыбора, , , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыбора) <> Тип("Структура")
		 ИЛИ НЕ РезультатВыбора.Свойство("НачалоПериода")
		 ИЛИ НЕ РезультатВыбора.Свойство("КонецПериода") Тогда
		
		Возврат;
		
	КонецЕсли;
	
	НачалоПериода = ?(ЗначениеЗаполнено(РезультатВыбора.НачалоПериода), НачалоДня(РезультатВыбора.НачалоПериода), РезультатВыбора.НачалоПериода);
	КонецПериода  = ?(ЗначениеЗаполнено(РезультатВыбора.КонецПериода),  КонецДня(РезультатВыбора.КонецПериода),   РезультатВыбора.КонецПериода);
	
	ПериодОтчета = ПредставлениеПериода(НачалоПериода, КонецПериода, "ФП=Истина");
	
	УстановитьОтборы(ЭтаФорма);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборПериодОтчетаОчистка(Элемент, СтандартнаяОбработка)
	
	НачалоПериода = Дата(1, 1, 1);
	КонецПериода  = Дата(1, 1, 1);
	
	УстановитьОтборы(ЭтаФорма);
	
	СохранитьНастройки();
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаявлениеНаПодключениеК1СОтчетностиНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуМастераЗаявленияНаПодключение(Организация, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокЗаявленийНажатие(Элемент)
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОткрытьФормуСпискаЗаявленийНаПодключение(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкиДокументооборотаСГосОрганамиНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ОткрытьФормуОбщихНастроекЭДО();
	
КонецПроцедуры

&НаКлиенте
Процедура ЭДОСНалоговымиОрганамиПФРИРосстатомНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
	Иначе
		КонтекстЭДОКлиент.ОткрытьФормуНастройкиЭДОсФНСиПФРиРосстатом(Организация, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭДОСФССНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
	Иначе
		КонтекстЭДОКлиент.ОткрытьФормуНастройкиЭДОсФСС(Организация, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭДОСРосалкогольрегулированиемНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
	Иначе
		КонтекстЭДОКлиент.ОткрытьФормуНастройкиЭДОсФСРАР(Организация, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭДОСРосприроднадзоромНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
	Иначе
		КонтекстЭДОКлиент.ОткрытьФормуНастройкиЭДОсРПН(Организация, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭДОСФТСНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
	Иначе
		КонтекстЭДОКлиент.ОткрытьФормуНастройкиЭДОсФТС(Организация, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭДОСБанкомРоссииНажатие(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
	Иначе 
		КонтекстЭДОКлиент.ОткрытьФормуНастройкиЭДОсБанкомРоссии(Организация, ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияСверкиПриИзменении(Элемент)
	 ДействияПриИзмененииОтборов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидСверкиПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияЕГРЮЛПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияПисьмаПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияУведомленияПриИзменении(Элемент)
	ДействияПриИзмененииОтборов(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОтборОрганизацияВходящиеПриИзменении(Элемент)
	
	ДействияПриИзмененииОтборов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидУведомленияПриИзменении(Элемент)
	
	ВидУведомления = ПредставлениеВидаУведомления;
	ДействияПриИзмененииОтборов(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборВидУведомленияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаВыбораВидаУведомления", ЭтотОбъект);
	
	ПараметрыФормы 	= Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
  	ПараметрыФормы.Вставить("УчитыватьУведомленияНеВходящиеВБРО", Истина);
	
	Если ЗначениеЗаполнено(ПредставлениеВидаУведомления) И ЗначениеЗаполнено(ВидУведомления) Тогда
		ПараметрыФормы.Вставить("ВидУведомления", ВидУведомления);
	КонецЕсли;
	
	ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораВидаСообщения", ПараметрыФормы,,,,, 
		ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);

КонецПроцедуры

&НаКлиенте
Процедура ДокументыПоТребованиюФНСНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ДокументыПоТребованиюФНС.Форма.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодсказкиПроПроверкуПодписиОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ЧтоОзначаетПодписьНедействительна");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы

#Область ОбработчикиСобытийЭлементовТаблицыНовое

&НаКлиенте
Процедура ТаблицаНовоеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Ссылка = Элемент.ТекущиеДанные.Ссылка;
	Если Поле.Имя = "ТаблицаНовоеОбъектНаименованиеСсылка" Тогда
		Если Элемент.ТекущиеДанные.ЕстьОтвет Тогда // есть ответ
			Если КонтекстЭДОКлиент <> Неопределено Тогда			
				Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОН(Ссылка);
				ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОС(Ссылка);
				ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП") Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросВыпискиЕГРЮЛ_ЕГРИП(Ссылка);
				КонецЕсли;				
			КонецЕсли;
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ТаблицаНовоеСтатус" Тогда
		ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
	ИначеЕсли Поле.Имя = "ТаблицаНовоеЕстьКритическиеОшибкиОтправки" И Элемент.ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
		ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
	Иначе
		РедактироватьОбъект(Элемент);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыОтчеты

&НаКлиенте
Процедура ОтчетыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		Если Поле.Имя = "ОтчетыСтатус" Тогда
			
			// Переопределяем показ формы состояния отправки
			ОтображатьСтандартнуюФормуСостояния = Истина;
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ПоказатьСостояниеОтправкиОтчетаПереопределяемый(Ссылка, ОтображатьСтандартнуюФормуСостояния);
			
			// Если показ формы был выполнен в переопределяемой процедуре, то стандартную форму не показываем
			Если ОтображатьСтандартнуюФормуСостояния Тогда
				ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
			КонецЕсли;
			
		ИначеЕсли Поле.Имя = "ОтчетыЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
			
		Иначе
			
			ПоказатьЗначение(,Ссылка);
			
			Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПрисоединенныеФайлы")
				И ТипЗнч(Ссылка) <> Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов")
				И ТипЗнч(Ссылка) <> Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				
				// Если в результате выполнения "ПоказатьЗначение" открыта управляемая форма, не являющаяся
				// формой ни регламентированного отчета, ни электронного представления регламентированного
				// отчета, тогда необходимо оповестить динамический список отчетов в форме "1С-Отчетность"
				// об изменении индекса картинки (последнеее делается в случае присоединения файлов).
				
				АктивноеОкно = АктивноеОкно();
				
				Если АктивноеОкно <> Неопределено Тогда
					
					МассивУправлФорм = АктивноеОкно.Содержимое;
					
					Если МассивУправлФорм.Количество() > 0 Тогда
						
						Для каждого УправлФорма Из МассивУправлФорм Цикл
							
							Если УправлФорма.Параметры.Свойство("Ключ") Тогда
								
								Если УправлФорма.Параметры.Ключ = Ссылка Тогда
									
									ДопПарам = Новый Структура(
									"ВладелецФайла,Организация", Ссылка, ТекущиеДанные.Организация);
									
									УправлФорма.ОписаниеОповещенияОЗакрытии
									= Новый ОписаниеОповещения("ОтчетыВыборЗавершение", ЭтотОбъект, ДопПарам);
									
									Прервать;
									
								КонецЕсли;
								
							КонецЕсли;
							
						КонецЦикла;
						
					КонецЕсли;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Оповестить("Пометка прочтенным", , Ссылка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументооборотСКОКлиент.СоздатьЭлектронноеПредставлениеРегламентированныхОтчетовИзФайлов(ПараметрыПеретаскивания.Значение, УникальныйИдентификатор);

КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
		
		Если ТекущиеДанные <> Неопределено Тогда
			
			Ссылка = ТекущиеДанные.РегламентированныйОтчет;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка); 
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				
				ОткрытьФорму("Справочник.ЭлектронныеПредставленияРегламентированныхОтчетов.ФормаОбъекта", ПараметрыФормы);
				
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				
				ОткрытьФорму("Документ.РегламентированныйОтчет.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
				
			Иначе
				
				ИмяФормыОбъекта = ИмяФормыОбъектаПоУмолчанию(Ссылка);
				ОткрытьФорму(ИмяФормыОбъекта, ПараметрыФормы);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		
		СоздатьОтчет();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		ПоказатьЗначение(,Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтчетыПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыУведомления

&НаКлиенте
Процедура УведомленияПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура УведомленияВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда

		Ссылка = ТекущиеДанные.Ссылка;
		
		Если Поле.Имя = "УведомленияСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "УведомленияЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			Ссылка 			= ТекущиеДанные.Ссылка;
			ТипыСсылки 		= ТипЗнч(Ссылка);
			
			ПараметрыФормы 	= Новый Структура;
			ПараметрыФормы.Вставить("ЗначениеКопирования", Ссылка);
			
			Если ТипыСсылки = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
				// Это уведомления из БРО
				ПараметрыФормы.Вставить("Организация", 		ТекущиеДанные.Организация);
				ПараметрыФормы.Вставить("ВидУведомления", 	ВидУведомленияОСпецрежимахНалогообложения(ТекущиеДанные.Ссылка));
				ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.ФормаОбъекта", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				// Это электронные представления
				ОткрытьФорму("Справочник.ЭлектронныеПредставленияРегламентированныхОтчетов.ФормаОбъекта", ПараметрыФормы);
			Иначе
				// Это объекты из ЗУПа
				ИмяТипа = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолноеИмяТипа(ТипыСсылки);
				ОткрытьФорму(ИмяТипа + ".ФормаОбъекта", ПараметрыФормы);
			КонецЕсли;
			
		КонецЕсли;
		
	Иначе
		СоздатьНовоеУведомление();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
	ДокументооборотСКОКлиент.СоздатьЭлектронноеПредставлениеРегламентированныхОтчетовИзФайлов(ПараметрыПеретаскивания.Значение, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыПисьма

&НаКлиенте
Процедура ПисьмаПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПисьмаПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ПисьмаВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
		
		Если Поле.Имя = "ПисьмаСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "ПисьмаЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПисьмаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	Отказ = Истина;
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Письма.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			
			Ссылка = ТекущиеДанные.Ссылка;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка);
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
				ОткрытьФорму("Справочник.ПерепискаСКонтролирующимиОрганами.ФормаОбъекта", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
				ОткрытьФорму("Справочник.ОписиИсходящихДокументовВНалоговыеОрганы.ФормаОбъекта", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
				КонтекстЭДОКлиент.СоздатьПоясненияКДекларацииПоНДС(Ссылка);
			КонецЕсли;
				
		КонецЕсли;
		
	Иначе
		
		СоздатьНовоеПисьмо();

	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыСверки

&НаКлиенте
Процедура СверкиПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СверкиПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура СверкиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Ссылка = Элемент.ТекущиеДанные.Ссылка;

		Если Поле.Имя = "СверкиЕстьОтвет" Тогда
			Если Элемент.ТекущиеДанные.ЕстьОтвет Тогда // есть ответ
				Если КонтекстЭДОКлиент <> Неопределено Тогда
					
					Ссылка = Элемент.ТекущиеДанные.Ссылка;
					Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
						КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОН(Ссылка);
					ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
						КонтекстЭДОКлиент.ПоказатьОтветНаЗапросИОС(Ссылка);
					КонецЕсли;
					
				КонецЕсли;
			Иначе
				РедактироватьОбъект(Элемент);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "СверкиСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "СверкиЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СверкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.Сверки.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Ссылка = ТекущиеДанные.Ссылка;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка); 
			Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
				ОткрытьФорму("Документ.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика.Форма.ФормаДокумента", ПараметрыФормы);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
				ОткрытьФорму("Документ.ЗапросНаИнформационноеОбслуживаниеСтрахователя.Форма.ФормаДокумента", ПараметрыФормы);
			КонецЕсли;
		КонецЕсли;
		
	Иначе
		
		СоздатьНовуюСверку();

	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыЕГРЮЛ

&НаКлиенте
Процедура ЕГРЮЛПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	Если Копирование Тогда
		
		ТекущиеДанные = Элементы.ЕГРЮЛ.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			Ссылка = ТекущиеДанные.Ссылка;
			ПараметрыФормы = Новый Структура("ЗначениеКопирования", Ссылка);
			ОткрытьФорму("Документ.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП.Форма.ФормаДокумента", ПараметрыФормы);
		КонецЕсли;
		
	Иначе
		
		СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП();

	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛПередУдалением(Элемент, Отказ)
	Отказ = Истина;
	ПометитьНаУдалениеОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ЕГРЮЛВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = Элемент.ТекущиеДанные.Ссылка;
		
		СтандартнаяОбработка = Ложь;
		Если Поле.Имя = "ЕГРЮЛЕстьОтвет" Тогда
			Если Элемент.ТекущиеДанные.ЕстьОтвет Тогда // есть ответ
				Если КонтекстЭДОКлиент <> Неопределено Тогда
					КонтекстЭДОКлиент.ПоказатьОтветНаЗапросВыпискиЕГРЮЛ_ЕГРИП(Ссылка);
				КонецЕсли;
			Иначе
				РедактироватьОбъект(Элемент);
			КонецЕсли;
		ИначеЕсли Поле.Имя = "ЕГРЮЛСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "ЕГРЮЛЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыВходящие

&НаКлиенте
Процедура ВходящиеПередУдалением(Элемент, Отказ)
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеПередНачаломИзменения(Элемент, Отказ)
	Отказ = Истина;
	РедактироватьОбъект(Элемент);
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Ссылка = Элемент.ТекущиеДанные.Ссылка;

		Если Поле.Имя = "ВходящиеСтатусОтправки" Тогда
			ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
		ИначеЕсли Поле.Имя = "ВходящиеЕстьОтвет" 
			И ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") 
			И Элемент.ТекущиеДанные.ЕстьОтвет Тогда
			КонтекстЭДОКлиент.НажатиеНаКнопкуПоказатьОтветыПоТребованию(Ссылка);
		ИначеЕсли Поле.Имя = "ВходящиеЕстьКритическиеОшибкиОтправки" И ТекущиеДанные.ЕстьКритическиеОшибкиОтправки Тогда
			ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
		Иначе
			РедактироватьОбъект(Элемент);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВходящиеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

#Область КомандыЗакладкиОтчетность

&НаКлиенте
Процедура Создать(Команда)
	
	СоздатьОтчет();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтчет(знач ПараметрыФормы = Неопределено)
		
	Если ПараметрыФормы = Неопределено Тогда
		
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ПараметрыФормы.Вставить("ДатаНачалаПериодаОтчета", НачалоПериода);
		ПараметрыФормы.Вставить("ДатаКонцаПериодаОтчета", КонецПериода);
		ПараметрыФормы.Вставить("ПредставлениеВидаОтчета",ВидОтчета);
		ПараметрыФормы.Вставить("ИмяФормы","");
	
	КонецЕсли; 
	
	Если ПараметрыФормы.ИмяФормы <> "" Тогда
		
		// Сначала попробуем найти его среди открытых стартовых форм.
		// Необходимо для предотвращения
		// открытия нескольких стартовых форм одного отчета.
		НайденоОкно = Ложь;
		РегламентированнаяОтчетностьКлиент.ВебКлиентНайтиАктивизироватьОкно(ПараметрыФормы.ИмяФормы, ЭтаФорма, НайденоОкно);
		
		Если НайденоОкно <> Неопределено Тогда
			Если НайденоОкно Тогда
			
				Возврат;
			
			КонецЕсли;
		КонецЕсли;

	КонецЕсли; 
			
	ЭтаФорма.ПараметрыВыбораФормыСоздания = Неопределено;
	ОписаниеОповещенияОЗакрытииФормыСозданияОтчета = Новый ОписаниеОповещения("ВыполнитьПослеЗакрытияСозданияОтчета", ЭтотОбъект);
	ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ФормаСозданияОтчета", ПараметрыФормы, ЭтаФорма,,,, 
				ОписаниеОповещенияОЗакрытииФормыСозданияОтчета, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПослеЗакрытияСозданияОтчета(Параметр  = Неопределено, ДопПараметр = Неопределено) Экспорт

	Если ЭтаФорма.ПараметрыВыбораФормыСоздания = Неопределено Тогда
		Возврат;	
	КонецЕсли; 
	
	СоздатьОтчет(ЭтаФорма.ПараметрыВыбораФормыСоздания);
			
КонецПроцедуры

&НаКлиенте
Процедура ПереустановитьКомпонентыПечатиМашиночитаемыхФорм(Команда)
	
	КаталогКомпонентыМЧБ = "";
	
	#Если ВебКлиент Тогда
		
		Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ПереустановитьКомпонентыПечатиМашиночитаемыхФормЗавершение", ЭтотОбъект);
			НачатьУстановкуРасширенияРаботыСФайлами(ОписаниеОповещения);
		Иначе
			ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
			
			ДиалогВыбораКаталога.Заголовок = "Выберите каталог для сохранения дистрибутива компоненты печати машиночитаемых форм.";
			
			Если ДиалогВыбораКаталога.Выбрать() Тогда
				КаталогКомпонентыМЧБ = ДиалогВыбораКаталога.Каталог;
			КонецЕсли;
			
			Если КаталогКомпонентыМЧБ = Неопределено
				ИЛИ ПустаяСтрока(КаталогКомпонентыМЧБ)
				ИЛИ КаталогКомпонентыМЧБ = Ложь Тогда	
				Возврат;
			КонецЕсли;
			
			КаталогКомпонентыМЧБ = КаталогКомпонентыМЧБ + "\";
			УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ);
			
		КонецЕсли;
		
	#Иначе
		
		УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ);
		
	#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ПереустановитьКомпонентыПечатиМашиночитаемыхФормЗавершение(ДополнительныеПараметры) Экспорт
	
	Если НЕ ПодключитьРасширениеРаботыСФайлами() Тогда
		ПоказатьПредупреждение(,НСтр("ru='Не удалось подключить расширение работы с файлами!
		|Установка компоненты печати машиночитаемых форм невозможна.'"));
	Иначе
		ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
		
		ДиалогВыбораКаталога.Заголовок = "Выберите каталог для сохранения дистрибутива компоненты печати машиночитаемых форм.";
		
		Если ДиалогВыбораКаталога.Выбрать() Тогда
			КаталогКомпонентыМЧБ = ДиалогВыбораКаталога.Каталог;
		КонецЕсли;
		
		Если КаталогКомпонентыМЧБ = Неопределено
			ИЛИ ПустаяСтрока(КаталогКомпонентыМЧБ)
			ИЛИ КаталогКомпонентыМЧБ = Ложь Тогда	
			Возврат;
		КонецЕсли;
		
		КаталогКомпонентыМЧБ = КаталогКомпонентыМЧБ + "\";
		УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьКомпонентуПечати(КаталогКомпонентыМЧБ)
	
	ДистрибутивКомпонентыПечатиМашиночитаемыхФорм = Новый СписокЗначений;
	РегламентированнаяОтчетностьВызовСервера.ПолучитьДистрибутивКомпонентыПечатиМашиночитаемыхФорм(ДистрибутивКомпонентыПечатиМашиночитаемыхФорм);
	РегламентированнаяОтчетностьКлиент.УстановитьКомпонентуПечатиМашиночитаемыхФорм(Истина, ДистрибутивКомпонентыПечатиМашиночитаемыхФорм, КаталогКомпонентыМЧБ);
	УправлениеЭУПанельУстановкаКомпонентыПечати();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанные(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ИдентификаторВладельца", УникальныйИдентификатор);
	ОткрытьФорму("Обработка.ДокументооборотСКонтролирующимиОрганами.Форма.ДлительнаяОперация", ПараметрыФормы, ЭтаФорма);
			
	ПодключитьОбработчикОжидания("Подключаемый_ПроизвестиОбменСКонтролирующимОрганами", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	
	РегламентированнаяОтчетностьКлиентПереопределяемый.ФормаРегламентированнойОтчетности_ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроизвестиОбменСКонтролирующимОрганами()
	
	Активизировать();
	ПроизвестиОбменСКонтролирующимОрганами();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьНадписьВремяОбновления()
	
	НадписьВремяОбновления = ОбновитьНадписьВремяОбновления(ДатаПоследнегоОбновления);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроизвестиДействияПослеПоказаФормы()
	
	Активизировать();
	
	// Показываем предложение на подключение в случае, если ни одна организация
	// не подключена к ЭДО.
	Если КонтекстЭДОКлиент <> Неопределено Тогда
		
		Если ПоказатьПредложениеОформитьЗаявлениеНаПодключение Тогда
			ПараметрыСообщения = Новый Структура;
			ПараметрыСообщения.Вставить("ПоказыватьКнопки", Истина);
			
			ОткрытьФорму("Обработка.ОбщиеОбъектыРеглОтчетности.Форма.ПредложениеОформитьЗаявлениеНаПодключение", ПараметрыСообщения, ЭтотОбъект, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Не ПустаяСтрока(ВидОтчета) Тогда
		Если Элементы.ОтборВидОтчета.СписокВыбора.НайтиПоЗначению(ВидОтчета) = Неопределено Тогда
			СоздатьОтчет();
		КонецЕсли;
	КонецЕсли;
	
	// Если Форма авторизации ИПП была открыта раньше формы "1С-Отчетность" - нужно переоткрыть
	Если НЕ СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().РазделениеВключено Тогда
		
		ПереоткрытьОкноАвторизацииИППЕслиОткрыто();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОбновитьНадписьВремяОбновления(ДатаПоследнегоОбновления)
	
	ДатаПоследнегоОбновления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ДатаПоследнегоОбновленияСобытий();
	Возврат ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьНадписьВремяОбновления(ДатаПоследнегоОбновления);
	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьИзФайла(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ПолучениеФайловДляИмпортаНачало(УникальныйИдентификатор);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернете(Команда)
	
	ОнлайнПроверка(Элементы.Отчеты);
	
КонецПроцедуры

&НаКлиенте
Процедура Выгрузить(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			Ссылка = ТекущиеДанные.РегламентированныйОтчет;
			НаименованиеОтчета = ТекущиеДанные.НаименованиеОтчета;
			ВыгрузкаЭлектронногоПредставления(Ссылка, НаименованиеОтчета);
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			
			Ссылка               = ТекущиеДанные.РегламентированныйОтчет;
			НаименованиеОтчета   = ТекущиеДанные.НаименованиеОтчета;
			ВариантОтчета        = ТекущиеДанные.ВариантОтчета;
			ПредставлениеПериода = ТекущиеДанные.ФинансовыйПериод;
			ОрганизацияСсылка	 = ТекущиеДанные.Организация;
						
			ВыгрузкаРегламентированногоОтчета(Ссылка, ОрганизацияСсылка, НаименованиеОтчета, ВариантОтчета, ПредставлениеПериода);
			
		Иначе
			
			// Остальные виды рег отчетов
			РегламентированнаяОтчетностьКлиент.Выгрузить(Ссылка);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьОтчет(Команда)
	
	ПечатьОтчетности(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчет(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	Закладка = Элементы.Отчеты;
	
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТипЗнч(ТекущиеДанные.РегламентированныйОтчет) = Тип("СправочникСсылка.МакетыПенсионныхДел")
			И Не ЕщеНеОтправлялся(ТекущиеДанные.РегламентированныйОтчет) Тогда			
			ТекстСообщения = НСтр("ru = 'Макет пенсионного дела уже отправлен'");			
			ПоказатьПредупреждение( , ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		Если ВыполнениеКомандыОтменено(?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование), ТекущиеДанные.Организация, Команда.Имя) Тогда
			Возврат;
		КонецЕсли;
		
		// Переопределяем отправку в контролирующий орган
		ВыполнитьСтандартнуюОтправкуОтчета = Истина;
		
		ТекущиеДанныеНаименованиеОтчета = ?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование);
		
		////////////////////////////////////////////////////////
		// Заглушка до момента, когда будет ясно куда отправлять.
		Если СтрНайти(ТекущиеДанныеНаименованиеОтчета, "винограда") > 0 Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Проверка в интернете, отправка в электронном виде редакции формы, утвержденной ПП РФ №411 от 13.05.2016, будет реализована в будущих версиях программы.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			
			Возврат;
			
		КонецЕсли;
		//
		////////////////////////////////////////////////////////
		
		// Обход ошибки платформы.
		Если РегламентированнаяОтчетностьКлиент.ВебБраузер() = "MSIE" Тогда
			Если (ТекущиеДанныеНаименованиеОтчета = "РСВ-1 ПФР" И ТипЗнч(ТекущиеДанные.РегламентированныйОтчет) <> Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов"))
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Декларация по НДС"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Бухгалтерская отчетность (с 2011 года)"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Бухгалтерская отчетность малых предприятий" Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='Для выгрузки (проверки выгрузки, проверки в интернете, отправки) в браузере ""Internet Explorer"" необходимо открыть форму регламентированного отчета ""%1""'"),
					ТекущиеДанныеНаименованиеОтчета);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущиеДанныеНаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
			ОписаниеДохИно = ОписаниеРегОтчета(ТекущиеДанные.РегламентированныйОтчет);
			Если ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
				И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
				Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
					Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru='%1 Отправка прервана.'"), ОписаниеДохИно.ТекстСообщения);
						Сообщение.Сообщить();
					КонецЕсли;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если ТекущиеДанныеНаименованиеОтчета = "РСВ-1 ПФР" И ТипЗнч(ТекущиеДанные.РегламентированныйОтчет) <> Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			ОписаниеРСВ_1 = ОписаниеРегОтчета(ТекущиеДанные.РегламентированныйОтчет);
			Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' И НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
				ПоказатьПредупреждение(,НСтр("ru='Внимание! Отправлять в электронном виде форму РСВ-1 необходимо
				|из специализированного рабочего места подготовки квартальной
				|отчетности в ПФР. Отправка прервана.'"));
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьВКонтролирующийОрганПереопределяемый(
			ТекущиеДанные.РегламентированныйОтчет, ТекущиеДанные.ВидКонтролирующегоОргана, ТекущиеДанные.КодКонтролирующегоОргана, ВыполнитьСтандартнуюОтправкуОтчета);
			
		// Если отправка была выполнена в переопределяемой процедуре, то стандартную отправку не выполняем
		Если ВыполнитьСтандартнуюОтправкуОтчета Тогда
			Если КонтекстЭДОКлиент = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
				Возврат;
			КонецЕсли;
			
			Если ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган Тогда
				
				ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Отправка не предусмотрена'"));

			Иначе
				
				Ссылка = ТекущиеДанные.РегламентированныйОтчет;
				
				КонтролирующийОрган = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ИмяПеречисления(ТекущиеДанные.ВидКонтролирующегоОргана);
				Если ЗначениеЗаполнено(КонтролирующийОрган) Тогда
					Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.МакетыПенсионныхДел") Тогда
						ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПисьмаЗавершение", ЭтотОбъект);
						КонтекстЭДОКлиент.ОтправкаМакетаПенсионногоДела(Ссылка, ТекущиеДанные.Организация, ОписаниеОповещения);
					Иначе
					ПараметрыФормы = Новый Структура("СсылкаНаОтчет", Ссылка);
					ДополнительныеПараметры = Новый Структура();
					ДополнительныеПараметры.Вставить("КонтролирующийОрган", КонтролирующийОрган);
					ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
					ДополнительныеПараметры.Вставить("Организация", ТекущиеДанные.Организация);
					ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтчетПодтверждениеОтправкиОтчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
					ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ПодтверждениеОтправкиОтчета", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
					КонецЕсли;
				Иначе
					ПоказатьПредупреждение(, НСтр("ru = 'Не указан контролирующий орган. Отправка невозможна'"));
				КонецЕсли;
				
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчетПодтверждениеОтправкиОтчетаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = ДополнительныеПараметры.Ссылка;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой", ЭтотОбъект, ДополнительныеПараметры);
		РегламентированнаяОтчетностьКлиент.ПередОтправкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
		Возврат;
		
	Иначе
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтчетЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Ложь);
		Возврат;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверкаВозможностиВыгрузкиРегламентированногоОтчетаПередОтправкой(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Ссылка = ДополнительныеПараметры.Ссылка;
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьОтчетЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьОтчетЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, ДополнительныеПараметры.КонтролирующийОрган, , 
		Истина, ДополнительныеПараметры.Ссылка, ДополнительныеПараметры.Организация);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПечатьОтчетШтрих(Команда)
	
	ПечатьОтчетности(Команда.Имя);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузку(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	Закладка = Элементы.Отчеты;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Проверка выгрузки не предусмотрена'"));
			Возврат;
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
			
			СтандартнаяОбработка = Истина;
			
			Если ВыполнениеКомандыОтменено(?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование), ТекущиеДанные.Организация) Тогда
				Возврат;
			КонецЕсли;
			
			ТекущиеДанныеНаименованиеОтчета = ?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование);
			
			ДокументСсылкаАлкоОтчет = ТекущиеДанные.РегламентированныйОтчет;			
			Описание = ОписаниеРегОтчета(ДокументСсылкаАлкоОтчет);
			ВыбраннаяФорма = Описание.ВыбраннаяФорма; 
			// АлкоОтчеты с реализацией проверки с использованием длительных опреаций,
			// виноградные декларации 13, 14, 15.
			Если СтрНайти(ТекущиеДанныеНаименованиеОтчета, "Прил. 13: Объем собранного винограда для производства винодельческой продукции") > 0
				или
				СтрНайти(ТекущиеДанныеНаименованиеОтчета, "Прил. 14: Объем винограда, использованного для производства вина, игристого вина (шампанского)") > 0
				или
				СтрНайти(ТекущиеДанныеНаименованиеОтчета, "Прил. 15: Объем винограда, использованного для производства винодельческой продукции с защищенным географическим указанием") > 0
				Тогда
				
				Если ВыбраннаяФорма = "ФормаОтчета2016Кв2" Тогда
				
					ТекстПредупреждения = НСтр("ru = 'Проверка выгрузки, выгрузка редакции формы, утвержденной ПП РФ №411 от 13.05.2016, 
													|реализована начиная с отчетного периода за 2016 год.
													|Создайте новый отчет за период 2016 год или позже.'");
					ПоказатьПредупреждение(, ТекстПредупреждения);
					Возврат;
					
				КонецЕсли;
				
				ДлитОперация = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.ПроверкаВыгрузки");				
				ДлительныеОперацииАлко(ДокументСсылкаАлкоОтчет, ДлитОперация);
			    Возврат;
				
			КонецЕсли;
			
			// Обход ошибки платформы.
			Если РегламентированнаяОтчетностьКлиент.ВебБраузер() = "MSIE" Тогда
				Если ТекущиеДанныеНаименованиеОтчета = "РСВ-1 ПФР"
					ИЛИ ТекущиеДанныеНаименованиеОтчета = "Декларация по НДС"
					ИЛИ ТекущиеДанныеНаименованиеОтчета = "Бухгалтерская отчетность (с 2011 года)"
					ИЛИ ТекущиеДанныеНаименованиеОтчета = "Бухгалтерская отчетность малых предприятий" Тогда
					ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru='Для выгрузки (проверки выгрузки, проверки в интернете, отправки) в браузере ""Internet Explorer"" необходимо открыть форму регламентированного отчета ""%1""'"),
						ТекущиеДанныеНаименованиеОтчета);
					ПоказатьПредупреждение(, ТекстПредупреждения);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущиеДанныеНаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
				ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
				Если ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
					И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
					Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
						Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
							Сообщение = Новый СообщениеПользователю;
							Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
							"ru='%1 Проверка выгрузки прервана.'"), ОписаниеДохИно.ТекстСообщения);
							Сообщение.Сообщить();
						КонецЕсли;
						Возврат;
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекущиеДанныеНаименованиеОтчета = "РСВ-1 ПФР" Тогда
				ОписаниеРСВ_1 = ОписаниеРегОтчета(Элементы.Отчеты.ТекущиеДанные.РегламентированныйОтчет);
				Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' Тогда
					Если НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
						РегламентированнаяОтчетностьКлиент.ПриПроверкеВыгрузкиРСВ_1ИзЖурналаРеглОтчетов(Ссылка, ОписаниеРСВ_1, СтандартнаяОбработка);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Если (ТекущиеДанныеНаименованиеОтчета = "Статистика: Форма учета перемещения товаров"
				ИЛИ ТекущиеДанныеНаименованиеОтчета = "Статистическая форма учета перемещения товаров") И КонтекстЭДОКлиент <> Неопределено Тогда
				ДокументЗаявлениеОВвозеТоваров = Неопределено;
				ИмяДокументаЗаявлениеОВвозеТоваров = Неопределено;
				РезультатПолученияЗаявленияОВвозеТоваров = КонтекстЭДОКлиент.ПопытатьсяПолучитьЗаявлениеОВвозеТоваровИзСтатформы(
					Ссылка, ДокументЗаявлениеОВвозеТоваров, ИмяДокументаЗаявлениеОВвозеТоваров);
				Если РезультатПолученияЗаявленияОВвозеТоваров = Неопределено Тогда
					Возврат;
				ИначеЕсли РезультатПолученияЗаявленияОВвозеТоваров Тогда
					ДополнительныеПараметры = Новый Структура();
					ДополнительныеПараметры.Вставить("ДокументЗаявлениеОВвозеТоваров", ДокументЗаявлениеОВвозеТоваров);
					ДополнительныеПараметры.Вставить("ИмяДокументаЗаявлениеОВвозеТоваров", ИмяДокументаЗаявлениеОВвозеТоваров);
					ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершениеИзСтатформы", ЭтотОбъект, ДополнительныеПараметры);
					РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
					Возврат;
				КонецЕсли;
			КонецЕсли;
			
			Если СтандартнаяОбработка Тогда
				
				ДополнительныеПараметры = Новый Структура();
				ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьВыгрузкуЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				
				РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
				Возврат;
				
			КонецЕсли;
			
		Иначе
			
			// Остальные виды отчетов
			ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Проверка выгрузки не предусмотрена'"));
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СписокОтчетов = Новый СписокЗначений;
	СписокОтчетов.Добавить(ДополнительныеПараметры.Ссылка);
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ПроверитьРегламентированныеОтчеты(СписокОтчетов) Тогда
		ПоказатьПредупреждение(,НСтр("ru='Проверка прервана!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВыгрузкуЗавершениеИзСтатформы(Отказ, ДополнительныеПараметры) Экспорт
	
	ДокументЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ДокументЗаявлениеОВвозеТоваров;
	ИмяДокументаЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ИмяДокументаЗаявлениеОВвозеТоваров;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	АдресФайлаВыгрузки = Неопределено;
	ИмяФайлаВыгрузки = "";
	
	РезультатВыгрузки = КонтекстЭДОКлиент.ПопытатьсяВыгрузитьСтатформуИзЗаявленияОВвозеТоваров(
		АдресФайлаВыгрузки,
		ИмяФайлаВыгрузки,
		,
		Истина,
		Истина,
		ДокументЗаявлениеОВвозеТоваров,
		ИмяДокументаЗаявлениеОВвозеТоваров);
	
	Если РезультатВыгрузки = Истина Тогда
		ПоказатьПредупреждение(,НСтр("ru='Ошибок не обнаружено!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПротоколовОтчетов(Команда)
	
	ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка 								= ТекущиеДанные.РегламентированныйОтчет;
		ВидКонтролирующегоОргана 			= ТекущиеДанные.ВидКонтролирующегоОргана;
		СостояниеСдачиОтчетности			= ТекущиеДанные.СостояниеСдачиОтчетности;
		НеОтправляетсяВКонтролирующийОрган	= ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган;
		
		ПечатьПротоколовПоСсылке(
			Элементы.Отчеты, 
			Ссылка, 
			ВидКонтролирующегоОргана, 
			НеОтправляетсяВКонтролирующийОрган, 
			СостояниеСдачиОтчетности);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныеФайлыОтчета(Команда)
	
	ОткрытьПрисоединенныеФайлы();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиСверки

&НаКлиенте
Процедура ОтправитьСверку(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Сверки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;
		ТекущаяОрганизация = ТекущиеДанные.Организация;
		//Если запрос уже отправлялся, то повторно отправлять не позволяем
		Если ЕщеНеОтправлялся(ОтправляемыйОбъект) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьСверкуЗавершение", ЭтотОбъект);
			Если ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
				КонтекстЭДОКлиент.ОтправкаЗапросаНаИнформационноеОбслуживание(ОтправляемыйОбъект, ТекущаяОрганизация,  ОписаниеОповещения);
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
				КонтекстЭДОКлиент.ОтправкаЗапросаИОС(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
			Иначе
				КонтекстЭДОКлиент.ОбъектЗаполненКорректно(ОтправляемыйОбъект);
			КонецЕсли;
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Запрос на сверку уже отправлен'"));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьСверкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.Сверки.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеАктовСверкиРасчетов(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеПеречняБухгалтерскойИНалоговойОтчетности(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеСправкиОСостоянииРасчетовСБюджетом(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьПредставлениеСправкиОбИсполненииОбязанностейПоУплате(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСверкаФИОиСНИЛС(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СверкаФИОиСНИЛС"));
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСправкаОСостоянииРасчетов(Команда)
	СоздатьСверку(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СправкаОСостоянииРасчетов"));
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьСверку(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Сверки.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;

		Если ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика") Тогда
			КонтекстЭДОКлиент.ВыгрузитьЗапросИОНВФайл(ОтправляемыйОбъект, УникальныйИдентификатор);
		Иначе
			КонтекстЭДОКлиент.ВыгрузитьЗапросИОСВФайл(ОтправляемыйОбъект, УникальныйИдентификатор);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьСверки(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТекущаяТаблица(ЭтотОбъект).ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ПечатаемыйОбъект = ТекущиеДанные.Ссылка;
		
		Если НЕ КонтекстЭДОКлиент.ОбъектЗаполненКорректно(ПечатаемыйОбъект) Тогда
			Возврат;
		КонецЕсли;
		
		ПечатнаяФормаСверки = ТабличныйДокументСверки(ПечатаемыйОбъект);
		КонтекстЭДОКлиент.НапечататьДокумент(ПечатнаяФормаСверки, Строка(ПечатаемыйОбъект));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиЕГРЮЛ

&НаКлиенте
Процедура СоздатьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП(Команда)
	
	СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.ЕГРЮЛ.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;
		ТекущаяОрганизация = ТекущиеДанные.Организация;
		//Если запрос уже отправлялся, то повторно отправлять не позволяем
		Если ЕщеНеОтправлялся(ОтправляемыйОбъект) Тогда
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИПЗавершение", ЭтотОбъект);
			КонтекстЭДОКлиент.ОтправкаЗапросаНаВыпискуИзЕГР(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Запрос уже отправлен'"));
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросНаВыпискуИзЕГРЮЛ_ЕГРИПЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Элементы.ЕГРЮЛ.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиПисьма

&НаКлиенте
Процедура СоздатьИсходящееПисьмо(Команда)
	
	СоздатьНовоеИсходящееПисьмо();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПисьма(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
 
	ТекущиеДанные = Элементы.Письма.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОтправляемыйОбъект = ТекущиеДанные.Ссылка;
		ТекущаяОрганизация = ТекущиеДанные.Организация;
		//Если письмо уже отправлялось, то повторно отправлять не позволяем
		Если ЕщеНеОтправлялся(ОтправляемыйОбъект) Тогда
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьПисьмаЗавершение", ЭтотОбъект);
			
			Если ТипЗнч(ОтправляемыйОбъект) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
				
				ВидКонтролирующегоОргана = ТекущиеДанные.ВидКонтролирующегоОргана;
				Если ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФНС") Тогда
					КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВФНС(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
				ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ПФР") Тогда
					КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВПФР(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
				ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСГС") Тогда
					КонтекстЭДОКлиент.ОтправкаНеформализованногоДокументаВФСГС(ОтправляемыйОбъект, ТекущаяОрганизация, ОписаниеОповещения);
				Иначе
					КонтекстЭДОКлиент.ОбъектЗаполненКорректно(ОтправляемыйОбъект);
				КонецЕсли;
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
				
				КонтекстЭДОКлиент.ОтправкаОписиИсходящихДокументовВФНС(
					ОтправляемыйОбъект,
					ТекущаяОрганизация,
					УникальныйИдентификатор,
					ОписаниеОповещения);
				
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
				ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтправитьПояснение(КонтекстЭДОКлиент, ОтправляемыйОбъект, ТекущаяОрганизация, УникальныйИдентификатор, ОписаниеОповещения);
			КонецЕсли;
		Иначе
			
			Если ТипЗнч(ОтправляемыйОбъект) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы") Тогда
				ТекстСообщения = НСтр("ru = 'Ответ на требование уже отправлен'");
			ИначеЕсли ТипЗнч(ОтправляемыйОбъект) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
				ТекстСообщения = НСтр("ru = 'Пояснение уже отправлено'");
			Иначе
				ТекстСообщения = НСтр("ru = 'Письмо уже отправлено'");
			КонецЕсли;
			
			ПоказатьПредупреждение( , ТекстСообщения);
			
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПисьмаЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ОбработкаОповещения("Завершение отправки в контролирующий орган", , Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПисьма(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = ТекущаяТаблица(ЭтотОбъект).ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		ПечатаемыйОбъект = ТекущиеДанные.Ссылка;
		
		Если ТипЗнч(ПечатаемыйОбъект) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Печать требования не предусмотрена'"));
			Возврат;
		ИначеЕсли ТипЗнч(ПечатаемыйОбъект) = Тип("СправочникСсылка.ОписиИсходящихДокументовВНалоговыеОрганы")
			ИЛИ ТипЗнч(ПечатаемыйОбъект) = Тип("ДокументСсылка.ПоясненияКДекларацииПоНДС") Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Печать ответа на требование не предусмотрена'"));
			Возврат;
		КонецЕсли;
		
		ПечатнаяФормаПисьма = ТабличныйДокументПисьма(ПечатаемыйОбъект);
		КонтекстЭДОКлиент.НапечататьДокумент(ПечатнаяФормаПисьма, Строка(ПечатаемыйОбъект));
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиУведомления

&НаКлиенте
Процедура СоздатьУведомление(Команда)
	
	СоздатьНовоеУведомление();
		
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомления(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	Закладка 		= Элементы.Уведомления;
	ТекущиеДанные 	= Элементы.Уведомления.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка 			= ТекущиеДанные.Ссылка;
		ТипыСсылки 		= ТипЗнч(Ссылка);
		
		Если ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган Тогда
			
			ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Отправка не предусмотрена'"));
			
		Иначе
			
			ПараметрыФормы = Новый Структура("СсылкаНаОтчет", Ссылка);
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ТипыСсылки", ТипыСсылки);
			ДополнительныеПараметры.Вставить("ВидКонтролирующегоОргана", ТекущиеДанные.ВидКонтролирующегоОргана);
			ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
			ДополнительныеПараметры.Вставить("Организация", ТекущиеДанные.Организация);
			ОписаниеОповещения = Новый ОписаниеОповещения("ОтправитьУведомленияПодтверждениеОтправкиОтчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.ПодтверждениеОтправкиОтчета", ПараметрыФормы,,,,, ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Элементы.Уведомления.Обновить();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьУведомленияПодтверждениеОтправкиОтчетаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ <> КодВозвратаДиалога.ОК Тогда
		Возврат;
	КонецЕсли;
	
	ТипыСсылки 					= ДополнительныеПараметры.ТипыСсылки;
	ВидКонтролирующегоОргана 	= ДополнительныеПараметры.ВидКонтролирующегоОргана;
	Ссылка 						= ДополнительныеПараметры.Ссылка;
	ОрганизацияВДокументе 		= ДополнительныеПараметры.Организация;
	
	Если ТипыСсылки = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") 
		ИЛИ ТипыСсылки = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
		// Это уведомления из БРО или электронное представление реестра ФСС
		
		КонтролирующийОрган = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ИмяПеречисления(ВидКонтролирующегоОргана);
		
		РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, КонтролирующийОрган, , 
			Истина, Ссылка, ОрганизацияВДокументе);
		
	Иначе
		
		// Это объекты из ЗУПа
		КонтролирующийОрган = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ИмяПеречисления(ВидКонтролирующегоОргана);
		РегламентированнаяОтчетностьКлиент.ПриНажатииНаКнопкуОтправкиВКонтролирующийОрган(ЭтаФорма, КонтролирующийОрган, , 
			Истина, Ссылка, ОрганизацияВДокументе);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьВИнтернетеУведомления(Команда)
	
	ОнлайнПроверка(Элементы.Уведомления);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьУведомлений(Команда)
	
	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		УведомлениеДляПечати = ТекущиеДанные.Ссылка;
		Если Не ЗначениеЗаполнено(УведомлениеДляПечати) Тогда 
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(УведомлениеДляПечати) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		
			МассивПечати = Новый Массив;
			МассивПечати.Добавить(УведомлениеДляПечати);
			
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
				"Документ.УведомлениеОСпецрежимахНалогообложения",
				"Уведомление", МассивПечати, Неопределено);
				
		ИначеЕсли ТипЗнч(УведомлениеДляПечати) = Тип("ДокументСсылка.УведомлениеОПолучателеДокументов") Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Печать уведомления о получателе документов не предусмотрена.'");
			ПоказатьПредупреждение(,ТекстПредупреждения);
			
		ИначеЕсли ТипЗнч(УведомлениеДляПечати) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			// Печать электронного представления
			Ссылка 			= ТекущиеДанные.Ссылка;
			Наименование 	= ТекущиеДанные.Наименование;
			
			ПечатьЭлектронногоПредставления(Ссылка, Наименование);
			
		Иначе
			
			СведенияПоОбъекту = РегламентированнаяОтчетностьВызовСервера.СведенияПоОбъектуНеВходящемуВБРО(УведомлениеДляПечати);
			Если СведенияПоОбъекту <> Неопределено Тогда // это объект, не входящий в БРО
				НапечататьОбъектНеВходящийВБРО(УведомлениеДляПечати, СведенияПоОбъекту);
			КонецЕсли;
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьУведомления(Команда)
	
	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		УведомлениеДляВыгрузки = ТекущиеДанные.Ссылка;
		Если Не ЗначениеЗаполнено(УведомлениеДляВыгрузки) Тогда 
			Возврат;
		КонецЕсли;
		
		Если ТипЗнч(УведомлениеДляВыгрузки) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
			
			ВыгружаемыеДанные = СформироватьXMLНаСервере(УникальныйИдентификатор, УведомлениеДляВыгрузки);
			Если ВыгружаемыеДанные <> Неопределено Тогда 
				РегламентированнаяОтчетностьКлиент.ВыгрузитьФайлы(ВыгружаемыеДанные);
			КонецЕсли;
			
		ИначеЕсли ТипЗнч(УведомлениеДляВыгрузки) = Тип("ДокументСсылка.УведомлениеОПолучателеДокументов") Тогда
			
			Если КонтекстЭДОКлиент = Неопределено Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
				Возврат;
			КонецЕсли;
			
			КонтекстЭДОКлиент.ВыгрузитьУведомлениеОПолучателеДокументовВФайл(УведомлениеДляВыгрузки, УникальныйИдентификатор);
			
		ИначеЕсли ТипЗнч(УведомлениеДляВыгрузки) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			
			Наименование = ТекущиеДанные.Наименование;
			ВыгрузкаЭлектронногоПредставления(УведомлениеДляВыгрузки, Наименование);
				
		Иначе
				
			РегламентированнаяОтчетностьКлиент.ВыгрузитьПереопределяемый(УведомлениеДляВыгрузки);
				
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПротоколовУведомлений(Команда)
	
	ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка 								= ТекущиеДанные.Ссылка;
		ВидКонтролирующегоОргана 			= ТекущиеДанные.ВидКонтролирующегоОргана;
		СостояниеСдачиОтчетности			= ТекущиеДанные.СостояниеСдачиОтчетности;
		НеОтправляетсяВКонтролирующийОрган	= ТекущиеДанные.НеОтправляетсяВКонтролирующийОрган;
		
		ПечатьПротоколовПоСсылке(
			Элементы.Уведомления, 
			Ссылка, 
			ВидКонтролирующегоОргана, 
			НеОтправляетсяВКонтролирующийОрган, 
			СостояниеСдачиОтчетности);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	УправлениеПечатьюКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Уведомления);
КонецПроцедуры

&НаКлиенте
Процедура УведомленияПриАктивизацииСтроки(Элемент)
	УправлениеПечатьюКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныеФайлыУведомления(Команда)
	
	ОткрытьПрисоединенныеФайлы();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиВходящие

&НаКлиенте
Процедура СоздатьОтвет(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Входящие.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
		Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
			
			КонтекстЭДОКлиент.СоздатьПисьмоОтвет(Ссылка);
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов") Тогда
			
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ЗапретитьСозданиеПисьмаВОтветНаТребованиеПоДекларации", Истина);
			
			КонтекстЭДОКлиент.СоздатьОтветНаДокументРеализацииПолномочий(Ссылка, ДополнительныеПараметры);
			
		ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.ТранспортноеСообщение") Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Создание ответа на это письмо не предусмотрено'");
			ПоказатьПредупреждение(,ТекстПредупреждения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПометитьПрочитанным(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	Результат = ПометитьПрочтенными();
	
	Если ТекущаяСтраница(ЭтотОбъект) <> ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие")
		И Результат.ВсеВыделенныеОбъекты.Количество() > 0
		И Результат.СтавшиеПрочтенными.Количество() = 0
		И Результат.СтавшиеНепрочтенными.Количество() = 0 Тогда
		СообщитьОНевозможностиИзменитьПрочтенность();
		Возврат;
	КонецЕсли;

	Для каждого ИзменившийсяОбъект Из Результат.СтавшиеПрочтенными Цикл
		Оповестить("Пометка прочтенным", , ИзменившийсяОбъект);
	КонецЦикла;
	
	Для каждого ИзменившийсяОбъект Из Результат.СтавшиеНепрочтенными Цикл
		Оповестить("Пометка непрочтенным", , ИзменившийсяОбъект);
	КонецЦикла;
	
	// Обновление динамического списка.
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	ТекущаяТаблица.Обновить();
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиНастройки

&НаКлиенте
Процедура ПроиндексироватьОбъекты(Команда)
	
	ДописатьОбъектыОтсутствующиеВРегистре(Истина);
	УправлениеЭУ(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьНастройкиМеханизмаОнлайнСервисовРОНажатие(Элемент)
	
	ОткрытьФорму("Обработка.ОнлайнСервисыРегламентированнойОтчетности.Форма.НастройкиМеханизмаОнлайнСервисов");
	
КонецПроцедуры

&НаКлиенте
Процедура НадписьИнформацияОбОбновленияхНажатие(Элемент)
	
	ОнлайнСервисыРегламентированнойОтчетностиКлиент.ОткрытьФормуДоступныхОбновленийРО();
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналОбменаСКонтролирующимиОрганами(Команда)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.УправлениеОбменом");
КонецПроцедуры

&НаКлиенте
Процедура СкрытыеОтчетыИДокументы()
	
	СтандартнаяОбработка = Ложь;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
			
	ОткрытьФорму(КонтекстЭДОКлиент.ПутьКОбъекту + ".Форма.СкрытыеОбъекты");
	
КонецПроцедуры

#КонецОбласти

#Область КомандыЗакладкиСервисы

&НаКлиенте
Процедура СервисНажатие(Элемент)
	
	ПерейтиПоНавигационнойСсылке(Элемент.Подсказка);
	
КонецПроцедуры

&НаКлиенте
Процедура ФНССервис(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;

	КонтекстЭДОКлиент.ПерейтиПоСсылкеВИнтернетЭксплорере(Элемент.Подсказка);
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеКоманды

&НаКлиенте
Процедура Скрыть(Команда)
	
	СтандартнаяОбработка = Ложь;
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПослеПодтвержденияСкрытия", 
		ЭтотОбъект);
	
	ПодтвердитьСкрытие(ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура СообщитьОНевозможностиИзменитьПрочтенность()
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	ТекстСообщения = "";
	Если ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		ТекстСообщения = НСтр("ru = 'Отчет уже прочитан'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		ТекстСообщения = НСтр("ru = 'Уведомление уже прочитано'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки") Тогда
		ТекстСообщения = НСтр("ru = 'Запрос на сверку уже прочитан'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.ЕГРЮЛ") Тогда
		ТекстСообщения = НСтр("ru = 'Запрос выписки из ЕГРЮЛ уже прочитан'");
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие") Тогда
		ТекстСообщения = НСтр("ru = 'Письмо уже прочитано'");
	КонецЕсли;
		
	Если ЗначениеЗаполнено(ТекстСообщения) Тогда
		ПоказатьПредупреждение(,ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСтраницы(Страница)
	
	Если Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Новое") Тогда
		ИмяРаздела = "Новое";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		ИмяРаздела = "Отчеты";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		ИмяРаздела = "Уведомления";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Письма") Тогда
		ИмяРаздела = "Письма";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки") Тогда
		ИмяРаздела = "Сверки";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.ЕГРЮЛ") Тогда
		ИмяРаздела = "ЕГРЮЛ";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие") Тогда
		ИмяРаздела = "Входящие";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Настройки") Тогда
		ИмяРаздела = "Настройки";
	ИначеЕсли Страница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сервисы") Тогда
		ИмяРаздела = "Сервисы";
	КонецЕсли;
	
	Возврат ИмяРаздела;
	
КонецФункции

&НаСервере
Функция СкрытьОбъект1СОтчетности()

	СкрытыеОбъекты = Новый Массив;
	
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	Для каждого Строка Из ТекущаяТаблица.ВыделенныеСтроки Цикл
		
		СкрытыйОбъект = Новый Структура;
		СкрытыйОбъект.Вставить("Ссылка", 		Строка.Ссылка);
		СкрытыйОбъект.Вставить("Организация", 	Строка.Организация);
		
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьВосстановитьОбъект1СОтчетности(
			СкрытыйОбъект.Ссылка, 
			Истина);
			
		СкрытыеОбъекты.Добавить(СкрытыйОбъект);
				
	КонецЦикла;

	Возврат СкрытыеОбъекты;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТабличныйДокументПисьма(Письмо)
	
	Возврат Справочники.ПерепискаСКонтролирующимиОрганами.ПечатнаяФорма(Письмо);

КонецФункции

&НаСервереБезКонтекста
Функция ТабличныйДокументСверки(Сверка)
	
	Если ТипЗнч(Сверка) = Тип("ДокументСсылка.ЗапросНаИнформационноеОбслуживаниеСтрахователя") Тогда
		Источник = Документы.ЗапросНаИнформационноеОбслуживаниеСтрахователя;
	Иначе
		Источник = Документы.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика;
	КонецЕсли;
	ТабличныйДокумент = Источник.ПечатнаяФорма(Сверка);
	
	Возврат ТабличныйДокумент;

КонецФункции

&НаСервереБезКонтекста
Функция ТекущаяДатаНаСервере()
	
	Возврат ТекущаяДатаСеанса();
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьСкрытие(ВыполняемоеОповещение)
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	
	ТекущаяТаблица 			= ТекущаяТаблица(ЭтотОбъект);
	ВыделеноНесколькоСтрок 	= ТекущаяТаблица.ВыделенныеСтроки.Количество() > 1;
	
	Если ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'отчеты'");
		Иначе
			ВидОбъекта = НСтр("ru = 'отчет'");
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'уведомления'");
		Иначе
			ВидОбъекта = НСтр("ru = 'уведомление'");
		КонецЕсли;

	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Сверки") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'запросы на сверку'");
		Иначе
			ВидОбъекта = НСтр("ru = 'запрос на сверку'");
		КонецЕсли;

	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.ЕГРЮЛ") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'выписки из ЕГРЮЛ'");
		Иначе
			ВидОбъекта = НСтр("ru = 'выписку из ЕГРЮЛ'");
		КонецЕсли;
		
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Входящие")
		ИЛИ ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Письма") Тогда
		
		Если ВыделеноНесколькоСтрок Тогда 
			ВидОбъекта = НСтр("ru = 'письма'");
		Иначе
			ВидОбъекта = НСтр("ru = 'письмо'");
		КонецЕсли;
		
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Вы уверены, что хотите скрыть %1?
                           |
                           |Скрытые отчеты и документы можно восстановить в разделе
                           |""Настройка"" - ""Скрытые отчеты и документы"" формы 1С-Отчетность'");
	ТекстСообщения = СтрШаблон(ТекстСообщения, ВидОбъекта);
	
	ПоказатьВопрос(ВыполняемоеОповещение,ТекстСообщения, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияСкрытия(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		СкрытыеОбъекты = СкрытьОбъект1СОтчетности();
	
		Для каждого СкрытыйОбъект Из СкрытыеОбъекты Цикл
			
			Оповестить("Скрытие/восстановление объекта", 
				СкрытыйОбъект, 
				СкрытыйОбъект.Ссылка);

		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция Разделы(Форма, ТолькоРазделыСТаблицами = Ложь)
	
	ОписаниеРазделов = Новый Массив;
	
	Если НЕ ТолькоРазделыСТаблицами Тогда
		ОписаниеРазделов.Добавить("Новое");
	КонецЕсли;
	
	ОписаниеРазделов.Добавить("Отчеты");
	ОписаниеРазделов.Добавить("Уведомления");
	ОписаниеРазделов.Добавить("Письма");
	ОписаниеРазделов.Добавить("Сверки");
	ОписаниеРазделов.Добавить("ЕГРЮЛ");
	ОписаниеРазделов.Добавить("Входящие");
	
	Если НЕ ТолькоРазделыСТаблицами Тогда
		ОписаниеРазделов.Добавить("Сервисы");
		ОписаниеРазделов.Добавить("Настройки");
	КонецЕсли;
	
	Возврат ОписаниеРазделов;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоНовых(Форма)
	
	Форма.КоличествоНовых = 0;
	
	КоличествоПолученныеСообщения = 0;
	КоличествоОбработанныеЗапросы = 0;
	КоличествоЗавершенныеОтправки = 0;
	
	СтрокаБлокПолученныеСообщения = Неопределено;
	СтрокаБлокОбработанныеЗапросы = Неопределено;
	СтрокаБлокЗавершенныеОтправки = Неопределено;
	
	СтрокиДерева = Форма.Новое.ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.ПолученныеСообщения") Тогда
			Если СтрокаДерева.ЭтоЗаголовокБлока Тогда
				СтрокаБлокПолученныеСообщения = СтрокаДерева;
			ИначеЕсли СтрокаДерева.НеПрочитано Тогда
				КоличествоПолученныеСообщения = КоличествоПолученныеСообщения + 1;
			КонецЕсли;
				
		ИначеЕсли СтрокаДерева.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.ОбработанныеЗапросы") Тогда 
			Если СтрокаДерева.ЭтоЗаголовокБлока Тогда
				СтрокаБлокОбработанныеЗапросы = СтрокаДерева;
			ИначеЕсли СтрокаДерева.НеПрочитано Тогда
				КоличествоОбработанныеЗапросы = КоличествоОбработанныеЗапросы + 1;
			КонецЕсли;
			
		ИначеЕсли СтрокаДерева.Группа = ПредопределенноеЗначение("Перечисление.ГруппыНовыхСобытийДокументооборотаСКонтролирующимиОрганами.ЗавершенныеОтправки") Тогда 
			Если СтрокаДерева.ЭтоЗаголовокБлока Тогда
				СтрокаБлокЗавершенныеОтправки = СтрокаДерева;
			ИначеЕсли СтрокаДерева.НеПрочитано Тогда
				КоличествоЗавершенныеОтправки = КоличествоЗавершенныеОтправки + 1;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
	Форма.КоличествоНовых = КоличествоПолученныеСообщения + КоличествоОбработанныеЗапросы + КоличествоЗавершенныеОтправки;
	
	Если КоличествоПолученныеСообщения = 0 Тогда
		СтрокаБлокПолученныеСообщения.ЗаголовокБлока = НСтр("ru = 'Полученные сообщения'");	
	Иначе
		СтрокаБлокПолученныеСообщения.ЗаголовокБлока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Полученные сообщения (%1)'"), КоличествоПолученныеСообщения);
	КонецЕсли;
	
	Если КоличествоОбработанныеЗапросы = 0 Тогда
		СтрокаБлокОбработанныеЗапросы.ЗаголовокБлока = НСтр("ru = 'Обработанные запросы'");	
	Иначе
		СтрокаБлокОбработанныеЗапросы.ЗаголовокБлока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Обработанные запросы (%1)'"), КоличествоОбработанныеЗапросы);
	КонецЕсли;

	Если КоличествоЗавершенныеОтправки = 0 Тогда
		СтрокаБлокЗавершенныеОтправки.ЗаголовокБлока = НСтр("ru = 'Завершенные отправки'");	
	Иначе
		СтрокаБлокЗавершенныеОтправки.ЗаголовокБлока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = 'Завершенные отправки (%1)'"), КоличествоЗавершенныеОтправки);
	КонецЕсли;

	// Количество в разделе "Новое"
	Если Форма.КоличествоНовых > 0 Тогда
		ТекстНовое = НСтр("ru = 'Новое (%1)'");
		ТекстНовое = СтрШаблон(ТекстНовое, ?(Форма.КоличествоНовых > 99, "99+", Форма.КоличествоНовых));
		Форма.ГиперссылкаНовое = ТекстНовое;
	Иначе
		Форма.ГиперссылкаНовое = НСтр("ru = 'Новое'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ЗаполнитьДеревоНовое(ПервоначальноеЗаполнение = Ложь)
	
	ЕстьИзмененияВНовыхСообщениях = Ложь;
	
	ДеревоНовое = РеквизитФормыВЗначение("Новое");
	
	ТекущиеСобытия = Новый Соответствие;
	Для Каждого СтрокаДерева Из ДеревоНовое.Строки Цикл
		Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
			ТекущиеСобытия.Вставить(Строка(СтрокаДерева.Ссылка.УникальныйИдентификатор()) + СтрокаДерева.Статус, Истина);
		КонецЕсли;
		Для Каждого ПодСтрокаДерева Из СтрокаДерева.Строки Цикл
			Если ЗначениеЗаполнено(ПодСтрокаДерева.Ссылка) Тогда
				ТекущиеСобытия.Вставить(Строка(ПодСтрокаДерева.Ссылка.УникальныйИдентификатор()) + ПодСтрокаДерева.Статус, Истина);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;

	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ЗаполнитьДеревоНовое(ДеревоНовое);
	
	Для Каждого СтрокаДерева Из ДеревоНовое.Строки Цикл
		Если ЗначениеЗаполнено(СтрокаДерева.Ссылка) Тогда
			КлючПоиска = Строка(СтрокаДерева.Ссылка.УникальныйИдентификатор()) + СтрокаДерева.Статус; 
			Если ТекущиеСобытия.Получить(КлючПоиска) = Неопределено Тогда
				ЕстьИзмененияВНовыхСообщениях = Истина;
				Прервать;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ДеревоНовое, "Новое");
	
	ДатаПоследнегоОбновления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ДатаПоследнегоОбновленияСобытий();
	НадписьВремяОбновления = ЭлектронныйДокументооборотСКонтролирующимиОрганами.ПолучитьНадписьВремяОбновления(ДатаПоследнегоОбновления);
	
	ОбновитьКоличествоНовых(ЭтаФорма);

	Возврат ЕстьИзмененияВНовыхСообщениях И Не ПервоначальноеЗаполнение;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСписокОтчетовИзРегистраСведений(ПараметрыОтбора = Неопределено)
	
	Запрос = Новый Запрос;
	ТекстЗапроса     = "ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
	                   |	Журнал.НаименованиеОтчета КАК НаименованиеОтчета
	                   |ИЗ
	                   |	РегистрСведений.ЖурналОтчетовСтатусы КАК Журнал
	                   |";
	ТекстУпорядочить = "
	                   |УПОРЯДОЧИТЬ ПО
	                   |	НаименованиеОтчета";
	
	ТекстУсловия = "";
	
	Если НЕ ПараметрыОтбора = Неопределено Тогда
		
		ЕстьПараметры = Ложь;
		
		Если НЕ ПараметрыОтбора.Организация = Справочники.Организации.ПустаяСсылка() Тогда
			ТекстУсловия  = Символы.ПС + "	Журнал.Организация = &Организация ";
			Запрос.УстановитьПараметр("Организация", ПараметрыОтбора.Организация);
			ЕстьПараметры = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.НачалоПериода)Тогда
			ТекстУсловия  = ТекстУсловия  + Символы.ПС + "	" + ?(ЕстьПараметры, "И ", "") + "Журнал.ДатаНачала >= &НачалоПериода";
			Запрос.УстановитьПараметр("НачалоПериода", ПараметрыОтбора.НачалоПериода);
			ЕстьПараметры = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ПараметрыОтбора.КонецПериода)Тогда
			ТекстУсловия  = ТекстУсловия  + Символы.ПС + "	" + ?(ЕстьПараметры, "И ", "") + "Журнал.ДатаОкончания <= &КонецПериода";
			Запрос.УстановитьПараметр("КонецПериода", ПараметрыОтбора.КонецПериода);
			ЕстьПараметры = Истина;
		КонецЕсли;
		
		Если ЗначениеЗаполнено(ТекстУсловия) Тогда
			ТекстЗапроса = ТекстЗапроса + "ГДЕ " + ТекстУсловия;
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса + ТекстУпорядочить;
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Возврат РезультатЗапроса.ВыгрузитьКолонку("НаименованиеОтчета");
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ВывестиТаблицуРаздела(Форма, РазделОтчетности)
	
	ТекущийРаздел 		= ТекущийРазделФормы(Форма);
	ИмяТекущейТаблицы 	= СтрЗаменить(ТекущийРаздел.Имя, "Гиперссылка", ""); 
	
	Форма.Элементы.Разделы.ТекущаяСтраница = Форма.Элементы["Страница" + ИмяТекущейТаблицы];
	
	ТекущийРаздел = Форма.Элементы.Разделы.ТекущаяСтраница;
	
	// Эсли форма открыта на закладке с таблицей, то активизируем эту таблицу
	Если ТекущийРаздел <> Форма.Элементы.СтраницаНовое И ТекущийРаздел <> Форма.Элементы.СтраницаНастройки Тогда 
		
		ИмяЭлемента = СтрЗаменить(РазделОтчетности, "Гиперссылка", "");
		
		ЭлементТаблица = Форма.Элементы.Найти(ИмяЭлемента);
		Если ЭлементТаблица <> Неопределено Тогда 
			Форма.ТекущийЭлемент = ЭлементТаблица;
		КонецЕсли;
		
	КонецЕсли;		
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭУПанельУстановкаКомпонентыПечати(ОперацияСКомпонентойСовершена = Ложь)
	
	Если РегламентированнаяОтчетностьКлиент.КомпонентаПечатиМашиночитаемыхФормДоступна() Тогда
		Элементы.ПереустановитьКомпонентыПечатиМашиночитаемыхФорм.Заголовок = НСтр("ru = 'Переустановить компоненты печати машиночитаемых форм'");
	Иначе
		Элементы.ПереустановитьКомпонентыПечатиМашиночитаемыхФорм.Заголовок = НСтр("ru = 'Установка компоненты печати'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеЭУГруппаНастройки()
	
	Элементы.ЗаявлениеНаПодключениеК1СОтчетности.Заголовок = 
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЗаголовокГиперссылкиЗаявления(Организация);
		
	ВебБраузер = РегламентированнаяОтчетностьКлиент.ВебБраузер();
	
	Если ЗначениеЗаполнено(ВебБраузер)
	   И НЕ ВебБраузер = "MSIE" Тогда
	   
	   Элементы.ГруппаДвухмерногоШтрихкода.Видимость = Ложь;
		
	КонецЕсли;			
	
КонецПроцедуры

&НаКлиенте
Процедура ШаблоныПечатиМашиночитаемыхФормНажатие()
	
	ОткрытьФорму("РегистрСведений.ШаблоныПечатиМашиночитаемыхФорм.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаXMLШаблоновНажатие(Элемент)
	
	ОткрытьФорму("РегистрСведений.ШаблоныЭВФОтчетовСтатистики.ФормаСписка");
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаФорматаВыгрузкиНажатие(Элемент)
	
	РегламентированнаяОтчетностьКлиент.ПоказатьФормуНастроекФорматаВыгрузкиСтатистическойОтчетностиВЭлектронномВиде(Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ЖурналВыгруженныхОтчетовНажатие(Элемент)
	
	ОткрытьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаСписка", , ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СправочникРегламентированныхОтчетовНажатие(Элемент)
	
	ОткрытьФорму("Справочник.РегламентированныеОтчеты.Форма.ФормаСписка");
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборы(Форма)
	
	ОтборДинамическогоСписка = Форма.Отчеты.КомпоновщикНастроек.Настройки.Отбор;
	
	ОтборДинамическогоСписка.Элементы.Очистить();
	
	ОтборВидОтчета        = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборОрганизация      = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборДатаНачала       = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборДатаОкончания1   = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборДатаОкончания2   = ОтборДинамическогоСписка.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	
	ОтборВидОтчета.ЛевоеЗначение      = Новый ПолеКомпоновкиДанных("НаименованиеОтчета");
	ОтборОрганизация.ЛевоеЗначение    = Новый ПолеКомпоновкиДанных("Организация");
	ОтборДатаНачала.ЛевоеЗначение     = Новый ПолеКомпоновкиДанных("ДатаНачала");
	ОтборДатаОкончания1.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончания");
	ОтборДатаОкончания2.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ДатаОкончания");
	
	Если НЕ ЗначениеЗаполнено(Форма.Организация) Тогда
		ОтборОрганизация.Использование  = Ложь;
	Иначе
		ОтборОрганизация.Использование  = Истина;
		ОтборОрганизация.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
		ОтборОрганизация.ПравоеЗначение = Форма.Организация;
	КонецЕсли;
	
	Если Форма.ВидОтчета = "" Тогда
		ОтборВидОтчета.Использование = Ложь;
	Иначе
		ОтборВидОтчета.Использование = Истина;
		Если СокрЛП(Форма.ВидОтчета) = "Макет пенсионного дела" Тогда
			ОтборВидОтчета.ВидСравнения = ВидСравненияКомпоновкиДанных.Содержит;
		Иначе
			ОтборВидОтчета.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		КонецЕсли;
		ОтборВидОтчета.ПравоеЗначение = Форма.ВидОтчета;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Форма.ПериодОтчета) Тогда
		ОтборДатаНачала.Использование     = Ложь;
		ОтборДатаОкончания1.Использование = Ложь;
		ОтборДатаОкончания2.Использование = Ложь;
	Иначе
		Если ЗначениеЗаполнено(Форма.НачалоПериода) Тогда
			ОтборДатаНачала.Использование      = Истина;
			ОтборДатаНачала.ВидСравнения       = ?(Форма.ТолькоУказанныйПериод, ВидСравненияКомпоновкиДанных.Равно, ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
			ОтборДатаНачала.ПравоеЗначение     = ?(Форма.ТолькоУказанныйПериод, Форма.НачалоПериода, НачалоГода(Форма.НачалоПериода));
			
			ОтборДатаОкончания1.Использование  = НЕ Форма.ТолькоУказанныйПериод;
			ОтборДатаОкончания1.ВидСравнения   = ВидСравненияКомпоновкиДанных.Больше;
			ОтборДатаОкончания1.ПравоеЗначение = Форма.НачалоПериода;
		Иначе
			ОтборДатаНачала.Использование      = Ложь;
			ОтборДатаОкончания1.Использование  = Ложь;
		КонецЕсли;
		Если ЗначениеЗаполнено(Форма.КонецПериода) Тогда
			Если Форма.ТолькоУказанныйПериод Тогда
				ОтборДатаОкончания1.Использование  = Истина;
				ОтборДатаОкончания1.ВидСравнения   = ВидСравненияКомпоновкиДанных.БольшеИлиРавно;
				ОтборДатаОкончания1.ПравоеЗначение = НачалоДня(Форма.КонецПериода);
			КонецЕсли;
			
			ОтборДатаОкончания2.Использование  = Истина;
			ОтборДатаОкончания2.ВидСравнения   = ВидСравненияКомпоновкиДанных.МеньшеИлиРавно;
			ОтборДатаОкончания2.ПравоеЗначение = Форма.КонецПериода;
		Иначе
			ОтборДатаОкончания2.Использование  = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	ОтборДинамическогоСписка.ИдентификаторПользовательскойНастройки = Строка(Новый УникальныйИдентификатор);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВсеОрганизации()

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	Организации.Ссылка КАК Организация
		|ИЗ
		|	Справочник.Организации КАК Организации
		|ГДЕ
		|	Организации.ПометкаУдаления = ЛОЖЬ";

	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаОрганизаций = РезультатЗапроса.Выгрузить();
	
	МассивОрганизаций = ТаблицаОрганизаций.ВыгрузитьКолонку("Организация");
	
	Возврат МассивОрганизаций;
	
КонецФункции

&НаКлиенте
Процедура ПроизвестиОбменСКонтролирующимОрганами()
	
	ВыбранныеОрганизации = ВсеОрганизации();
	
	Если ВыбранныеОрганизации.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Организация не выбрана'"), ,"Организация");
		Возврат;
	КонецЕсли;
	
	ОсуществитьОбменПоВыбраннымОрганизациям(ВыбранныеОрганизации, 0);
		
КонецПроцедуры

&НаКлиенте
Процедура ОсуществитьОбменПоВыбраннымОрганизациям(ВыбранныеОрганизации, НомерОбъекта)

	Если НомерОбъекта + 1 > ВыбранныеОрганизации.Количество() Тогда
		УправлениеЭУ(ЭтаФорма);		
		Оповестить("Завершение обновления");
		Оповестить("Закрыть_ДлительнаяОперация",, УникальныйИдентификатор);
	Иначе
		// Получим очередную организацию для обмена
		ОрганизацияДляОбмена = ВыбранныеОрганизации[НомерОбъекта];
		ДополнительныеПараметры = Новый Структура("ВыбранныеОрганизации, НомерОбъекта", ВыбранныеОрганизации, НомерОбъекта);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОсуществитьОбменПоВыбраннымОрганизациямЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		КонтекстЭДОКлиент.ОсуществитьОбменПоОрганизации(ЭтаФорма, ОрганизацияДляОбмена, ОписаниеОповещения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОсуществитьОбменПоВыбраннымОрганизациямЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбранныеОрганизации = ДополнительныеПараметры.ВыбранныеОрганизации;
	НомерОбъекта = ДополнительныеПараметры.НомерОбъекта;
	
	ОсуществитьОбменПоВыбраннымОрганизациям(ВыбранныеОрганизации, НомерОбъекта + 1)
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьСверку(ВидУслуги)
		
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;

	Если ТипЗнч(ВидУслуги) = Тип("ПеречислениеСсылка.ВидыУслугПриИОН") Тогда
		// Это запрос ИОН
		ФормаДокумента = "Документ.ЗапросНаИнформационноеОбслуживаниеНалогоплательщика.Форма.ФормаДокумента";
		
	Иначе
		// Это запрос ИОС
		ФормаДокумента = "Документ.ЗапросНаИнформационноеОбслуживаниеСтрахователя.Форма.ФормаДокумента";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("ВидУслуги, Организация", ВидУслуги, Организация));
	ОткрытьФорму(ФормаДокумента, ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьОбъект(Элемент)
	
	ТекущиеДанные = Элемент.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.Ссылка;
		Если ЗначениеЗаполнено(Ссылка) Тогда
			ПоказатьЗначение(,Ссылка);
			Оповестить("Пометка прочтенным", , Ссылка);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПометитьНаУдалениеОбъект(Таблица)
	
	ТекущиеДанные = Таблица.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Таблица = Элементы.Отчеты Тогда 
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
	Иначе
		Ссылка = ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	ПометитьНаУдалениеОбъектНаСервере(Ссылка);
	
	// Обновление списка "точечно"
	ПараметрОповещения = Новый Структура(); 
	ПараметрОповещения.Вставить("Ссылка", Ссылка);
	ПараметрОповещения.Вставить("Организация", ТекущиеДанные.Организация);
	Оповестить("Изменение пометки удаления объекта", ПараметрОповещения, Ссылка);

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПометитьНаУдалениеОбъектНаСервере(Ссылка)
	
	УдалаяемыйОбъект = Ссылка.ПолучитьОбъект();
	УдалаяемыйОбъект.ПометкаУдаления = НЕ Ссылка.ПометкаУдаления;
	УдалаяемыйОбъект.Записать();
		
КонецПроцедуры

&НаСервере
Функция ЕщеНеОтправлялся(Ссылка)
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	СтатусОтправки = КонтекстЭДОСервер.ПолучитьСтатусОтправкиОбъекта(Ссылка);
	
	Возврат НЕ ЗначениеЗаполнено(СтатусОтправки) ИЛИ СтатусОтправки = Перечисления.СтатусыОтправки.ВКонверте;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущийРазделФормы(Форма)
	
	Результат = Неопределено;
	
	Для каждого Раздел Из Разделы(Форма) Цикл
		
		Гиперссылка = Форма.Элементы["Гиперссылка" + Раздел];
		
		Если Гиперссылка.ЦветФона = Форма.Желтый Тогда
			Результат = Гиперссылка;
		КонецЕсли;
	
	КонецЦикла; 
	
	Возврат Результат;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяТаблица(Форма)
	
	ТекущийРаздел 		= ТекущийРазделФормы(Форма);
	ИмяТекущейТаблицы 	= СтрЗаменить(ТекущийРаздел.Имя, "Гиперссылка", ""); 
	
	Возврат Форма.Элементы[ИмяТекущейТаблицы];
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяСтраница(Форма)
	
	ТекущийРаздел 		= ТекущийРазделФормы(Форма);
	ИмяТекущейТаблицы 	= СтрЗаменить(ТекущийРаздел.Имя, "Гиперссылка", ""); 
	
	Возврат ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность." + ИмяТекущейТаблицы);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборПоОрганизацииИВидуДокумента(ДинамическийСписок, ОрганизацияДляОтбора = Неопределено, ВидОбъектаДляОтбора = Неопределено)
	
	ДинамическийСписок.Отбор.Элементы.Очистить();
	ГруппаОтбора = ДинамическийСписок.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	// отбор по организации
	Если ЗначениеЗаполнено(ОрганизацияДляОтбора) Тогда
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Организация");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ОрганизацияДляОтбора;
	КонецЕсли;

	// отбор по виду сверки
	Если ЗначениеЗаполнено(ВидОбъектаДляОтбора) Тогда
		ЭлементОтбора = ГруппаОтбора.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Наименование");
		ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ЭлементОтбора.Использование = Истина;
		ЭлементОтбора.ПравоеЗначение = ВидОбъектаДляОтбора;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ВидУведомленияОСпецрежимахНалогообложения(Ссылка)

	Возврат Ссылка.ВидУведомления;

КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ДействияПриИзмененииОтборов(Форма)
	
	УстановитьОтборы(Форма);
	УстановитьОтборПоОрганизацииИВидуДокумента(Форма.Уведомления, 	Форма.Организация, Форма.ПредставлениеВидаУведомления);
	УстановитьОтборПоОрганизацииИВидуДокумента(Форма.Письма, 		Форма.Организация);
	УстановитьОтборПоОрганизацииИВидуДокумента(Форма.Сверки, 		Форма.Организация, Форма.ВидСверки);
	УстановитьОтборПоОрганизацииИВидуДокумента(Форма.ЕГРЮЛ, 		Форма.Организация);
	УстановитьОтборПоОрганизацииИВидуДокумента(Форма.Входящие, 		Форма.Организация);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбораВидаУведомления(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если ТипЗнч(Результат) = Тип("ПеречислениеСсылка.ВидыУведомленийОСпецрежимахНалогообложения") Тогда
			ВидУведомления 					= Результат;
			ПредставлениеВидаУведомления 	= Строка(Результат);
		Иначе
			ВидУведомления 					= Результат.Тип;
			ПредставлениеВидаУведомления 	= Результат.Наименование;
		КонецЕсли;
	КонецЕсли;
	
	ДействияПриИзмененииОтборов(ЭтаФорма);

КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьXMLНаСервере(УникальныйИдентификатор, Объект)
	ДокОбъект = Объект.ПолучитьОбъект();
	Возврат ДокОбъект.ВыгрузитьДокумент(УникальныйИдентификатор);
КонецФункции

&НаСервереБезКонтекста
Функция КоличествоНепрочитанныхОбъектов()
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналОтчетовСтатусы.Ссылка) КАК КоличествоНепрочтенных,
		|	ЗНАЧЕНИЕ(Перечисление.СтраницыЖурналаОтчетность.Отчеты) КАК СтраницаЖурнала
		|ИЗ
		|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
		|ГДЕ
		|	ЖурналОтчетовСтатусы.Непрочтенное = ИСТИНА
		|	И НЕ ЖурналОтчетовСтатусы.Скрыт
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ЖурналОтправокВКонтролирующиеОрганы.Ссылка),
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала
		|ИЗ
		|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
		|ГДЕ
		|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное = ИСТИНА
		|	И НЕ ЖурналОтправокВКонтролирующиеОрганы.Скрыт
		|
		|СГРУППИРОВАТЬ ПО
		|	ЖурналОтправокВКонтролирующиеОрганы.СтраницаЖурнала";
		
		
	ТаблицаНепрочтенных = Запрос.Выполнить().Выгрузить();
	МассивНепрочтенных = ОбщегоНазначения.ТаблицаЗначенийВМассив(ТаблицаНепрочтенных);

	Возврат МассивНепрочтенных;
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОбновитьКоличествоНепрочтенныхОбъектов(Форма)
	
	// Сбрасываем количества во всех заголовках.
	Для каждого Раздел Из Разделы(Форма, Истина) Цикл
		Форма["Гиперссылка" + Раздел] = Раздел;
	КонецЦикла;
	
	// Устанавливаем количества только там, где есть непрочтенные.
	НепрочтенныеОбъектыПоРазделам = КоличествоНепрочитанныхОбъектов();
	
	Для каждого НепрочтенныеОбъектыРаздела Из НепрочтенныеОбъектыПоРазделам Цикл
		
		КоличествоНепрочитанныхОбъектов = НепрочтенныеОбъектыРаздела.КоличествоНепрочтенных;
		СтраницаЖурнала 				= НепрочтенныеОбъектыРаздела.СтраницаЖурнала;
		
		НадписьКоличество = "";
		Если КоличествоНепрочитанныхОбъектов > 99 Тогда  
			НадписьКоличество = " (99+)";
		ИначеЕсли КоличествоНепрочитанныхОбъектов <> 0 Тогда
			НадписьКоличество = " (" + Строка(КоличествоНепрочитанныхОбъектов) + ")";
		КонецЕсли;
		
		Форма["Гиперссылка" + ИмяСтраницы(СтраницаЖурнала)] = ИмяСтраницы(СтраницаЖурнала) + НадписьКоличество;
			
	КонецЦикла; 

КонецПроцедуры

&НаКлиенте
Процедура ПечатьОтчетности(ВидПечати)
	
	ТекущиеДанные 	= Элементы.Отчеты.ТекущиеДанные;
	Закладка	 	= Элементы.Отчеты;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Ссылка = ТекущиеДанные.РегламентированныйОтчет;
		
		Если ВидПечати = "ПечатьОтчет" Тогда
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				// Печать электронного представления
				Ссылка 				= ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета 	= ТекущиеДанные.НаименованиеОтчета;
				
				ПечатьЭлектронногоПредставления(Ссылка, НаименованиеОтчета);
				
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				
				// Печать регламентированного отчета
				Ссылка             = ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета = ТекущиеДанные.НаименованиеОтчета;
								
				ДокументСсылкаАлкоОтчет = ТекущиеДанные.РегламентированныйОтчет;			
				Описание = ОписаниеРегОтчета(ДокументСсылкаАлкоОтчет);
				ВыбраннаяФорма = Описание.ВыбраннаяФорма;
				// АлкоОтчеты с реализацией проверки с использованием длительных опреаций,
				// виноградные декларации 13, 14, 15.
				Если СтрНайти(НаименованиеОтчета, "Прил. 13: Объем собранного винограда для производства винодельческой продукции") > 0
					или
					СтрНайти(НаименованиеОтчета, "Прил. 14: Объем винограда, использованного для производства вина, игристого вина (шампанского)") > 0
					или
					СтрНайти(НаименованиеОтчета, "Прил. 15: Объем винограда, использованного для производства винодельческой продукции с защищенным географическим указанием") > 0
				
					Тогда
					
					Если ВыбраннаяФорма <> "ФормаОтчета2016Кв2" Тогда
					
						ДлитОперация = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Печать");				
						ДлительныеОперацииАлко(ДокументСсылкаАлкоОтчет, ДлитОперация, ВидПечати);
					    Возврат;
					
					КонецЕсли;
					
				КонецЕсли;
			
				Если НаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
					ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
					Если ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
						И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
						Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
							Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
								Сообщение = Новый СообщениеПользователю;
								Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
								"ru='%1 Печать прервана.'"), ОписаниеДохИно.ТекстСообщения);
								Сообщение.Сообщить();
							КонецЕсли;
							Возврат;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДополнительныеПараметры = Новый Структура();
				ДополнительныеПараметры.Вставить("НаименованиеОтчета", НаименованиеОтчета);
				ДополнительныеПараметры.Вставить("Ссылка", Ссылка);
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПечатьОтчетностиЗавершение", ЭтотОбъект, ДополнительныеПараметры);
				
				РегламентированнаяОтчетностьКлиент.ПередПечатьюРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
				Возврат;
				
			Иначе
				// Печать объекта, не входящего в БРО
				СведенияПоОбъекту = РегламентированнаяОтчетностьВызовСервера.СведенияПоОбъектуНеВходящемуВБРО(Ссылка);
				Если СведенияПоОбъекту <> Неопределено Тогда
					НапечататьОбъектНеВходящийВБРО(Ссылка, СведенияПоОбъекту);
				КонецЕсли;
			КонецЕсли;
			
		ИначеЕсли ВидПечати = "ПечатьОтчетШтрих" Тогда
			
			Если ТипЗнч(Ссылка) = Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
				// Печать электронного представления
				Ссылка 				= ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета 	= ТекущиеДанные.НаименованиеОтчета;
				
				ПечатьЭлектронногоПредставления(Ссылка, НаименованиеОтчета);
			ИначеЕсли ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
				// Печать регламентированного отчета со штрихкодом PDF417
				Ссылка             = ТекущиеДанные.РегламентированныйОтчет;
				НаименованиеОтчета = ТекущиеДанные.НаименованиеОтчета;
				
				Если ВыполнениеКомандыОтменено(?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование), ТекущиеДанные.Организация) Тогда
					Возврат;
				КонецЕсли;
				
				ПечатьРегламентированногоОтчетаШтрих(Ссылка, НаименованиеОтчета);
			Иначе
				
				// Печать объекта, не входящего в БРО
				ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Печать со штрихкодом PDF417 не предусмотрена'"));
			
			КонецЕсли;
		
		ИначеЕсли ВидПечати = "ПечатьПротоколы" Тогда
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ВыполнениеКомандыОтменено(НаименованиеОтчета, Организация, НаименованиеКоманды = "")
	
	Если (НаименованиеОтчета = "Бухгалтерская отчетность (с 2011 года)"
		ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность малых предприятий"
		ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность СО НКО"
		ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность СО НКО (до 2015 года)")
		И НЕ РегламентированнаяОтчетностьВызовСервера.ЭтоЮридическоеЛицо(Организация) Тогда
		
		Если НаименованиеКоманды = "ОтправитьОтчет" Тогда
			РегламентированнаяОтчетностьКлиент.ПоказатьПредупреждениеОНевозможностиОтправкиОтчета(); 
		Иначе
			РегламентированнаяОтчетностьКлиент.ПоказатьПредупреждениеОНевозможностиВыгрузкиОтчета(); 
		КонецЕсли;
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ПечатьОтчетностиЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ПечатьРегламентированногоОтчета(ДополнительныеПараметры.Ссылка, ДополнительныеПараметры.НаименованиеОтчета);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьЭлектронногоПредставления(Ссылка, Наименование)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
 
	КонтекстЭДОКлиент.ПечатьЭлектронногоПредставления(Ссылка, , Наименование);
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьРегламентированногоОтчета(Ссылка, Наименование)
	
	СтандартнаяОбработка = Истина;
	
	Если Наименование = "РСВ-1 ПФР" Тогда
		
		ОписаниеРСВ_1 = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' Тогда
			Если НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
				РегламентированнаяОтчетностьКлиент.ПриПечатиРСВ_1ИзЖурналаРеглОтчетов(Ссылка, ОписаниеРСВ_1, СтандартнаяОбработка);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Если (Наименование = "Статистика: Форма учета перемещения товаров"
		ИЛИ Наименование = "Статистическая форма учета перемещения товаров") И КонтекстЭДОКлиент <> Неопределено Тогда
		
		ДокументЗаявлениеОВвозеТоваров = Неопределено;
		ИмяДокументаЗаявлениеОВвозеТоваров = Неопределено;
		РезультатПолученияЗаявленияОВвозеТоваров = КонтекстЭДОКлиент.ПопытатьсяПолучитьЗаявлениеОВвозеТоваровИзСтатформы(
			Ссылка, ДокументЗаявлениеОВвозеТоваров, ИмяДокументаЗаявлениеОВвозеТоваров);
		Если РезультатПолученияЗаявленияОВвозеТоваров = Неопределено Тогда
			Возврат;
		ИначеЕсли РезультатПолученияЗаявленияОВвозеТоваров Тогда
			МассивОбъектов = Новый Массив;
			МассивОбъектов.Добавить(ДокументЗаявлениеОВвозеТоваров);
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ." + ИмяДокументаЗаявлениеОВвозеТоваров, "СтатистическаяФормаУчетаПеремещенияТоваров", МассивОбъектов, ЭтаФорма);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
		
		СписокОтчетов = Новый СписокЗначений;
		СписокОтчетов.Добавить(Ссылка);
		
		РегламентированнаяОтчетностьКлиент.НапечататьОтчеты(СписокОтчетов, "ПоказатьБланк");
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПечатьРегламентированногоОтчетаШтрих(Ссылка, Наименование)
	
	ФормаОтчета = Неопределено; // Обход ошибки в платформе.
	
	ПредставлениеДокументаРеглОтч = "";
	
	ФормаОтчета = РегламентированнаяОтчетностьКлиент.ИнициализацияОтчета(Ссылка, ПредставлениеДокументаРеглОтч);
	Если ФормаОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Наименование = "Декларация по УСН" Тогда
		ОписаниеУСН = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеУСН.ВыбраннаяФорма = "ФормаОтчета2014Кв1" И ОписаниеУСН.РекомендованнаяФорма Тогда
			ПоказатьПредупреждение(, НСтр("ru='Печать с двухмерным штрихкодом PDF417 формы, рекомендованной письмом ФНС от 20.05.2015 № ГД-4-3/8533@, не предусмотрена.'"));
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Наименование = "Доходы, выплаченные иностранным организациям" Тогда
		ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
			И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
			Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
				Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='%1 Печать формы с двухмерным штрихкодом PDF417 прервана.'"), ОписаниеДохИно.ТекстСообщения);
					Сообщение.Сообщить();
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ФормаОтчета.СтруктураРеквизитовФормы.Свойство("ДоступенМеханизмПечатиРеглОтчетностиСДвухмернымШтрихкодомPDF417")
	   И СтрНайти(ФормаОтчета.ИмяФормы, "РегламентированныйОтчетСтатистика") = 0 Тогда
		РегламентированнаяОтчетностьКлиент.ВывестиМашиночитаемуюФорму(ФормаОтчета, "ПоказатьСДвухмернымШтрихкодомPDF417");
	Иначе
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Для отчета """ + Наименование + """ не предусмотрена печать бланка формы с двухмерным штрихкодом PDF417!'");
		Сообщение.Сообщить();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаЭлектронногоПредставления(Ссылка, НаименованиеОтчета)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ВыгрузкаЭлектронногоПредставления(Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчета(Ссылка, ОрганизацияСсылка, НаименованиеОтчета, ВариантОтчета, ПредставлениеПериода);
	
	Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6 = Ложь;
	
	СтандартнаяОбработка = Истина;
	
	Если ВыполнениеКомандыОтменено(НаименованиеОтчета, ОрганизацияСсылка) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
	
		ДокументСсылкаАлкоОтчет = Ссылка;					
		Описание = ОписаниеРегОтчета(ДокументСсылкаАлкоОтчет);
		ВыбраннаяФорма = Описание.ВыбраннаяФорма;
		// АлкоОтчеты с реализацией проверки с использованием длительных опреаций,
		// виноградные декларации 13, 14, 15.
		Если СтрНайти(НаименованиеОтчета, "Прил. 13: Объем собранного винограда для производства винодельческой продукции") > 0
			или
			СтрНайти(НаименованиеОтчета, "Прил. 14: Объем винограда, использованного для производства вина, игристого вина (шампанского)") > 0
			или
			СтрНайти(НаименованиеОтчета, "Прил. 15: Объем винограда, использованного для производства винодельческой продукции с защищенным географическим указанием") > 0
				
			Тогда
			
			Если ВыбраннаяФорма = "ФормаОтчета2016Кв2" Тогда
			
				ТекстПредупреждения = НСтр("ru = 'Проверка выгрузки, выгрузка редакции формы, утвержденной ПП РФ №411 от 13.05.2016, 
												|реализована начиная с отчетного периода за 2016 год.
												|Создайте новый отчет за период 2016 год или позже.'");
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Возврат;
				
			КонецЕсли;
			
			ДлитОперация = ПредопределенноеЗначение("Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.Выгрузка");
			ДлительныеОперацииАлко(ДокументСсылкаАлкоОтчет, ДлитОперация);
		    Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Обход ошибки платформы.
	Если РегламентированнаяОтчетностьКлиент.ВебБраузер() = "MSIE" Тогда
		Если НаименованиеОтчета = "РСВ-1 ПФР"
			ИЛИ НаименованиеОтчета = "Декларация по НДС"
			ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность (с 2011 года)"
			ИЛИ НаименованиеОтчета = "Бухгалтерская отчетность малых предприятий" Тогда
			ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
				"ru='Для выгрузки (проверки выгрузки, проверки в интернете, отправки) в браузере ""Internet Explorer"" необходимо открыть форму регламентированного отчета ""%1""'"),
				НаименованиеОтчета);
			ПоказатьПредупреждение(, ТекстПредупреждения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "Доходы, выплаченные иностранным организациям" Тогда
		ОписаниеДохИно = ОписаниеРегОтчета(Ссылка);
		Если ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
			И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
			Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
				Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='%1 Выгрузка прервана.'"), ОписаниеДохИно.ТекстСообщения);
					Сообщение.Сообщить();
				КонецЕсли;
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "РСВ-1 ПФР" Тогда
		
		ОписаниеРСВ_1 = ОписаниеРегОтчета(Ссылка);
		
		Если ОписаниеРСВ_1.ДатаКонцаПериодаОтчета >= '20140101' Тогда
			
			Если НЕ (ПерсонифицированныйУчетВедетсяВоВнешнейПрограмме И ОписаниеРСВ_1.ОтчетСтарогоОбразца_20141101) Тогда
				РегламентированнаяОтчетностьКлиент.ПриВыгрузкеРСВ_1ИзЖурналаРеглОтчетов(
					Ссылка, ОписаниеРСВ_1, СтандартнаяОбработка);
			КонецЕсли;
			
			Если ОписаниеРСВ_1.ЗагруженыВнешниеСведенияРаздела6 Тогда
				Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6 = Истина;
			КонецЕсли;
			
		Иначе
			
			Если СокрЛП(ВариантОтчета) = "П"
				И НЕ РегламентированнаяОтчетностьКлиентСерверПереопределяемый.ИмяОсновнойФормыРСВ1() = "ОсновнаяФорма" Тогда
				
				ПоказатьПредупреждение(,НСтр("ru='Внимание! Выгружать в электронном виде первичную форму РСВ-1 необходимо
				|в комплекте с документами квартальной отчетности персонифицированного
				|учета из специализированного рабочего места подготовки квартальной
				|отчетности в ПФР. Выгрузка прервана.'"));
				
				Возврат;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если (НаименованиеОтчета = "Статистика: Форма учета перемещения товаров"
		ИЛИ НаименованиеОтчета = "Статистическая форма учета перемещения товаров") И КонтекстЭДОКлиент <> Неопределено Тогда
		ДокументЗаявлениеОВвозеТоваров = Неопределено;
		ИмяДокументаЗаявлениеОВвозеТоваров = Неопределено;
		РезультатПолученияЗаявленияОВвозеТоваров = КонтекстЭДОКлиент.ПопытатьсяПолучитьЗаявлениеОВвозеТоваровИзСтатформы(
			Ссылка, ДокументЗаявлениеОВвозеТоваров, ИмяДокументаЗаявлениеОВвозеТоваров);
		Если РезультатПолученияЗаявленияОВвозеТоваров = Неопределено Тогда
			Возврат;
		ИначеЕсли РезультатПолученияЗаявленияОВвозеТоваров Тогда
			ДополнительныеПараметры = Новый Структура();
			ДополнительныеПараметры.Вставить("ДокументЗаявлениеОВвозеТоваров", ДокументЗаявлениеОВвозеТоваров);
			ДополнительныеПараметры.Вставить("ИмяДокументаЗаявлениеОВвозеТоваров", ИмяДокументаЗаявлениеОВвозеТоваров);
			ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформы", ЭтотОбъект, ДополнительныеПараметры);
			РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, Ссылка);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если НаименованиеОтчета = "Статистика: Форма П-1" Тогда
			
		Если НЕ ПроверитьВозможностьВыгрузкиП1ИзФормыОтчетностьНаСервере(Ссылка) Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Для отчета ""Статистика: Форма П-1 за %1"" не выбрано Приложение №3.
				|Необходимо сначала осуществить выгрузку из формы отчета! Выгрузка прервана.'"), ПредставлениеПериода);
			Сообщение.Сообщить();
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НаименованиеОтчета = "Статистика: Форма П-1 Приложение 3" Тогда 
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Выгрузка отчета ""Статистика: форма П-1 (приложение 3) за %1"" производится из формы П-1 за соответствующий период.'"), ПредставлениеПериода);
		Сообщение.Сообщить();
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
	Если СтандартнаяОбработка Тогда
	
		СписокОтчетов = Новый СписокЗначений;
		СписокОтчетов.Добавить(Ссылка);
		
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("СписокОтчетов", СписокОтчетов);
		ДополнительныеПараметры.Вставить("Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6", Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6);
			
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаПослеПодключенияРасширенияРаботыСФайлами", ЭтотОбъект, ДополнительныеПараметры);
	
		НачатьПодключениеРасширенияРаботыСФайлами(ОписаниеОповещения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаПослеПодключенияРасширенияРаботыСФайлами(Подключено, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("ПодключеноРасширениеРаботыСФайлами", Подключено);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаПродолжение", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстВопроса = "Выгрузить выбранный отчет?";
	ПоказатьВопрос(ОписаниеОповещения, СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	НСтр("ru='%1'"), ТекстВопроса), РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаПродолжение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	СписокОтчетов = ДополнительныеПараметры.СписокОтчетов;

	Если РезультатВопроса = КодВозвратаДиалога.Да Тогда
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыгрузкаРегламентированногоОтчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
		
		РегламентированнаяОтчетностьКлиент.ПередВыгрузкойРегламентированногоОтчета(ОписаниеОповещения, СписокОтчетов[0].Значение)
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаЗавершение(Отказ, ДополнительныеПараметры) Экспорт
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	СписокОтчетов = ДополнительныеПараметры.СписокОтчетов;
	
	ВВебКлиентеСохранятьФайлВыгрузкиИнтерактивно = Истина;
	
	Если ДополнительныеПараметры.Это_РСВ_1_2014_И_ЗагруженыВнешниеСведенияРаздела6
		И ДополнительныеПараметры.ПодключеноРасширениеРаботыСФайлами Тогда
		ВВебКлиентеСохранятьФайлВыгрузкиИнтерактивно = Ложь;
	КонецЕсли;
	
	Если НЕ РегламентированнаяОтчетностьКлиент.ВыгрузитьОтчеты(
		СписокОтчетов, , , ВВебКлиентеСохранятьФайлВыгрузкиИнтерактивно) Тогда
		ПоказатьПредупреждение(,НСтр("ru='Выгрузка прервана!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаРегламентированногоОтчетаЗавершениеИзСтатформы(Отказ, ДополнительныеПараметры) Экспорт
	
	ДокументЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ДокументЗаявлениеОВвозеТоваров;
	ИмяДокументаЗаявлениеОВвозеТоваров = ДополнительныеПараметры.ИмяДокументаЗаявлениеОВвозеТоваров;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	АдресФайлаВыгрузки = Неопределено;
	ИмяФайлаВыгрузки = "";
	
	РезультатВыгрузки = КонтекстЭДОКлиент.ПопытатьсяВыгрузитьСтатформуИзЗаявленияОВвозеТоваров(
		АдресФайлаВыгрузки,
		ИмяФайлаВыгрузки,
		,
		Истина,
		,
		ДокументЗаявлениеОВвозеТоваров,
		ИмяДокументаЗаявлениеОВвозеТоваров);
	
	Если РезультатВыгрузки = Истина Тогда
		ПолучитьФайл(АдресФайлаВыгрузки, ИмяФайлаВыгрузки);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПроверитьВозможностьВыгрузкиП1ИзФормыОтчетностьНаСервере(ОтчетСсылка)
	
	Перем ВыбранныйОтчетПриложение3;
	
	ДанныеОтчетаХЗ = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ОтчетСсылка, "ДанныеОтчета, ДатаНачала");
	Если Год(ДанныеОтчетаХЗ.ДатаНачала) >= 2017 Тогда 
		Возврат Истина;
	КонецЕсли;
	
	ДанныеВыгружаемогоОтчета = ДанныеОтчетаХЗ.ДанныеОтчета.Получить();
	
	Если НЕ ДанныеВыгружаемогоОтчета.Свойство("ВыбранныйОтчетПриложение3", ВыбранныйОтчетПриложение3) Тогда
		Возврат Ложь;
	ИначеЕсли ВыбранныйОтчетПриложение3 = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьФормуСтатусовОтправкиИзСписка(Элемент)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ПоказатьФормуСтатусовОтправкиИзСписка(Элемент);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ОписаниеРегОтчета(ДокументРегОтчет)
	
	ОписаниеРегОтч = Новый Структура;
	
	ОбъектРегОтч = ДокументРегОтчет.ПолучитьОбъект();
	
	ОписаниеРегОтч.Вставить("Организация",             ОбъектРегОтч.Организация);
	ОписаниеРегОтч.Вставить("ДатаНачалаПериодаОтчета", ОбъектРегОтч.ДатаНачала);
	ОписаниеРегОтч.Вставить("ДатаКонцаПериодаОтчета",  ОбъектРегОтч.ДатаОкончания);
	ОписаниеРегОтч.Вставить("КорректирующаяФорма",     ОбъектРегОтч.Вид > 0);
	ОписаниеРегОтч.Вставить("ВыбраннаяФорма",          СокрЛП(ОбъектРегОтч.ВыбраннаяФорма));
	
	Если СокрЛП(ОбъектРегОтч.НаименованиеОтчета) = "РСВ-1 ПФР" Тогда
		
		Если ОписаниеРегОтч.ВыбраннаяФорма = "ФормаОтчета2015Кв1" Тогда
			
			ОписаниеРегОтч.Вставить("ОтчетСтарогоОбразца_20141101", Ложь);
			
			ЗагруженыВнешниеСведенияРаздела6 = Неопределено;
			ОбъектРегОтч.ДанныеОтчета.Получить().Свойство("ВнешниеСведенияРаздела6", ЗагруженыВнешниеСведенияРаздела6);
			ЗагруженыВнешниеСведенияРаздела6 = ?(ЗагруженыВнешниеСведенияРаздела6 = Неопределено, Ложь, Истина);
			ОписаниеРегОтч.Вставить("ЗагруженыВнешниеСведенияРаздела6", ЗагруженыВнешниеСведенияРаздела6);
			
		ИначеЕсли ОписаниеРегОтч.ВыбраннаяФорма = "ФормаОтчета2014Кв1" Тогда
			
			ОтчетСтарогоОбразца_20141101 = Ложь;
			ОбъектРегОтч.ДанныеОтчета.Получить().Свойство("ОтчетСтарогоОбразца_20141101", ОтчетСтарогоОбразца_20141101);
			ОтчетСтарогоОбразца_20141101 = ?(ОтчетСтарогоОбразца_20141101 = Неопределено, Истина, ОтчетСтарогоОбразца_20141101);
			ОписаниеРегОтч.Вставить("ОтчетСтарогоОбразца_20141101", ОтчетСтарогоОбразца_20141101);
			
			ЗагруженыВнешниеСведенияРаздела6 = Неопределено;
			ОбъектРегОтч.ДанныеОтчета.Получить().Свойство("ВнешниеСведенияРаздела6", ЗагруженыВнешниеСведенияРаздела6);
			ЗагруженыВнешниеСведенияРаздела6 = ?(ЗагруженыВнешниеСведенияРаздела6 = Неопределено, Ложь, Истина);
			ОписаниеРегОтч.Вставить("ЗагруженыВнешниеСведенияРаздела6", ЗагруженыВнешниеСведенияРаздела6);
			
		КонецЕсли;
		
	ИначеЕсли СокрЛП(ОбъектРегОтч.НаименованиеОтчета) = "Декларация по УСН" Тогда
		
		Если ОписаниеРегОтч.ВыбраннаяФорма = "ФормаОтчета2014Кв1" Тогда
			
			ПоказателиОтчета = Неопределено;
			СписокСохранения = ОбъектРегОтч.ДанныеОтчета.Получить();
			СписокСохранения.Свойство("ПоказателиОтчета", ПоказателиОтчета);
			Если ПоказателиОтчета.ПолеТабличногоДокументаРаздел21.Свойство("П000210012003") Тогда
				ОписаниеРегОтч.Вставить("РекомендованнаяФорма",
					ПоказателиОтчета.ПолеТабличногоДокументаРаздел21.П000210012003 <> 6);
			Иначе
				ОписаниеРегОтч.Вставить("РекомендованнаяФорма", Ложь);
			КонецЕсли;
			
		КонецЕсли;
		
	ИначеЕсли СокрЛП(ОбъектРегОтч.НаименованиеОтчета) = "Доходы, выплаченные иностранным организациям" Тогда
		
		Если ОписаниеРегОтч.ВыбраннаяФорма = "ФормаОтчета2016Кв1" Тогда
			
			СписокСохранения = ОбъектРегОтч.ДанныеОтчета.Получить();
			ДопПараметры = Неопределено;
			Если СписокСохранения.Свойство("ОкружениеСохранения") Тогда
				ОписаниеРегОтч.Вставить("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении",
				РегламентированнаяОтчетность.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении(
				ОбъектРегОтч, ДопПараметры));
				Если НЕ ОписаниеРегОтч.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении
					И ТипЗнч(ДопПараметры) = Тип("Структура") Тогда
					Если ДопПараметры.Свойство("ТекстСообщения") Тогда
						ОписаниеРегОтч.Вставить("ТекстСообщения", ДопПараметры.ТекстСообщения);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ОписаниеРегОтч;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПротоколЕсть(Отправка)
	
	Если ЗначениеЗаполнено(Отправка) Тогда
		Возврат ЗначениеЗаполнено(Отправка.Протокол.Получить());
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура НапечататьОбъектНеВходящийВБРО(Ссылка, СведенияПоОбъекту)
	
	МакетПечати = СведенияПоОбъекту.ИмяОсновногоМакетаДляПечати;
	
	СтандартнаяПечатьОбъекта = Истина;
	РегламентированнаяОтчетностьКлиент.ПечатьПереопределяемый(Ссылка, МакетПечати, СтандартнаяПечатьОбъекта);
	
	Если СтандартнаяПечатьОбъекта Тогда
		
		Если МакетПечати <> Неопределено Тогда
			
			ИмяМенеджера = ИмяМенеджераПечати(Ссылка);
			
			МассивПечати = Новый Массив;
			МассивПечати.Добавить(Ссылка);
			
			Попытка
				УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ИмяМенеджера, МакетПечати, МассивПечати, Неопределено);
			Исключение
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'При печати возникла ошибка'"));
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИмяМенеджераПечати(Ссылка)
	
	Возврат Ссылка.Метаданные().ПолноеИмя();
	
КонецФункции

&НаКлиенте
Процедура ПечатьПротоколовПоСсылке(
	Закладка, 
	Ссылка, 
	ВидКонтролирующегоОргана, 
	НеОтправляетсяВКонтролирующийОрган, 
	СостояниеСдачиОтчетности)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	Если НеОтправляетсяВКонтролирующийОрган Тогда
		ПредупредитьОбОграничениях(Закладка, НСтр("ru = 'Печать протоколов не предусмотрена'"));
	Иначе
		
		Если ТипЗнч(Ссылка) <> Тип("СправочникСсылка.ДокументыРеализацииПолномочийНалоговыхОрганов")
			И ТипЗнч(Ссылка) <> Тип("СправочникСсылка.ПерепискаСКонтролирующимиОрганами") Тогда
			ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиент.ОтметитьКакПрочтенное(Ссылка);
		КонецЕсли;

		ПоследнийЦиклИлиОтправка = Неопределено;

		
		// ФСС
		Если ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВФСС(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка);
		// ФСРАР
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСРАР") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВФСРАР(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка);
		// РПН
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РПН") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВРПН(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка);
		// ФТС
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФТС") Тогда
			ПоследнийЦиклИлиОтправка = КонтекстЭДОКлиент.ПолучитьПоследнююОтправкуОтчетаВФТС(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка);
		// Банк России
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.БанкРоссии") Тогда
		    МодульДокументооборотСБанкомРоссииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДокументооборотСБанкомРоссииКлиент");
			ПоследнийЦиклИлиОтправка = МодульДокументооборотСБанкомРоссииКлиент.ПолучитьПоследнююОтправкуОтчета(Ссылка);
			ПротоколЕсть 			 = ПротоколЕсть(ПоследнийЦиклИлиОтправка);
		// Остальные контролирующие органы
		Иначе
			ПоследнийЦиклИлиОтправка = ДокументооборотСКОВызовСервера.ПолучитьПоследнийЦиклОбмена(Ссылка);
			ПротоколЕсть = Истина;
		КонецЕсли;
		
		// Предупреждаем об ошибке, если нет протокола  
		Если НЕ ЗначениеЗаполнено(ПоследнийЦиклИлиОтправка) ИЛИ НЕ ПротоколЕсть Тогда
			Если СостояниеСдачиОтчетности = ПредопределенноеЗначение("Перечисление.СостояниеСдачиОтчетности.ДокументооборотНеНачат") Тогда
				КонтекстЭДОКлиент.ПредупредитьОбОтсутствииПротокола();
			Иначе
				ТекстПредупреждения = НСтр("ru = 'У пользователя недостаточно прав для просмотра и печати протоколов.'");
				ПоказатьПредупреждение(,ТекстПредупреждения);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		// ФСС
		Если ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСС") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФСС(ПоследнийЦиклИлиОтправка);
		// ФСРАР
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФСРАР") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФСРАР(ПоследнийЦиклИлиОтправка);
		// РПН
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.РПН") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляРПН(ПоследнийЦиклИлиОтправка);
		// ФТС
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.ФТС") Тогда
			КонтекстЭДОКлиент.ПоказатьПротоколОбработкиПоСсылкеИсточникаДляФТС(ПоследнийЦиклИлиОтправка);
		// Банк России
		ИначеЕсли ВидКонтролирующегоОргана = ПредопределенноеЗначение("Перечисление.ТипыКонтролирующихОрганов.БанкРоссии") Тогда
		    МодульДокументооборотСБанкомРоссииКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ДокументооборотСБанкомРоссииКлиент");
			МодульДокументооборотСБанкомРоссииКлиент.ПоказатьПротокол(ПоследнийЦиклИлиОтправка);
		// Остальные контролирующие органы
		Иначе
			КонтекстЭДОКлиент.ПоказатьДокументыЦикловОбмена(ПоследнийЦиклИлиОтправка, Истина);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьРезультатОбновленияИнформацииМеханизмаОнлайнСервисовРО()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗаданияМОС) Тогда // задание выполнено
			ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьРезультатПроверкиОбновленияИнформацииМОС", ЭтотОбъект);
			ВыполнитьОбработкуОповещения(ОписаниеОповещения);
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжиданияМОС);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьРезультатОбновленияИнформацииМеханизмаОнлайнСервисовРО",
				ПараметрыОбработчикаОжиданияМОС.ТекущийИнтервал, Истина);
		КонецЕсли;
			
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
			
		ТекстСообщенияОбОшибке =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при проверке результата обновления информации механизма онлайн-сервисов регламентированной отчетности
					 |%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
					 
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			НСтр("ru = 'Регламентированная отчетность. Проверка результата обновления информации механизма онлайн-сервисов регламентированной отчетности'"), 
			"Ошибка", ТекстСообщенияОбОшибке);
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатПроверкиОбновленияИнформацииМОС(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(АдресХранилищаМОС);
	Если НЕ ЗначениеЗаполнено(РезультатПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(РезультатПроверки) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
		
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
						НСтр("ru = 'Обновлены внешние формы отчетов - %1'"),
						Формат(РезультатПроверки.Количество(), "ЧГ="));
		
	Для каждого СтрокаСообщения Из РезультатПроверки Цикл
		
		ТекстСообщения = ТекстСообщения + Символы.ПС + Символы.ПС
					   + СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1'"), СтрокаСообщения);
		
	КонецЦикла;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

&НаСервере
Функция ПроверитьКоличествоОбъектовНеЗаписанныхВРегистр()
	
	ИдентификаторЗадания = Неопределено;
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	
	Результат = РегламентированнаяОтчетность.КоличествоОбъектовНеЗаписанныхВРегистр(ПараметрыЗапуска);
	
	Если Результат = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	АдресХранилища       = Результат.АдресХранилища;
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если Не Результат.ЗаданиеВыполнено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеПодсчетаКоличестваНезаписанныхОбъектов()

	Попытка
	
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда
			ПоказатьРезультатПроверки();
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеПодсчетаКоличестваНезаписанныхОбъектов",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		КонецЕсли;
			
	Исключение
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ТекстСообщенияОбОшибке =
			СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
				НСтр("ru = 'Ошибка при подсчете количества объектов, не отображаемых в форме 1С-Отчетность 
					 |%1'"), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
					 
		ЖурналРегистрацииКлиент.ДобавитьСообщениеДляЖурналаРегистрации(
			НСтр("ru = 'Регламентированная отчетность. Проверка количества незаписанных объектов'"), 
			"Ошибка", 
			ТекстСообщенияОбОшибке,
			,
			Истина);
			
	КонецПопытки;

КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРезультатПроверки()
	
	РезультатПроверки = ПолучитьИзВременногоХранилища(АдресХранилища);
	Если НЕ ЗначениеЗаполнено(РезультатПроверки) Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатПроверки.КоличествоНезаписанныхОбъектов = 0 И РезультатПроверки.КоличествоНекорректныхЗаписей = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДописатьОбъектыОтсутствующиеВРегистре();
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура ДописатьОбъектыОтсутствующиеВРегистре(СОчисткойРегистров = Ложь)
	
	Результат  = ЗапуститьДобавлениеОбъектовВРегистр(СОчисткойРегистров);
	
	Если Результат <> Неопределено Тогда
		// Задание отработало, результат получен
		Оповестить("Завершение индексирования объектов журнала Отчетность");
	ИначеЕсли ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьРезультатДобавленияОбъектовОтсутствующихВРегистре", 1, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, ИдентификаторЗадания);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьРезультатДобавленияОбъектовОтсутствующихВРегистре()

	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда			
			ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Оповестить("Завершение индексирования объектов журнала Отчетность");
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания("Подключаемый_ПроверитьРезультатДобавленияОбъектовОтсутствующихВРегистре",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение;
	КонецПопытки;

КонецПроцедуры

&НаСервере
Функция ЗапуститьДобавлениеОбъектовВРегистр(СОчисткойРегистров = Ложь)
	
	ИдентификаторЗадания = Неопределено;
	
	ПараметрыЗапуска = Новый Структура;
	ПараметрыЗапуска.Вставить("УникальныйИдентификаторФормы", УникальныйИдентификатор);
	ПараметрыЗапуска.Вставить("СОчисткойРегистров", СОчисткойРегистров);
	
	Результат = РегламентированнаяОтчетность.ДобавлениеОбъектовОтсутствующихВРегистре(ПараметрыЗапуска);
	
	Если Результат = Неопределено Тогда
		Возврат Результат;
	КонецЕсли;
	
	АдресХранилища       = Результат.АдресХранилища;
	ИдентификаторЗадания = Результат.ИдентификаторЗадания;
	
	Если Не Результат.ЗаданиеВыполнено Тогда
		Возврат Неопределено;
	КонецЕсли;

	Возврат Результат;
	
КонецФункции

&НаСервереБезКонтекста
Функция ИмяФормыОбъектаПоУмолчанию(Ссылка)
	
	Возврат Ссылка.Метаданные().ПолноеИмя() + ".ФормаОбъекта";
	
КонецФункции

&НаКлиенте
Процедура ОбновитьТекущуюТаблицу()
	
	Для каждого Раздел Из Разделы(ЭтотОбъект, Истина) Цикл
		
		ТекущаяСтраницаРаздела = Элементы["Страница" + Раздел];
		
		Если Элементы.Разделы.ТекущаяСтраница = ТекущаяСтраницаРаздела Тогда
			Элементы[Раздел].Обновить();
		КонецЕсли;
		
	КонецЦикла; 
		
КонецПроцедуры

&НаКлиенте
Процедура ОнлайнПроверка(Закладка)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные 	= Закладка.ТекущиеДанные;
	ЭтоОтчеты = Закладка = Элементы.Отчеты;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		Если ВыполнениеКомандыОтменено(?(ТекущиеДанные.Свойство("НаименованиеОтчета"), ТекущиеДанные.НаименованиеОтчета, ТекущиеДанные.Наименование), ТекущиеДанные.Организация, "ОтправитьОтчет") Тогда
			Возврат;
		КонецЕсли;
		
		ПроверяемыйОбъект = ?(ЭтоОтчеты, ТекущиеДанные.РегламентированныйОтчет, ТекущиеДанные.Ссылка);
		ОрганизацияПроверяемогоОбъекта = ТекущиеДанные.Организация;
		Если ЭтоОтчеты Тогда
			Представление = ТекущиеДанные.НаименованиеОтчета;
		Иначе
			Представление = ТекущиеДанные.Наименование;
		КонецЕсли;
		
		////////////////////////////////////////////////////////
		// Заглушка до момента, когда будет ясно куда отправлять.
		Если СтрНайти(Представление, "винограда") > 0 Тогда
			
			ТекстПредупреждения = НСтр("ru = 'Проверка в интернете, отправка в электронном виде редакции формы, утвержденной ПП РФ №411 от 13.05.2016, будет реализована в будущих версиях программы.'");
			ПоказатьПредупреждение(, ТекстПредупреждения);
			
			Возврат;
			
		КонецЕсли;
		//
		////////////////////////////////////////////////////////
		
		Если Представление = "РСВ-1 ПФР" И ТипЗнч(ПроверяемыйОбъект) <> Тип("СправочникСсылка.ЭлектронныеПредставленияРегламентированныхОтчетов") Тогда
			ОписаниеРСВ1 = ОписаниеРегОтчета(ПроверяемыйОбъект);
			Если ОписаниеРСВ1.ВыбраннаяФорма = "ФормаОтчета2015Кв1" ИЛИ ОписаниеРСВ1.ВыбраннаяФорма = "ФормаОтчета2014Кв1" Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='Для проверки в интернете необходимо открыть форму регламентированного отчета ""%1""'"),
					Представление);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Возврат;
			КонецЕсли;
		Конецесли;
		
		// Обход ошибки платформы.
		Если РегламентированнаяОтчетностьКлиент.ВебБраузер() = "MSIE" Тогда
			Если Представление = "Декларация по НДС"
				ИЛИ Представление = "Бухгалтерская отчетность (с 2011 года)"
				ИЛИ Представление = "Бухгалтерская отчетность малых предприятий" Тогда
				ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
					"ru='Для выгрузки (проверки выгрузки, проверки в интернете, отправки) в браузере ""Internet Explorer"" необходимо открыть форму регламентированного отчета ""%1""'"),
					Представление);
				ПоказатьПредупреждение(, ТекстПредупреждения);
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		Если Представление = "Доходы, выплаченные иностранным организациям" Тогда
			ОписаниеДохИно = ОписаниеРегОтчета(ПроверяемыйОбъект);
			Если ОписаниеДохИно.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
				И ОписаниеДохИно.Свойство("ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении") Тогда
				Если НЕ ОписаниеДохИно.ВозможноВосстановитьДанныеРеглОтчетаСозданногоВОбычномПриложении Тогда
					Если ОписаниеДохИно.Свойство("ТекстСообщения") Тогда
						Сообщение = Новый СообщениеПользователю;
						Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр(
						"ru='%1 Проверка в интернете прервана.'"), ОписаниеДохИно.ТекстСообщения);
						Сообщение.Сообщить();
					КонецЕсли;
					Возврат;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		КонтекстЭДОКлиент.ПроверитьВИнтернетеНачало(
			ПроверяемыйОбъект, Представление, ЭтоОтчеты, ОрганизацияПроверяемогоОбъекта, ТекущиеДанные.ВидКонтролирующегоОргана, КонтекстЭДОКлиент)
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ПредупредитьОбОграничениях(Закладка, Знач ТекстПредупреждения, Знач ВидОбъекта = Неопределено)
	
	// Переводим первый символ в нижний регистр
	ТекстПредупреждения = НРег(Лев(ТекстПредупреждения, 1)) + Сред(ТекстПредупреждения, 2);
	
	ТекстПредупреждения = НСтр("ru = 'Для %1 ""%2"" '") + ТекстПредупреждения;
	
	ЭтоОтчеты = Закладка = Элементы.Отчеты;
	ТекущиеДанные = Закладка.ТекущиеДанные;
	
	Если ВидОбъекта = Неопределено Тогда
		Если ЭтоОтчеты Тогда
			ВидОбъекта = НСтр("ru = 'отчета'");
		Иначе
			ВидОбъекта = НСтр("ru = 'уведомления'"); 
		КонецЕсли;
	КонецЕсли;
	
	Наименование = "";
	Если ЭтоОтчеты Тогда
		Наименование = ТекущиеДанные.НаименованиеОтчета;
	Иначе
		Наименование = ТекущиеДанные.Наименование;
	КонецЕсли;
	
	ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%1", ВидОбъекта);
	ТекстПредупреждения = СтрЗаменить(ТекстПредупреждения, "%2", Наименование);
	
	ПоказатьПредупреждение(,ТекстПредупреждения);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьНастройки()
		
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_ТекущаяСтраницаРазделов", Элементы.Разделы.ТекущаяСтраница.Имя);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_ТолькоУказанныйПериод", ТолькоУказанныйПериод);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_ПериодОтчета",  ПериодОтчета);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_НачалоПериода", НачалоПериода);
	ХранилищеНастроекДанныхФорм.Сохранить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_КонецПериода",  КонецПериода);
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЭлементовПриОтключенномЭДО()
	
	Элементы.ГруппаПанельИнформацииСКнопкой.Видимость 	= ЭлектронныйДокументооборотДоступен;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЛичныхКабинетов()
	
	УправлениеВидимостьюЛичныхКабинетовНаправлений();
	УправлениеВидимостьюЛичныхКабинетовСпецоператоров();
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЛичныхКабинетовНаправлений()
	
	СтрокаСписокПоддерживаемыхКО = "ФНСЮЛ,ФНСИП,ПФР,ФСС,ФСРАР,РПН,ФТС";
	НаправленияЭталон = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСписокПоддерживаемыхКО, ",");
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ПереопределитьСписокПоддерживаемыхНаправлений(СтрокаСписокПоддерживаемыхКО);
	НаправленияПереопределенный = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(СтрокаСписокПоддерживаемыхКО, ",");
	
	Для каждого СтрокаНаправление Из НаправленияЭталон Цикл
		Если НаправленияПереопределенный.Найти(СтрокаНаправление) = Неопределено Тогда
			ИмяЭлемента = "Группа" + СтрокаНаправление + "Сервис";
			
			ЭлементГруппа = Элементы.Найти(ИмяЭлемента);
			Если ЭлементГруппа <> Неопределено Тогда 
				ЭлементГруппа.Видимость = Ложь;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеВидимостьюЛичныхКабинетовСпецоператоров()
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	Если КонтекстЭДОСервер = Неопределено Тогда
		Возврат;
	КонецЕсли;

	//получение списка используемых операторов
	МассивИспользуемыхСпецоператоров = КонтекстЭДОСервер.ПолучитьМассивИспользуемыхСпецоператоров();
	
	//заполнение таблицы свойств операторов
	ТаблицаСвойствОператоров = Новый ТаблицаЗначений;
	ТаблицаСвойствОператоров.Колонки.Добавить("ЛичныйКабинетНаименованиеСервиса");
	ТаблицаСвойствОператоров.Колонки.Добавить("ЛичныйКабинетАдресСервиса");
	ТаблицаСвойствОператоров.Колонки.Добавить("ЛичныйКабинетОписаниеСервиса");
	
	Для каждого ИспользуемыйСпецоператор Из МассивИспользуемыхСпецоператоров Цикл
		ПараметрыСтруктура = Новый Структура("ЛичныйКабинетНаименованиеСервиса, ЛичныйКабинетАдресСервиса, ЛичныйКабинетОписаниеСервиса");
		КонтекстЭДОСервер.ЗаполнитьПараметрыСпецоператора(ИспользуемыйСпецоператор, ПараметрыСтруктура);
		
		Если ЗначениеЗаполнено(ПараметрыСтруктура.ЛичныйКабинетАдресСервиса) Тогда
			//в таблицу попадают лишь спецоператоры, у которых есть личный кабинет
			ЗаполнитьЗначенияСвойств(ТаблицаСвойствОператоров.Добавить(), ПараметрыСтруктура);
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаСвойствОператоров.Сортировать("ЛичныйКабинетАдресСервиса");	
	
	//заполнение таблицы личных кабинетов на основе свойств операторов
	//в этой таблице будут сгруппированы в одну строку спецоператоры с одинаковым адресом личных кабинетов
	//наименование и описание сервиса - по первому из операторов
	ТаблицаЛичныхКабинетов = Новый ТаблицаЗначений;
	ТаблицаЛичныхКабинетов.Колонки.Добавить("ЛичныйКабинетНаименованиеСервиса");
	ТаблицаЛичныхКабинетов.Колонки.Добавить("ЛичныйКабинетАдресСервиса");
	ТаблицаЛичныхКабинетов.Колонки.Добавить("ЛичныйКабинетОписаниеСервиса");
	
	ТекущееНазваниеСервиса 		= "";
	ТекущийАдресСервиса 		= "";
	ТекущееОписаниеСервиса 		= "";
	Для каждого СвойстваОператора Из ТаблицаСвойствОператоров Цикл
		
		Если ТекущийАдресСервиса <> СвойстваОператора.ЛичныйКабинетАдресСервиса Тогда
			//новый личный кабинет
			НовыйЛичныйКабинет = ТаблицаЛичныхКабинетов.Добавить();
			ТекущееНазваниеСервиса 		= СвойстваОператора.ЛичныйКабинетНаименованиеСервиса;
			ТекущийАдресСервиса 		= СвойстваОператора.ЛичныйКабинетАдресСервиса;
			ТекущееОписаниеСервиса 		= СвойстваОператора.ЛичныйКабинетОписаниеСервиса;
		КонецЕсли;
		
		НовыйЛичныйКабинет.ЛичныйКабинетНаименованиеСервиса = ТекущееНазваниеСервиса;
		НовыйЛичныйКабинет.ЛичныйКабинетАдресСервиса 	= ТекущийАдресСервиса;
		НовыйЛичныйКабинет.ЛичныйКабинетОписаниеСервиса = ТекущееОписаниеСервиса;
		
	КонецЦикла;

	// заполнение свойств элементов формы на основе таблицы личных кабинетов 
	Для каждого СтрокаЛичныйКабинет Из ТаблицаЛичныхКабинетов Цикл
		
		НомерЛК = ТаблицаЛичныхКабинетов.Индекс(СтрокаЛичныйКабинет) + 1;
		ЦветГиперссылкаБРО = Новый Цвет(28, 85, 174);
		НазваниеСервиса = Новый ФорматированнаяСтрока(СтрокаЛичныйКабинет.ЛичныйКабинетНаименованиеСервиса, , ЦветГиперссылкаБРО, , СтрокаЛичныйКабинет.ЛичныйКабинетАдресСервиса);
		
		ЭлементНазваниеСервиса = Элементы.Найти("ЛичныйКабинет1Сотчетность" + НомерЛК);
		Если ЭлементНазваниеСервиса <> Неопределено Тогда 
			ЭлементНазваниеСервиса.Заголовок = НазваниеСервиса;
			ЭлементНазваниеСервиса.Подсказка = СтрокаЛичныйКабинет.ЛичныйКабинетАдресСервиса;
		КонецЕсли;

		ЭлементОписаниеСервиса = Элементы.Найти("ПояснениеЛичныйКабинет1Сотчетность" + НомерЛК);
		Если ЭлементОписаниеСервиса <> Неопределено Тогда 
			ЭлементОписаниеСервиса.Заголовок = СтрокаЛичныйКабинет.ЛичныйКабинетОписаниеСервиса;
		КонецЕсли;

		ЭлементГруппаЛичныйКабинет = Элементы.Найти("ГруппаЛичныйКабинет1Сотчетность" + НомерЛК);
		Если ЭлементОписаниеСервиса <> Неопределено Тогда 
			ЭлементГруппаЛичныйКабинет.Видимость = Истина;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКритическиеОшибкиПоСсылке(Ссылка)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОКлиент.ПоказатьКритическиеОшибкиПоСсылке(Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОткрытьНаНужнойСтранице(Параметры)
	
	// Если в качестве параметра открытия формы 1С-Отчетность была передана закладка, то открываем на этой закладке,
	// инчае открываем на той, на которой форму закрыли в последний раз,
	// либо на закладке Отчеты, если форма открывается впервые
	Если Параметры.Свойство("ВидУведомления") И ЗначениеЗаполнено(Параметры.ВидУведомления) Тогда
		
		Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаУведомления;
	
	ИначеЕсли Параметры.Свойство("Раздел") Тогда
		
		Элементы.Разделы.ТекущаяСтраница = Элементы["Страница" + ИмяСтраницы(Параметры.Раздел)];
			
	Иначе
		
		// Восстанавливаем запомненную страницу
		ТекСтраница = ХранилищеНастроекДанныхФорм.Загрузить(ЭтаФорма.ИмяФормы, "Форма1СОтчетность_ТекущаяСтраницаРазделов");
		Если ТекСтраница <> Неопределено И Элементы.Разделы.ПодчиненныеЭлементы.Найти(ТекСтраница) <> Неопределено Тогда
			Элементы.Разделы.ТекущаяСтраница = Элементы[ТекСтраница];
		Иначе
			Элементы.Разделы.ТекущаяСтраница = Элементы.СтраницаОтчеты;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура НазначитьВидОтчетаИзПараметра(Параметры)
	
	Если Параметры.Свойство("ВидОтчета") Тогда
		
		ПараметрВидОтчета = СокрЛП(Параметры.ВидОтчета);
		
		Если ЗначениеЗаполнено(ПараметрВидОтчета) Тогда
			
			МассивВидовОтчетов = ПолучитьСписокОтчетовИзРегистраСведений();
			Элементы.ОтборВидОтчета.СписокВыбора.ЗагрузитьЗначения(МассивВидовОтчетов);
			ВидОтчета = ПараметрВидОтчета;
			
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НазначитьВидУведомленияИзПараметра(Параметры)
	
	Если Параметры.Свойство("ВидУведомления") И ЗначениеЗаполнено(Параметры.ВидУведомления) Тогда
		
		ВидУведомления = Параметры.ВидУведомления;
		ПредставлениеВидаУведомления = СокрЛП(Параметры.ВидУведомления);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СделатьВсеГиперссылкиНеактивными()
	
	Для каждого Раздел Из Разделы(ЭтотОбъект) Цикл
		
		Гиперссылка = Элементы["Гиперссылка" + Раздел];
		
		Гиперссылка.ЦветФона		= Серый;
		Гиперссылка.Гиперссылка		= Истина;
		Гиперссылка.ЦветТекста		= Черный;
	
	КонецЦикла; 
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеУведомление()
	
	ПараметрыФормы 	= Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("Создание_УведомлениеОСпецрежимахНалогообложения", Истина);
	ПараметрыФормы.Вставить("УчитыватьУведомленияНеВходящиеВБРО", Истина);
	ПараметрыФормы.Вставить("UIDФорма1СОтчетность", УникальныйИдентификатор);
	
	Если ЗначениеЗаполнено(ПредставлениеВидаУведомления) И ЗначениеЗаполнено(ВидУведомления) Тогда
		ПараметрыФормы.Вставить("ВидУведомления", ВидУведомления);
	КонецЕсли;
	
	ОткрытьФорму("Документ.УведомлениеОСпецрежимахНалогообложения.Форма.ФормаВыбораВидаСообщения", ПараметрыФормы,,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовыйЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП()
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Организация", Организация));
	
	ОткрытьФорму("Документ.ЗапросНаВыпискуИзЕГРЮЛ_ЕГРИП.Форма.ФормаДокумента", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеПисьмо()
	
	СписокВидовПисьма = Новый СписокЗначений;
	СписокВидовПисьма.Добавить(НСтр("ru = 'Письмо'"));
	СписокВидовПисьма.Добавить(НСтр("ru = 'Ответ на требование'"));
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораВидаПисьма", ЭтотОбъект);
	СписокВидовПисьма.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Выберите вид письма'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораВидаПисьма(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		// Значит пользователь нажал Отмена
	ИначеЕсли Результат.Значение = НСтр("ru = 'Письмо'") Тогда
		 СоздатьНовоеИсходящееПисьмо();
	ИначеЕсли Результат.Значение = НСтр("ru = 'Ответ на требование'") Тогда 
		 СоздатьОтветНаТребование();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовоеИсходящееПисьмо()
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", Новый Структура("Отправитель", Организация));
	
	ОткрытьФорму("Справочник.ПерепискаСКонтролирующимиОрганами.ФормаОбъекта", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьНовуюСверку()
	
	СписокВыбораВидаСверки = Новый СписокЗначений;
	
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеАктовСверкиРасчетов"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеВыпискиОперацийИзКарточкиРасчетыСБюджетом"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеПеречняБухгалтерскойИНалоговойОтчетности"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОСостоянииРасчетовСБюджетом"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОН.ПредставлениеСправкиОбИсполненииОбязанностейПоУплате"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СверкаФИОиСНИЛС"));
	СписокВыбораВидаСверки.Добавить(ПредопределенноеЗначение("Перечисление.ВидыУслугПриИОС.СправкаОСостоянииРасчетов"));

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершениеВыбораВидаСверки", ЭтотОбъект);
	
	СписокВыбораВидаСверки.ПоказатьВыборЭлемента(ОписаниеОповещения, НСтр("ru = 'Выберите вид сверки'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершениеВыбораВидаСверки(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		СоздатьСверку(Результат.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭУВРежимеСервиса()
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда 
		Элементы.НадписьИнформацияОбОбновлениях.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьДоступностьЭДО()
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	Если КонтекстЭДОСервер <> Неопределено Тогда
		ЭлектронныйДокументооборотДоступен = КонтекстЭДОСервер.ХотяБыОднаОрганизацияИспользуетДокументооборот();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПометитьПрочтенными()
	
	ВсеВыделенныеОбъекты 	= Новый Массив;
	СтавшиеПрочтенными 		= Новый Массив;
	СтавшиеНепрочтенными 	= Новый Массив;
	
	ТекущаяТаблица = ТекущаяТаблица(ЭтотОбъект);
	Для каждого Строка Из ТекущаяТаблица.ВыделенныеСтроки Цикл
		ВсеВыделенныеОбъекты.Добавить(Строка.Ссылка);
	КонецЦикла;
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	
	Если ТекущаяСтраница = Перечисления.СтраницыЖурналаОтчетность.Входящие Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка,
			|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
			|ГДЕ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка В(&Ссылки)";
		
		Запрос.УстановитьПараметр("Ссылки", ВсеВыделенныеОбъекты);
		
		ВходящиеПисьма = Запрос.Выполнить().Выгрузить();
		ЕстьНепрочтенные 	= ВходящиеПисьма.Найти(Истина, 	"Непрочтенное") <> Неопределено;
		ЕстьПрочтенные 		= ВходящиеПисьма.Найти(Ложь, 	"Непрочтенное") <> Неопределено;
		ВсеНепрочтенные  	= НЕ ЕстьПрочтенные;
		
		Для каждого ВходящийОбъект Из ВходящиеПисьма Цикл
			
			Если ЕстьНепрочтенные И ЕстьПрочтенные Тогда
				
				Если ВходящийОбъект.Непрочтенное Тогда
					
					СтавшиеПрочтенными.Добавить(ВходящийОбъект.Ссылка);
			
					ЭлектронныйДокументооборотСКонтролирующимиОрганами.УстановитьНепрочтенность(
						ВходящийОбъект.Ссылка, 
						Ложь);
						
				КонецЕсли;
					
			Иначе
				
				СтавшиеПрочтенными.Добавить(ВходящийОбъект.Ссылка);
				
				Непрочтенное = НЕ ВходящийОбъект.Непрочтенное;
				
				Если Непрочтенное Тогда
					СтавшиеНепрочтенными.Добавить(ВходящийОбъект.Ссылка);
				Иначе
					СтавшиеПрочтенными.Добавить(ВходящийОбъект.Ссылка);
				КонецЕсли;
				
				ЭлектронныйДокументооборотСКонтролирующимиОрганами.УстановитьНепрочтенность(
					ВходящийОбъект.Ссылка, 
					Непрочтенное);
					
			КонецЕсли;
			
		КонецЦикла;
		
	Иначе
		
		Запрос = Новый Запрос;
		Если ТекущаяСтраница = Перечисления.СтраницыЖурналаОтчетность.Отчеты Тогда
		
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
			|	ЖурналОтчетовСтатусы.Ссылка,
			|	ЖурналОтчетовСтатусы.Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналОтчетовСтатусы КАК ЖурналОтчетовСтатусы
			|ГДЕ
			|	ЖурналОтчетовСтатусы.Ссылка В(&Ссылки)";
			
		Иначе
			
			Запрос.Текст = 
			"ВЫБРАТЬ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка,
			|	ЖурналОтправокВКонтролирующиеОрганы.Непрочтенное
			|ИЗ
			|	РегистрСведений.ЖурналОтправокВКонтролирующиеОрганы КАК ЖурналОтправокВКонтролирующиеОрганы
			|ГДЕ
			|	ЖурналОтправокВКонтролирующиеОрганы.Ссылка В(&Ссылки)";

		КонецЕсли;
	
		Запрос.УстановитьПараметр("Ссылки", ВсеВыделенныеОбъекты);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Непрочтенное Тогда
				
				СтавшиеПрочтенными.Добавить(Выборка.Ссылка);
				
				ЭлектронныйДокументооборотСКонтролирующимиОрганами.УстановитьНепрочтенность(
					Выборка.Ссылка, 
					Ложь);
					
			КонецЕсли;
			
		КонецЦикла;
			
	КонецЕсли;
	
	Результат = Новый Структура;
	Результат.Вставить("ВсеВыделенныеОбъекты", 		ВсеВыделенныеОбъекты);
	Результат.Вставить("СтавшиеПрочтенными", 		СтавшиеПрочтенными);
	Результат.Вставить("СтавшиеНепрочтенными", 		СтавшиеНепрочтенными);

	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОтчетыВыборЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	РегламентированнаяОтчетностьКлиент.ОповеститьСписокОтчетовОбИзмененииИндексаКартинки(
	ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьПрисоединенныеФайлы()
	
	ТекущиеДанные = Неопределено;
	
	ТекущаяСтраница = ТекущаяСтраница(ЭтотОбъект);
	
	Если ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Отчеты") Тогда
		
		ТекущиеДанные = Элементы.Отчеты.ТекущиеДанные;
		СсылкаНаОбъект = ТекущиеДанные.РегламентированныйОтчет;
		Наименование = ТекущиеДанные.НаименованиеОтчета;
		
	ИначеЕсли ТекущаяСтраница = ПредопределенноеЗначение("Перечисление.СтраницыЖурналаОтчетность.Уведомления") Тогда
		
		ТекущиеДанные = Элементы.Уведомления.ТекущиеДанные;
		СсылкаНаОбъект = ТекущиеДанные.Ссылка;
		Наименование = ТекущиеДанные.Наименование;
		
	КонецЕсли;
	
	Если ТекущиеДанные <> Неопределено Тогда
		
		ДопПараметры = Новый Структура;
		ДопПараметры.Вставить("ВладелецФайла",  СсылкаНаОбъект);
		ДопПараметры.Вставить("Организация",    ТекущиеДанные.Организация);
		ДопПараметры.Вставить("ТолькоПросмотр", Ложь);
		ДопПараметры.Вставить("ЗаголовокФормы", "Присоединенные файлы: " + Наименование);
		
		РегламентированнаяОтчетностьКлиент.ОткрытьФормуПрисоединенныеФайлы(, ДопПараметры);
		
	КонецЕсли;
	
КонецПроцедуры


#Область ДлительныеОперацииАЛКО

////////////////////////////////////////////////////////////////////////////////
//
// Механизм длительных операций отчетов подсистемы ОтчетностьПоАлкогольнойПродукции
//		  
////////////////////////////////////////////////////////////////////////////////

// Выполняет все длительные операции отчета.
//
// Параметры:
//
//		ДокументСсылкаАлкоОтчет		- Документ.РегламентированныйОтчет.Ссылка - Документ отчета.
//	 	ВидДлОперации 				- Перечисление.ВидыДлительныхОперацийРегламентированныхОтчетов.
//									Задает вид выполняемой длительной операции отчета.
// 		ВидПечати 					- Строка - Необязательный. Задает вид печати отчета при длительных
//									операцях печати.
//
&НаКлиенте
Процедура ДлительныеОперацииАлко(ДокументСсылкаАлкоОтчет, ВидДлОперации, ВидПечати = Неопределено)

	СтруктураВспомДанныхДлитОперацииАЛКО = Новый Структура;
	СтруктураВспомДанныхДлитОперацииАЛКО.Вставить("ВидДлительнойОперации", ВидДлОперации);
	СтруктураВспомДанныхДлитОперацииАЛКО.Вставить("ВидПечати", ВидПечати);
		
	ФормаОтчетаДлитОперации = РегламентированнаяОтчетностьКлиент.ИнициализацияОтчета(ДокументСсылкаАлкоОтчет);
	
	СтруктураВспомДанныхДлитОперацииАЛКО.Вставить("ФормаОтчетаДлитОперации", ФормаОтчетаДлитОперации);
		
	ФормаОтчетаДлитОперации.ВыполнитьДлительнуюОперацию_НаКлиенте(ВидДлОперации, ВидПечати, ЭтаФорма);	

КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте()
	
	ФормаОтчетаДлитОперации = СтруктураВспомДанныхДлитОперацииАЛКО.ФормаОтчетаДлитОперации;
	ОперацияЗавершена = РегламентированнаяОтчетностьАЛКОКлиент.ВыполнитьДействияПриПроверкеВыполненияДлительнойОперации(ФормаОтчетаДлитОперации);
	
	Если ОперацияЗавершена Тогда
				
		ПараметрыОбработчикаОжидания = Неопределено;
		СтруктураВспомДанныхДлитОперацииАЛКО = Неопределено;
		
	    Возврат;
		
	КонецЕсли;
	
	ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.ТекущийИнтервал * ПараметрыОбработчикаОжидания.КоэффициентУвеличенияИнтервала;
	
	Если ПараметрыОбработчикаОжидания.ТекущийИнтервал > ПараметрыОбработчикаОжидания.МаксимальныйИнтервал Тогда
		ПараметрыОбработчикаОжидания.ТекущийИнтервал = ПараметрыОбработчикаОжидания.МаксимальныйИнтервал;
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеДлительнойОперацииНаКлиенте", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
	
КонецПроцедуры

#КонецОбласти


#КонецОбласти

#Область Новости

// Процедура показывает новости, требующие прочтения (важные и очень важные)
//
// Параметры:
//  Нет
//
&НаКлиенте
Процедура Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии()

	// ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии
	ИдентификаторыСобытийПриОткрытии = "ПриОткрытии";
	// Конец ИнтернетПоддержкаПользователей.Новости.Подключаемый_ПоказатьНовостиТребующиеПрочтенияПриОткрытии

	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ПоказатьНовостиТребующиеПрочтенияПриОткрытии(ЭтаФорма, ИдентификаторыСобытийПриОткрытии);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыНовости(Команда)
		
	Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("ИнтернетПоддержкаПользователей.Новости") Тогда
		
		МодульОбработкаНовостейКлиент = ОбщегоНазначенияКлиент.ОбщийМодуль("ОбработкаНовостейКлиент");
		
		МодульОбработкаНовостейКлиент.КонтекстныеНовости_ОбработкаКомандыНовости(ЭтаФорма, Команда);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьОтветНаТребование(Команда = Неопределено)
	
	Если КонтекстЭДОКлиент = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибкиИнициализацииКонтекстаЭДО);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ЗапретитьСозданиеПисьмаВОтветНаТребованиеПоДекларации", Истина);
			
	КонтекстЭДОКлиент.СоздатьОтветНаТребованиеСВыборомТребования(,Организация,ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПереоткрытьОкноАвторизацииИППЕслиОткрыто()
	
	ОткрытыеОкна = ПолучитьОкна();
	
	Для каждого ОткрытоеОкно Из ОткрытыеОкна Цикл
		
		Если ОткрытоеОкно = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Содержимое = ОткрытоеОкно.Содержимое;
		
		Если Содержимое.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущаяФорма = Содержимое[0];
		
		Если ТекущаяФорма.ИмяФормы = "ОбщаяФорма.АвторизацияНаСайтеПоддержкиПользователей"
		 ИЛИ ТекущаяФорма.ИмяФормы = "ОбщаяФорма.ПодключениеИнтернетПоддержки" Тогда
			
			// После закрытия формы и проверки параметров авторизации она будет открыта повторно
			ТекущаяФорма.Закрыть(Новый Структура("Логин,Пароль", "", ""));
			
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти