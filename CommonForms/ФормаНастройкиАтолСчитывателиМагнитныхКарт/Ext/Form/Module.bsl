
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Параметры.Свойство("Идентификатор", Идентификатор);
	Параметры.Свойство("ДрайверОборудования", ДрайверОборудования);
	
	Заголовок = НСтр("ru='Оборудование:'") + Символы.НПП  + Строка(Идентификатор);
	
	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;

	СписокПорт = Элементы.Порт.СписокВыбора;
	СписокПорт.Добавить(100, НСтр("ru='<Клавиатура>'"));
	Для Индекс = 1 По 64 Цикл
		СписокПорт.Добавить(Индекс, "COM" + СокрЛП(Индекс));
	КонецЦикла;
	
	СписокСкорость = Элементы.Скорость.СписокВыбора;
	СписокСкорость.Добавить(1,  "300");
	СписокСкорость.Добавить(2,  "600");
	СписокСкорость.Добавить(3,  "1200");
	СписокСкорость.Добавить(4,  "2400");
	СписокСкорость.Добавить(5,  "4800");
	СписокСкорость.Добавить(7,  "9600");
	СписокСкорость.Добавить(9,  "14400");
	СписокСкорость.Добавить(10, "19200");
	СписокСкорость.Добавить(12, "38400");
	
	СписокБитДанных = Элементы.БитДанных.СписокВыбора;
	СписокБитДанных.Добавить(3, НСтр("ru='7 бит'"));
	СписокБитДанных.Добавить(4, НСтр("ru='8 бит'"));
	
	СписокСтопБит = Элементы.СтопБит.СписокВыбора;
	СписокСтопБит.Добавить(0, НСтр("ru='1 стоп-бит'"));
	СписокСтопБит.Добавить(2, НСтр("ru='2 стоп-бита'"));
	
	СписокЧетность = Элементы.Четность.СписокВыбора;
	СписокЧетность.Добавить(0, НСтр("ru='Нет'"));
	СписокЧетность.Добавить(1, НСтр("ru='Нечетность'"));
	СписокЧетность.Добавить(2, НСтр("ru='Четность'"));
	СписокЧетность.Добавить(3, НСтр("ru='Установлен'"));
	СписокЧетность.Добавить(4, НСтр("ru='Сброшен'"));
	
	времПорт             = Неопределено;
	времСкорость         = Неопределено;
	времБитДанных        = Неопределено;
	времСтопБит          = Неопределено;
	времЧетность         = Неопределено;
	времЧувствительность = Неопределено;
	времМодель           = Неопределено;
	
	Параметры.ПараметрыОборудования.Свойство("Порт"            , времПорт);
	Параметры.ПараметрыОборудования.Свойство("Скорость"        , времСкорость);
	Параметры.ПараметрыОборудования.Свойство("БитДанных"       , времБитДанных);
	Параметры.ПараметрыОборудования.Свойство("СтопБит"         , времСтопБит);
	Параметры.ПараметрыОборудования.Свойство("Четность"        , времЧетность);
	Параметры.ПараметрыОборудования.Свойство("Чувствительность", времЧувствительность);
	Параметры.ПараметрыОборудования.Свойство("Модель"          , времМодель);

	Порт             = ?(времПорт             = Неопределено,         1, времПорт);
	Скорость         = ?(времСкорость         = Неопределено,         7, времСкорость);
	БитДанных        = ?(времБитДанных        = Неопределено,         3, времБитДанных);
	СтопБит          = ?(времСтопБит          = Неопределено,         0, времСтопБит);
	Четность         = ?(времЧетность         = Неопределено,         0, времЧетность);
	Чувствительность = ?(времЧувствительность = Неопределено,        30, времЧувствительность);
	Модель           = ?(времМодель           = Неопределено, Элементы.Модель.СписокВыбора[0], времМодель);

	времПараметрыДорожек = Неопределено;
	Если Не Параметры.Свойство("ПараметрыДорожек", времПараметрыДорожек) Тогда
		времПараметрыДорожек = Новый Массив();
		Для Индекс = 1 По 3 Цикл
			НоваяСтрока = Новый Структура();
			НоваяСтрока.Вставить("НомерДорожки", Индекс);
			НоваяСтрока.Вставить("Префикс"     , "");
			НоваяСтрока.Вставить("Суффикс"     , ?(Индекс = 2, "#13", ""));
			НоваяСтрока.Вставить("Использовать", ?(Индекс = 2, Истина, Ложь));
			времПараметрыДорожек.Добавить(НоваяСтрока);
		КонецЦикла;
	КонецЕсли;

	Для Каждого СтрокаДорожки Из времПараметрыДорожек Цикл
		НоваяСтрока = ПараметрыДорожек.Добавить();
		НоваяСтрока.НомерДорожки = СтрокаДорожки.НомерДорожки;
		НоваяСтрока.Префикс      = СтрокаДорожки.Префикс;
		НоваяСтрока.Суффикс      = СтрокаДорожки.Суффикс;
		НоваяСтрока.Использовать = СтрокаДорожки.Использовать;
	КонецЦикла;
	
	Элементы.УстановитьДрайвер.Видимость = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьИнформациюОДрайвере();

	ПортПриИзменении();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПортПриИзменении()

	Элементы.Скорость.Доступность  = (Порт <> 100);
	Элементы.БитДанных.Доступность = (Порт <> 100);
	Элементы.СтопБит.Доступность   = (Порт <> 100);
	Элементы.Четность.Доступность  = (Порт <> 100);

КонецПроцедуры

&НаКлиенте
Процедура ПрефиксОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ВыбранноеЗначение <> Неопределено Тогда
		Элементы.ПараметрыДорожек.ТекущиеДанные.Префикс = 
		    Элементы.ПараметрыДорожек.ТекущиеДанные.Префикс + ВыбранноеЗначение;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура СуффиксОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)

	Если ВыбранноеЗначение <> Неопределено Тогда
		Элементы.ПараметрыДорожек.ТекущиеДанные.Суффикс = 
		    Элементы.ПараметрыДорожек.ТекущиеДанные.Суффикс + ВыбранноеЗначение;
	КонецЕсли;
	СтандартнаяОбработка = Ложь;

КонецПроцедуры

&НаКлиенте
Процедура ПараметрыДорожекПриАктивизацииЯчейки(Элемент)
	
	Если (Элемент.ТекущийЭлемент.Имя = "Префикс" Или Элемент.ТекущийЭлемент.Имя = "Суффикс")
		И Элемент.ТекущийЭлемент.СписокВыбора.Количество() = 0 Тогда
		
		СписокПараметрыДорожек = Элемент.ТекущийЭлемент.СписокВыбора;
		
		Для КодЭлемента = 0 По 127 Цикл
			СимволДорожки = "";
			Если КодЭлемента > 32 Тогда
				СимволДорожки = " ( " + Символ(КодЭлемента) + " )";
			ИначеЕсли КодЭлемента = 8 Тогда
				СимволДорожки = " (BACKSPACE)";
			ИначеЕсли КодЭлемента = 9 Тогда
				СимволДорожки = " (TAB)";
			ИначеЕсли КодЭлемента = 10 Тогда
				СимволДорожки = " (LF)";
			ИначеЕсли КодЭлемента = 13 Тогда
				СимволДорожки = " (CR)";
			ИначеЕсли КодЭлемента = 16 Тогда
				СимволДорожки = " (SHIFT)";
			ИначеЕсли КодЭлемента = 17 Тогда
				СимволДорожки = " (CONTROL)";
			ИначеЕсли КодЭлемента = 18 Тогда
				СимволДорожки = " (ALT)";
			ИначеЕсли КодЭлемента = 27 Тогда
				СимволДорожки = " (ESCAPE)";
			ИначеЕсли КодЭлемента = 32 Тогда
				СимволДорожки = " (SPACE)";
			КонецЕсли;
			СписокПараметрыДорожек.Добавить("#" + СокрЛП(КодЭлемента), "#" + СокрЛП(КодЭлемента) + СимволДорожки);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	ОчиститьСообщения();
	
	НастроеноДорожек = 0;
	ДорожкаСПустымСуффиксом = Ложь;
	времПараметрыДорожек = Новый Массив();

	Для Индекс = 1 По 3 Цикл
		Если ПараметрыДорожек[3 - Индекс].Использовать = Истина Тогда
			ДорожкаСПустымСуффиксом = ДорожкаСПустымСуффиксом ИЛИ (ПараметрыДорожек[3 - Индекс].Суффикс = "");
			НастроеноДорожек = НастроеноДорожек + 1;
		КонецЕсли;
	КонецЦикла;

	Если Не ДорожкаСПустымСуффиксом Тогда
		Для Индекс = 1 По 3 Цикл
			НоваяСтрока = Новый Структура();
			НоваяСтрока.Вставить("НомерДорожки", ПараметрыДорожек[Индекс - 1].НомерДорожки);
			НоваяСтрока.Вставить("Использовать", ПараметрыДорожек[Индекс - 1].Использовать);
			НоваяСтрока.Вставить("Префикс"     , ПараметрыДорожек[Индекс - 1].Префикс);
			НоваяСтрока.Вставить("Суффикс"     , ПараметрыДорожек[Индекс - 1].Суффикс);
			времПараметрыДорожек.Добавить(НоваяСтрока);
		КонецЦикла;
	КонецЕсли;

	Если НастроеноДорожек > 0 И Не ДорожкаСПустымСуффиксом Тогда
		
		НовыеЗначениеПараметров = Новый Структура;
		НовыеЗначениеПараметров.Вставить("Порт"             , Порт);
		НовыеЗначениеПараметров.Вставить("Скорость"         , Скорость);
		НовыеЗначениеПараметров.Вставить("БитДанных"        , БитДанных);
		НовыеЗначениеПараметров.Вставить("СтопБит"          , СтопБит);
		НовыеЗначениеПараметров.Вставить("Четность"         , Четность);
		НовыеЗначениеПараметров.Вставить("Чувствительность" , Чувствительность);
		НовыеЗначениеПараметров.Вставить("ПараметрыДорожек" , времПараметрыДорожек);
		НовыеЗначениеПараметров.Вставить("Модель"           , Модель);
		
		Результат = Новый Структура;
		Результат.Вставить("Идентификатор", Идентификатор);
		Результат.Вставить("ПараметрыОборудования", НовыеЗначениеПараметров);
		
		Закрыть(Результат);
		
	ИначеЕсли НастроеноДорожек = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Необходимо указать использование хотя бы одной дорожки для считывателя.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	ИначеЕсли ДорожкаСПустымСуффиксом Тогда
		ТекстСообщения = НСтр("ru = 'Для каждой используемой дорожки должен быть указан не пустой суффикс.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура УстановкаДрайвераЗавершение(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		АдресЗагрузкиДрайвера = "http://www.atol.ru/link/cat/7/";
		ПерейтиПоНавигационнойСсылке(АдресЗагрузкиДрайвера);
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайвер(Команда)

	ОчиститьСообщения();
	Текст = НСтр("ru = 'Установка производиться средствами дистрибутива поставщика.
		|Перейти на сайт поставщика для скачивания?'");
	Оповещение = Новый ОписаниеОповещения("УстановкаДрайвераЗавершение",  ЭтотОбъект);
	ПоказатьВопрос(Оповещение, Текст, РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьИнформациюОДрайвере()

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("Порт"            , Порт);
	времПараметрыУстройства.Вставить("Скорость"        , Скорость);
	времПараметрыУстройства.Вставить("БитДанных"       , БитДанных);
	времПараметрыУстройства.Вставить("СтопБит"         , СтопБит);
	времПараметрыУстройства.Вставить("Четность"        , Четность);
	времПараметрыУстройства.Вставить("Чувствительность", Чувствительность);
	времПараметрыУстройства.Вставить("ПараметрыДорожек", Новый Массив());
	времПараметрыУстройства.Вставить("Модель"          , Модель);

	Если МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ПолучитьВерсиюДрайвера",
	                                                               ВходныеПараметры,
	                                                               ВыходныеПараметры,
	                                                               Идентификатор,
	                                                               времПараметрыУстройства) Тогда
		Драйвер = ВыходныеПараметры[0];
		Версия  = ВыходныеПараметры[1];
	Иначе
		Драйвер = ВыходныеПараметры[2];
		Версия  = НСтр("ru='Не определена'");
	КонецЕсли;

	Элементы.Драйвер.ЦветТекста = ?(Драйвер = НСтр("ru='Не установлен'"), ЦветОшибки, ЦветТекста);
	Элементы.Версия.ЦветТекста  = ?(Версия  = НСтр("ru='Не определена'"), ЦветОшибки, ЦветТекста);
	
	Элементы.УстановитьДрайвер.Доступность = Не (Драйвер = НСтр("ru='Установлен'"));
	
КонецПроцедуры

#КонецОбласти