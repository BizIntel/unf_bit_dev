////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Параметры.Свойство("Значение", ВыбираемыйПериод);
	Если НЕ ЗначениеЗаполнено(ВыбираемыйПериод) Тогда
		ВыбираемыйПериод = НачалоМесяца(ТекущаяДатаСеанса());
	КонецЕсли;
	Если НЕ Параметры.Свойство("ЗапрашиватьРежимВыбораПериодаУВладельца", ЗапрашиватьРежимВыбораПериодаУВладельца) Тогда
		ЗапрашиватьРежимВыбораПериодаУВладельца = Ложь;
	КонецЕсли;
	Если НЕ ЗапрашиватьРежимВыбораПериодаУВладельца Тогда
		Параметры.Свойство("РежимВыбораПериода", РежимВыбораПериода);
		Если ПустаяСтрока(РежимВыбораПериода) Тогда
			Если НЕ ЗапрашиватьРежимВыбораПериодаУВладельца Тогда
				РежимВыбораПериода = "МЕСЯЦ";
			КонецЕсли; 
		Иначе
			РежимВыбораПериода = ВРег(РежимВыбораПериода);
		КонецЕсли;
	КонецЕсли; 
	ЦветФонаКнопкиВыбранногоПериода = ЦветаСтиля.ФонУправляющегоПоля;
	ЦветФонаКнопки = ЦветаСтиля.ЦветФонаКнопки;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОбновитьОтображение();
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ВыбираемыйПериодПриИзменении(Элемент)
	ИзменилсяГод = Истина;
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), Месяц(ВыбираемыйПериод));
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаМесяц01(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 1);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц02(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 2);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц03(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 3);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц04(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 4);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц05(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 5);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц06(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 6);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц07(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 7);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц08(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 8);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц09(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 9);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц10(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 10);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц11(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 11);
КонецПроцедуры

&НаКлиенте
Процедура КомандаМесяц12(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 12);
КонецПроцедуры

&НаКлиенте
Процедура КомандаКвартал1(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 1);
КонецПроцедуры

&НаКлиенте
Процедура КомандаКвартал2(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 4);
КонецПроцедуры

&НаКлиенте
Процедура КомандаКвартал3(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 7);
КонецПроцедуры

&НаКлиенте
Процедура КомандаКвартал4(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 10);
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолугодие1(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 1);
КонецПроцедуры

&НаКлиенте
Процедура КомандаПолугодие2(Команда)
	УстановитьВыбираемыйПериод(Год(ВыбираемыйПериод), 7);
КонецПроцедуры

&НаКлиенте
Процедура КомандаВыбрать(Команда)
	ВыполнитьВыбор();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаПролистатьГодВМинус(Команда)
	ВыбираемыйГод = Год(ВыбираемыйПериод) - 1;
	УстановитьВыбираемыйПериод(ВыбираемыйГод, Месяц(ВыбираемыйПериод));
КонецПроцедуры

&НаКлиенте
Процедура КомандаПролистатьГодВПлюс(Команда)
	ВыбираемыйГод = Год(ВыбираемыйПериод) + 1;
	УстановитьВыбираемыйПериод(ВыбираемыйГод, Месяц(ВыбираемыйПериод));
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПроверитьРежимВыбораПериода()
	Если НЕ ЗапрашиватьРежимВыбораПериодаУВладельца Тогда
		Возврат;
	КонецЕсли;
	РежимВыбора = ВладелецФормы.РежимВыбораПериода(ВыбираемыйПериод);
	Если РежимВыбораПериода = ВРег(РежимВыбора) ИЛИ  ПустаяСтрока(РежимВыбора) Тогда
		Возврат;
	КонецЕсли;
	РежимВыбораПериода = ВРег(РежимВыбора);
	УстановитьРежимВыбораПериода();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьРежимВыбораПериода()
	Если РежимВыбораПериода = "МЕСЯЦ" Тогда
		Элементы.ГруппаМесяцы.Видимость = Истина;
		Элементы.ГруппаКварталы.Видимость = Ложь;
		Элементы.ГруппаПолугодия.Видимость = Ложь;
		ВыбираемыйПериод = НачалоМесяца(ВыбираемыйПериод);
	ИначеЕсли РежимВыбораПериода = "КВАРТАЛ" Тогда
		Элементы.ГруппаМесяцы.Видимость = Ложь;
		Элементы.ГруппаКварталы.Видимость = Истина;
		Элементы.ГруппаПолугодия.Видимость = Ложь;
		НомерКвартала = Цел((Месяц(ВыбираемыйПериод) - 1) / 3 + 1);
		ВыбираемыйПериод = Дата(Год(ВыбираемыйПериод), (НомерКвартала - 1) * 3 + 1, 1);
	ИначеЕсли РежимВыбораПериода = "ПОЛУГОДИЕ" Тогда
		Элементы.ГруппаМесяцы.Видимость = Ложь;
		Элементы.ГруппаКварталы.Видимость = Ложь;
		Элементы.ГруппаПолугодия.Видимость = Истина;
		ВыбираемыйПериод = Дата(Год(ВыбираемыйПериод), ?(Месяц(ВыбираемыйПериод) < 7, 1, 7), 1);
	КонецЕсли; 
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОтображение()
	ПроверитьРежимВыбораПериода();
	Если РежимВыбораПериода = "МЕСЯЦ" Тогда
		Для НомерМесяца = 1 По 12 Цикл
			КнопкаМесяца = Элементы["КомандаМесяц" + Формат(НомерМесяца, "ЧЦ=2; ЧВН=")];
			Если НомерМесяца = Месяц(ВыбираемыйПериод) Тогда
				Если КнопкаМесяца.ЦветФона <> ЦветФонаКнопкиВыбранногоПериода Тогда
					КнопкаМесяца.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
				КонецЕсли;
				ЭтаФорма.ТекущийЭлемент = КнопкаМесяца;
			Иначе
				Если КнопкаМесяца.ЦветФона <> ЦветФонаКнопки Тогда
					КнопкаМесяца.ЦветФона = ЦветФонаКнопки;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		ЗарплатаКадрыКлиентСервер.ЗаполнитьМесяцПоДате(ЭтаФорма, "ВыбираемыйПериод", "ПериодСтрокой");
	ИначеЕсли РежимВыбораПериода = "КВАРТАЛ" Тогда
		КварталМесяца = (Месяц(ВыбираемыйПериод) - 1) / 3 + 1;
		Для НомерКвартала = 1 По 4 Цикл
			КнопкаКвартала = Элементы["КомандаКвартал" + Формат(НомерКвартала, "ЧЦ=1")];
			Если НомерКвартала = КварталМесяца Тогда
				Если КнопкаКвартала.ЦветФона <> ЦветФонаКнопкиВыбранногоПериода Тогда
					КнопкаКвартала.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
				КонецЕсли;
				ЭтаФорма.ТекущийЭлемент = КнопкаКвартала;
			Иначе
				Если КнопкаКвартала.ЦветФона <> ЦветФонаКнопки Тогда
					КнопкаКвартала.ЦветФона = ЦветФонаКнопки;
				КонецЕсли;
			КонецЕсли;
		КонецЦИкла;
		ПериодСтрокой = Формат(КварталМесяца, "ЧЦ=1") + " квартал " + Формат(ВыбираемыйПериод,"ДФ=гггг");
	Иначе
		Если Месяц(ВыбираемыйПериод) = 1 Тогда
			Элементы.КомандаПолугодие1.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
			Элементы.КомандаПолугодие2.ЦветФона = ЦветФонаКнопки;
			ПериодСтрокой = "1 полугодие " + Формат(ВыбираемыйПериод,"ДФ=гггг");
		Иначе
			Элементы.КомандаПолугодие1.ЦветФона = ЦветФонаКнопки;
			Элементы.КомандаПолугодие2.ЦветФона = ЦветФонаКнопкиВыбранногоПериода;
			ПериодСтрокой = "2 полугодие " + Формат(ВыбираемыйПериод,"ДФ=гггг");
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УстановитьВыбираемыйПериод(Год, Месяц)
	Если Год = Год(ВыбираемыйПериод) И Месяц = Месяц(ВыбираемыйПериод) И НЕ ИзменилсяГод Тогда
		ВыполнитьВыбор();
	КонецЕсли; 
	Если Год < 1 Тогда
		Год = 1;
	КонецЕсли; 
	ИзменилсяГод = Ложь;
	ВыбираемыйПериод = Дата(Год, Месяц, 1);
	ОбновитьОтображение();
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьВыбор()
	Закрыть(ВыбираемыйПериод);
КонецПроцедуры

