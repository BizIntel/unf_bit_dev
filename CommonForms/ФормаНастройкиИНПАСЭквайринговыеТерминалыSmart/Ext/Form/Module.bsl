
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Идентификатор", Идентификатор);
	Параметры.Свойство("ДрайверОборудования", ДрайверОборудования);
	
	Заголовок = НСтр("ru='Оборудование:'") + Символы.НПП  + Строка(Идентификатор);
	
	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;

	СписокЗначений = Элементы.Порт.СписокВыбора;
	Для Индекс = 1 По 64 Цикл
		СписокЗначений.Добавить(Индекс, "COM" + Строка(Индекс));
	КонецЦикла;
	
	СписокЗначений = Элементы.Скорость.СписокВыбора;
	СписокЗначений.Добавить(9600,     "9600");
	СписокЗначений.Добавить(19200,   "19200");
	СписокЗначений.Добавить(38400,   "38400");
	СписокЗначений.Добавить(57600,   "57600");
	СписокЗначений.Добавить(115200, "115200");
	
	времИДТерминала = Неопределено;
	времПорт        = Неопределено;
	времСкорость    = Неопределено;
	времБитыДанных  = Неопределено;
	времЧетность    = Неопределено;
	времСтопБиты    = Неопределено;
	времУправлениеПотоком    = Неопределено;
	времКодВалюты            = Неопределено;
	времКоличествоКопий      = Неопределено;
	времШиринаСлипЧека       = Неопределено;
	времКодСимволаОтреза     = Неопределено;
	времТекстШапки           = Неопределено;
	времТекстПодвала         = Неопределено;
	времДанныеМакетаСлипЧека = Неопределено;
	
	Параметры.ПараметрыОборудования.Свойство("ИДТерминала"       , времИДТерминала);
	Параметры.ПараметрыОборудования.Свойство("Порт"              ,   времПорт);
	Параметры.ПараметрыОборудования.Свойство("Скорость"          , времСкорость);
	Параметры.ПараметрыОборудования.Свойство("БитыДанных"        , времБитыДанных);
	Параметры.ПараметрыОборудования.Свойство("Четность"          , времЧетность);
	Параметры.ПараметрыОборудования.Свойство("СтопБиты"          , времСтопБиты);
	Параметры.ПараметрыОборудования.Свойство("УправлениеПотоком" , времУправлениеПотоком);	
	Параметры.ПараметрыОборудования.Свойство("КодВалюты"         , времКодВалюты);
	Параметры.ПараметрыОборудования.Свойство("КоличествоКопий"   , времКоличествоКопий);
	Параметры.ПараметрыОборудования.Свойство("КодСимволаОтреза"  , времКодСимволаОтреза);
	Параметры.ПараметрыОборудования.Свойство("ТекстШапки"        , времТекстШапки);
	Параметры.ПараметрыОборудования.Свойство("ТекстПодвала"      , времТекстПодвала);
	Параметры.ПараметрыОборудования.Свойство("ДанныеМакетаСлипЧека", времДанныеМакетаСлипЧека);
	
	ИДТерминала = ?(времИДТерминала = Неопределено, "00000", времИДТерминала);
	Порт = ?(времПорт  = Неопределено, 1, времПорт);
	Скорость = ?(времСкорость  = Неопределено, 9600, времСкорость);
	БитыДанных = ?(времБитыДанных  = Неопределено, 8, времБитыДанных);
	Четность = ?(времЧетность  = Неопределено, 0, времЧетность);
	СтопБиты = ?(времСтопБиты  = Неопределено, 1, времСтопБиты);
	УправлениеПотоком    = ?(времУправлениеПотоком  = Неопределено, 2, времУправлениеПотоком);
	КодВалюты = ?(времКодВалюты  = Неопределено, 643, времКодВалюты);
	ШиринаСлипЧека = ?(времШиринаСлипЧека = Неопределено, 36, времШиринаСлипЧека);
	КоличествоКопий = ?(времКоличествоКопий  = Неопределено, 2, времКоличествоКопий);
	ТекстШапки = ?(времТекстШапки  = Неопределено, "", времТекстШапки);	
	ТекстПодвала = ?(времТекстПодвала  = Неопределено, "", времТекстПодвала);
	КодСимволаОтреза = ?(времКодСимволаОтреза  = Неопределено, 22, времКодСимволаОтреза);
	ДанныеМакетаСлипЧека = времДанныеМакетаСлипЧека;
	
	ПрочитатьДанныеМакета();
	
	Элементы.ТестУстройства.Видимость    = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто);
	Элементы.УстановитьДрайвер.Видимость = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьИнформациюОДрайвере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()

	ЗаписатьДанныеМакета();
	
	НовыеЗначениеПараметров = Новый Структура;
	НовыеЗначениеПараметров.Вставить("ИДТерминала"          , ИДТерминала);
	НовыеЗначениеПараметров.Вставить("Порт"                 , Порт);
	НовыеЗначениеПараметров.Вставить("Скорость"             , Скорость);
	НовыеЗначениеПараметров.Вставить("БитыДанных"           , БитыДанных);
	НовыеЗначениеПараметров.Вставить("Четность"             , Четность);
	НовыеЗначениеПараметров.Вставить("СтопБиты"             , СтопБиты);
	НовыеЗначениеПараметров.Вставить("УправлениеПотоком"    , УправлениеПотоком);
	НовыеЗначениеПараметров.Вставить("КодВалюты"            , КодВалюты);
	НовыеЗначениеПараметров.Вставить("ШиринаСлипЧека"       , ШиринаСлипЧека);
	НовыеЗначениеПараметров.Вставить("КоличествоКопий"      , КоличествоКопий);
	НовыеЗначениеПараметров.Вставить("КодСимволаОтреза"     , КодСимволаОтреза);
	НовыеЗначениеПараметров.Вставить("ТекстШапки"           , ТекстШапки);
	НовыеЗначениеПараметров.Вставить("ТекстПодвала"         , ТекстПодвала);
	НовыеЗначениеПараметров.Вставить("ДанныеМакетаСлипЧека" , ДанныеМакетаСлипЧека);
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", Идентификатор);
	Результат.Вставить("ПараметрыОборудования", НовыеЗначениеПараметров);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайверИзАрхиваПриЗавершении(Результат) Экспорт 
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.'")); 
	ОбновитьИнформациюОДрайвере();
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайверИзДистрибутиваПриЗавершении(Результат, Параметры) Экспорт 
	
	Если Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.'")); 
		ОбновитьИнформациюОДрайвере();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При установке драйвера из дистрибутива произошла ошибка.'")); 
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайвер(Команда)

	ОчиститьСообщения();
	ОповещенияДрайверИзДистрибутиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзДистрибутиваПриЗавершении", ЭтотОбъект);
	ОповещенияДрайверИзАрхиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзАрхиваПриЗавершении", ЭтотОбъект);
	МенеджерОборудованияКлиент.УстановитьДрайвер(ДрайверОборудования, ОповещенияДрайверИзДистрибутиваПриЗавершении, ОповещенияДрайверИзАрхиваПриЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ТестУстройства(Команда)
	
	ОчиститьСообщения();
	
	РезультатТеста = Неопределено;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("ИДТерминала", ИДТерминала);
	времПараметрыУстройства.Вставить("Порт", Порт);
	времПараметрыУстройства.Вставить("Скорость", Скорость);
	времПараметрыУстройства.Вставить("БитыДанных", БитыДанных);
	времПараметрыУстройства.Вставить("Четность", Четность);
	времПараметрыУстройства.Вставить("СтопБиты", СтопБиты);
	времПараметрыУстройства.Вставить("УправлениеПотоком", УправлениеПотоком);
	времПараметрыУстройства.Вставить("КодВалюты", КодВалюты);
	времПараметрыУстройства.Вставить("КодСимволаОтреза", КодСимволаОтреза);
	времПараметрыУстройства.Вставить("ТекстШапки", ТекстШапки);
	времПараметрыУстройства.Вставить("ТекстПодвала", ТекстПодвала);
	

	Результат = МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("CheckHealth",
	                                                                      ВходныеПараметры,
	                                                                      ВыходныеПараметры,
	                                                                      Идентификатор,
	                                                                      времПараметрыУстройства);

	Если Результат Тогда
		ТекстСообщения = НСтр("ru = 'Тест успешно выполнен.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		ДополнительноеОписание = ?(ТипЗнч(ВыходныеПараметры) = Тип("Массив")
		                           И ВыходныеПараметры.Количество() >= 2,
		                           НСтр("ru = 'Дополнительное описание:'") + " " + ВыходныеПараметры[1],
		                           "");


		ТекстСообщения = НСтр("ru = 'Тест не пройден.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбновитьИнформациюОДрайвере()

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;
	
	времПараметрыУстройства = Новый Структура(); 
	
	Если МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ПолучитьНомерВерсии",
	                                                               ВходныеПараметры,
	                                                               ВыходныеПараметры,
	                                                               Идентификатор,
	                                                               времПараметрыУстройства) Тогда
		Драйвер = ВыходныеПараметры[0];
		Версия  = ВыходныеПараметры[1];
	Иначе
		Драйвер = ВыходныеПараметры[2];
		Версия  = НСтр("ru='Не определена'");
	КонецЕсли;
	
	Элементы.УстановитьДрайвер.Доступность = Не (Драйвер = НСтр("ru='Установлен'"));
	Элементы.Драйвер.ЦветТекста = ?(Драйвер = НСтр("ru='Не установлен'"), ЦветОшибки, ЦветТекста);
	Элементы.Версия.ЦветТекста  = ?(Версия  = НСтр("ru='Не определена'"), ЦветОшибки, ЦветТекста);

КонецПроцедуры

Процедура ПрочитатьДанныеМакета()

	Если ДанныеМакетаСлипЧека.Количество() = 0 Тогда
		ДанныеМакетаСлипЧека.Добавить("БАНК"   , "Банк");
		ДанныеМакетаСлипЧека.Добавить(""       , "Организация");
		ДанныеМакетаСлипЧека.Добавить(""       , "Город");
		ДанныеМакетаСлипЧека.Добавить(""       , "Адрес");
		ДанныеМакетаСлипЧека.Добавить("ОТДЕЛ1" , "Отдел");
		ДанныеМакетаСлипЧека.Добавить("КАССИР" , "Кассир");
		ДанныеМакетаСлипЧека.Добавить("ЗДРАВСТВУЙТЕ", "ТекстШапки");
		ДанныеМакетаСлипЧека.Добавить("СПАСИБО", "ТекстПодвала");
	КонецЕсли;

	Банк         = ДанныеМакетаСлипЧека[0].Значение;
	Организация  = ДанныеМакетаСлипЧека[1].Значение;
	Город        = ДанныеМакетаСлипЧека[2].Значение;
	Адрес        = ДанныеМакетаСлипЧека[3].Значение;
	Отдел        = ДанныеМакетаСлипЧека[4].Значение;
	Кассир       = ДанныеМакетаСлипЧека[5].Значение;
	ТекстПодвала = ДанныеМакетаСлипЧека[6].Значение;
	ТекстШапки   = ДанныеМакетаСлипЧека[7].Значение;

КонецПроцедуры

Процедура ЗаписатьДанныеМакета()

	ДанныеМакетаСлипЧека[0].Значение = Банк;
	ДанныеМакетаСлипЧека[1].Значение = Организация;
	ДанныеМакетаСлипЧека[2].Значение = Город;
	ДанныеМакетаСлипЧека[3].Значение = Адрес;
	ДанныеМакетаСлипЧека[4].Значение = Отдел;
	ДанныеМакетаСлипЧека[5].Значение = Кассир;
	ДанныеМакетаСлипЧека[6].Значение = ТекстПодвала;
	ДанныеМакетаСлипЧека[7].Значение = ТекстШапки;
	
КонецПроцедуры

#КонецОбласти