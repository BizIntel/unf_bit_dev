#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Сертификат = Параметры.Отпечаток;
	Организация = Параметры.Организация;
	
	ТекстПроСертификат = Новый ФорматированнаяСтрока(
		Символы.ПС,
		"Если у вас нет сертификата или его срок действия закончился, можно ",
		Новый ФорматированнаяСтрока(
			"получить новый",,,, ?(ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ЭлектроннаяПодпись"), "Подключиться", "")),
		" с помощью",
		Символы.ПС,
		"сервиса ",
		Новый ФорматированнаяСтрока(
			"1С:Подпись",,,, "https://portal.1c.ru/applications/31"),
		" или обратиться в один из ",
		Новый ФорматированнаяСтрока(
			"удостоверяющих центров",,,, "СписокУЦ"),
		Символы.ПС
	);
	
	Элементы.ТекстПроСертификат.Заголовок = ТекстПроСертификат;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СертификатПредставление = Сертификат;
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат, 
		Сертификат, ЭтаФорма, "СертификатПредставление");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СертификатНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Оповещение = Новый ОписаниеОповещения(
		"СертификатНачалоВыбораЗавершение", ЭтотОбъект, Новый Структура("Элемент", Элемент));

	ПараметрыФормы = Новый Структура("Отпечаток", Сертификат);
	ОткрытьФорму("ОбщаяФорма.СписокСертификатов", ПараметрыФормы,,,,, Оповещение);
		
КонецПроцедуры

&НаКлиенте
Процедура СертификатНачалоВыбораЗавершение(Результат, ВходящийКонтекст) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка")
		И ЗначениеЗаполнено(Результат) Тогда
		
		Элемент = ВходящийКонтекст.Элемент;
		
		Сертификат = Результат;
		
		КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
			ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат,
			Сертификат, ЭтотОбъект, "СертификатПредставление");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатОчистка(Элемент, СтандартнаяОбработка)
	
	Сертификат = "";
	
	КриптографияЭДКОКлиент.ОтобразитьПредставлениеСертификата(
		ЭтоЭлектроннаяПодписьВМоделиСервиса, Элементы.Сертификат, 
		Сертификат, ЭтотОбъект, "СертификатПредставление");

КонецПроцедуры

&НаКлиенте
Процедура СертификатОткрытие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если ЗначениеЗаполнено(Сертификат) Тогда
		КриптографияЭДКОКлиент.ПоказатьСертификат(
			Новый Структура("ЭлектроннаяПодписьВМоделиСервиса, Отпечаток", 
				ЭтоЭлектроннаяПодписьВМоделиСервиса, Сертификат));
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТекстПроСертификатОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	Если НавигационнаяСсылкаФорматированнойСтроки = "Подключиться" Тогда
		СтандартнаяОбработка = Ложь;
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Организация", Организация);
		ИмяФормыЗаявления = "Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма.ЗаявлениеНаВыпускНовогоКвалифицированногоСертификата";
		ОткрытьФорму(ИмяФормыЗаявления, ПараметрыФормы);
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "СписокУЦ" Тогда
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("ОбщаяФорма.СписокУЦ",, ЭтотОбъект);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Не ПроверитьЗаполнение();
	
	Если Не Отказ Тогда
		Закрыть(Сертификат);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти