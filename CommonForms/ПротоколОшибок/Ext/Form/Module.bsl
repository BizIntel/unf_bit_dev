
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Отчет = Параметры.Отчет;
	ВывестиПротокол(Отчет);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ВывестиПротокол(Отчет)
	
	Ошибки = Неопределено;
	
	Статус = СдачаОтчетностиЧерезСервисСпецоператораВызовСервера.ПолучитьСтатусОтправки(Отчет, Ошибки);
	ОтчетНаименование = ПредставлениеОтчета(Отчет);
	
	Протокол = Протокол + "<html><head><style type=""text/css"">";
	Протокол = Протокол + "TABLE {border: 1px solid #B3AC86; width: 100%; border-collapse: collapse;}";
	Протокол = Протокол + "TD { border: 1px solid #B3AC86; width: 50%;}";
	Протокол = Протокол + "</style></head>";
	Протокол = Протокол + "<body>";
	
	Если Статус = Перечисления.СтатусыОтправки.Сдан Тогда
		Протокол = Протокол + "<h2>Отчет сдан</h2>";
	ИначеЕсли Статус = Перечисления.СтатусыОтправки.НеПринят Тогда
		
		Протокол =  Протокол + "<h2>Отчет не принят ("+ОтчетНаименование+")</h2><br>";
		Протокол =  Протокол + "<table><tr bgcolor=""#F4ECC5"" align=center><td><b>Причина</b></td><td><b>Пояснение</b></td></tr>";
		Для Каждого Ошибка Из Ошибки Цикл
			
			Протокол =  Протокол + "<tr><td>" + Ошибка.Причина + "</td><td><i>" + Ошибка.Пояснение +"</i></td></tr>";
			
		КонецЦикла;
		
		Протокол =  Протокол + "</table>";
		
	Иначе
		Протокол = Протокол + "<h2>Отчет находится в статусе «"+Статус+"»</h2>";
	КонецЕсли;
	
	Протокол = Протокол +"</body></html>";
	
КонецПроцедуры

Функция ПредставлениеОтчета(Отчет) Экспорт
	
	Если ТипЗнч(Отчет) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Возврат РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(Отчет);
	Иначе
		Возврат Строка(Отчет);
	КонецЕсли;
	
КонецФункции

#КонецОбласти
