
#Область ОбработчикиКомандФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Свойство("Идентификатор", Идентификатор);
	Параметры.Свойство("ДрайверОборудования", ДрайверОборудования);
	
	Заголовок = НСтр("ru='Оборудование:'") + Символы.НПП  + Строка(Идентификатор);
	
	ЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
	ЦветОшибки = ЦветаСтиля.ЦветОтрицательногоЧисла;

	СписокПорт = Элементы.Порт.СписокВыбора;
	Для Номер = 1 По 64 Цикл
		СписокПорт.Добавить(Номер, "COM" + Формат(Номер, "ЧЦ=2; ЧДЦ=0; ЧН=0; ЧГ=0"));
	КонецЦикла;  
	
	СписокИнтерфейс = Элементы.Интерфейс.СписокВыбора;
	СписокИнтерфейс.Добавить(0, "RS-232");
	СписокИнтерфейс.Добавить(1, "Ethernet");

    СписокСкорость = Элементы.Скорость.СписокВыбора; 
	СписокСкорость.Добавить(0,  "2400");
	СписокСкорость.Добавить(1,  "4800");
	СписокСкорость.Добавить(2,  "9600");
	СписокСкорость.Добавить(3, "19200");
	СписокСкорость.Добавить(4, "38400");
	СписокСкорость.Добавить(5, "57600");
	СписокСкорость.Добавить(6,"115200");
	
	времНаименование        = Неопределено;
	времИнтерфейс           = Неопределено;
	времПорт                = Неопределено;
	времСкорость            = Неопределено;
	времТаймаут             = Неопределено;
	времIPАдрес             = Неопределено;
	времUDPПортПолучателя   = Неопределено;
	времUDPТаймаут          = Неопределено;
	времПароль              = Неопределено;
	времБыстраяЗагрузка     = Неопределено;
	
	Параметры.ПараметрыОборудования.Свойство("Интерфейс"        , времИнтерфейс);
	Параметры.ПараметрыОборудования.Свойство("Порт"             , времПорт);
	Параметры.ПараметрыОборудования.Свойство("Скорость"         , времСкорость);
	Параметры.ПараметрыОборудования.Свойство("Таймаут"          , времТаймаут);
	Параметры.ПараметрыОборудования.Свойство("IPАдрес"          , времIPАдрес);
	Параметры.ПараметрыОборудования.Свойство("UDPПортПолучателя", времUDPПортПолучателя);
	Параметры.ПараметрыОборудования.Свойство("UDPТаймаут"       , времUDPТаймаут);
	Параметры.ПараметрыОборудования.Свойство("Пароль"           , времПароль);
	Параметры.ПараметрыОборудования.Свойство("БыстраяЗагрузка"  , времБыстраяЗагрузка);
	Параметры.ПараметрыОборудования.Свойство("Наименование"     , времНаименование);
	                          
	Интерфейс          = ?(времИнтерфейс           = Неопределено, 0, времИнтерфейс);
	Порт               = ?(времПорт                = Неопределено, 1, времПорт);
	Скорость           = ?(времСкорость            = Неопределено, 2, времСкорость);
	Таймаут            = ?(времТаймаут             = Неопределено, 50, времТаймаут);
	IPАдрес            = ?(времIPАдрес             = Неопределено, "192.168.0.1", времIPАдрес);
	UDPПортПолучателя  = ?(времUDPПортПолучателя   = Неопределено, 1111, времUDPПортПолучателя);
	UDPТаймаут         = ?(времUDPТаймаут          = Неопределено, 500, времUDPТаймаут);
	Пароль             = ?(времПароль              = Неопределено, "0030", времПароль);
	БыстраяЗагрузка    = ?(времБыстраяЗагрузка     = Неопределено, Ложь, времБыстраяЗагрузка);
	
	Наименование = ?(времНаименование = Неопределено, НСтр("ru = 'ШТРИХ-ПРИНТ'"), времНаименование);
	Элементы.ТестУстройства.Видимость    = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто);
	Элементы.УстановитьДрайвер.Видимость = (ПараметрыСеанса.РабочееМестоКлиента = Идентификатор.РабочееМесто); 
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)

	ОбновитьИнформациюОДрайвере();

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИнтерфейсПриИзменении(Элемент)
	
	ПроверкаДоступности();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗаписатьИЗакрытьВыполнить()
	
	НовыеЗначениеПараметров = Новый Структура;
	НовыеЗначениеПараметров.Вставить("Интерфейс"        , Интерфейс);
	НовыеЗначениеПараметров.Вставить("Порт"             , Порт);
	НовыеЗначениеПараметров.Вставить("Скорость"         , Скорость);
	НовыеЗначениеПараметров.Вставить("Таймаут"          , Таймаут);
	НовыеЗначениеПараметров.Вставить("IPАдрес"          , IPАдрес);
	НовыеЗначениеПараметров.Вставить("UDPПортПолучателя", UDPПортПолучателя);
	НовыеЗначениеПараметров.Вставить("UDPТаймаут"       , UDPТаймаут);
	НовыеЗначениеПараметров.Вставить("Пароль"           , Пароль);
	НовыеЗначениеПараметров.Вставить("БыстраяЗагрузка"  , БыстраяЗагрузка);
	НовыеЗначениеПараметров.Вставить("Наименование"     , Наименование);
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор", Идентификатор);
	Результат.Вставить("ПараметрыОборудования", НовыеЗначениеПараметров);
	
	Закрыть(Результат);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДрайверИзАрхиваПриЗавершении(Результат) Экспорт 
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.'")); 
	ОбновитьИнформациюОДрайвере();
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайверИзДистрибутиваПриЗавершении(Результат, Параметры) Экспорт 
	
	Если Результат Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Установка драйвера завершена.'")); 
		ОбновитьИнформациюОДрайвере();
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='При установке драйвера из дистрибутива произошла ошибка.'")); 
	КонецЕсли;

КонецПроцедуры 

&НаКлиенте
Процедура УстановитьДрайвер(Команда)
	
	ОчиститьСообщения();
	ОповещенияДрайверИзДистрибутиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзДистрибутиваПриЗавершении", ЭтотОбъект);
	ОповещенияДрайверИзАрхиваПриЗавершении = Новый ОписаниеОповещения("УстановитьДрайверИзАрхиваПриЗавершении", ЭтотОбъект);
	МенеджерОборудованияКлиент.УстановитьДрайвер(ДрайверОборудования, ОповещенияДрайверИзДистрибутиваПриЗавершении, ОповещенияДрайверИзАрхиваПриЗавершении);
	
КонецПроцедуры

&НаКлиенте
Процедура ТестУстройства(Команда)

	РезультатТеста = Неопределено;

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("Интерфейс"    		, Интерфейс);
	времПараметрыУстройства.Вставить("Порт"     	 		, Порт);
	времПараметрыУстройства.Вставить("Скорость" 	  		, Скорость);
	времПараметрыУстройства.Вставить("Таймаут"				, Таймаут);	
	времПараметрыУстройства.Вставить("IPАдрес"				, IPАдрес);
	времПараметрыУстройства.Вставить("UDPПортПолучателя"	, UDPПортПолучателя);
	времПараметрыУстройства.Вставить("UDPТаймаут"      		, UDPТаймаут);
	времПараметрыУстройства.Вставить("Пароль"				, Пароль);
	времПараметрыУстройства.Вставить("Наименование"			, Наименование);
	времПараметрыУстройства.Вставить("БыстраяЗагрузка"		, БыстраяЗагрузка);

	Результат = МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("CheckHealth",
	                                                                      ВходныеПараметры,
	                                                                      ВыходныеПараметры,
	                                                                      Идентификатор,
	                                                                      времПараметрыУстройства);

	Если Результат Тогда
		ТекстСообщения = НСтр("ru = 'Тест успешно выполнен.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	Иначе
		ДополнительноеОписание = ?(ТипЗнч(ВыходныеПараметры) = Тип("Массив")
		                           И ВыходныеПараметры.Количество() >= 2,
		                           НСтр("ru = 'Дополнительное описание:'") + " " + ВыходныеПараметры[1],
		                           "");


		ТекстСообщения = НСтр("ru = 'Тест не пройден.%ПереводСтроки%%ДополнительноеОписание%'");
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ПереводСтроки%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                  "",
		                                                                  Символы.ПС));
		ТекстСообщения = СтрЗаменить(ТекстСообщения, "%ДополнительноеОписание%", ?(ПустаяСтрока(ДополнительноеОписание),
		                                                                           "",
		                                                                           ДополнительноеОписание));
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПроверкаДоступности()

	Элементы.Порт.Доступность          		= (Интерфейс = 0);
	Элементы.Скорость.Доступность      		= (Интерфейс = 0);
	Элементы.Таймаут.Доступность      		= (Интерфейс = 0);
	Элементы.IPАдрес.Доступность       		= Не (Интерфейс = 0);
	Элементы.UDPПортПолучателя.Доступность  = Не (Интерфейс = 0);
	Элементы.UDPТаймаут.Доступность         = Не (Интерфейс = 0);
           
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьИнформациюОДрайвере()

	ВходныеПараметры  = Неопределено;
	ВыходныеПараметры = Неопределено;

	времПараметрыУстройства = Новый Структура();
	времПараметрыУстройства.Вставить("Интерфейс"    		, Интерфейс);
	времПараметрыУстройства.Вставить("Порт"     	 		, Порт);
	времПараметрыУстройства.Вставить("Скорость" 	  		, Скорость);
	времПараметрыУстройства.Вставить("Таймаут"				, Таймаут);	
	времПараметрыУстройства.Вставить("IPАдрес"				, IPАдрес);
	времПараметрыУстройства.Вставить("UDPПортПолучателя"	, UDPПортПолучателя);
	времПараметрыУстройства.Вставить("UDPТаймаут"      		, UDPТаймаут);
	времПараметрыУстройства.Вставить("Пароль"				, Пароль);
	времПараметрыУстройства.Вставить("Наименование"			, Наименование);
	времПараметрыУстройства.Вставить("БыстраяЗагрузка"		, БыстраяЗагрузка);
	
	Если МенеджерОборудованияКлиент.ВыполнитьДополнительнуюКоманду("ПолучитьВерсиюДрайвера",
	                                                               ВходныеПараметры,
	                                                               ВыходныеПараметры,
	                                                               Идентификатор,
	                                                               времПараметрыУстройства) Тогда
		Драйвер = ВыходныеПараметры[0];
		Версия  = ВыходныеПараметры[1];
	Иначе
		Драйвер = ВыходныеПараметры[2];
		Версия  = НСтр("ru='Не определена'");
	КонецЕсли;

	Элементы.Драйвер.ЦветТекста = ?(Драйвер = НСтр("ru='Не установлен'"), ЦветОшибки, ЦветТекста);
	Элементы.Версия.ЦветТекста  = ?(Версия  = НСтр("ru='Не определена'"), ЦветОшибки, ЦветТекста);

	Элементы.УстановитьДрайвер.Доступность = Не (Драйвер = НСтр("ru='Установлен'"));

	ПроверкаДоступности();
	
КонецПроцедуры

#КонецОбласти