#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Сертификат = Параметры.Сертификат;
	
	Если Не ЗначениеЗаполнено(Сертификат) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СпособДоставкиПаролей = "phone";
	
	Результат = СервисКриптографии.ПолучитьНастройкиПолученияВременныхПаролей(Сертификат.Идентификатор);
	Телефон          = Результат.Телефон;
	ЭлектроннаяПочта = Результат.ЭлектроннаяПочта;
	
	Получатель = Телефон;
	
	ПарольОтправлен = ПолучитьПарольНаСервере();
	
	Элементы.ИзменитьПолучателя.Заголовок = НСтр("ru = 'Изменить номер'");
	Элементы.ПолучитьВременныйПарольДругимСпособом.Видимость = Ложь;	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ПарольОтправлен Тогда	
		ЗапуститьОбратныйОтсчет();
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Сервис отправки SMS-сообщений временно недоступен. Повторите попытку позже.'"));
		Отказ = Истина;
	КонецЕсли; 
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ПарольПриИзменении(Элемент)
	
	ПарольИзменениеТекстаРедактирования(Элемент, Элемент.ТекстРедактирования, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ПарольИзменениеТекстаРедактирования(Элемент, Текст, СтандартнаяОбработка)
	
	Пароль = СокрЛП(Текст);
	Если ЗначениеЗаполнено(Пароль) И СтрДлина(Пароль) = 6 Тогда
		ОтключитьОбработчикОжидания("Подключаемый_ПроверитьПароль");
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьПароль", 1.5, Истина);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ИзменитьПолучателя(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Состояние", "ИзменениеНастроекПолученияВременныхПаролей");
	ПараметрыФормы.Вставить("Сертификат", Сертификат);
	
	Закрыть(ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПарольПовторно(Команда)

	Пароль = Неопределено;
	ЗапуститьОбратныйОтсчет();

	ПарольОтправлен = ПолучитьПарольНаСервере(Истина);
	
	УправлениеФормой(ЭтаФорма);
	
	ПодключитьОбработчикОжидания("Подключаемый_АктивироватьПолеДляВводаПароля", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подтвердить(Команда)
	
	ОчиститьСообщения();
	ТекстОшибки = "";
	Пароль = СокрЛП(Пароль);
	Если ЗначениеЗаполнено(Пароль) И СтрДлина(Пароль) = 6 Тогда
		Попытка
			ПодтвердитьНаСервере();
			Закрыть(Новый Структура("Состояние", "ПарольПринят"));
		Исключение
			ТекстИсключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			Если СтрНайти(ТекстИсключения, "Invalid password") Тогда
				ТекстОшибки = НСтр("ru = 'Указан неверный пароль'");
			ИначеЕсли СтрНайти(ТекстИсключения, "MaxAttemptsInputPasswordExceededError") Тогда
				Закрыть(Новый Структура("Состояние, ОписаниеОшибки", "ПарольНеПринят", НСтр("ru = 'Превышен лимит попыток ввода пароля'"))); 
			ИначеЕсли СтрНайти(ТекстИсключения, "PasswordExpiredError") Тогда
				Закрыть(Новый Структура("Состояние, ОписаниеОшибки", "ПарольНеПринят", НСтр("ru = 'Срок действия пароля истек'")));
			Иначе 
				Закрыть(Новый Структура("Состояние, ОписаниеОшибки", "ПарольНеПринят", НСтр("ru = 'Выполнение операции временно невозможно'")));
			КонецЕсли;
		КонецПопытки;		
	ИначеЕсли ЗначениеЗаполнено(Пароль) И СтрДлина(Пароль) <> 6 Тогда
		ТекстОшибки = НСтр("ru = 'Пароль должен состоять из 6 цифр'");
	Иначе
		ТекстОшибки = НСтр("ru = 'Пароль не указан'");
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьВременныйПарольДругимСпособом(Команда)
	
	Если СпособДоставкиПаролей = "phone" Тогда
		СпособДоставкиПаролей = "email";
	Иначе
		СпособДоставкиПаролей = "phone";
	КонецЕсли;
	
	ОтправитьПарольПовторно(Неопределено);
	
	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура Подключаемый_ПроверитьПароль()
	
	Подтвердить(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АктивироватьПолеДляВводаПароля()
	
	Пароль = Неопределено;
	Элементы.Пароль.ОбновитьТекстРедактирования();
	ТекущийЭлемент = Элементы.Пароль;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьПарольНаСервере(Повторно = Ложь)
	
	Попытка
		Результат = СервисКриптографииСлужебный.ПолучитьВременныйПароль(
			Сертификат.Идентификатор, 
			Повторно, 
			СпособДоставкиПаролей);
		Таймер = Результат.ЗадержкаПередПовторнойОтправкой;
	Исключение
		Возврат Ложь;
	КонецПопытки;
		
	Возврат Истина;
	
КонецФункции

&НаКлиенте
Процедура ЗапуститьОбратныйОтсчет()
	
	ПодключитьОбработчикОжидания("Подключаемый_ОбработчикОбратногоОтсчета", 1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбработчикОбратногоОтсчета()
	
	Таймер = Таймер - 1;
	Если Таймер >= 0 Тогда
		НадписьОбратногоОтсчета = СтрШаблон(НСтр("ru = 'Запросить пароль повторно можно будет через %1 сек.'"), Таймер);
		ПодключитьОбработчикОжидания("Подключаемый_ОбработчикОбратногоОтсчета", 1, Истина);		
	Иначе
		НадписьОбратногоОтсчета = "";
		ПарольОтправлен = Ложь;		
		Элементы.ПолучитьВременныйПарольДругимСпособом.Видимость = ЗначениеЗаполнено(ЭлектроннаяПочта);
		Элементы.ИзменитьПолучателя.Заголовок = НСтр("ru = 'Изменить адрес'");
		
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.НадписьОбратногоОтсчета.Видимость = Форма.ПарольОтправлен;	
	Элементы.ОтправитьПарольПовторно.Видимость = Не Форма.ПарольОтправлен;
	
	Если Форма.СпособДоставкиПаролей = "phone" Тогда
		Элементы.Пояснение.Заголовок = НСтр("ru = 'Пароль отправлен в SMS-сообщении на номер'");
		ТекстКоманды = НСтр("ru = 'Отправить пароль на %1'");
		Элементы.ИзменитьПолучателя.Заголовок = НСтр("ru = 'Изменить номер'");
		Элементы.ПолучитьВременныйПарольДругимСпособом.Заголовок = СтрШаблон(ТекстКоманды, Форма.ЭлектроннаяПочта);
		Форма.Получатель = Форма.Телефон;
		Элементы.Пароль.ПодсказкаВвода = НСтр("ru = 'Введите пароль из SMS'");
	ИначеЕсли Форма.СпособДоставкиПаролей = "email" Тогда
		Элементы.Пояснение.Заголовок = НСтр("ru = 'Пароль отправлен в письме на адрес'");
		ТекстКоманды = НСтр("ru = 'Отправить пароль на %1'");
		Элементы.ИзменитьПолучателя.Заголовок = НСтр("ru = 'Изменить адрес'");
		Элементы.ПолучитьВременныйПарольДругимСпособом.Заголовок = СтрШаблон(ТекстКоманды, Форма.Телефон);
		Форма.Получатель = Форма.ЭлектроннаяПочта;
		Элементы.Пароль.ПодсказкаВвода = НСтр("ru = 'Введите пароль из письма'");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция ПодтвердитьНаСервере()
	
	Возврат СервисКриптографииСлужебный.ПолучитьСеансовыйКлюч(Сертификат.Идентификатор, Пароль);
	
КонецФункции

#КонецОбласти