&НаСервере
Перем мДерево, мУжеВСписке;

&НаСервере
Перем мКэшРеквизитовДокумента;

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Процедура вывода дерева структуры подчиненности документа.
//
Процедура ВывестиДеревоДокументов()
	
	Попытка
	ДеревоДокументов.ПолучитьЭлементы().Очистить();
	мДерево = ДеревоДокументов;	
	мУжеВСписке = Новый Соответствие;
	мКэшРеквизитовДокумента = Новый Соответствие;
	
	ВывестиРодительскиеДокументы(ДокументСсылка);
	
	ВывестиПодчиненныеДокументы(мДерево);
	
	Исключение
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
// Процедура выполняет вывод родительских документов.
//
Процедура ВывестиРодительскиеДокументы(ДокументСсылка)
	
	МетаданныеДокумента = ДокументСсылка.Метаданные();
	СписокРеквизитов = Новый СписокЗначений;
	
	Для Каждого Реквизит ИЗ МетаданныеДокумента.Реквизиты Цикл
		ТипыРеквизита = Реквизит.Тип.Типы();
		Для Каждого ТекущийТип ИЗ ТипыРеквизита Цикл
			МетаданныеРеквизита = Метаданные.НайтиПоТипу(ТекущийТип);
			
			Если МетаданныеРеквизита<>Неопределено И Метаданные.Документы.Содержит(МетаданныеРеквизита) 
				 И ПравоДоступа("Чтение", МетаданныеРеквизита) Тогда
				Попытка
					ЗначениеРеквизита = ДокументСсылка[Реквизит.Имя];
				Исключение
					Прервать;
				КонецПопытки;
				ЕСли ЗначениеРеквизита<>Неопределено И НЕ ЗначениеРеквизита.Пустая() И ТипЗнч(ЗначениеРеквизита) = ТекущийТип 
					 И мУжеВСписке[ЗначениеРеквизита] = Неопределено И СписокРеквизитов.НайтиПоЗначению(ДокументСсылка[Реквизит.Имя]) = Неопределено Тогда
					Попытка
						СписокРеквизитов.Добавить(ЗначениеРеквизита,Формат(ЗначениеРеквизита.Дата,"ДФ=yyyyMMddЧЧММсс"));
					Исключение
						 ОтладкаТекстОшибки = ОписаниеОшибки();
					КонецПопытки;
				КонецЕсли;
			КонецЕсли;
			
		КонецЦикла;
	КонецЦикла;
	
	Для Каждого ТЧ Из МетаданныеДокумента.ТабличныеЧасти Цикл
		
		Если МетаданныеДокумента = Метаданные.Документы.СверкаВзаиморасчетов Тогда
			
			Прервать;
			
		КонецЕсли;
		
		СтрРеквизитов = "";
		
		Попытка
			СодержимоеТЧ = ДокументСсылка[ТЧ.Имя].Выгрузить();
		Исключение
			Прервать;
		КонецПопытки;
		
		Для Каждого Реквизит ИЗ ТЧ.Реквизиты Цикл
			ТипыРеквизита = Реквизит.Тип.Типы();
			Для Каждого ТекущийТип ИЗ ТипыРеквизита Цикл
				МетаданныеРеквизита = Метаданные.НайтиПоТипу(ТекущийТип);
				Если МетаданныеРеквизита<>Неопределено И Метаданные.Документы.Содержит(МетаданныеРеквизита) 
					И ПравоДоступа("Чтение", МетаданныеРеквизита) Тогда
					СтрРеквизитов = СтрРеквизитов + ?(СтрРеквизитов = "", "", ", ") + Реквизит.Имя;
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		СодержимоеТЧ.Свернуть(СтрРеквизитов);
		Для Каждого КолонкаТЧ ИЗ СодержимоеТЧ.Колонки Цикл
			Для Каждого СтрокаТЧ ИЗ СодержимоеТЧ Цикл
				Попытка
					ЗначениеРеквизита = СтрокаТЧ[КолонкаТЧ.Имя];
				Исключение
					Продолжить;
				КонецПопытки;
				МетаданныеЗначения = Метаданные.НайтиПоТипу(ТипЗнч(ЗначениеРеквизита));
				Если МетаданныеЗначения = Неопределено Тогда
					// базовый тип
					Продолжить;
				КонецЕсли;
				
				ЕСли ЗначениеРеквизита<>Неопределено И НЕ ЗначениеРеквизита.Пустая()
					 И Метаданные.Документы.Содержит(МетаданныеЗначения)
					 И мУжеВСписке[ЗначениеРеквизита] = Неопределено Тогда
					Если СписокРеквизитов.НайтиПоЗначению(ЗначениеРеквизита) = Неопределено Тогда
						Попытка
							СписокРеквизитов.Добавить(ЗначениеРеквизита,Формат(ЗначениеРеквизита.Дата,"ДФ=yyyyMMddЧЧММсс"));
						Исключение
							ОтладкаТекстОшибки = ОписаниеОшибки();
						КонецПопытки;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	СписокРеквизитов.СортироватьПоПредставлению();
	мУжеВСписке.Вставить(ДокументСсылка, Истина);
	
	Если СписокРеквизитов.Количество() = 1 Тогда
		ВывестиРодительскиеДокументы(СписокРеквизитов[0].Значение);
	ИначеЕсли СписокРеквизитов.Количество() > 1 Тогда
		ВывестиБезРодителей(СписокРеквизитов);
	КонецЕсли;
	
	СтрокаДерева = мДерево.ПолучитьЭлементы().Добавить();
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ Ссылка, Проведен, ПометкаУдаления, Представление, #Валюта, #Сумма, #Комментарий, """ + МетаданныеДокумента.Имя + """ КАК Метаданные
						   | ИЗ Документ."+МетаданныеДокумента.Имя + " ГДЕ Ссылка = &Ссылка");
						   
	Если МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаДокумента КАК ВалютаДокумента");
	ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ВалютаДенежныхСредств") <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаДенежныхСредств КАК ВалютаДокумента");
	// Эквайринг
	ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ВалютаРасчетов") <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаРасчетов КАК ВалютаДокумента");
	// Конец Эквайринг
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "NULL КАК ВалютаДокумента");
	КонецЕсли;
	
	Если МетаданныеДокумента.Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Сумма", "СуммаДокумента КАК СуммаДокумента");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Сумма", "0 КАК СуммаДокумента");
	КонецЕсли;
	
	Если МетаданныеДокумента.Реквизиты.Найти("Комментарий") <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", "Комментарий КАК Комментарий");
	ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("Тема") <> Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", 
			"ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Тема) = ТИП(Справочник.ТемыСобытий) ТОГДА ВЫРАЗИТЬ(Тема КАК Справочник.ТемыСобытий).Наименование ИНАЧЕ ВЫРАЗИТЬ(Тема КАК СТРОКА) КОНЕЦ КАК Комментарий");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", """"" КАК Комментарий");
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	Выборка  = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтрокаДерева.Ссылка = Выборка.Ссылка;
		СтрокаДерева.ДокументПредставление = Выборка.Представление;
		СтрокаДерева.ВидДокумента = Выборка.Метаданные;
		СтрокаДерева.ВалютаДокумента = Выборка.ВалютаДокумента;
		СтрокаДерева.СуммаДокумента = Выборка.СуммаДокумента;
		СтрокаДерева.Комментарий = ПреобразоватьМногострочнуюСтроку(Выборка.Комментарий);
		СтрокаДерева.Проведен = Выборка.Проведен;
		СтрокаДерева.ПометкаУдаления = Выборка.ПометкаУдаления;
		СтрокаДерева.Картинка = НомерКартинки(СтрокаДерева);
		СтрокаДерева.ПроведениеРазрешено = Выборка.Ссылка.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить;
	Иначе
		СтрокаДерева.Ссылка = ДокументСсылка;
		СтрокаДерева.ДокументПредставление = Строка(ДокументСсылка);
		СтрокаДерева.ВалютаДокумента = Выборка.ВалютаДокумента;
		СтрокаДерева.СуммаДокумента = Выборка.СуммаДокумента;
		СтрокаДерева.Комментарий = ПреобразоватьМногострочнуюСтроку(Выборка.Комментарий);
		СтрокаДерева.Проведен = Выборка.Проведен;
		СтрокаДерева.ПометкаУдаления = Выборка.ПометкаУдаления;
		СтрокаДерева.Картинка = НомерКартинки(СтрокаДерева);
		СтрокаДерева.ПроведениеРазрешено = ДокументСсылка.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить;
	КонецЕсли;
	
	мДерево = СтрокаДерева;
	
КонецПроцедуры

&НаСервере
// Процедура осуществляет вывод родительских документов с ограничением по уровню в дереве.
//
Процедура ВывестиБезРодителей(СписокДокументов)
	
	Для Каждого ЭлементСписка Из СписокДокументов Цикл
		
		МетаданныеДокумента = ЭлементСписка.Значение.Метаданные();
		
		Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ Ссылка, Проведен, ПометкаУдаления, Представление, #Валюта, #Сумма, #Комментарий, """ + МетаданныеДокумента.Имя + """ КАК Метаданные
		| ИЗ Документ."+МетаданныеДокумента.Имя + " ГДЕ Ссылка = &Ссылка");
		
		Если МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаДокумента КАК ВалютаДокумента");
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ВалютаДенежныхСредств") <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаДенежныхСредств КАК ВалютаДокумента");
		// Эквайринг
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("ВалютаРасчетов") <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаРасчетов КАК ВалютаДокумента");
		// Конец Эквайринг
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "NULL КАК ВалютаДокумента");
		КонецЕсли;
		
		Если МетаданныеДокумента.Реквизиты.Найти("СуммаДокумента") <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Сумма", "СуммаДокумента КАК СуммаДокумента");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Сумма", "0 КАК СуммаДокумента");
		КонецЕсли;
		
		Если МетаданныеДокумента.Реквизиты.Найти("Комментарий") <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", "Комментарий КАК Комментарий");
		ИначеЕсли МетаданныеДокумента.Реквизиты.Найти("Тема") <> Неопределено Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", 
				"ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Тема) = ТИП(Справочник.ТемыСобытий) ТОГДА ВЫРАЗИТЬ(Тема КАК Справочник.ТемыСобытий).Наименование ИНАЧЕ ВЫРАЗИТЬ(Тема КАК СТРОКА) КОНЕЦ КАК Комментарий");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", """"" КАК Комментарий");
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Ссылка", ЭлементСписка.Значение);
		
		Выборка  = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			Если мУжеВСписке[Выборка.Ссылка] = Неопределено Тогда
				СтрокаДерева = мДерево.ПолучитьЭлементы().Добавить();
				СтрокаДерева.Ссылка= Выборка.Ссылка;
				СтрокаДерева.ВалютаДокумента= Выборка.ВалютаДокумента;
				СтрокаДерева.СуммаДокумента= Выборка.СуммаДокумента;
				СтрокаДерева.Комментарий = ПреобразоватьМногострочнуюСтроку(Выборка.Комментарий);
				СтрокаДерева.ДокументПредставление = Выборка.Представление;
				СтрокаДерева.ВидДокумента = Выборка.Метаданные;
				СтрокаДерева.Проведен = Выборка.Проведен;
				СтрокаДерева.ПометкаУдаления = Выборка.ПометкаУдаления;
				СтрокаДерева.ОграничениеПоРодителям = Истина;
				СтрокаДерева.Картинка = НомерКартинки(СтрокаДерева);
				мУжеВСписке.Вставить(Выборка.Ссылка, Истина);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	мДерево = СтрокаДерева;
	
КонецПроцедуры

&НаСервере
// Процедура осуществляет вывод подчиненных документов.
//
Процедура ВывестиПодчиненныеДокументы(СтрокаДерева)
	
	ТекущийДокумент = СтрокаДерева.Ссылка;
	Таблица = ПолучитьСписокПодчиненныхДокументов(ТекущийДокумент);
	КэшПоТипамДокументов = Новый Соответствие;
	
	Для Каждого СтрокаТаблицы ИЗ Таблица Цикл
		МетаданныеДокумента = СтрокаТаблицы.Ссылка.Метаданные();
		Если НЕ ПравоДоступа("Чтение", МетаданныеДокумента) Тогда
			Продолжить;
		КонецЕсли;
		ИмяДокумента = МетаданныеДокумента.Имя;
		СинонимДокумента = МетаданныеДокумента.Синоним;
		
		ДополнитьКэшМетаданных(МетаданныеДокумента, ИмяДокумента);
		
		СтруктураТипа = КэшПоТипамДокументов[ИмяДокумента];
		Если СтруктураТипа = Неопределено Тогда
			СтруктураТипа = Новый Структура("Синоним, МассивСсылок", СинонимДокумента, Новый Массив);
			КэшПоТипамДокументов.Вставить(ИмяДокумента, СтруктураТипа);
		КонецЕсли;
		СтруктураТипа.МассивСсылок.Добавить(СтрокаТаблицы.Ссылка);
	КонецЦикла;
	
	Если КэшПоТипамДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстЗапросаНачало = "ВЫБРАТЬ РАЗРЕШЕННЫЕ * ИЗ (";
	ТекстЗапросаКонец = ") КАК ПодчиненныеДокументы УПОРЯДОЧИТЬ ПО ПодчиненныеДокументы.Дата";
	Запрос = Новый Запрос;
	Для Каждого КлючИЗначение ИЗ КэшПоТипамДокументов Цикл
		Запрос.Текст = Запрос.Текст + ?(Запрос.Текст = "", "
					|ВЫБРАТЬ ", "
					|ОБЪЕДИНИТЬ ВСЕ
					|ВЫБРАТЬ") + "
					|Дата, Ссылка, Проведен, ПометкаУдаления, Представление,  """ + КлючИЗначение.Ключ + """ КАК Метаданные, #Валюта, #Сумма, #Комментарий 
					|ИЗ Документ." + КлючИЗначение.Ключ + "
					|ГДЕ Ссылка В (&" + КлючИЗначение.Ключ + ")";
					
		Если мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["ВалютаДокумента"] Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаДокумента КАК ВалютаДокумента");
		ИначеЕсли мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["ВалютаДенежныхСредств"] Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаДенежныхСредств КАК ВалютаДокумента");
		// Эквайринг
		ИначеЕсли мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["ВалютаРасчетов"] Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "ВалютаРасчетов КАК ВалютаДокумента");
		// Конец Эквайринг
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Валюта", "NULL КАК ВалютаДокумента");
		КонецЕсли;
		
		Если мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["СуммаДокумента"] Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Сумма", "СуммаДокумента КАК СуммаДокумента");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Сумма", "0 КАК СуммаДокумента");
		КонецЕсли;
		
		Если мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["Комментарий"] Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", "Комментарий КАК Комментарий");
		ИначеЕсли мКэшРеквизитовДокумента[КлючИЗначение.Ключ]["Тема"] Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", 
				"ВЫБОР КОГДА ТИПЗНАЧЕНИЯ(Тема) = ТИП(Справочник.ТемыСобытий) ТОГДА ВЫРАЗИТЬ(Тема КАК Справочник.ТемыСобытий).Наименование ИНАЧЕ ВЫРАЗИТЬ(Тема КАК СТРОКА) КОНЕЦ КАК Комментарий");
		Иначе
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "#Комментарий", """"" КАК Комментарий");
		КонецЕсли;
		
		Запрос.УстановитьПараметр(КлючИЗначение.Ключ, КлючИЗначение.Значение.МассивСсылок);
	КонецЦикла;
	
	Запрос.Текст = ТекстЗапросаНачало + Запрос.Текст + ТекстЗапросаКонец;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если мУжеВСписке[Выборка.Ссылка] = Неопределено Тогда
			НоваяСтрока = СтрокаДерева.ПолучитьЭлементы().Добавить();
			НоваяСтрока.Ссылка = Выборка.Ссылка;
			НоваяСтрока.СуммаДокумента = Выборка.СуммаДокумента;
			НоваяСтрока.ВалютаДокумента = Выборка.ВалютаДокумента;
			НоваяСтрока.Комментарий = ПреобразоватьМногострочнуюСтроку(Выборка.Комментарий);
			НоваяСтрока.ДокументПредставление = Выборка.Представление;
			НоваяСтрока.Проведен = Выборка.Проведен;
			НоваяСтрока.ПометкаУдаления = Выборка.ПометкаУдаления;
			НоваяСтрока.Картинка = НомерКартинки(НоваяСтрока);
			мУжеВСписке.Вставить(Выборка.Ссылка, Истина);
			ВывестиПодчиненныеДокументы(НоваяСтрока);
			НоваяСтрока.ВидДокумента = Выборка.Метаданные;
			НоваяСтрока.ПроведениеРазрешено = Выборка.Ссылка.Метаданные().Проведение = Метаданные.СвойстваОбъектов.Проведение.Разрешить;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
// Процедура выполняет открытие формы текущего документа.
//                                                         
Процедура ОткрытьФормуДокумента()
	
	Попытка
		
		СтруктураОткрытия = Новый Структура("Ключ", Элементы.ДеревоДокументов.ТекущиеДанные.Ссылка);
		Форма = ПолучитьФорму("Документ." + Элементы.ДеревоДокументов.ТекущиеДанные.ВидДокумента + ".ФормаОбъекта", СтруктураОткрытия);
		
		Если Элементы.ДеревоДокументов.ТекущиеДанные.Ссылка = ДокументСсылка И Форма.Открыта() Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Документ уже открыт!'");
			Сообщение.Сообщить();
		КонецЕсли;
		
		Форма.Открыть();
		
	Исключение
		
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ДокументСсылка, ОписаниеОшибки());
		
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
// Функция выполняет поиск подчиненных документов текущего документа.
//
Функция ПолучитьСписокПодчиненныхДокументов(ДокументОснование) Экспорт
		
	Запрос = Новый Запрос;
	ТекстЗапроса = "";
	
	Для Каждого ЭлементСостава ИЗ Метаданные.КритерииОтбора.СтруктураПодчиненности.Состав Цикл
		
		Если НЕ ЭлементСостава.Тип.СодержитТип(ТипЗнч(ДокументОснование)) Тогда
			Продолжить;
		КонецЕсли;
		
		ПутьКДанным = ЭлементСостава.ПолноеИмя();
		СтруктураПутьКДанным = РазобратьПутьКОбъектуМетаданных(ПутьКДанным);
		
		ЕСли НЕ ПравоДоступа("Чтение", СтруктураПутьКДанным.Метаданные) Тогда
			Продолжить;
		КонецЕсли;
		
		ИмяОбъекта = СтруктураПутьКДанным.ТипОбъекта + "." + СтруктураПутьКДанным.ВидОбъекта;
		
		ТекущаяСтрокаГДЕ = "ГДЕ " + СтруктураПутьКДанным.ВидОбъекта + "." +СтруктураПутьКДанным.ИмяРеквизита + " = &ЗначениеКритерияОтбора";
			
		ИмяТЧ = Лев(СтруктураПутьКДанным.ИмяРеквизита, СтрНайти(СтруктураПутьКДанным.ИмяРеквизита, ".")-1);
		ИмяРеквизита = Лев(СтруктураПутьКДанным.ИмяРеквизита, СтрНайти(СтруктураПутьКДанным.ИмяРеквизита, ".")-1);
		ТекстЗапроса = ТекстЗапроса + ?(ТекстЗапроса = "", "ВЫБРАТЬ РАЗРЕШЕННЫЕ", "ОБЪЕДИНИТЬ
		|ВЫБРАТЬ") + "
		|" + СтруктураПутьКДанным.ВидОбъекта +".Ссылка ИЗ " + ИмяОбъекта + "." + СтруктураПутьКДанным.ИмяТаблЧасти + " КАК " + СтруктураПутьКДанным.ВидОбъекта + "
		|" + СтрЗаменить(ТекущаяСтрокаГДЕ, "..", ".") + "
		|";
		
	КонецЦикла;
	
	Если ТекстЗапроса<>"" Тогда
		Запрос.Текст = ТекстЗапроса;
		Запрос.УстановитьПараметр("ЗначениеКритерияОтбора", ДокументОснование);
		Возврат Запрос.Выполнить().Выгрузить();
	Иначе	
		Возврат Новый ТаблицаЗначений;
	КонецЕсли;
	
КонецФункции

&НаСервере
// Функция возвращает путь к объекту метаданных
// ТипОбъектаМетаданных.ИмяДокумента.ТабличнаяЧасть.ИмяТабличнойЧасти.Реквизит.ИмяРеквизита.
// ТипОбъектаМетаданных должен быть Справочник или Документ.
//
// Параметры:
//  ПутьКДанным - строка.
//
// Возвращаемое значение:
//  Структура - путь к объекту метаданных
//
Функция РазобратьПутьКОбъектуМетаданных(ПутьКДанным) Экспорт
	
	Структура = Новый Структура;
	
	СоответствиеИмен = Новый Массив();
	СоответствиеИмен.Добавить("ТипОбъекта");
	СоответствиеИмен.Добавить("ВидОбъекта");
	СоответствиеИмен.Добавить("ПутьКДанным");
	СоответствиеИмен.Добавить("ИмяТаблЧасти");
	СоответствиеИмен.Добавить("ИмяРеквизита");
	
	Для индекс = 1 по 3 Цикл
		
		Точка = СтрНайти(ПутьКДанным, ".");
		ТекущееЗначение = Лев(ПутьКДанным, Точка-1);
		Структура.Вставить(СоответствиеИмен[индекс-1], ТекущееЗначение);
		ПутьКДанным = Сред(ПутьКДанным, Точка+1);
		
	КонецЦикла;
	
	ПутьКДанным = СтрЗаменить(ПутьКДанным, "Реквизит.", "");
	
	Если Структура.ПутьКДанным = "ТабличнаяЧасть" Тогда
		
		Для индекс = 4 по 5  Цикл 
			
			Точка = СтрНайти(ПутьКДанным, ".");
			Если Точка = 0 Тогда
				ТекущееЗначение = ПутьКДанным;
			Иначе
				ТекущееЗначение = Лев(ПутьКДанным, Точка-1);
			КонецЕсли;
			
			Структура.Вставить(СоответствиеИмен[индекс-1], ТекущееЗначение);
			ПутьКДанным = Сред(ПутьКДанным,  Точка+1);
			
		КонецЦикла;
		
	Иначе
		
		Структура.Вставить(СоответствиеИмен[3], "");
		Структура.Вставить(СоответствиеИмен[4], ПутьКДанным);
		
	КонецЕсли;
	
	Если Структура.ТипОбъекта = "Документ" Тогда
		Структура.Вставить("Метаданные", Метаданные.Документы[Структура.ВидОбъекта]);
	Иначе
		Структура.Вставить("Метаданные", Метаданные.Справочники[Структура.ВидОбъекта]);
	КонецЕсли;
	
	Возврат Структура;
	
КонецФункции // РазобратьПутьКОбъектуМетаданных()

&НаКлиенте
// Процедура закрывает форму с предупреждением.
//
Процедура ЗакрытьФормуСПредупреждением(ТекстПредупреждения)
	
	ВызватьИсключение ТекстПредупреждения;
	
КонецПроцедуры

&НаСервере
// Процедура дополняет кэш метаданных.
//
Процедура ДополнитьКэшМетаданных(МетаданныеДокумента, ИмяДокумента)
	
	РеквизитыДокумента = мКэшРеквизитовДокумента[ИмяДокумента];
	Если РеквизитыДокумента = Неопределено Тогда
		РеквизитыДокумента = Новый Соответствие;
		РеквизитыДокумента.Вставить("ВалютаДокумента", МетаданныеДокумента.Реквизиты.Найти("ВалютаДокумента") <> Неопределено);
		РеквизитыДокумента.Вставить("ВалютаДенежныхСредств", МетаданныеДокумента.Реквизиты.Найти("ВалютаДенежныхСредств") <> Неопределено);
		РеквизитыДокумента.Вставить("ВалютаРасчетов", МетаданныеДокумента.Реквизиты.Найти("ВалютаРасчетов") <> Неопределено); // Эквайринг
		РеквизитыДокумента.Вставить("СуммаДокумента", МетаданныеДокумента.Реквизиты.Найти("СуммаДокумента") <> Неопределено);
		РеквизитыДокумента.Вставить("Комментарий", МетаданныеДокумента.Реквизиты.Найти("Комментарий") <> Неопределено);
		РеквизитыДокумента.Вставить("Тема", МетаданныеДокумента.Реквизиты.Найти("Тема") <> Неопределено);
		мКэшРеквизитовДокумента.Вставить(ИмяДокумента, РеквизитыДокумента);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
// Функция проверяет доступность изменяемого документа.
//
Функция ОсновнойДокументЕщеДоступен()
	
	ИмяТекущегоДокумента = ДокументСсылка.Метаданные().Имя;
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ Представление ИЗ Документ." + ИмяТекущегоДокумента + " ГДЕ Ссылка = &ТекущийДокумент";
	Запрос.УстановитьПараметр("ТекущийДокумент", ДокументСсылка);
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Функция возвращает номер картинки.
//
Функция НомерКартинки(СтрокаДерева)
	
	Если СтрокаДерева.ПометкаУдаления Тогда
		Возврат 2;
	ИначеЕсли СтрокаДерева.Проведен Тогда
		Возврат 1;
	Иначе	
	    Возврат 0;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
// Процедура обновляет доступность кнопок Провести и Отменить проведение.
//
Процедура ОбновитьДоступностьКнопок()
	
	ДанныеТекущейСтроки = Элементы.ДеревоДокументов.ТекущиеДанные;
	
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		ЗначениеСвойства = Элементы.ДеревоДокументов.ТекущиеДанные.ПроведениеРазрешено;
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Провести", "Доступность", ЗначениеСвойства);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ОтменаПроведения", "Доступность", ЗначениеСвойства);
		
	Конецесли;

КонецПроцедуры 

&НаСервере
// Функция осуществляет проведение выбранного документа.
//
Функция ПровестиСервер(ДокументСсылка)
	
	Объект = ДокументСсылка.ПолучитьОбъект();
	Попытка
	    Объект.Заблокировать();
		Объект.Записать(РежимЗаписиДокумента.Проведение, РежимПроведенияДокумента.Неоперативный);
		Объект.Разблокировать();
	Исключение
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Невозможно заблокировать документ.'");
		Сообщение.Сообщить();
	КонецПопытки; 

	Возврат Объект.Проведен;
	
КонецФункции

&НаСервере
// Функция осуществляет отмену проведения выбранного документа.
//
Функция ОтменаПроведенияСервер(ДокументСсылка)
	
	Объект = ДокументСсылка.ПолучитьОбъект();
	Попытка
	    Объект.Заблокировать();
		Объект.Записать(РежимЗаписиДокумента.ОтменаПроведения);
		Объект.Разблокировать();
	Исключение
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Невозможно заблокировать документ.'");
		Сообщение.Сообщить();
	КонецПопытки; 

	Возврат Объект.Проведен;
	
КонецФункции

&НаСервере
// Функция устанавливает пометку на удаление выбранного документа.
//
Функция УстановитьПометкуУдаленияСервер(ДокументСсылка)
	
	Объект = ДокументСсылка.ПолучитьОбъект();
	Попытка
		
		Объект.Заблокировать();
		Объект.УстановитьПометкуУдаления(НЕ Объект.ПометкаУдаления);
		Объект.Разблокировать();
		
	Исключение
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Невозможно заблокировать документ.'");
		Сообщение.Сообщить();
		
	КонецПопытки; 

	Возврат Новый Структура("ПометкаУдаления, Проведен", ДокументСсылка.ПометкаУдаления, ДокументСсылка.Проведен);
	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
// Функция преобразует многострочную строку в однострочную.
//
Функция ПреобразоватьМногострочнуюСтроку(МногострочнаяСтрока)
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(МногострочнаяСтрока, Символы.ПС, Истина);
	Возврат СтрСоединить(МассивСтрок, " ");
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.ДокументСсылка = Неопределено ИЛИ Параметры.ДокументСсылка.Пустая() Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;

	ДокументСсылка = Параметры.ДокументСсылка; 
	
	ВывестиДеревоДокументов();

КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриОткрытии формы.
//
Процедура ПриОткрытии(Отказ)
	 
	Элементы.ДеревоДокументов.ТекущаяСтрока = ДеревоДокументов.ПолучитьЭлементы()[0];

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события Перед началом изменения табличного поля ДеревоДокументов.
//
Процедура ДеревоДокументовПередНачаломИзменения(Элемент, Отказ)	
	ОткрытьФормуДокумента();
	Отказ = Истина;
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия Открыть.
//
Процедура ОткрытьДокумент(Команда)
	
	Если Элементы.ДеревоДокументов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуДокумента();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия Обновить.
//
Процедура Обновить(Команда)
	
	Если ОсновнойДокументЕщеДоступен() Тогда
		ВывестиДеревоДокументов(); 
	Иначе
		ЗакрытьФормуСПредупреждением(НСтр("ru = 'Документ, для которого сформирован отчет о структуре
									 |подчиненности был удален, или же стал недоступен.'"));
	КонецЕсли;		
	
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия ВывестиДляТекущего.
//
Процедура ВывестиДляТекущего(Команда)	
	
	Если Элементы.ДеревоДокументов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДокументСсылка = Элементы.ДеревоДокументов.ТекущиеДанные.Ссылка;
	Если ОсновнойДокументЕщеДоступен() Тогда
		ДеревоДокументов.ПолучитьЭлементы().Очистить();	
		ВывестиДеревоДокументов();
	Иначе
		ЗакрытьФормуСПредупреждением(НСтр("ru = 'Документ, для которого сформирован отчет о структуре
									 |подчиненности был удален, или же стал недоступен.'"));
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия НайтиВСписке.
//
Процедура НайтиВСписке(Команда)
	
	Если Элементы.ДеревоДокументов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		ОткрытьФорму("Документ."+Элементы.ДеревоДокументов.ТекущиеДанные.ВидДокумента + ".ФормаСписка", Новый Структура("ТекущаяСтрока", Элементы.ДеревоДокументов.ТекущиеДанные.Ссылка));
	Исключение
		УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(ДокументСсылка, ОписаниеОшибки());
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия Провести.
//
Процедура Провести(Кнопка)
		
	Если Элементы.ДеревоДокументов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка       
		Элементы.ДеревоДокументов.ТекущиеДанные.Проведен = ПровестиСервер(Элементы.ДеревоДокументов.ТекущиеДанные.Ссылка);
		Элементы.ДеревоДокументов.ТекущиеДанные.Картинка = НомерКартинки(Элементы.ДеревоДокументов.ТекущиеДанные);
	Исключение
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не удалось провести документ %Документ%!'");;
		Сообщение.Текст = СтрЗаменить(Сообщение.Текст, "%Документ%", Элементы.ДеревоДокументов.ТекущиеДанные.ДокументПредставление);
		Сообщение.Сообщить();
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия Отмена проведения.
//
Процедура ОтменаПроведения(Кнопка)
		
	Если Элементы.ДеревоДокументов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Элементы.ДеревоДокументов.ТекущиеДанные.Проведен Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		Элементы.ДеревоДокументов.ТекущиеДанные.Проведен = ОтменаПроведенияСервер(Элементы.ДеревоДокументов.ТекущиеДанные.Ссылка);
		Элементы.ДеревоДокументов.ТекущиеДанные.Картинка = НомерКартинки(Элементы.ДеревоДокументов.ТекущиеДанные);
	Исключение
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не удалось сделать непроведенным документ %Документ%!'");;
		Сообщение.Текст = СтрЗаменить(Сообщение.Текст, "%Документ%", Элементы.ДеревоДокументов.ТекущиеДанные.ДокументПредставление);
		Сообщение.Сообщить();
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриАктивизацииСтроки реквизита ДеревоДокументов.
//
Процедура ДеревоДокументовПриАктивизацииСтроки(Элемент)
	ОбновитьДоступностьКнопок();	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия УстановитьПометкуУдаления.
//
Процедура УстановитьПометкуУдаления(Кнопка)
	
	ТекущиеДанныеСтроки = Элементы.ДеревоДокументов.ТекущиеДанные;
	Если ТекущиеДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Попытка
		
		Результат = УстановитьПометкуУдаленияСервер(ТекущиеДанныеСтроки.Ссылка);
		ТекущиеДанныеСтроки.ПометкаУдаления = Результат.ПометкаУдаления;
		ТекущиеДанныеСтроки.Проведен		= Результат.Проведен;
		ТекущиеДанныеСтроки.Картинка		= НомерКартинки(ТекущиеДанныеСтроки);
		ОбновитьДоступностьКнопок();
		
	Исключение
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не удалось установить пометку удаления на документ %Документ%!'");;
		Сообщение.Текст = СтрЗаменить(Сообщение.Текст, "%Документ%", ТекущиеДанныеСтроки.ДокументПредставление);
		Сообщение.Сообщить();
		
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик действия Выбор дерева документов.
//
Процедура ДеревоДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Элементы.ДеревоДокументов.ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуДокумента();
	
КонецПроцедуры
