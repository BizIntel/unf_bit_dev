///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервере
// Формирует запрос по получению таблицы остатков планируемых платежей.
//
Функция ЗапросПоОстаткамПланируемыхПлатежей()

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫБОР
	|		КОГДА ПлатежныйКалендарь.ПериодСекунда < &РабочаяДата
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК ПорядокПлатежа,
	|	ВЫБОР
	|		КОГДА ПлатежныйКалендарь.ПериодСекунда < &РабочаяДата
	|			ТОГДА &ПросроченныеПлатежи
	|		ИНАЧЕ &ЗапланированныеПлатежи
	|	КОНЕЦ КАК СтатусПлатежа,
	|	ПлатежныйКалендарь.ПериодДень КАК День,
	|	ПлатежныйКалендарь.ПериодСекунда КАК ДатаПлатежа,
	|	ПлатежныйКалендарь.Регистратор.Дата КАК ДатаДокумента,
	|	ПлатежныйКалендарь.Регистратор КАК Документ,
	|	ПлатежныйКалендарь.Валюта,
	|	ПлатежныйКалендарь.Валюта КАК ВалютаПлатежа,
	|	ПлатежныйКалендарь.БанковскийСчетКасса,
	|	ПлатежныйКалендарь.ТипДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ПлатежныйКалендарь.СчетНаОплату = НЕОПРЕДЕЛЕНО
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПлатежныйКалендарь.Статья.Представление
	|		ИНАЧЕ ПлатежныйКалендарь.СчетНаОплату.Представление
	|	КОНЕЦ КАК Платеж,
	|	ПлатежныйКалендарь.СтатусУтвержденияПлатежа КАК СтатусУтвержденияПлатежа,
	|	ЛОЖЬ КАК СтрокаТекущийОстаток,
	|	ЕСТЬNULL(ПлатежныйКалендарь.Регистратор.Контрагент.Представление, """") КАК КонтрагентПредставление,
	|	ПлатежныйКалендарь.Статья.Представление КАК СтатьяПредставление,
	|	ПОДСТРОКА(ПлатежныйКалендарь.Регистратор.Комментарий, 1, 100) КАК Комментарий,
	|	&ТекстИтога КАК ТекстИтога,
	|	ПОДСТРОКА("""", 1, 200) КАК ДанныеПлатежа,
	|	МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот) КАК СуммаОборот,
	|	СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) КАК СуммаВсегоОборот,
	|	МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0)) КАК СуммаОплатыОборот,
	|	ВЫБОР
	|		КОГДА МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот) > 0
	|			ТОГДА ВЫБОР
	|					КОГДА СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) - МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0)) >= 0
	|						ТОГДА МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот)
	|					КОГДА СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) + МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот) - МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0)) < 0
	|						ТОГДА 0
	|					ИНАЧЕ СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) + МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот) - МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0))
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) - МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0)) <= 0
	|					ТОГДА МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот)
	|				КОГДА СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) + МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот) - МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0)) > 0
	|					ТОГДА 0
	|				ИНАЧЕ СУММА(ЕСТЬNULL(ПлатежныйКалендарьДвижения.СуммаОборот, 0)) + МАКСИМУМ(ПлатежныйКалендарь.СуммаОборот) - МАКСИМУМ(ЕСТЬNULL(ПлатежныйКалендарьОплата.СуммаОплатыОборот, 0))
	|			КОНЕЦ
	|	КОНЕЦ КАК Сумма,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ПлатежныйКалендарь.Регистратор ССЫЛКА Документ.ПеремещениеДСПлан
	|					И ПлатежныйКалендарь.СуммаОборот < 0
	|				ТОГДА -ПлатежныйКалендарь.СуммаОборот
	|			ИНАЧЕ ПлатежныйКалендарь.СуммаОборот
	|		КОНЕЦ) КАК СуммаПлатежа,
	|	0 КАК СуммаПриход,
	|	0 КАК СуммаРасход,
	|	0 КАК НачальныйОстатокПоСчету,
	|	0 КАК КонечныйОстатокПоСчету,
	|	0 КАК НачальныйОстатокПоВалюте,
	|	0 КАК КонечныйОстатокПоВалюте,
	|	0 КАК НачальныйОстатокПоТипуДенежныхСредств,
	|	0 КАК КонечныйОстатокПоТипуДенежныхСредств
	|ИЗ
	|	РегистрНакопления.ПлатежныйКалендарь.Обороты(&ДатаНачала, &ДатаОкончания, Авто, Организация = &Организация) КАК ПлатежныйКалендарь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлатежныйКалендарь.Обороты(
	|				&ДатаНачала,
	|				&ДатаОкончания,
	|				Авто,
	|				Организация = &Организация
	|					И СтатусУтвержденияПлатежа = ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден)) КАК ПлатежныйКалендарьДвижения
	|		ПО (ПлатежныйКалендарь.ПериодСекунда > ПлатежныйКалендарьДвижения.ПериодСекунда
	|				ИЛИ ПлатежныйКалендарь.ПериодСекунда = ПлатежныйКалендарьДвижения.ПериодСекунда
	|					И ПлатежныйКалендарь.Регистратор > ПлатежныйКалендарьДвижения.Регистратор)
	|			И (ВЫБОР
	|				КОГДА (ПлатежныйКалендарьДвижения.СчетНаОплату = НЕОПРЕДЕЛЕНО
	|						ИЛИ ПлатежныйКалендарьДвижения.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|						ИЛИ ПлатежныйКалендарьДвижения.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|						ИЛИ ПлатежныйКалендарьДвижения.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|						ИЛИ ПлатежныйКалендарьДвижения.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка))
	|						И (ПлатежныйКалендарь.СчетНаОплату = НЕОПРЕДЕЛЕНО
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка))
	|						И ПлатежныйКалендарьДвижения.Статья <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|						И ПлатежныйКалендарьДвижения.Статья = ПлатежныйКалендарь.Статья
	|					ТОГДА ИСТИНА
	|				КОГДА ПлатежныйКалендарьДвижения.СчетНаОплату <> НЕОПРЕДЕЛЕНО
	|						И ПлатежныйКалендарьДвижения.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|						И ПлатежныйКалендарьДвижения.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|						И ПлатежныйКалендарьДвижения.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|						И ПлатежныйКалендарьДвижения.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|						И ПлатежныйКалендарь.СчетНаОплату = ПлатежныйКалендарьДвижения.СчетНаОплату
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПлатежныйКалендарь.Обороты(
	|				&ДатаНачала,
	|				&ДатаОкончания,
	|				Авто,
	|				Организация = &Организация
	|					И СтатусУтвержденияПлатежа = ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден)) КАК ПлатежныйКалендарьОплата
	|		ПО (ПлатежныйКалендарь.СтатусУтвержденияПлатежа = ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден))
	|			И (ВЫБОР
	|				КОГДА (ПлатежныйКалендарьОплата.СчетНаОплату = НЕОПРЕДЕЛЕНО
	|						ИЛИ ПлатежныйКалендарьОплата.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|						ИЛИ ПлатежныйКалендарьОплата.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|						ИЛИ ПлатежныйКалендарьОплата.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|						ИЛИ ПлатежныйКалендарьОплата.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка))
	|						И (ПлатежныйКалендарь.СчетНаОплату = НЕОПРЕДЕЛЕНО
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка))
	|						И ПлатежныйКалендарьОплата.Статья <> ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|						И ПлатежныйКалендарьОплата.Статья = ПлатежныйКалендарь.Статья
	|					ТОГДА ИСТИНА
	|				КОГДА ПлатежныйКалендарьОплата.СчетНаОплату <> НЕОПРЕДЕЛЕНО
	|						И ПлатежныйКалендарьОплата.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|						И ПлатежныйКалендарьОплата.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|						И ПлатежныйКалендарьОплата.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|						И ПлатежныйКалендарьОплата.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|						И ПлатежныйКалендарь.СчетНаОплату = ПлатежныйКалендарьОплата.СчетНаОплату
	|					ТОГДА ИСТИНА
	|				ИНАЧЕ ЛОЖЬ
	|			КОНЕЦ)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ПлатежныйКалендарь.ПериодСекунда < &РабочаяДата
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ,
	|	ПлатежныйКалендарь.ПериодДень,
	|	ПлатежныйКалендарь.ПериодСекунда,
	|	ПлатежныйКалендарь.Регистратор,
	|	ПлатежныйКалендарь.Валюта,
	|	ПлатежныйКалендарь.БанковскийСчетКасса,
	|	ПлатежныйКалендарь.ТипДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ПлатежныйКалендарь.СчетНаОплату = НЕОПРЕДЕЛЕНО
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ИЛИ ПлатежныйКалендарь.СчетНаОплату = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ПлатежныйКалендарь.Статья.Представление
	|		ИНАЧЕ ПлатежныйКалендарь.СчетНаОплату.Представление
	|	КОНЕЦ,
	|	ПлатежныйКалендарь.Регистратор.Дата,
	|	ПОДСТРОКА(ПлатежныйКалендарь.Регистратор.Комментарий, 1, 100),
	|	ПлатежныйКалендарь.Статья.Представление,
	|	ВЫБОР
	|		КОГДА ПлатежныйКалендарь.ПериодСекунда < &РабочаяДата
	|			ТОГДА &ПросроченныеПлатежи
	|		ИНАЧЕ &ЗапланированныеПлатежи
	|	КОНЕЦ,
	|	ПлатежныйКалендарь.СтатусУтвержденияПлатежа,
	|	ЕСТЬNULL(ПлатежныйКалендарь.Регистратор.Контрагент.Представление, """"),
	|	ПлатежныйКалендарь.Валюта
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПорядокПлатежа,
	|	ПлатежныйКалендарь.ПериодДень,
	|	ПлатежныйКалендарь.ПериодСекунда,
	|	ПлатежныйКалендарь.Регистратор,
	|	ПлатежныйКалендарь.БанковскийСчетКасса";
	
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	Запрос.УстановитьПараметр("ДатаНачала", Отчет.ПериодПросроченныхПлатежей.ДатаНачала);
	Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(Отчет.ПериодБудущихПлатежей.ДатаОкончания));
	Запрос.УстановитьПараметр("РабочаяДата", Отчет.РабочаяДата);
	Запрос.УстановитьПараметр("ПросроченныеПлатежи", НСтр("ru = 'Итого просроченные платежи'"));
	Запрос.УстановитьПараметр("ЗапланированныеПлатежи", НСтр("ru = 'Итого запланированные платежи'"));
	Запрос.УстановитьПараметр("ТекстИтога", НСтр("ru = 'Всего платежи'"));
	
	Возврат Запрос;

КонецФункции // ЗапросПоОстаткамПланируемыхПлатежей()

&НаСервере
// Формирует запрос по получению фактических остатков денежных средств.
//
Функция ЗапросПоОстаткамДенежныхСредств()

	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	0 КАК ПорядокПлатежа,
	|	&СтатусПлатежа КАК СтатусПлатежа,
	|	&ТекстИтога КАК ТекстИтога,
	|	ДенежныеСредства.ТипДенежныхСредств,
	|	&РабочаяДата КАК День,
	|	&РабочаяДата КАК ДатаПлатежа,
	|	&Платеж КАК Платеж,
	|	ИСТИНА КАК СтрокаТекущийОстаток,
	|	ДенежныеСредства.БанковскийСчетКасса,
	|	ДенежныеСредства.Валюта,
	|	ДенежныеСредства.СуммаВалОстаток КАК КонечныйОстатокПоСчету,
	|	ДенежныеСредства.СуммаВалОстаток КАК КонечныйОстатокПоВалюте,
	|	ДенежныеСредства.СуммаВалОстаток КАК КонечныйОстатокПоТипуДенежныхСредств,
	|	0 КАК Сумма
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства.Остатки(, Организация = &Организация) КАК ДенежныеСредства
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДенежныеСредства.БанковскийСчетКасса";
	Запрос = Новый Запрос;
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("Организация", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	Запрос.УстановитьПараметр("РабочаяДата", Отчет.РабочаяДата);
	Запрос.УстановитьПараметр("СтатусПлатежа", НСтр("ru = 'Доступный остаток'"));
	Запрос.УстановитьПараметр("ТекстИтога", НСтр("ru = 'Всего платежи'"));
	Запрос.УстановитьПараметр("Платеж", НСтр("ru = 'Текущий остаток'"));
	
	Возврат Запрос;

КонецФункции // ЗапросПоОстаткамПланируемыхПлатежей()

&НаСервере
// Формирует таблицу планируемых платежей.
//
Функция ПолучитьТаблицуПланируемыхПлатежей()

	Запрос = ЗапросПоОстаткамПланируемыхПлатежей();
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаПлатежей = РезультатЗапроса.Выгрузить();
	
	СоответствиеОстатокПоСчету = Новый Соответствие;
	СоответствиеОстатокПоВалюте = Новый Соответствие;
	СоответствиеОстатокПоТипуДенежныхСредств = Новый Соответствие;
	
	ЗапросПоОстаткам = ЗапросПоОстаткамДенежныхСредств();
	РезультатЗапросаПоОстаткам = ЗапросПоОстаткам.Выполнить();
	Выборка = РезультатЗапросаПоОстаткам.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		НоваяСтрока = ТаблицаПлатежей.Вставить(0);
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		
		КлючСоответствия = Строка(Выборка.БанковскийСчетКасса) + Строка(Выборка.Валюта);
		СоответствиеОстатокПоСчету.Вставить(КлючСоответствия, Выборка.КонечныйОстатокПоСчету);
		
		КлючСоответствия = Строка(Выборка.Валюта);
		ОстатокПоВалюте = СоответствиеОстатокПоВалюте[КлючСоответствия];
		ОстатокПоВалюте = ?(ОстатокПоВалюте = Неопределено, 0, ОстатокПоВалюте);
		СоответствиеОстатокПоВалюте.Вставить(КлючСоответствия, ОстатокПоВалюте + Выборка.КонечныйОстатокПоВалюте);
		
		КлючСоответствия = Строка(Выборка.ТипДенежныхСредств) + Строка(Выборка.Валюта);
		ОстатокПоТипуДенежныхСредств = СоответствиеОстатокПоТипуДенежныхСредств[КлючСоответствия];
		ОстатокПоТипуДенежныхСредств = ?(ОстатокПоТипуДенежныхСредств = Неопределено, 0, ОстатокПоТипуДенежныхСредств);
		СоответствиеОстатокПоТипуДенежныхСредств.Вставить(КлючСоответствия, ОстатокПоТипуДенежныхСредств + Выборка.КонечныйОстатокПоТипуДенежныхСредств);
		
	КонецЦикла;
	
	Для каждого СтрокаТаблицы Из ТаблицаПлатежей Цикл
		
		Если СтрокаТаблицы.СтрокаТекущийОстаток Тогда
			
			КлючСоответствия = Строка(СтрокаТаблицы.ТипДенежныхСредств) + Строка(СтрокаТаблицы.Валюта);
			ОстатокПоТипуДенежныхСредств = СоответствиеОстатокПоТипуДенежныхСредств[КлючСоответствия];
			СтрокаТаблицы.КонечныйОстатокПоТипуДенежныхСредств = ОстатокПоТипуДенежныхСредств;
			
			КлючСоответствия = Строка(СтрокаТаблицы.Валюта);
			ОстатокПоВалюте = СоответствиеОстатокПоВалюте[КлючСоответствия];
			СтрокаТаблицы.КонечныйОстатокПоВалюте = ОстатокПоВалюте;
			
			Продолжить;
		КонецЕсли;
		
		ДанныеПлатежаСтрока = СтрокаТаблицы.КонтрагентПредставление
			+ ?(ПустаяСтрока(СтрокаТаблицы.СтатьяПредставление), "", ", " + СтрокаТаблицы.СтатьяПредставление)
			+ ?(ПустаяСтрока(СтрокаТаблицы.Комментарий), "", ", " + СтрокаТаблицы.Комментарий);
		
		Если Лев(ДанныеПлатежаСтрока, 1) = "," Тогда
			СтрокаТаблицы.ДанныеПлатежа = СокрЛП(Сред(ДанныеПлатежаСтрока, 2));
		Иначе
			СтрокаТаблицы.ДанныеПлатежа = СокрЛП(ДанныеПлатежаСтрока);
		КонецЕсли;
		
		// Если платеж полностью оплачен, он не должен выводится в отчет.
		Если СтрокаТаблицы.Сумма = 0
			И НЕ СтрокаТаблицы.СтрокаТекущийОстаток Тогда
			СтрокаТаблицы.Документ = NULL;
		КонецЕсли;
		
		Если СтрокаТаблицы.Сумма < 0 Тогда
			СтрокаТаблицы.СуммаРасход = -СтрокаТаблицы.Сумма;
		Иначе
			СтрокаТаблицы.СуммаПриход = СтрокаТаблицы.Сумма;
		КонецЕсли;
		
		// Рассчитаем текущий остаток по счету.
		КлючСоответствия = Строка(СтрокаТаблицы.БанковскийСчетКасса) + Строка(СтрокаТаблицы.Валюта);
		ОстатокПоСчету = СоответствиеОстатокПоСчету[КлючСоответствия];
		ОстатокПоСчету = ?(ОстатокПоСчету = Неопределено, 0, ОстатокПоСчету);
		СоответствиеОстатокПоСчету.Вставить(КлючСоответствия, ОстатокПоСчету + СтрокаТаблицы.Сумма);
		СтрокаТаблицы.НачальныйОстатокПоСчету = ОстатокПоСчету;
		СтрокаТаблицы.КонечныйОстатокПоСчету = ОстатокПоСчету + СтрокаТаблицы.Сумма;
		
		// Рассчитаем текущий остаток по валюте.
		КлючСоответствия = Строка(СтрокаТаблицы.Валюта);
		ОстатокПоВалюте = СоответствиеОстатокПоВалюте[КлючСоответствия];
		ОстатокПоВалюте = ?(ОстатокПоВалюте = Неопределено, 0, ОстатокПоВалюте);
		СоответствиеОстатокПоВалюте.Вставить(КлючСоответствия, ОстатокПоВалюте + СтрокаТаблицы.Сумма);
		СтрокаТаблицы.НачальныйОстатокПоВалюте = ОстатокПоВалюте;
		СтрокаТаблицы.КонечныйОстатокПоВалюте = ОстатокПоВалюте + СтрокаТаблицы.Сумма;
		
		// Рассчитаем текущий остаток по типу денежных средств.
		КлючСоответствия = Строка(СтрокаТаблицы.ТипДенежныхСредств) + Строка(СтрокаТаблицы.Валюта);
		ОстатокПоТипуДенежныхСредств = СоответствиеОстатокПоТипуДенежныхСредств[КлючСоответствия];
		ОстатокПоТипуДенежныхСредств = ?(ОстатокПоТипуДенежныхСредств = Неопределено, 0, ОстатокПоТипуДенежныхСредств);
		СоответствиеОстатокПоТипуДенежныхСредств.Вставить(КлючСоответствия, ОстатокПоТипуДенежныхСредств + СтрокаТаблицы.Сумма);
		СтрокаТаблицы.НачальныйОстатокПоТипуДенежныхСредств = ОстатокПоТипуДенежныхСредств;
		СтрокаТаблицы.КонечныйОстатокПоТипуДенежныхСредств = ОстатокПоТипуДенежныхСредств + СтрокаТаблицы.Сумма;
		
	КонецЦикла;
	
	Возврат ТаблицаПлатежей;
	
КонецФункции // ПолучитьТаблицуПланируемыхПлатежей()

&НаСервере
// Процедура формирует платежный календарь.
//
Процедура ВывестиОтчет(НеВыводитьСообщения = Ложь)
	
	ОтчетОбъект = РеквизитФормыВЗначение("Отчет");
	Результат.Очистить();
	
	Отказ = Не ОтчетОбъект.ПроверитьЗаполнение();
	
	Если НеВыводитьСообщения Тогда
		ПолучитьСообщенияПользователю(Истина);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Установим значения параметров коипоновки данных.
	ПараметрОрганизация = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Организация"));
	Если ПараметрОрганизация <> Неопределено Тогда
		ПараметрОрганизация.Значение = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация);
		ПараметрОрганизация.Использование = Истина;
	КонецЕсли;
	
	ПараметрДатаНачала = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаНачала"));
	Если ПараметрДатаНачала <> Неопределено Тогда
		ПараметрДатаНачала.Значение = Отчет.ПериодПросроченныхПлатежей.ДатаНачала;
		ПараметрДатаНачала.Использование = Истина;
	КонецЕсли;
	
	ПараметрДатаОкончания = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ДатаОкончания"));
	Если ПараметрДатаОкончания <> Неопределено Тогда
		ПараметрДатаОкончания.Значение = КонецДня(Отчет.ПериодБудущихПлатежей.ДатаОкончания);
		ПараметрДатаОкончания.Использование = Истина;
	КонецЕсли;
	
	ПараметрРабочаяДата = Отчет.КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("РабочаяДата"));
	Если ПараметрРабочаяДата <> Неопределено Тогда
		ПараметрРабочаяДата.Значение = Отчет.РабочаяДата;
		ПараметрРабочаяДата.Использование = Истина;
	КонецЕсли;
	
	Для каждого ЭлементНастройки Из Отчет.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
	
		Если ТипЗнч(ЭлементНастройки) = Тип("ЗначениеПараметраНастроекКомпоновкиДанных") Тогда
			
			Если ЭлементНастройки.Параметр = Новый ПараметрКомпоновкиДанных("ДатаНачалаОплата") Тогда
				ЭлементНастройки.Значение = Отчет.ПериодПросроченныхПлатежей.ДатаНачала;
			ИначеЕсли ЭлементНастройки.Параметр = Новый ПараметрКомпоновкиДанных("ДатаОкончанияОплата") Тогда
				ЭлементНастройки.Значение = КонецДня(Отчет.ПериодБудущихПлатежей.ДатаОкончания);
			ИначеЕсли ЭлементНастройки.Параметр = Новый ПараметрКомпоновкиДанных("ОрганизацияОплата") Тогда
				ЭлементНастройки.Значение = Организация;
			КонецЕсли;
		
		КонецЕсли;
	
	КонецЦикла;
	
	НастройкиКомпоновкиДанных = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
	ПараметрыОтчета = ПодготовитьПараметрыОтчета(НастройкиКомпоновкиДанных);
	
	УправлениеНебольшойФирмойОтчеты.УстановитьМакетОформленияОтчета(НастройкиКомпоновкиДанных);
	УправлениеНебольшойФирмойОтчеты.ВывестиЗаголовокОтчета(ПараметрыОтчета, Результат);
	
	// Подготовим и выведем отчет.
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	Схема = ОтчетОбъект.СхемаКомпоновкиДанных;
	
	ДанныеРасшифровкиКомпоновки = Новый ДанныеРасшифровкиКомпоновкиДанных;
	
	МакетКомпоновки = КомпоновщикМакета.Выполнить(
		Схема, 
		НастройкиКомпоновкиДанных,
		ДанныеРасшифровкиКомпоновки
	);
	
	ТаблицаПлатежей = ПолучитьТаблицуПланируемыхПлатежей();
	ВнешниеНаборыДанных = Новый Структура("ТаблицаПлатежей", ТаблицаПлатежей);
	
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(
		МакетКомпоновки,
		ВнешниеНаборыДанных,
		ДанныеРасшифровкиКомпоновки
	);
	
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(Результат);
	ПроцессорВывода.НачатьВывод();
	ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
	ПроцессорВывода.ЗакончитьВывод();
	
	ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеРасшифровкиКомпоновки, Новый УникальныйИдентификатор);
	СхемаКомпоновки   = ПоместитьВоВременноеХранилище(Схема, Новый УникальныйИдентификатор);
	
КонецПроцедуры // ВывестиОтчет()

&НаСервере
Функция ПодготовитьПараметрыОтчета(НастройкиОтчета)
	
	НачалоПериода = Отчет.ПериодПросроченныхПлатежей.ДатаНачала;
	КонецПериода = Отчет.ПериодБудущихПлатежей.ДатаОкончания;
	ВыводитьЗаголовок = Ложь;
	Заголовок = "Платежный календарь";
	
	ПараметрВыводитьЗаголовок = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ВыводитьЗаголовок"));
	Если ПараметрВыводитьЗаголовок <> Неопределено
		И ПараметрВыводитьЗаголовок.Использование Тогда
		
		ВыводитьЗаголовок = ПараметрВыводитьЗаголовок.Значение;
	КонецЕсли;
	
	ПараметрВывода = НастройкиОтчета.ПараметрыВывода.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Заголовок"));
	Если ПараметрВывода <> Неопределено
		И ПараметрВывода.Использование Тогда
		Заголовок = ПараметрВывода.Значение;
	КонецЕсли;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("НачалоПериода"            , НачалоПериода);
	ПараметрыОтчета.Вставить("КонецПериода"             , КонецПериода);
	ПараметрыОтчета.Вставить("ВыводитьЗаголовок"        , ВыводитьЗаголовок);
	ПараметрыОтчета.Вставить("Заголовок"                , Заголовок);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета"      , "ПлатежныйКалендарь");
	ПараметрыОтчета.Вставить("НастройкиОтчета", НастройкиОтчета);
		
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
// Процедура заполняет организацию в форме отчета.
//
Процедура ЗаполнитьОрганизацию()

	ВестиУчетПоКомпании = Константы.УчетПоКомпании.Получить();
	
	Если ВестиУчетПоКомпании Тогда
		Организация = Константы.Компания.Получить();
	Иначе
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
		Если ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			Организация = ЗначениеНастройки;
		Иначе
			Организация = Справочники.Организации.ОсновнаяОрганизация;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.Организация.Доступность = Не ВестиУчетПоКомпании;

КонецПроцедуры // ЗаполнитьОрганизацию()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЯ ФОРМЫ

&НаСервере
// Процедура - обработчик события "ПриСозданииНаСервере" формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СформироватьПриОткрытии") Тогда
		Параметры.СформироватьПриОткрытии = Ложь;
	КонецЕсли;
	
	ВыводитьФактическиеПлатежи = Истина;
	
	ЗаполнитьОрганизацию();
	
	Отчет.ПериодПросроченныхПлатежей.Вариант = ВариантСтандартногоПериода.Последние7Дней;
	Отчет.ПериодБудущихПлатежей.Вариант = ВариантСтандартногоПериода.Следующие7Дней;
	Отчет.РабочаяДата = ТекущаяДата();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
// Процедура - обработчик события "ПриСохраненииВариантаНаСервере" формы.
//
Процедура ПриСохраненииВариантаНаСервере(Настройки)
	
	НаименованиеТекущегоВарианта = ПредставлениеТекущегоВарианта;
	
КонецПроцедуры // ПриСохраненииВариантаНаСервере()

&НаСервере
// Процедура - обработчик события "ПриЗагрузкеВариантаНаСервере" формы.
//
Процедура ПриЗагрузкеВариантаНаСервере(Настройки)

	НаименованиеТекущегоВарианта = ПредставлениеТекущегоВарианта;

КонецПроцедуры // ПриЗагрузкеВариантаНаСервере()

&НаСервере
Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(Настройки)
	
	ВывестиОтчет();
	
КонецПроцедуры // ПриЗагрузкеПользовательскихНастроекНаСервере()

&НаКлиенте
// Процедура - обработчик события "ОбработкаОповещения" формы.
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	ВывестиОтчет(Истина);

КонецПроцедуры // ОбработкаОповещения()

&НаСервере
Процедура ПриСохраненииПользовательскихНастроекНаСервере(Настройки)
	ВариантыОтчетов.ПриСохраненииПользовательскихНастроекНаСервере(ЭтотОбъект, Настройки);
КонецПроцедуры

///////////////////////////////////////////////////////////////////////////////

&НаКлиенте
// Процедура выполняется при нажатии кнопки "Сформировать".
//
Процедура СформироватьВыполнить()
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ФормированиеОтчёта_ПлатежныйКалендарь");
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
	ВывестиОтчет();
	
КонецПроцедуры // СформироватьВыполнить()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ПОЛЕЙ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "Организация".
//
Процедура ОрганизацияПриИзменении(Элемент)

	ВывестиОтчет();
	
КонецПроцедуры // ОрганизацияПриИзменении()

///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЧНОГО ПОЛЯ

&НаСервере
// Получает значение поля расшифровки.
//
Функция ПолучитьЗначениеПоляРасшифровки(Расшифровка)
	
	ИсточникНастроек = Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновки);
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, ИсточникНастроек);
		
	ДанныеРасшифровкиИзХранилища = ПолучитьИзВременногоХранилища(ДанныеРасшифровки);
	
	// Создадим и инициализируем обработчик расшифровки.
	СтруктураРасшифровки = Новый Структура;
	
	ЭлементРасшифровки = ДанныеРасшифровкиИзХранилища.Элементы.Получить(Расшифровка);
	
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		Для каждого ЗначениеПоляРасшифровки Из ЭлементРасшифровки.ПолучитьПоля() Цикл
			Если ЗначениеПоляРасшифровки.Поле = "Документ"
			  И ЗначениеЗаполнено(ЗначениеПоляРасшифровки.Значение) Тогда
				СтруктураВозврата = Новый Структура;
				СтруктураВозврата.Вставить("ИмяФормыДокумента", "Документ." + ЗначениеПоляРасшифровки.Значение.Метаданные().Имя + ".ФормаОбъекта");
				СтруктураВозврата.Вставить("СсылкаДокумента", ЗначениеПоляРасшифровки.Значение);
				Возврат СтруктураВозврата;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Возврат Неопределено;

КонецФункции // ПолучитьЗначениеПоляРасшифровки()

&НаКлиенте
// Процедура - обработчик события "ОбработкаРасшифровки" поля "Результат".
//
Процедура РезультатОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)

	Если ТипЗнч(Расшифровка) <> Тип("ИдентификаторРасшифровкиКомпоновкиДанных") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ЗначениеПоляРасшифровки = ПолучитьЗначениеПоляРасшифровки(Расшифровка);
	
	Если ЗначениеЗаполнено(ЗначениеПоляРасшифровки) Тогда
		СтруктураПараметры = Новый Структура("Ключ", ЗначениеПоляРасшифровки.СсылкаДокумента);
		ОткрытьФорму(ЗначениеПоляРасшифровки.ИмяФормыДокумента, СтруктураПараметры);
	КонецЕсли;

КонецПроцедуры // РезультатОбработкаРасшифровки()

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "ПериодПросроченныхПлатежей".
//
Процедура ПериодПросроченныхПлатежейПриИзменении(Элемент)
	
	ВывестиОтчет();

КонецПроцедуры // ПериодПросроченныхПлатежейПриИзменении()

&НаКлиенте
// Процедура - обработчик события "ПриИзменении" поля "ПериодБудущихПлатежей".
//
Процедура ПериодБудущихПлатежейПриИзменении(Элемент)
	
	ВывестиОтчет();
	
КонецПроцедуры // ПериодБудущихПлатежейПриИзменении()
