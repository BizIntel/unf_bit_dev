#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.Вставить("УровеньГруппировкиСтрок", 2);
	НастройкиВариантов["Остатки"].Вставить("ТолькоРесурсыОстатков", Истина);
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

Процедура ПриКонтекстномОткрытии(СсылкаНаОбъект, ПолеСвязи, Отборы, Отказ) Экспорт
	
	Если ПолеСвязи = "ЗаказПоставщику" Тогда
		Отборы.Вставить(ПолеСвязи, УправлениеНебольшойФирмойСервер.МассивЗаказов(СсылкаНаОбъект));
		Отборы.Вставить("СтПериод", Новый СтандартныйПериод);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейКоличеств = Новый Массив;
	Для каждого ДоступноеПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если НЕ ДоступноеПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		ИмяПоля = Строка(ДоступноеПоле.Поле);
		МассивПолейКоличеств.Добавить(ИмяПоля);
	КонецЦикла; 
	
	Для каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = НастройкиТекВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, МассивПолейКоличеств);
			
	КонецЦикла; 
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	НастройкиВариантов["Ведомость"].Теги = НСТР("ru = 'Заказы'");
	НастройкиВариантов["Остатки"].Теги = НСТР("ru = 'Заказы'");
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.ПриходнаяНакладная", Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика, Истина, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.ПриходнаяНакладная", Перечисления.ВидыОперацийПриходнаяНакладная.ПриемВПереработку, Истина, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.ПриходнаяНакладная", Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаКомиссию, Истина, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.ПриходнаяНакладная", Перечисления.ВидыОперацийПриходнаяНакладная.ПриемНаОтветхранение, Истина, Истина);
	
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.РасходнаяНакладная", Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратИзПереработки, Истина, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.РасходнаяНакладная", Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратКомитенту, Истина, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.РасходнаяНакладная", Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратПоставщику, Истина, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.РасходнаяНакладная", Перечисления.ВидыОперацийРасходнаяНакладная.ВозвратСОтветхранения, Истина, Истина);
	
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.ЗаказПоставщику",,, Истина);
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказПоставщику", "Документ.ОтчетПереработчика",, Истина, Истина);
	
КонецПроцедуры

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли
