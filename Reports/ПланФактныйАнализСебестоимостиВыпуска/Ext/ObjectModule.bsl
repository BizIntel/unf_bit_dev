#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиВариантов["Основной"].Рекомендуемый = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	Перем ТаблицаВыпуска;
	Перем ТаблицаПлановыхЗатратНаВыпуск;
	
	СтандартнаяОбработка = Ложь;
	
	НастройкиОтчета = КомпоновщикНастроек.Настройки;
	ПараметрыОтчета = ОтчетыУНФ.ПараметрыФормированияОтчета(НастройкиОтчета);
	
	УправлениеНебольшойФирмойОтчеты.УстановитьМакетОформленияОтчета(НастройкиОтчета);
	УправлениеНебольшойФирмойОтчеты.ВывестиЗаголовокОтчета(ПараметрыОтчета, ДокументРезультат);
	
	КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
	МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиОтчета, ДанныеРасшифровки);
	
	ЗначенияПараметровМКД = МакетКомпоновки.ЗначенияПараметров;
	ПараметрКомпоновкиДанных = ЗначенияПараметровМКД.Найти("КонецПериода");
	Если НЕ ПараметрКомпоновкиДанных = Неопределено Тогда
		
		Если ТипЗнч(ПараметрКомпоновкиДанных.Значение) = Тип("Дата")
			И ПараметрКомпоновкиДанных.Значение = Дата(1,1,1) ТОГДА
		
			ПараметрКомпоновкиДанных.Значение = Дата(3999,12,31);
			
		КонецЕсли;
	
	КонецЕсли;
	
	СформироватьТаблицыВыпускаИПлановыхЗатрат(ТаблицаВыпуска, ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД);
	
	РассчитатьПлановуюСебестоимостьЗатратНаВыпуск(ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД);
	
	ВнешниеНаборыДанных = Новый Структура;
	ВнешниеНаборыДанных.Вставить("ТаблицаВыпуска", ТаблицаВыпуска);
	ВнешниеНаборыДанных.Вставить("ТаблицаПлановыхЗатратНаВыпуск", ТаблицаПлановыхЗатратНаВыпуск);
	
	//Создадим и инициализируем процессор компоновки
	ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
	ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровки, Истина);
	
	//Создадим и инициализируем процессор вывода результата
	ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
	ПроцессорВывода.УстановитьДокумент(ДокументРезультат);
	
	//Обозначим начало вывода
	ПроцессорВывода.НачатьВывод();
	ТаблицаЗафиксирована = Ложь;
	
	ДокументРезультат.ФиксацияСверху = 0;
	//Основной цикл вывода отчета
	Пока Истина Цикл
		//Получим следующий элемент результата компоновки
		ЭлементРезультата = ПроцессорКомпоновки.Следующий();
		
		Если ЭлементРезультата = Неопределено Тогда
			//Следующий элемент не получен - заканчиваем цикл вывода
			Прервать;
		Иначе
			// Зафиксируем шапку
			Если  Не ТаблицаЗафиксирована 
				  И ЭлементРезультата.ЗначенияПараметров.Количество() > 0 
				  И ТипЗнч(КомпоновщикНастроек.Настройки.Структура[0]) <> Тип("ДиаграммаКомпоновкиДанных") Тогда
				
				ТаблицаЗафиксирована = Истина;
				ДокументРезультат.ФиксацияСверху = ДокументРезультат.ВысотаТаблицы;
			
			КонецЕсли;
			//Элемент получен - выведем его при помощи процессора вывода
			ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
		КонецЕсли;
	КонецЦикла;
	
	ПроцессорВывода.ЗакончитьВывод();
	
	ОтчетыУНФ.ОбоработатьДиаграммыТабличногоДокумента(ДокументРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура СформироватьТаблицыВыпускаИПлановыхЗатрат(ТаблицаВыпуска, ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД)
	
	Запрос = Новый Запрос;
	УстановитьПериодВЗапросе(Запрос, ЗначенияПараметровМКД);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыпускПродукцииОбороты.Организация КАК Организация,
	|	ВыпускПродукцииОбороты.СтруктурнаяЕдиница КАК Подразделение,
	|	ВыпускПродукцииОбороты.Номенклатура КАК Продукция,
	|	ВыпускПродукцииОбороты.Характеристика КАК ХарактеристикаПродукции,
	|	ВыпускПродукцииОбороты.Партия КАК ПартияПродукции,
	|	ВыпускПродукцииОбороты.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ВыпускПродукцииОбороты.Спецификация КАК СпецификацияПродукции,
	|	ВыпускПродукцииОбороты.КоличествоОборот КАК КоличествоПродукции,
	|	NULL КАК Номенклатура
	|ПОМЕСТИТЬ ВременнаяТаблицаВыпуск
	|ИЗ
	|	РегистрНакопления.ВыпускПродукции.Обороты(&НачалоПериода, &КонецПериода, , Номенклатура.ТипНоменклатуры <> ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.Услуга)) КАК ВыпускПродукцииОбороты
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Продукция,
	|	ХарактеристикаПродукции,
	|	СпецификацияПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВыпускПродукции.Организация,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.ПартияПродукции,
	|	ВыпускПродукции.ЗаказПокупателя,
	|	ВыпускПродукции.СпецификацияПродукции,
	|	ВыпускПродукции.КоличествоПродукции
	|ИЗ
	|	ВременнаяТаблицаВыпуск КАК ВыпускПродукции
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВыпускПродукции.Организация,
	|	ВыпускПродукции.Подразделение,
	|	ВыпускПродукции.Продукция,
	|	ВыпускПродукции.ХарактеристикаПродукции,
	|	ВыпускПродукции.ПартияПродукции,
	|	ВыпускПродукции.ЗаказПокупателя,
	|	ВыпускПродукции.СпецификацияПродукции КАК СпецификацияВыпускаПродукции,
	|	ВЫБОР
	|		КОГДА ВыпускПродукции.СпецификацияПродукции = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|			ТОГДА ВыпускПродукции.Продукция.Спецификация
	|		ИНАЧЕ ВыпускПродукции.СпецификацияПродукции
	|	КОНЕЦ КАК СпецификацияПродукции,
	|	ВыпускПродукции.КоличествоПродукции КАК КоличествоВыпускаПродукции,
	|	СпецификацииСостав.Номенклатура,
	|	СпецификацииСостав.Характеристика,
	|	ВЫБОР
	|		КОГДА СпецификацииСостав.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА СпецификацииСостав.Количество * ВЫРАЗИТЬ(СпецификацииСостав.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ СпецификацииСостав.Количество
	|	КОНЕЦ / ЕСТЬNULL(СпецификацииСостав.КоличествоПродукции, 1) * ВыпускПродукции.КоличествоПродукции КАК Количество,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава
	|ПОМЕСТИТЬ ВременнаяТаблицаСоставВыпуска
	|ИЗ
	|	ВременнаяТаблицаВыпуск КАК ВыпускПродукции
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Спецификации.Состав КАК СпецификацииСостав
	|		ПО (НЕ СпецификацииСостав.Ссылка.ПометкаУдаления)
	|			И (ВЫБОР
	|				КОГДА ВыпускПродукции.СпецификацияПродукции = ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка)
	|					ТОГДА ВыпускПродукции.Продукция.Спецификация
	|				ИНАЧЕ ВыпускПродукции.СпецификацияПродукции
	|			КОНЕЦ = СпецификацииСостав.Ссылка)
	|			И ВыпускПродукции.Продукция = СпецификацииСостав.Ссылка.Владелец
	|			И ВыпускПродукции.ХарактеристикаПродукции = СпецификацииСостав.Ссылка.ХарактеристикаПродукции
	|			И (СпецификацииСостав.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Расход))
	|			И (СпецификацииСостав.Номенклатура <> ВыпускПродукции.Продукция)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставВыпуска.Организация,
	|	СоставВыпуска.Подразделение,
	|	СоставВыпуска.Продукция,
	|	СоставВыпуска.ХарактеристикаПродукции,
	|	СоставВыпуска.ПартияПродукции,
	|	СоставВыпуска.ЗаказПокупателя,
	|	СоставВыпуска.СпецификацияВыпускаПродукции,
	|	СоставВыпуска.СпецификацияПродукции,
	|	СоставВыпуска.КоличествоВыпускаПродукции,
	|	СоставВыпуска.Номенклатура,
	|	СоставВыпуска.Характеристика,
	|	СоставВыпуска.Количество,
	|	СоставВыпуска.Спецификация,
	|	СоставВыпуска.ТипСтрокиСостава
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставВыпуска.Номенклатура КАК НоменклатураУзел,
	|	СоставВыпуска.Характеристика КАК ХарактеристикаУзла,
	|	СоставВыпуска.Спецификация КАК СпецификацияУзла
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|ГДЕ
	|	СоставВыпуска.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаВыпуска = Результат[1].Выгрузить();
	ТаблицаПлановыхЗатратНаВыпуск = Результат[3].Выгрузить();
	ТаблицаУзловСпецификацийКРазузлованию = Результат[4].Выгрузить();
	
	Пока ТаблицаУзловСпецификацийКРазузлованию.Количество() > 0 Цикл
		
		РазузловатьУзлыСпецификаций(ТаблицаУзловСпецификацийКРазузлованию, ТаблицаПлановыхЗатратНаВыпуск);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура РазузловатьУзлыСпецификаций(ТаблицаУзловСпецификацийКРазузлованию, ТаблицаПлановыхЗатратНаВыпуск)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ТаблицаУзловСпецификацийКРазузлованию", ТаблицаУзловСпецификацийКРазузлованию);
	Запрос.УстановитьПараметр("ТаблицаПлановыхЗатратНаВыпуск", ТаблицаПлановыхЗатратНаВыпуск);
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ТаблицаУзловСпецификацийКРазузлованию.НоменклатураУзел КАК НоменклатураУзел,
	|	ТаблицаУзловСпецификацийКРазузлованию.ХарактеристикаУзла КАК ХарактеристикаУзла,
	|	ТаблицаУзловСпецификацийКРазузлованию.СпецификацияУзла КАК СпецификацияУзла
	|ПОМЕСТИТЬ Вт_ТаблицаУзловСпецификацийКРазузлованию
	|ИЗ
	|	&ТаблицаУзловСпецификацийКРазузлованию КАК ТаблицаУзловСпецификацийКРазузлованию
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СпецификацииСостав.Ссылка.Владелец КАК НоменклатураУзел,
	|	СпецификацииСостав.Ссылка КАК СпецификацияУзла,
	|	СпецификацииСостав.Ссылка.ХарактеристикаПродукции КАК ХарактеристикаУзла,
	|	СпецификацииСостав.Номенклатура,
	|	СпецификацииСостав.Характеристика,
	|	ВЫБОР
	|		КОГДА СпецификацииСостав.ЕдиницаИзмерения ССЫЛКА Справочник.ЕдиницыИзмерения
	|			ТОГДА СпецификацииСостав.Количество * ВЫРАЗИТЬ(СпецификацииСостав.ЕдиницаИзмерения КАК Справочник.ЕдиницыИзмерения).Коэффициент
	|		ИНАЧЕ СпецификацииСостав.Количество
	|	КОНЕЦ / ЕСТЬNULL(СпецификацииСостав.КоличествоПродукции, 1) КАК Количество,
	|	СпецификацииСостав.Спецификация,
	|	СпецификацииСостав.ТипСтрокиСостава
	|ПОМЕСТИТЬ ВременнаяТаблицаСоставУзлов
	|ИЗ
	|	Справочник.Спецификации.Состав КАК СпецификацииСостав
	|ГДЕ
	|	НЕ СпецификацииСостав.Ссылка.ПометкаУдаления
	|	И (СпецификацииСостав.Ссылка.Владелец, СпецификацииСостав.Ссылка.ХарактеристикаПродукции, СпецификацииСостав.Ссылка) В
	|			(ВЫБРАТЬ
	|				Вт_ТаблицаУзловСпецификацийКРазузлованию.НоменклатураУзел,
	|				Вт_ТаблицаУзловСпецификацийКРазузлованию.ХарактеристикаУзла,
	|				Вт_ТаблицаУзловСпецификацийКРазузлованию.СпецификацияУзла
	|			ИЗ
	|				Вт_ТаблицаУзловСпецификацийКРазузлованию)
	|	И СпецификацииСостав.Номенклатура <> СпецификацииСостав.Ссылка.Владелец
	|	И СпецификацииСостав.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Расход)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НоменклатураУзел,
	|	ХарактеристикаУзла,
	|	СпецификацияУзла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеЗатратыНаВыпуск.Организация,
	|	ПлановыеЗатратыНаВыпуск.Подразделение,
	|	ПлановыеЗатратыНаВыпуск.Продукция,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ПлановыеЗатратыНаВыпуск.ПартияПродукции,
	|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции,
	|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
	|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
	|	ПлановыеЗатратыНаВыпуск.Количество,
	|	ПлановыеЗатратыНаВыпуск.Спецификация КАК Спецификация,
	|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава
	|ПОМЕСТИТЬ ВременнаяТаблицаПлановыеЗатраты
	|ИЗ
	|	&ТаблицаПлановыхЗатратНаВыпуск КАК ПлановыеЗатратыНаВыпуск
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Характеристика,
	|	Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеЗатратыНаВыпуск.Организация,
	|	ПлановыеЗатратыНаВыпуск.Подразделение,
	|	ПлановыеЗатратыНаВыпуск.Продукция,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции,
	|	ПлановыеЗатратыНаВыпуск.ПартияПродукции,
	|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции,
	|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции,
	|	ЕСТЬNULL(СоставУзлов.Номенклатура, ПлановыеЗатратыНаВыпуск.Номенклатура) КАК Номенклатура,
	|	ЕСТЬNULL(СоставУзлов.Характеристика, ПлановыеЗатратыНаВыпуск.Характеристика) КАК Характеристика,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			ТОГДА ВЫБОР
	|					КОГДА ЕСТЬNULL(ПлановыеЗатратыНаВыпуск.Количество, 0) = 0
	|						ТОГДА 1
	|					ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество
	|				КОНЕЦ * СоставУзлов.Количество
	|		ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество
	|	КОНЕЦ КАК Количество,
	|	ЕСТЬNULL(СоставУзлов.Спецификация, ПлановыеЗатратыНаВыпуск.Спецификация) КАК Спецификация,
	|	ЕСТЬNULL(СоставУзлов.ТипСтрокиСостава, ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава) КАК ТипСтрокиСостава,
	|	ВЫБОР
	|		КОГДА ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|				И СоставУзлов.НоменклатураУзел ЕСТЬ NULL 
	|			ТОГДА ЛОЖЬ
	|		ИНАЧЕ ИСТИНА
	|	КОНЕЦ КАК Разузлован
	|ПОМЕСТИТЬ ВременнаяТаблицаСоставВыпуска
	|ИЗ
	|	ВременнаяТаблицаПлановыеЗатраты КАК ПлановыеЗатратыНаВыпуск
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСоставУзлов КАК СоставУзлов
	|		ПО ПлановыеЗатратыНаВыпуск.Номенклатура = СоставУзлов.НоменклатураУзел
	|			И ПлановыеЗатратыНаВыпуск.Характеристика = СоставУзлов.ХарактеристикаУзла
	|			И ПлановыеЗатратыНаВыпуск.Спецификация = СоставУзлов.СпецификацияУзла
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СоставВыпуска.Организация,
	|	СоставВыпуска.Подразделение,
	|	СоставВыпуска.Продукция,
	|	СоставВыпуска.ХарактеристикаПродукции,
	|	СоставВыпуска.ПартияПродукции,
	|	СоставВыпуска.ЗаказПокупателя,
	|	СоставВыпуска.СпецификацияВыпускаПродукции,
	|	СоставВыпуска.СпецификацияПродукции,
	|	СоставВыпуска.КоличествоВыпускаПродукции,
	|	СоставВыпуска.Номенклатура,
	|	СоставВыпуска.Характеристика,
	|	СоставВыпуска.Количество,
	|	СоставВыпуска.Спецификация,
	|	СоставВыпуска.ТипСтрокиСостава
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|ГДЕ
	|	СоставВыпуска.Разузлован
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	СоставВыпуска.Номенклатура КАК НоменклатураУзел,
	|	СоставВыпуска.Характеристика КАК ХарактеристикаУзла,
	|	СоставВыпуска.Спецификация КАК СпецификацияУзла
	|ИЗ
	|	ВременнаяТаблицаСоставВыпуска КАК СоставВыпуска
	|ГДЕ
	|	СоставВыпуска.ТипСтрокиСостава = ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)";
	
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаПлановыхЗатратНаВыпуск = Результат[4].Выгрузить();
	ТаблицаУзловСпецификацийКРазузлованию = Результат[5].Выгрузить();
	
КонецПроцедуры

Процедура РассчитатьПлановуюСебестоимостьЗатратНаВыпуск(ТаблицаПлановыхЗатратНаВыпуск, ЗначенияПараметровМКД)
	
	Запрос = Новый Запрос;
	УстановитьПериодВЗапросе(Запрос, ЗначенияПараметровМКД);
	Запрос.УстановитьПараметр("ТаблицаПлановыхЗатратНаВыпуск", ТаблицаПлановыхЗатратНаВыпуск);
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
	|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
	|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
	|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияПродукции КАК СпецификацияПродукции,
	|	ПлановыеЗатратыНаВыпуск.КоличествоВыпускаПродукции КАК КоличествоВыпускаПродукции,
	|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
	|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
	|	ПлановыеЗатратыНаВыпуск.Количество КАК Количество,
	|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава
	|ПОМЕСТИТЬ ВременнаяТаблицаПлановыеЗатратыНаВыпуск
	|ИЗ
	|	&ТаблицаПлановыхЗатратНаВыпуск КАК ПлановыеЗатратыНаВыпуск
	|ГДЕ
	|	ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Узел)
	|			И ПлановыеЗатратыНаВыпуск.ТипСтрокиСостава <> ЗНАЧЕНИЕ(Перечисление.ТипыСтрокСоставаСпецификации.Расход)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Запасы.Организация КАК Организация,
	|	Запасы.КоррСтруктурнаяЕдиница КАК Подразделение,
	|	Запасы.КоррНоменклатура КАК Продукция,
	|	Запасы.КоррХарактеристика КАК ХарактеристикаПродукции,
	|	Запасы.КоррПартия КАК ПартияПродукции,
	|	Запасы.КоррЗаказПокупателя КАК ЗаказПокупателя,
	|	Запасы.КоррСпецификация КАК СпецификацияПродукции,
	|	Запасы.Номенклатура КАК Номенклатура,
	|	Запасы.Характеристика КАК Характеристика,
	|	Запасы.Спецификация КАК Спецификация,
	|	СУММА(Запасы.Сумма) КАК Сумма,
	|	СУММА(Запасы.Количество) КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаЗатратыНаВыпуск
	|ИЗ
	|	РегистрНакопления.Запасы КАК Запасы
	|ГДЕ
	|	Запасы.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	И Запасы.ЗатратыНаВыпуск
	|	И Запасы.Период МЕЖДУ &НачалоПериода И &КонецПериода
	|	И НЕ Запасы.Номенклатура = ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	Запасы.КоррСпецификация,
	|	Запасы.Характеристика,
	|	Запасы.КоррСтруктурнаяЕдиница,
	|	Запасы.КоррНоменклатура,
	|	Запасы.КоррПартия,
	|	Запасы.Номенклатура,
	|	Запасы.КоррЗаказПокупателя,
	|	Запасы.КоррХарактеристика,
	|	Запасы.Организация,
	|	Запасы.Спецификация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеЗатратыНаВыпуск.Организация КАК Организация,
	|	ПлановыеЗатратыНаВыпуск.Подразделение КАК Подразделение,
	|	ПлановыеЗатратыНаВыпуск.Продукция КАК Продукция,
	|	ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции КАК ХарактеристикаПродукции,
	|	ПлановыеЗатратыНаВыпуск.ПартияПродукции КАК ПартияПродукции,
	|	ПлановыеЗатратыНаВыпуск.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции КАК СпецификацияПродукции,
	|	ПлановыеЗатратыНаВыпуск.Номенклатура КАК Номенклатура,
	|	ПлановыеЗатратыНаВыпуск.Характеристика КАК Характеристика,
	|	ФактическиеЗатратыНаВыпуск.Спецификация КАК Спецификация,
	|	ПлановыеЗатратыНаВыпуск.Количество КАК КоличествоЗатратПлан,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ФактическиеЗатратыНаВыпуск.Сумма, 0) = 0
	|				ИЛИ ЕСТЬNULL(ФактическиеЗатратыНаВыпуск.Количество, 0) = 0
	|			ТОГДА 0
	|		ИНАЧЕ ПлановыеЗатратыНаВыпуск.Количество * (ФактическиеЗатратыНаВыпуск.Сумма / ФактическиеЗатратыНаВыпуск.Количество)
	|	КОНЕЦ КАК СтоимостьЗатратПлан
	|ИЗ
	|	ВременнаяТаблицаПлановыеЗатратыНаВыпуск КАК ПлановыеЗатратыНаВыпуск
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаЗатратыНаВыпуск КАК ФактическиеЗатратыНаВыпуск
	|		ПО ПлановыеЗатратыНаВыпуск.Организация = ФактическиеЗатратыНаВыпуск.Организация
	|			И ПлановыеЗатратыНаВыпуск.Подразделение = ФактическиеЗатратыНаВыпуск.Подразделение
	|			И ПлановыеЗатратыНаВыпуск.Продукция = ФактическиеЗатратыНаВыпуск.Продукция
	|			И ПлановыеЗатратыНаВыпуск.ХарактеристикаПродукции = ФактическиеЗатратыНаВыпуск.ХарактеристикаПродукции
	|			И ПлановыеЗатратыНаВыпуск.ПартияПродукции = ФактическиеЗатратыНаВыпуск.ПартияПродукции
	|			И ПлановыеЗатратыНаВыпуск.ЗаказПокупателя = ФактическиеЗатратыНаВыпуск.ЗаказПокупателя
	|			И ПлановыеЗатратыНаВыпуск.СпецификацияВыпускаПродукции = ФактическиеЗатратыНаВыпуск.СпецификацияПродукции
	|			И ПлановыеЗатратыНаВыпуск.Номенклатура = ФактическиеЗатратыНаВыпуск.Номенклатура
	|			И ПлановыеЗатратыНаВыпуск.Характеристика = ФактическиеЗатратыНаВыпуск.Характеристика";
	
	ТаблицаПлановыхЗатратНаВыпуск = Запрос.Выполнить().Выгрузить();
	
КонецПроцедуры

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейСумм = Новый Массив;
	МассивПолейКоличества = Новый Массив;
	Для каждого ДоступноеПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если НЕ ДоступноеПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		ИмяПоля = Строка(ДоступноеПоле.Поле);
		Если СтрНачинаетсяС(ИмяПоля, "Количество") Тогда
			МассивПолейКоличества.Добавить(ИмяПоля);
		Иначе
			МассивПолейСумм.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = НастройкиТекВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияСумм(ВариантыОформления, МассивПолейСумм);
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, МассивПолейКоличества);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		НастройкиТекВарианта.Значение.Теги = НСтр("ru = 'Производство,Себестоимость,Продукция'");
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "ЗаказПокупателя", "Документ.ЗаказПокупателя");
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "Продукция", "Справочник.Номенклатура");
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьПериодВЗапросе(Запрос, Знач ЗначенияПараметровМКД)
	
	ДатаНачалаПериода = Дата(1,1,1);
	ДатаКонцаПериода = Дата(3999,12,31);
	
	ПараметрКомпоновкиДанных = ЗначенияПараметровМКД.Найти("НачалоПериода");
	Если НЕ ПараметрКомпоновкиДанных = Неопределено 
		И ТипЗнч(ПараметрКомпоновкиДанных.Значение) = Тип("Дата")
		И ПараметрКомпоновкиДанных.Значение <> Дата(1,1,1) ТОГДА
		
		ДатаНачалаПериода = ПараметрКомпоновкиДанных.Значение;
	КонецЕсли;
	
	ПараметрКомпоновкиДанных = ЗначенияПараметровМКД.Найти("КонецПериода");
	Если НЕ ПараметрКомпоновкиДанных = Неопределено 
		И ТипЗнч(ПараметрКомпоновкиДанных.Значение) = Тип("Дата")
		И ПараметрКомпоновкиДанных.Значение <> Дата(1,1,1) ТОГДА
		
		ДатаКонцаПериода = ПараметрКомпоновкиДанных.Значение;
	КонецЕсли;
	
	Запрос.УстановитьПараметр("НачалоПериода", ДатаНачалаПериода);
	Запрос.УстановитьПараметр("КонецПериода",  ДатаКонцаПериода);
	
КонецПроцедуры

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли
