#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ПрограммноеИзменениеФормыОтчета = Истина;
	НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("ОбъектПланирования");
	НастройкиОтчета.ДополнительныеГруппировкиКолонок.Добавить("СценарийПланирования");
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	НастройкиВариантов["Основной"].Рекомендуемый = Истина;
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

Процедура ОбновитьНастройкиНаФорме(НастройкиОтчета, НастройкиСКД, Форма) Экспорт
	
	// Выводим заголовок поля для параметра "Измерение планирования"
	ИмяРеквизита = "";
	ОписаниеПоля = Форма.СтрокаОписанияПоляВызов("Параметр", "ИзмерениеПланирования");
	Если ОписаниеПоля <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ОписаниеПоля.Элементы Цикл
			Поле = Форма.Элементы.Найти(КлючИЗначение.Ключ);
			Если Поле <> Неопределено Тогда
				Поле.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
				ИмяРеквизита = КлючИЗначение.Значение;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Установим связь параметров выбора для поля-отбора "Сценарий планирования" по полю-параметру "Измерение планирования"
	ОписаниеПоля = Форма.СтрокаОписанияПоляВызов("Фильтр", "СценарийПланирования");
	Если ОписаниеПоля <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ОписаниеПоля.Элементы Цикл
			Поле = Форма.Элементы.Найти(КлючИЗначение.Ключ);
			Если Поле <> Неопределено И Поле.Вид = ВидПоляФормы.ПолеВвода И ИмяРеквизита <> "" Тогда
				НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.ИзмерениеПланирования", ИмяРеквизита);
				НовыйМассив = Новый Массив();
				НовыйМассив.Добавить(НоваяСвязь);
				Поле.СвязиПараметровВыбора = Новый ФиксированныйМассив(НовыйМассив);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// Очистка полей, созданных по другому варианту отчета
	УдаляемыеЭлементы = Новый Массив;
	Для Каждого ЭлементФормы Из Форма.Элементы.ГруппаНастройкиДиаграмм.ПодчиненныеЭлементы Цикл
		Если ЭлементФормы.Вид = ВидПоляФормы.ПолеПереключателя И ЭлементФормы.ВидПереключателя = ВидПереключателя.Тумблер Тогда
			УдаляемыеЭлементы.Добавить(ЭлементФормы);
		КонецЕсли;
	КонецЦикла;
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Форма.Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
	// Параметр отчета "ПоказательДиаграммы" отображаем в группе "Диаграммы"
	ОписаниеПоля = Форма.СтрокаОписанияПоляВызов("Параметр", "ПоказательДиаграммы");
	Если ОписаниеПоля <> Неопределено Тогда
		Для Каждого КлючИЗначение Из ОписаниеПоля.Элементы Цикл
			Поле = Форма.Элементы.Найти(КлючИЗначение.Ключ);
			Если Поле <> Неопределено Тогда
				Форма.Элементы.Переместить(Поле, Форма.Элементы.ГруппаНастройкиДиаграмм, Форма.Элементы.ДиаграммаЗначения);
				СписокВыбора = Поле.СписокВыбора;
				Поле.Вид = ВидПоляФормы.ПолеПереключателя;
				Поле.ВидПереключателя = ВидПереключателя.Тумблер;
				Для Каждого ЭлементСписка Из СписокВыбора Цикл
					НовыйЭлемент = Поле.СписокВыбора.Добавить();
					ЗаполнитьЗначенияСвойств(НовыйЭлемент, ЭлементСписка);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	// В зависимости от параметра отчета "ИзмерениеПланирования" устанавливаем заголовок поля "ОбъектПланирования": Номенклатура / Менеджер и т.д.
	ЗначениеПараметраКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ИзмерениеПланирования"));
	Поле = СхемаКомпоновкиДанных.НаборыДанных[0].Поля.Найти("ОбъектПланирования");
	Если ЗначениеПараметраКД <> Неопределено И Поле <> Неопределено Тогда
		Поле.Заголовок = Строка(ЗначениеПараметраКД.Значение);
	КонецЕсли;
	
	// В зависимости от параметра отчета "ПоказательДиаграммы" устанавливаем нужные ресурсы в диаграмме (количественные или суммовые)
	ЗначениеПараметраКД = КомпоновщикНастроек.Настройки.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ПоказательДиаграммы"));
	Если ЗначениеПараметраКД <> Неопределено Тогда
		
		Для Каждого ЭлементСтруктуры Из КомпоновщикНастроек.Настройки.Структура Цикл
			
			Если ТипЗнч(ЭлементСтруктуры) <> Тип("ДиаграммаКомпоновкиДанных") Тогда
				Продолжить;
			КонецЕсли;
			
			ЭлементСтруктуры.Выбор.Элементы.Очистить();
			
			ВыбранноеПолеКД = ЭлементСтруктуры.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПолеКД.Поле = Новый ПолеКомпоновкиДанных(ЗначениеПараметраКД.Значение + "ФактПрогноз");
			ВыбранноеПолеКД.Использование = Истина;
			
			ВыбранноеПолеКД = ЭлементСтруктуры.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПолеКД.Поле = Новый ПолеКомпоновкиДанных(ЗначениеПараметраКД.Значение + "Факт");
			ВыбранноеПолеКД.Использование = Истина;
			
			ВыбранноеПолеКД = ЭлементСтруктуры.Выбор.Элементы.Добавить(Тип("ВыбранноеПолеКомпоновкиДанных"));
			ВыбранноеПолеКД.Поле = Новый ПолеКомпоновкиДанных(ЗначениеПараметраКД.Значение + "План");
			ВыбранноеПолеКД.Использование = Истина;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// Стандартная обработка отчета
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
	// Изменим цвета серий диаграммы
	Для Каждого Рисунок Из ДокументРезультат.Рисунки Цикл
		
		Если Рисунок.ТипРисунка <> ТипРисункаТабличногоДокумента.Диаграмма Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого Серия Из Рисунок.Объект.Серии Цикл
			Если СтрНайти(ВРег(Серия.Текст), ВРег("Прогноз")) > 0 Тогда
				Серия.Цвет = Новый Цвет(230, 210, 200);
			ИначеЕсли СтрНайти(ВРег(Серия.Текст), ВРег("Факт")) > 0 Тогда
				Серия.Цвет = Новый Цвет(255, 202, 125);
			ИначеЕсли СтрНайти(ВРег(Серия.Текст), ВРег("План")) > 0 Тогда
				Серия.Цвет = Новый Цвет(142, 201, 249);
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейСумм = Новый Массив;
	МассивПолейКоличества = Новый Массив;
	Для Каждого ДоступноеПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если НЕ ДоступноеПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		ИмяПоля = Строка(ДоступноеПоле.Поле);
		Если СтрНачинаетсяС(ИмяПоля, "Количество") Тогда
			МассивПолейКоличества.Добавить(ИмяПоля);
		ИначеЕсли СтрНачинаетсяС(ИмяПоля, "Сумма") Тогда
			МассивПолейСумм.Добавить(ИмяПоля);
		КонецЕсли;
	КонецЦикла;
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = НастройкиТекВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияСумм(ВариантыОформления, МассивПолейСумм);
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, МассивПолейКоличества);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		НастройкиТекВарианта.Значение.Теги = НСтр("ru = 'Компания,Продажи,CRM'");
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	Для Каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "ОбъектПланирования", "Справочник.Номенклатура");
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "ОбъектПланирования", "Справочник.КатегорииНоменклатуры");
		ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиТекВарианта.Значение.СвязанныеПоля, "ОбъектПланирования", "Справочник.Сотрудники");
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли