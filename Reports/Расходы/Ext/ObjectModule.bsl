#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ИспользоватьСравнение = Истина;
	НастройкиОтчета.ИспользоватьПериодичность = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	НастройкиВариантов["Расходы"].Теги = НСтр("ru = 'Главное,Расходы,Траты,Хозяйственная операция'");
	НастройкиВариантов["Расходы"].Рекомендуемый = Истина;
	НастройкиВариантов["Расходы"].ПоляПоиска = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок("Организация,ХозяйственнаяОперация,СчетУчета,НаправлениеДеятельности,Аналитика,СтруктурнаяЕдиница");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
	ОбработатьРезультат(ДокументРезультат);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	ОтчетыУНФ.ДобавитьВариантыОформленияСумм(
	НастройкиВариантов["Расходы"].ВариантыОформления,
	"СуммаРасходов");
	
КонецПроцедуры

Процедура ОбработатьРезультат(ТабличныйДокумент)
	
	Для НомерСтроки = 1 По ТабличныйДокумент.ВысотаТаблицы Цикл
		Для НомерКолонки = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
			Если ТабличныйДокумент.Область(НомерСтроки, НомерКолонки).Текст = НСтр("ru = 'Итого'") Тогда
				ТабличныйДокумент.Область(НомерСтроки, НомерКолонки).Текст = "";
			КонецЕсли;
			Если ТабличныйДокумент.Область(НомерСтроки, НомерКолонки).Текст = НСтр("ru = 'Сумма расходов.% в группировке'") Тогда
				ТабличныйДокумент.Область(НомерСтроки, НомерКолонки).Текст = "%";
				ТабличныйДокумент.Область(НомерСтроки, НомерКолонки).ГоризонтальноеПоложение = ГоризонтальноеПоложение.Право;
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Для НомерКолонки = 1 По ТабличныйДокумент.ШиринаТаблицы Цикл
		Если ТабличныйДокумент.Область(ТабличныйДокумент.ВысотаТаблицы, НомерКолонки).Текст = НСтр("ru = 'Итого'") Тогда
			ТабличныйДокумент.Область(ТабличныйДокумент.ВысотаТаблицы, НомерКолонки).Текст = "";
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли
