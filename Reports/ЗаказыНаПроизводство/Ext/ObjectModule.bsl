#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.Вставить("УровеньГруппировкиСтрок", 3);
	НастройкиВариантов["Остатки"].Вставить("ТолькоРесурсыОстатков", Истина);
	
	НастройкиВариантов["Ведомость"].Рекомендуемый = Истина;
	
	ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов);
	УстановитьТегиВариантов(НастройкиВариантов);
	ДобавитьОписанияСвязанныхПолей(НастройкиВариантов);
	
КонецПроцедуры

Процедура ПриКонтекстномОткрытии(СсылкаНаОбъект, ПолеСвязи, Отборы, Отказ) Экспорт
	
	Если ПолеСвязи = "ЗаказНаПроизводство" Тогда
		ЗаказНаПроизводство = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СсылкаНаОбъект, "ЗаказНаПроизводство");
		Если НЕ ЗначениеЗаполнено(ЗаказНаПроизводство) Тогда
			Отказ = Истина;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru = 'Отчет можно сформировать только при сборке на основании заказа на производство.'"));
			Возврат;
		КонецЕсли;
		Отборы.Вставить(ПолеСвязи, ЗаказНаПроизводство);
		Отборы.Вставить("СтПериод", Новый СтандартныйПериод);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗаполнитьПредопределенныеВариантыОформления(НастройкиВариантов)
	
	МассивПолейКоличеств = Новый Массив;
	Для каждого ДоступноеПоле Из КомпоновщикНастроек.Настройки.ДоступныеПоляВыбора.Элементы Цикл
		Если НЕ ДоступноеПоле.Ресурс Тогда
			Продолжить;
		КонецЕсли;
		ИмяПоля = Строка(ДоступноеПоле.Поле);
		МассивПолейКоличеств.Добавить(ИмяПоля);
	КонецЦикла;
	
	Для каждого НастройкиТекВарианта Из НастройкиВариантов Цикл
		
		ВариантыОформления = НастройкиТекВарианта.Значение.ВариантыОформления;
		ОтчетыУНФ.ДобавитьВариантыОформленияКоличества(ВариантыОформления, МассивПолейКоличеств);
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьТегиВариантов(НастройкиВариантов)
	
	НастройкиВариантов["Ведомость"].Теги = НСТР("ru = 'Производство,Заказы,Продукция'");
	НастройкиВариантов["Остатки"].Теги = НСТР("ru = 'Производство,Заказы,Продукция'");
	
КонецПроцедуры

Процедура ДобавитьОписанияСвязанныхПолей(НастройкиВариантов)
	
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказНаПроизводство", "Документ.ЗаказНаПроизводство");
	ОтчетыУНФ.ДобавитьОписаниеПривязки(НастройкиВариантов["Ведомость"].СвязанныеПоля, "ЗаказНаПроизводство", "Документ.СборкаЗапасов",, Истина);
	
КонецПроцедуры

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли
