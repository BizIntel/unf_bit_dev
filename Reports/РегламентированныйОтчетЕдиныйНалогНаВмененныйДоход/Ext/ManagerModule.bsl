#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс
	
Функция ВерсияФорматаВыгрузки(Знач НаДату = Неопределено, ВыбраннаяФорма = Неопределено) Экспорт
	
	Если НаДату = Неопределено Тогда
		НаДату = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НаДату >= '20090101' Тогда
		Возврат Перечисления.ВерсииФорматовВыгрузки.Версия500;
	ИначеЕсли НаДату >= '20050101' Тогда
		Возврат Перечисления.ВерсииФорматовВыгрузки.Версия300;
	КонецЕсли;
	
КонецФункции

Функция ТаблицаФормОтчета() Экспорт
	
	ОписаниеТиповСтрока = Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(0));
	
	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("Дата"));
	ОписаниеТиповДата = Новый ОписаниеТипов(МассивТипов, , Новый КвалификаторыДаты(ЧастиДаты.Дата));
	
	ТаблицаФормОтчета = Новый ТаблицаЗначений;
	ТаблицаФормОтчета.Колонки.Добавить("ФормаОтчета",        ОписаниеТиповСтрока);
	ТаблицаФормОтчета.Колонки.Добавить("ОписаниеОтчета",     ОписаниеТиповСтрока, "Утверждена",  20);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаНачалоДействия", ОписаниеТиповДата,   "Действует с", 5);
	ТаблицаФормОтчета.Колонки.Добавить("ДатаКонецДействия",  ОписаниеТиповДата,   "         по", 5);
	ТаблицаФормОтчета.Колонки.Добавить("РедакцияФормы",      ОписаниеТиповСтрока, "Редакция формы", 20);
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2010Кв1";
	НоваяФорма.ОписаниеОтчета     = "Форма утверждена приказом Минфина РФ от 08.12.2008 г. № 137н (в редакции приказа Минфина России от 19.11.2009 № 119н).";
	НоваяФорма.РедакцияФормы      = "от 19.11.2009 № 119н.";
	НоваяФорма.ДатаНачалоДействия = '2010-01-01';
	НоваяФорма.ДатаКонецДействия  = '2011-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2012Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение №1 к приказу ФНС России от 23.01.2012 № ММВ-7-3/13@.";
	НоваяФорма.РедакцияФормы      = "от 23.01.2012 № ММВ-7-3/13@.";
	НоваяФорма.ДатаНачалоДействия = '2012-01-01';
	НоваяФорма.ДатаКонецДействия  = '2013-09-30';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2013Кв4";
	НоваяФорма.ОписаниеОтчета     = "Приложение №1 к приказу ФНС России от 23.01.2012 № ММВ-7-3/13@ (в ред. приказа ФНС России от 14 ноября 2013 г. № ММВ-7-3/501@).";
	НоваяФорма.РедакцияФормы      = "от 14 ноября 2013 г. № ММВ-7-3/501@.";
	НоваяФорма.ДатаНачалоДействия = '2013-10-01';
	НоваяФорма.ДатаКонецДействия  = '2014-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2015Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение №1 к приказу ФНС России от 04.07.2014 № ММВ-7-3/353@.";
	НоваяФорма.РедакцияФормы      = "от 04 июля 2014 г. № ММВ-7-3/353@.";
	НоваяФорма.ДатаНачалоДействия = '2015-01-01';
	НоваяФорма.ДатаКонецДействия  = '2015-12-31';
	
	НоваяФорма = ТаблицаФормОтчета.Добавить();
	НоваяФорма.ФормаОтчета        = "ФормаОтчета2016Кв1";
	НоваяФорма.ОписаниеОтчета     = "Приложение №1 к приказу ФНС России от 04.07.2014 № ММВ-7-3/353@ (в редакции приказа ФНС России от 22.12.2015 № ММВ-7-3/590@).";
	НоваяФорма.РедакцияФормы      = "от 22.12.2015 № ММВ-7-3/590@.";
	НоваяФорма.ДатаНачалоДействия = '2016-01-01';
	НоваяФорма.ДатаКонецДействия  = РегламентированнаяОтчетностьКлиентСервер.ПустоеЗначениеТипа(Тип("Дата"));
	
	Возврат ТаблицаФормОтчета;
	
КонецФункции

Функция ДанныеРеглОтчета(ЭкземплярРеглОтчета, ИмяРаздела = "Раздел1") Экспорт
	
	Перем Результат;
	
	Если ИмяРаздела = "Раздел1" Тогда
		Результат = ИнтерфейсыВзаимодействияБРО.НовыйТаблицаДанныхРеглОтчета();
	ИначеЕсли ИмяРаздела = "Раздел3" Тогда
		Результат = Новый Структура;
	КонецЕсли;
	
	Если ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2016Кв1"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2015Кв1" Тогда
		
		ДанныеОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда
			Возврат Результат;
		КонецЕсли;
		
		// Раздел 1 (многострочный).
		// 010 - ОКТМО налога.
		// 020 - Сумма налога.
		
		Если ИмяРаздела = "Раздел1" И ДанныеОтчета.ДанныеМногострочныхРазделов.Свойство("П00001000") Тогда
			
			Для Каждого СтрТаблицы Из ДанныеОтчета.ДанныеМногострочныхРазделов.П00001000 Цикл
				
				Сумма = Результат.Добавить();
				Сумма.ВидНалога = Перечисления.ВидыНалогов.ЕНВД;
				Сумма.Период    = ЭкземплярРеглОтчета.ДатаОкончания;
				Сумма.ОКАТО     = СтрТаблицы.П000010001003;
				Сумма.Сумма     = СтрТаблицы.П000010002003;
				
			КонецЦикла;
			
		ИначеЕсли ИмяРаздела = "Раздел3" И ДанныеОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел2") Тогда
			
			// В декларации 2015 года итог по налоговой базе разделе 3
			// отсутствует, поэтому собираем базу по данным раздела 2.
			
			КодСтрокиНалоговойБазы = "П000020010003";
			
			НалоговаяБаза = 0;
			Для Каждого СтрТаблицы Из ДанныеОтчета.ДанныеМногостраничныхРазделов.Раздел2 Цикл
				НалоговаяБаза = НалоговаяБаза + СтрТаблицы.Данные[КодСтрокиНалоговойБазы]
			КонецЦикла;
			
			Результат.Вставить("НалоговаяБаза", НалоговаяБаза);
			
		КонецЕсли;
		
	ИначеЕсли ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2013Кв4"
		ИЛИ ЭкземплярРеглОтчета.ВыбраннаяФорма = "ФормаОтчета2012Кв1" Тогда
		
		ДанныеОтчета = ЭкземплярРеглОтчета.ДанныеОтчета.Получить();
		Если ТипЗнч(ДанныеОтчета) <> Тип("Структура") Тогда
			Возврат Результат;
		КонецЕсли;
		
		Если ИмяРаздела = "Раздел1" И ДанныеОтчета.ДанныеМногостраничныхРазделов.Свойство("Раздел1") Тогда
			
			// Раздел 1 (многостраничный).
			// 010 - Код КБК.
			// 020 - ОКАТО налога по 8 штук на каждой странице.
			// 030 - сумма.
			
			Раздел1 = ДанныеОтчета.ДанныеМногостраничныхРазделов.Раздел1;
			
			Период = ЭкземплярРеглОтчета.ДатаОкончания;
			КодСтрокиКБК   = "П000010001003";
			КодСтрокиОКАТО = "П000010002003_";
			КодСтрокиСумма = "П000010003003_";
			
			Для Каждого Страница Из Раздел1 Цикл
				
				НалогКУплате = Страница.Данные;
				
				Для НомерОКАТО = 1 По 8 Цикл
					
					Сумма = Результат.Добавить();
					Сумма.Период = Период;
					Сумма.КБК    = НалогКУплате[КодСтрокиКБК];
					Сумма.ОКАТО  = НалогКУплате[КодСтрокиОКАТО + НомерОКАТО];
					Сумма.Сумма  = НалогКУплате[КодСтрокиСумма+ НомерОКАТО];
				КонецЦикла;
				
			КонецЦикла;
			
		ИначеЕсли ИмяРаздела = "Раздел3" И ДанныеОтчета.ПоказателиОтчета.Свойство("ПолеТабличногоДокументаРаздел3") Тогда
			
			// Раздел 3.
			// 010 - Налоговая база.
			
			Раздел3 = ДанныеОтчета.ПоказателиОтчета.ПолеТабличногоДокументаРаздел3;
			
			КодСтрокиНалоговойБазы = "П000030001003";
			
			Результат.Вставить("НалоговаяБаза",
				?(Раздел3.Свойство(КодСтрокиНалоговойБазы), Раздел3[КодСтрокиНалоговойБазы], 0));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция ДеревоФормИФорматов() Экспорт
	
	ФормыИФорматы = Новый ДеревоЗначений;
	ФормыИФорматы.Колонки.Добавить("Код");
	ФормыИФорматы.Колонки.Добавить("ДатаПриказа");
	ФормыИФорматы.Колонки.Добавить("НомерПриказа");
	ФормыИФорматы.Колонки.Добавить("ДатаНачалаДействия");
	ФормыИФорматы.Колонки.Добавить("ДатаОкончанияДействия");
	ФормыИФорматы.Колонки.Добавить("ИмяОбъекта");
	ФормыИФорматы.Колонки.Добавить("Описание");
	
	Форма20100101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152016", '2009-11-19', "119н", "ФормаОтчета2010Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20100101, "5.02");
	
	Форма20120101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152016", '2012-01-23', "ММВ-7-3/13@", "ФормаОтчета2012Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20120101, "5.03");
	
	Форма20131231 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152016", '2013-11-14', "ММВ-7-3/501@", "ФормаОтчета2013Кв4");
	ОпределитьФорматВДеревеФормИФорматов(Форма20131231, "5.04");
	
	Форма20150101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152016", '2014-07-04', "ММВ-7-3/353@", "ФормаОтчета2015Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20150101, "5.05");
	
	Форма20160101 = ОпределитьФормуВДеревеФормИФорматов(ФормыИФорматы, "1152016", '2015-12-22', "ММВ-7-3/590@", "ФормаОтчета2016Кв1");
	ОпределитьФорматВДеревеФормИФорматов(Форма20160101, "5.06");
	
	Возврат ФормыИФорматы;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОпределитьФормуВДеревеФормИФорматов(ДеревоФормИФорматов, Код, ДатаПриказа = '00010101', НомерПриказа = "", ИмяОбъекта = "",
			ДатаНачалаДействия = '00010101', ДатаОкончанияДействия = '00010101', Описание = "")
	
	НовСтр = ДеревоФормИФорматов.Строки.Добавить();
	НовСтр.Код = СокрЛП(Код);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ДатаНачалаДействия;
	НовСтр.ДатаОкончанияДействия = ДатаОкончанияДействия;
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

Функция ОпределитьФорматВДеревеФормИФорматов(Форма, Версия, ДатаПриказа = '00010101', НомерПриказа = "",
			ДатаНачалаДействия = Неопределено, ДатаОкончанияДействия = Неопределено, ИмяОбъекта = "", Описание = "")
	
	НовСтр = Форма.Строки.Добавить();
	НовСтр.Код = СокрЛП(Версия);
	НовСтр.ДатаПриказа = ДатаПриказа;
	НовСтр.НомерПриказа = СокрЛП(НомерПриказа);
	НовСтр.ДатаНачалаДействия = ?(ДатаНачалаДействия = Неопределено, Форма.ДатаНачалаДействия, ДатаНачалаДействия);
	НовСтр.ДатаОкончанияДействия = ?(ДатаОкончанияДействия = Неопределено, Форма.ДатаОкончанияДействия, ДатаОкончанияДействия);
	НовСтр.ИмяОбъекта = СокрЛП(ИмяОбъекта);
	НовСтр.Описание = СокрЛП(Описание);
	Возврат НовСтр;
	
КонецФункции

#КонецОбласти

#КонецЕсли