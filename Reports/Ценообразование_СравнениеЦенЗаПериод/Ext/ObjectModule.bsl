#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

Процедура ПриОпределенииНастроекОтчета(НастройкиОтчета, НастройкиВариантов) Экспорт
	
	НастройкиОтчета.ПоказыватьНастройкиДиаграммыНаФормеОтчета = Ложь;
	НастройкиОтчета.ПрограммноеИзменениеФормыОтчета = Истина;
	
	НастройкиВариантов["Основной"].Теги = НСтр("ru = 'Цены'");
	
КонецПроцедуры

Процедура ОбновитьНастройкиНаФорме(НастройкиОтчета, НастройкиСКД, Форма) Экспорт
	
	ДобавитьЭлементДляВыбораПараметра(
	НастройкиСКД,
	Форма,
	"ВидЦен",
	НСтр("ru = 'Колонка прайс-листа'"),
	Справочники.ВидыЦен.Оптовая);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ПриКомпоновкеРезультата(ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка)
	
	УстановитьЗаголовкиКолонок(КомпоновщикНастроек.Настройки);
	
	ОтчетыУНФ.ПриКомпоновкеРезультата(КомпоновщикНастроек, СхемаКомпоновкиДанных, ДокументРезультат, ДанныеРасшифровки, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьЭлементДляВыбораПараметра(НастройкиСКД, Форма, ИмяРеквизита, Заголовок, ЗначениеПоУмолчанию)
	
	Стр = Форма.ПоляНастроек.ПолучитьЭлементы().Добавить();
	Стр.Тип = "Параметр";
	Стр.Поле = ИмяРеквизита;
	Стр.ТипЗначения = Новый ОписаниеТипов("СправочникСсылка.ВидыЦен");
	Стр.Заголовок = Заголовок;
	Стр.ВидЭлемента = "Поле";
	Стр.Реквизиты = Новый Структура;
	Стр.Элементы = Новый Структура;
	Стр.ДополнительныеПараметры = Новый Структура;
	Стр.Реквизиты.Вставить(ИмяРеквизита, ЗначениеПоУмолчанию);
	МассивРеквизитов = Новый Массив;
	Для каждого ОписаниеРеквизита Из Стр.Реквизиты Цикл
		МассивРеквизитов.Добавить(Новый РеквизитФормы(ОписаниеРеквизита.Ключ, Стр.ТипЗначения,, Стр.Заголовок));
	КонецЦикла;
	Стр.Создан = Истина;
	
	Форма.ИзменитьРеквизиты(МассивРеквизитов);
	
	Форма[ИмяРеквизита] = ЗначениеПоУмолчанию;
	НастройкиСКД.ПараметрыДанных.УстановитьЗначениеПараметра(Стр.Поле, ЗначениеПоУмолчанию);
	Элемент = Форма.Элементы.Добавить(ИмяРеквизита, Тип("ПолеФормы"), Форма.Элементы.ГруппаПараметрыЭлементы);
	Элемент.Вид = ВидПоляФормы.ПолеВвода;
	Элемент.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Верх;
	Элемент.ПутьКДанным = ИмяРеквизита;
	Элемент.УстановитьДействие("ПриИзменении", "Подключаемый_ПараметрПриИзменении");
	Стр.Элементы.Вставить(Элемент.Имя, Элемент.ПутьКДанным);
	
	Элемент.КнопкаОткрытия = Ложь;
	Элемент.ИсторияВыбораПриВводе = ИсторияВыбораПриВводе.НеИспользовать;
	Выборка = Справочники.ВидыЦен.Выбрать();
	Пока Выборка.Следующий() Цикл
		Элемент.СписокВыбора.Добавить(Выборка.Ссылка, Выборка.Наименование);
	КонецЦикла;

КонецПроцедуры

Процедура УстановитьЗаголовкиКолонок(НастройкиОтчета)
	
	ПараметрПериодСравнения = НастройкиОтчета.ПараметрыДанных.НайтиЗначениеПараметра(Новый ПараметрКомпоновкиДанных("СтПериод"));
	
	Для каждого ВыбранноеПоле Из НастройкиОтчета.Структура[0].Выбор.Элементы Цикл
		
		Если ТипЗнч(ВыбранноеПоле) <> Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВыбранноеПоле.Элементы.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если ВыбранноеПоле.Элементы[0].Поле = Новый ПолеКомпоновкиДанных("ЦенаНачало") Тогда
			
			ВыбранноеПоле.Заголовок = Формат(ПараметрПериодСравнения.Значение.ДатаНачала, "ДФ=dd.MM.yyyy");
			
		ИначеЕсли ВыбранноеПоле.Элементы[0].Поле = Новый ПолеКомпоновкиДанных("ЦенаКонец") Тогда
			
			ВыбранноеПоле.Заголовок = Формат(ПараметрПериодСравнения.Значение.ДатаОкончания, "ДФ=dd.MM.yyyy");
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

ЭтоОтчетУНФ = Истина;

#КонецЕсли