#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДенежныеСредства(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПоступлениеДенежныхСредств", НСтр("ru = 'Поступление денежных средств'"));
	Запрос.УстановитьПараметр("ПоступлениеРозничнойВыручки", НСтр("ru = 'Поступление розничной выручки'"));
	Запрос.УстановитьПараметр("ПеремещениеДенежныхСредств", НСтр("ru = 'Перемещение денежных средств'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.УстановитьПараметр("ИспользуютсяПереводыВПути",РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата,СтруктураДополнительныеСвойства.ДляПроведения.Организация));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаПоступлениеВКассу.ВалютаДенежныхСредств);
	
	Если ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		Итог = ДокументСсылкаПоступлениеВКассу.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	Иначе
		Итог = 0;
	КонецЕсли;
	ОстатокСуммы = ДокументСсылкаПоступлениеВКассу.СуммаДокумента - Итог;
	Запрос.УстановитьПараметр("ПлатежныйКалендарь", ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"));
	Запрос.УстановитьПараметр("ОстатокСуммы", ОстатокСуммы);
	Запрос.УстановитьПараметр("УчетВалютныхОпераций", ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные) КАК ТипДенежныхСредств,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.Касса КАК БанковскийСчетКасса,
	|	ТаблицаДокумента.АналитикаДДС КАК Аналитика,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК Валюта,
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(&ОстатокСуммы) КАК СуммаВал,
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаДляОстатка,
	|	СУММА(&ОстатокСуммы) КАК СуммаВалДляОстатка,
	|	ТаблицаДокумента.КассаСчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|				ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА &ПоступлениеРозничнойВыручки
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА &ПеремещениеДенежныхСредств
	|		ИНАЧЕ &ПоступлениеДенежныхСредств
	|	КОНЕЦ КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаДенежныеСредства
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	(ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПодотчетника)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке))
	|	И &ОстатокСуммы > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.КассаСчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|				ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА &ПоступлениеРозничнойВыручки
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА &ПеремещениеДенежныхСредств
	|		ИНАЧЕ &ПоступлениеДенежныхСредств
	|	КОНЕЦ,
	|	ТаблицаДокумента.АналитикаДДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные),
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ТаблицаДокумента.Кратность) КАК ЧИСЛО(15, 2))),
	|	СУММА(&ОстатокСуммы),
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ТаблицаДокумента.Кратность) КАК ЧИСЛО(15, 2))),
	|	СУММА(&ОстатокСуммы),
	|	ТаблицаДокумента.Касса,
	|	&ПоступлениеДенежныхСредств
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|	И &ОстатокСуммы > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.Касса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВЫРАЗИТЬ(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаРасшифровкаПлатежа.Ссылка.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (ВременнаяТаблицаРасшифровкаПлатежа.Ссылка.Кратность * КурсыВалютУчетаСрезПоследних.Курс) КАК ЧИСЛО(15, 2))),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	СУММА(ВЫРАЗИТЬ(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * ВременнаяТаблицаРасшифровкаПлатежа.Ссылка.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (ВременнаяТаблицаРасшифровкаПлатежа.Ссылка.Кратность * КурсыВалютУчетаСрезПоследних.Курс) КАК ЧИСЛО(15, 2))),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	&ПоступлениеДенежныхСредств
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|	И &ПлатежныйКалендарь
	|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.АналитикаДДС,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность)),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность)),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|				ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА &ПоступлениеРозничнойВыручки
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА &ПеремещениеДенежныхСредств
	|		ИНАЧЕ &ПоступлениеДенежныхСредств
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	(ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПодотчетника)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке))
	|	И &ПлатежныйКалендарь
	|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.АналитикаДДС,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|				ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА &ПоступлениеРозничнойВыручки
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА &ПеремещениеДенежныхСредств
	|		ИНАЧЕ &ПоступлениеДенежныхСредств
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Контрагент
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Контрагент
	|	КОНЕЦ,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	&ПоступлениеДенежныхСредств
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	(ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Контрагент
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Контрагент
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.СчетОрганизации,
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))),
	|	СУММА(&ОстатокСуммы),
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))),
	|	СУММА(&ОстатокСуммы),
	|	ТаблицаДокумента.КассаСчетУчета,
	|	&ПеремещениеДенежныхСредств
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|	И НЕ &ИспользуютсяПереводыВПути
	|	И &ОстатокСуммы > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.КассаСчетУчета,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.СчетОрганизации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Ссылка.СчетОрганизации,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	&ПеремещениеДенежныхСредств
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|	И НЕ &ИспользуютсяПереводыВПути
	|	И &ПлатежныйКалендарь
	|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Ссылка.СчетОрганизации
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТипДенежныхСредств,
	|	БанковскийСчетКасса,
	|	Валюта,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаДенежныеСредства.Организация КАК Организация,
	|	ВременнаяТаблицаДенежныеСредства.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ВременнаяТаблицаДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ВременнаяТаблицаДенежныеСредства.Валюта КАК Валюта
	|ИЗ
	|	ВременнаяТаблицаДенежныеСредства";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДенежныеСредства");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыДенежныеСредства(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТЗРезультатЗапроса = МассивРезультатов[НомерЗапроса].Выгрузить();
	Если ДокументСсылкаПоступлениеВКассу.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты И ТЗРезультатЗапроса.Количество() > 1 Тогда
		ИтогСуммаУчета = 0;
		Для Каждого ТекущаяСтрока Из ТЗРезультатЗапроса Цикл
			Если ТекущаяСтрока.СуммаВал <> 0 Тогда // Исключаем курсовые разницы.
				ИтогСуммаУчета = ИтогСуммаУчета + ТекущаяСтрока.Сумма;
			КонецЕсли;
		КонецЦикла;
		Если ИтогСуммаУчета <> ДокументСсылкаПоступлениеВКассу.СуммаУчета Тогда
			Дельта = ИтогСуммаУчета - ДокументСсылкаПоступлениеВКассу.СуммаУчета;
			ТЗРезультатЗапроса.Сортировать("Сумма Убыв");
			ТЗРезультатЗапроса[0].Сумма = ТЗРезультатЗапроса[0].Сумма - Дельта;
			ТЗРезультатЗапроса.Сортировать("НомерСтроки Возр");
		КонецЕсли;
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредства", ТЗРезультатЗапроса);
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДенежныеСредстваВКассахККМ(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.КассаККМ КАК КассаККМ,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК Валюта,
	|	СУММА(ТаблицаДокумента.Сумма) КАК Сумма,
	|	СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаДляОстатка,
	|	СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВалДляОстатка,
	|	ТаблицаДокумента.КассаККМСчетУчета КАК СчетУчета,
	|	&РасходДенежныхСредств КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаДенежныеСредстваВКассахККМ
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.КассаККМ,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.КассаККМСчетУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	КассаККМ,
	|	Валюта,
	|	СчетУчета";
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("РасходДенежныхСредств", НСтр("ru = 'Расход денежных средств из кассы ККМ'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаДенежныеСредстваВКассахККМ.Организация КАК Организация,
	|	ВременнаяТаблицаДенежныеСредстваВКассахККМ.КассаККМ КАК КассаККМ
	|ИЗ
	|	ВременнаяТаблицаДенежныеСредстваВКассахККМ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДенежныеСредстваВКассахККМ");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыДенежныеСредстваВКассахККМ(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредстваВКассахККМ", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредстваВКассахККМ()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПодотчетниками(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПогашениеДолгаПодотчетника", НСтр("ru = 'Погашение долга подотчетника'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Касса КАК Касса,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ТаблицаДокумента.Подотчетник КАК Сотрудник,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК Валюта,
	|	СУММА(ТаблицаДокумента.Сумма) КАК Сумма,
	|	СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|	-СУММА(ТаблицаДокумента.Сумма) КАК СуммаДляОстатка,
	|	-СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВалДляОстатка,
	|	ТаблицаДокумента.ПодотчетникСчетРасчетовСПодотчетниками КАК СчетУчета,
	|	&ПогашениеДолгаПодотчетника КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПодотчетниками
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПодотчетника)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.Документ,
	|	ТаблицаДокумента.Подотчетник,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.ПодотчетникСчетРасчетовСПодотчетниками
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Сотрудник,
	|	Валюта,
	|	Документ,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Валюта КАК Валюта,
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Документ КАК Документ
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПодотчетниками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПодотчетниками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПодотчетниками(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПодотчетниками", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПодотчетниками()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеАвансаПокупателя", "Возникновение аванса покупателя");
	Запрос.УстановитьПараметр("ПогашениеОбязательствПокупателя", "Погашение обязательств покупателя");
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса КАК Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ КАК ТипРасчетов,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК Сумма,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВал,
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК СуммаДляОстатка,
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПокупателя
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПокупателем
	|	КОНЕЦ КАК СчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов КАК Валюта,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ВозникновениеАвансаПокупателя
	|		ИНАЧЕ &ПогашениеОбязательствПокупателя
	|	КОНЕЦ КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателями
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПокупателя
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПокупателем
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ВозникновениеАвансаПокупателя
	|		ИНАЧЕ &ПогашениеОбязательствПокупателя
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПокупателями.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПокупателями.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПокупателями.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПокупателями.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПокупателями.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПокупателями.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПокупателями");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПокупателями(Запрос.МенеджерВременныхТаблиц, Ложь, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПокупателями", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПокупателями()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПогашениеАвансаПоставщику", НСтр("ru = 'Погашение аванса поставщику'"));
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПередПоставщиком", НСтр("ru = 'Возникновение обязательств перед поставщиком'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса КАК Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Дата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ КАК ТипРасчетов,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК Сумма,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВал,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК СуммаДляОстатка,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПоставщику
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПоставщиком
	|	КОНЕЦ КАК СчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов КАК Валюта,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ПогашениеАвансаПоставщику
	|		ИНАЧЕ &ВозникновениеОбязательствПередПоставщиком
	|	КОНЕЦ КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПоставщиками
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПоставщику
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПоставщиком
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ПогашениеАвансаПоставщику
	|		ИНАЧЕ &ВозникновениеОбязательствПередПоставщиком
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПоставщиками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщиками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПоставщиками(Запрос.МенеджерВременныхТаблиц, Ложь, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПоставщиками()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ОтражениеДоходов", НСтр("ru = 'Отражение доходов'"));
	Запрос.УстановитьПараметр("ОтражениеРасходов", НСтр("ru = 'Отражение расходов'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК СтруктурнаяЕдиница,
	|	НЕОПРЕДЕЛЕНО КАК Заказ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Корреспонденция КАК СчетУчета,
	|	ТаблицаДокумента.АналитикаДоходовИРасходов КАК Аналитика,
	|	&ОтражениеДоходов КАК СодержаниеПроводки,
	|	ТаблицаДокумента.Сумма КАК СуммаДоходов,
	|	0 КАК СуммаРасходов,
	|	ТаблицаДокумента.Сумма КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	(ТаблицаДокумента.КорреспонденцияТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Доходы)
	|			ИЛИ ТаблицаДокумента.КорреспонденцияТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеДоходы))
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Подразделение,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаВыручкиОтПродаж,
	|	ТаблицаДокумента.КассаККМ,
	|	&ОтражениеДоходов,
	|	ТаблицаДокумента.Сумма,
	|	0,
	|	ТаблицаДокумента.Сумма
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницДенежныеСредства КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПодотчетниками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницДенежныеСредстваВКассахККМ КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницСуммовойУчетВРознице КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Подразделение,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	НЕОПРЕДЕЛЕНО,
	|	&ОтражениеРасходов,
	|	0,
	|	ТаблицаДокумента.Себестоимость,
	|	ТаблицаДокумента.Себестоимость
	|ИЗ
	|	ВременнаяТаблицаСебестоимость КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	11,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницПрочиеРасчеты КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("Содержание", НСтр("ru = 'Оприходование денежных средств с произвольного счета'"));
	Запрос.УстановитьПараметр("СодержаниеПокупкаВалюты", НСтр("ru = 'Покупка валюты'"));
	Запрос.УстановитьПараметр("СодержаниеРозничнаяВыручка", НСтр("ru = 'Розничная выручка'"));
	Запрос.УстановитьПараметр("СодержаниеСебестоимость", НСтр("ru = 'Себестоимость'"));
	Запрос.УстановитьПараметр("СодержаниеНаценка", НСтр("ru = 'Наценка'"));
		
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаДокумента.КассаСчетУчета КАК СчетДт,
	|	ТаблицаДокумента.Корреспонденция КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|				ТОГДА &СодержаниеПокупкаВалюты
	|			ИНАЧЕ &Содержание
	|		КОНЕЦ КАК СТРОКА(100)) КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	(ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.КассаСчетУчета,
	|	ТаблицаДокумента.КассаККМСчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаККМСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаККМСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ВЫРАЗИТЬ(&СодержаниеРозничнаяВыручка КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.КассаСчетУчета,
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаВыручкиОтПродаж,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаВыручкиОтПродаж.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.КассаСчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаВыручкиОтПродаж.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ВЫРАЗИТЬ(&СодержаниеРозничнаяВыручка КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.Касса.СчетУчета,
	|	ТаблицаДокумента.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПодотчетниками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПодотчетниками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.Касса.СчетУчета,
	|	ТаблицаДокумента.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.Касса.СчетУчета,
	|	ТаблицаДокумента.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницДенежныеСредства КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	11,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницДенежныеСредстваВКассахККМ КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	12,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницСуммовойУчетВРознице КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	13,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж.Валютный
	|			ТОГДА 0
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице.Валютный
	|			ТОГДА 0
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Себестоимость,
	|	ВЫРАЗИТЬ(&СодержаниеСебестоимость КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаСебестоимость КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	14,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаНаценки,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаНаценки.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице.Валютный
	|			ТОГДА 0
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаНаценки.Валютный
	|			ТОГДА 0
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	-(ТаблицаДокумента.Сумма - ТаблицаСебестоимость.Себестоимость),
	|	ВЫРАЗИТЬ(&СодержаниеНаценка КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСебестоимость КАК ТаблицаСебестоимость
	|		ПО ТаблицаДокумента.Организация = ТаблицаСебестоимость.Организация
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	15,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.Касса.СчетУчета,
	|	ТаблицаДокумента.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	16,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	17,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницПрочиеРасчеты КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	18,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.Касса.СчетУчета,
	|	ТаблицаДокумента.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Касса.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаПрочиеРасчеты КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаПоступлениеВКассу.ВалютаДенежныхСредств);
	
	Если ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		Итог = ДокументСсылкаПоступлениеВКассу.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	Иначе
		Итог = 0;
	КонецЕсли;
	ОстатокСуммы = ДокументСсылкаПоступлениеВКассу.СуммаДокумента - Итог;
	Запрос.УстановитьПараметр("ПлатежныйКалендарь", ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"));
	Запрос.УстановитьПараметр("ОстатокСуммы", ОстатокСуммы);
	Запрос.УстановитьПараметр("УчетВалютныхОпераций", ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ТаблицаДокумента.ДоговорОбслуживанияНаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.СуммаУчета КАК СуммаДоходов,
	|	0 КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|	И (ТаблицаДокумента.ПризнакАванса
	|			ИЛИ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	Таблица.СуммаДоходов,
	|	0
	|ИЗ
	|	ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные КАК Таблица
	|ГДЕ
	|	Таблица.СуммаДоходов > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ТаблицаДокумента.Статья,
	|	ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)),
	|	0
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником))
	|	И &ОстатокСуммы > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ТаблицаДокумента.Статья,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)),
	|	0
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником))
	|	И &ПлатежныйКалендарь
	|	И ТаблицаДокумента.СуммаПлатежа > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ТаблицаДокумента.Статья,
	|	ВЫРАЗИТЬ(&ОстатокСуммы * ТаблицаДокумента.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * ТаблицаДокумента.Кратность) КАК ЧИСЛО(15, 2)),
	|	0
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|	И &ОстатокСуммы > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ТаблицаДокумента.Статья,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * ТаблицаДокумента.Ссылка.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (ТаблицаДокумента.Ссылка.Кратность * КурсыВалютУчетаСрезПоследних.Курс) КАК ЧИСЛО(15, 2)),
	|	0
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|	И &ПлатежныйКалендарь
	|	И ТаблицаДокумента.СуммаПлатежа > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ТаблицаДокумента.ДоговорОбслуживанияНаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья,
	|	0,
	|	-ТаблицаДокумента.СуммаУчета
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|	И (ТаблицаДокумента.ПризнакАванса
	|			ИЛИ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	0,
	|	-Таблица.СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные КАК Таблица
	|ГДЕ
	|	Таблица.СуммаРасходов > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТЗРезультатЗапроса = РезультатЗапроса.Выгрузить();
	Если ДокументСсылкаПоступлениеВКассу.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты И ТЗРезультатЗапроса.Количество() > 1 Тогда
		ИтогСуммаУчета = ТЗРезультатЗапроса.Итог("СуммаДоходов");
		Если ИтогСуммаУчета <> ДокументСсылкаПоступлениеВКассу.СуммаУчета Тогда
			Дельта = ИтогСуммаУчета - ДокументСсылкаПоступлениеВКассу.СуммаУчета;
			ТЗРезультатЗапроса.Сортировать("СуммаДоходов Убыв");
			ТЗРезультатЗапроса[0].СуммаДоходов = ТЗРезультатЗапроса[0].СуммаДоходов - Дельта;
		КонецЕсли;
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", ТЗРезультатЗапроса);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.СуммаУчета КАК СуммаДоходов,
	|	0 КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|	И НЕ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И ТаблицаДокумента.ПризнакАванса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья,
	|	0,
	|	-ТаблицаДокумента.СуммаУчета
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|	И НЕ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И ТаблицаДокумента.ПризнакАванса
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.Дата);
	
	// Формирование таблицы с суммами к списанию.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	СУММА(ТаблицаДокумента.СуммаУчета) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|	И НЕ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И НЕ ТаблицаДокумента.ПризнакАванса
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДоходыИРасходыОтложенные");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Документ");
	Блокировка.Заблокировать();
	
	ТаблицаСуммыКСписанию = РезультатЗапроса.Выгрузить();
	
	// Формирование таблицы с остатками.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ДоходыИРасходыОтложенныеОстатки.Организация КАК Организация,
	|	ДоходыИРасходыОтложенныеОстатки.Документ КАК Документ,
	|	ДоходыИРасходыОтложенныеОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК Статья,
	|	0 КАК СуммаДоходов,
	|	0 КАК СуммаРасходов,
	|	СУММА(ДоходыИРасходыОтложенныеОстатки.СуммаДоходовОстаток) КАК СуммаДоходовОстаток,
	|	-СУММА(ДоходыИРасходыОтложенныеОстатки.СуммаРасходовОстаток) КАК СуммаРасходовОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДоходыИРасходыОтложенныеОстатки.Организация КАК Организация,
	|		ДоходыИРасходыОтложенныеОстатки.Документ КАК Документ,
	|		ДоходыИРасходыОтложенныеОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ДоходыИРасходыОтложенныеОстатки.СуммаДоходовОстаток КАК СуммаДоходовОстаток,
	|		ДоходыИРасходыОтложенныеОстатки.СуммаРасходовОстаток КАК СуммаРасходовОстаток
	|	ИЗ
	|		РегистрНакопления.ДоходыИРасходыОтложенные.Остатки(
	|				,
	|				Организация = &Организация
	|					И Документ В
	|						(ВЫБРАТЬ
	|							ТаблицаДокумента.Документ
	|						ИЗ
	|							ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|						ГДЕ
	|							(ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|								ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)))) КАК ДоходыИРасходыОтложенныеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаДоходыИРасходыОтложенные.Организация,
	|		ДвиженияДокументаДоходыИРасходыОтложенные.Документ,
	|		ДвиженияДокументаДоходыИРасходыОтложенные.НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаДоходыИРасходыОтложенные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаДоходов, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаДоходов, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаДоходыИРасходыОтложенные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаРасходов, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаРасходов, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ДоходыИРасходыОтложенные КАК ДвиженияДокументаДоходыИРасходыОтложенные
	|	ГДЕ
	|		ДвиженияДокументаДоходыИРасходыОтложенные.Регистратор = &Ссылка) КАК ДоходыИРасходыОтложенныеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИРасходыОтложенныеОстатки.Организация,
	|	ДоходыИРасходыОтложенныеОстатки.Документ,
	|	ДоходыИРасходыОтложенныеОстатки.НаправлениеДеятельности
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ";
	
	ТаблицаСуммыОстатки = Запрос.Выполнить().Выгрузить();

	ТаблицаСуммыОстатки.Индексы.Добавить("Документ");
	
	// Расчет сумм списания.
	Для каждого СтрокаСуммыКСписанию Из ТаблицаСуммыКСписанию Цикл
		СуммаКСписанию = СтрокаСуммыКСписанию.СуммаКСписанию;
		Отбор = Новый Структура("Документ", СтрокаСуммыКСписанию.Документ);
		МассивСтрокСуммыОстатки = ТаблицаСуммыОстатки.НайтиСтроки(Отбор);
		Для каждого СтрокаСуммыОстатки Из МассивСтрокСуммыОстатки Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаСуммыОстатки.СуммаДоходовОстаток < СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаДоходов = СтрокаСуммыОстатки.СуммаДоходовОстаток;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья; 
				СуммаКСписанию = СуммаКСписанию - СтрокаСуммыОстатки.СуммаДоходовОстаток;
			ИначеЕсли СтрокаСуммыОстатки.СуммаДоходовОстаток >= СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаДоходов = СуммаКСписанию;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Формирование таблицы с суммами к списанию.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	СУММА(ТаблицаДокумента.СуммаУчета) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|	И НЕ ТаблицаДокумента.ПризнакАванса
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья"; 
	
	ТаблицаСуммыКСписанию = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаСуммыКСписанию Из ТаблицаСуммыКСписанию Цикл
		СуммаКСписанию = СтрокаСуммыКСписанию.СуммаКСписанию;
		Отбор = Новый Структура("Документ", СтрокаСуммыКСписанию.Документ);
		МассивСтрокСуммыОстатки = ТаблицаСуммыОстатки.НайтиСтроки(Отбор);
		Для каждого СтрокаСуммыОстатки Из МассивСтрокСуммыОстатки Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаСуммыОстатки.СуммаРасходовОстаток < СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаРасходов = СтрокаСуммыОстатки.СуммаРасходовОстаток;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = СуммаКСписанию - СтрокаСуммыОстатки.СуммаРасходовОстаток;
			ИначеЕсли СтрокаСуммыОстатки.СуммаРасходовОстаток >= СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаРасходов = СуммаКСписанию;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Формирование временной таблицы с суммами, статьями и направлениями
	// деятельности. Понадобится для формирования движений по доходам и расходам
	// кассовым методом.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	Таблица.Статья КАК Статья,
	|	Таблица.СуммаДоходов КАК СуммаДоходов,
	|	Таблица.СуммаРасходов КАК СуммаРасходов,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ
	|	(Таблица.СуммаДоходов > 0
	|			ИЛИ Таблица.СуммаРасходов > 0)";
	
	Запрос.УстановитьПараметр("Таблица", ТаблицаСуммыОстатки);
	
	Запрос.Выполнить();
	
	// Формирование таблицы для записи в регистр.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	Таблица.Статья КАК Статья,
	|	Таблица.СуммаДоходов КАК СуммаДоходов,
	|	- Таблица.СуммаРасходов КАК СуммаРасходов,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные КАК Таблица";

	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()  

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПлатежныйКалендарь(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьПлатежныйКалендарь", Константы.ФункциональнаяОпцияПлатежныйКалендарь.Получить());
	Запрос.УстановитьПараметр("ИспользуютсяПереводыВПути",РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата,СтруктураДополнительныеСвойства.ДляПроведения.Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные) КАК ТипДенежныхСредств,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден) КАК СтатусУтвержденияПлатежа,
	|	ТаблицаДокумента.Касса КАК БанковскийСчетКасса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря КАК СчетНаОплату,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ТаблицаДокумента.СуммаПлатежа
	|		КОНЕЦ) КАК СуммаОплаты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Касса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря,
	|	ТаблицаДокумента.Статья
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2 КАК Порядок,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные) КАК ТипДенежныхСредств,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден) КАК СтатусУтвержденияПлатежа,
	|	ТаблицаДокумента.СчетОрганизации КАК БанковскийСчетКасса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря КАК СчетНаОплату,
	|	-СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ТаблицаДокумента.СуммаПлатежа
	|		КОНЕЦ) КАК СуммаОплаты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|		И НЕ &ИспользуютсяПереводыВПути
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Касса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря,
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.СчетОрганизации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.Наличные),
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден),
	|	ТаблицаДокумента.Касса,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	НЕОПРЕДЕЛЕНО,
	|	ТаблицаДокумента.СуммаДокумента
	|ИЗ
	|	Документ.ПоступлениеВКассу КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			КОЛИЧЕСТВО(ЕСТЬNULL(ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки, 0)) КАК Количество
	|		ИЗ
	|			ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа) КАК ВложенныйЗапрос
	|		ПО (ИСТИНА)
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|	И ТаблицаДокумента.Ссылка = &Ссылка
	|	И ВложенныйЗапрос.Количество = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлатежныйКалендарь", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПлатежныйКалендарь()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаПоступлениеНаСчет, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.СчетНаОплату КАК СчетНаОплату,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаАванса,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаОплаты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСчета
	|		ПО ТаблицаДокумента.СчетНаОплату.ВалютаДокумента = КурсыВалютСчета.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ВестиУчетОплатыПоСчетам
	|	И ТаблицаДокумента.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|	И ТаблицаДокумента.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|	И ТаблицаДокумента.СчетНаОплату <> НЕОПРЕДЕЛЕНО
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.СчетНаОплату,
	|	ТаблицаДокумента.ВидОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Заказ,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСчета
	|		ПО ТаблицаДокумента.Заказ.ВалютаДокумента = КурсыВалютСчета.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ВестиУчетОплатыПоСчетам
	|	И (ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Заказ) = ТИП(Документ.ЗаказПокупателя)
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Заказ) = ТИП(Документ.ЗаказПоставщику)
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Заказ) = ТИП(Документ.ПриемИПередачаВРемонт)
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ПриемИПередачаВРемонт.ПустаяСсылка))
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Заказ,
	|	ТаблицаДокумента.ВидОперации
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОплатаСчетовИЗаказов", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаОплатаСчетовИЗаказов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСуммовойУчетВРознице(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("РозничнаяВыручка", НСтр("ru='Розничная выручка'"));
	Запрос.УстановитьПараметр("Себестоимость", НСтр("ru='Себестоимость'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице КАК СтруктурнаяЕдиницаСчетУчетаВРознице,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж КАК НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК Валюта,
	|	СУММА(ТаблицаДокумента.Сумма) КАК Сумма,
	|	СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|	-СУММА(ТаблицаДокумента.Сумма) КАК СуммаДляОстатка,
	|	-СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВалДляОстатка,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице КАК СчетУчета,
	|	0 КАК Себестоимость,
	|	&РозничнаяВыручка КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаСуммовойУчетВРознице
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Подразделение,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	ТаблицаДокумента.СтруктурнаяЕдиница,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Валюта,
	|	СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.Валюта КАК Валюта,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж КАК НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	ТаблицаДокумента.СтруктурнаяЕдиницаСчетУчетаВРознице КАК СтруктурнаяЕдиницаСчетУчетаВРознице,
	|	ВЫБОР
	|		КОГДА СуммовойУчетВРозницеОстатки.СуммаВалОстаток - ТаблицаДокумента.СуммаВал = 0
	|			ТОГДА СуммовойУчетВРозницеОстатки.СебестоимостьОстаток
	|		КОГДА СуммовойУчетВРозницеОстатки.СуммаВалОстаток <> 0
	|			ТОГДА ВЫРАЗИТЬ(СуммовойУчетВРозницеОстатки.СебестоимостьОстаток * ТаблицаДокумента.СуммаВал / СуммовойУчетВРозницеОстатки.СуммаВалОстаток КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Себестоимость
	|ПОМЕСТИТЬ ВременнаяТаблицаСебестоимость
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			СуммовойУчетВРозницеОстатки.Организация КАК Организация,
	|			СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|			СуммовойУчетВРозницеОстатки.Валюта КАК Валюта,
	|			СУММА(СуммовойУчетВРозницеОстатки.СуммаОстаток) КАК СуммаОстаток,
	|			СУММА(СуммовойУчетВРозницеОстатки.СуммаВалОстаток) КАК СуммаВалОстаток,
	|			СУММА(СуммовойУчетВРозницеОстатки.СебестоимостьОстаток) КАК СебестоимостьОстаток
	|		ИЗ
	|			(ВЫБРАТЬ
	|				СуммовойУчетВРозницеОстатки.Организация КАК Организация,
	|				СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|				СуммовойУчетВРозницеОстатки.Валюта КАК Валюта,
	|				ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
	|				ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
	|				ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СебестоимостьОстаток, 0) КАК СебестоимостьОстаток
	|			ИЗ
	|				РегистрНакопления.СуммовойУчетВРознице.Остатки(
	|						&МоментВремени,
	|						(Организация, СтруктурнаяЕдиница, Валюта) В
	|							(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|								ВременнаяТаблицаСуммовойУчетВРознице.Организация,
	|								ВременнаяТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница,
	|								ВременнаяТаблицаСуммовойУчетВРознице.Валюта
	|							ИЗ
	|								ВременнаяТаблицаСуммовойУчетВРознице)) КАК СуммовойУчетВРозницеОстатки
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ДвиженияДокументаСуммовойУчетВРознице.Организация,
	|				ДвиженияДокументаСуммовойУчетВРознице.СтруктурнаяЕдиница,
	|				ДвиженияДокументаСуммовойУчетВРознице.Валюта,
	|				ВЫБОР
	|					КОГДА ДвиженияДокументаСуммовойУчетВРознице.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА -ЕСТЬNULL(ДвиженияДокументаСуммовойУчетВРознице.Сумма, 0)
	|					ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаСуммовойУчетВРознице.Сумма, 0)
	|				КОНЕЦ,
	|				ВЫБОР
	|					КОГДА ДвиженияДокументаСуммовойУчетВРознице.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА -ЕСТЬNULL(ДвиженияДокументаСуммовойУчетВРознице.СуммаВал, 0)
	|					ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаСуммовойУчетВРознице.СуммаВал, 0)
	|				КОНЕЦ,
	|				ВЫБОР
	|					КОГДА ДвиженияДокументаСуммовойУчетВРознице.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|						ТОГДА -ЕСТЬNULL(ДвиженияДокументаСуммовойУчетВРознице.Себестоимость, 0)
	|					ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаСуммовойУчетВРознице.Себестоимость, 0)
	|				КОНЕЦ
	|			ИЗ
	|				РегистрНакопления.СуммовойУчетВРознице КАК ДвиженияДокументаСуммовойУчетВРознице
	|			ГДЕ
	|				ДвиженияДокументаСуммовойУчетВРознице.Регистратор = &Ссылка
	|				И ДвиженияДокументаСуммовойУчетВРознице.Период <= &ПериодКонтроля) КАК СуммовойУчетВРозницеОстатки
	|		
	|		СГРУППИРОВАТЬ ПО
	|			СуммовойУчетВРозницеОстатки.Организация,
	|			СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница,
	|			СуммовойУчетВРозницеОстатки.Валюта) КАК СуммовойУчетВРозницеОстатки
	|		ПО ТаблицаДокумента.Организация = СуммовойУчетВРозницеОстатки.Организация
	|			И ТаблицаДокумента.СтруктурнаяЕдиница = СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница
	|			И ТаблицаДокумента.Валюта = СуммовойУчетВРозницеОстатки.Валюта
	|ГДЕ
	|	(ВЫБОР
	|				КОГДА СуммовойУчетВРозницеОстатки.СуммаВалОстаток - ТаблицаДокумента.СуммаВал = 0
	|					ТОГДА СуммовойУчетВРозницеОстатки.СебестоимостьОстаток
	|				КОГДА СуммовойУчетВРозницеОстатки.СуммаВалОстаток <> 0
	|					ТОГДА ВЫРАЗИТЬ(СуммовойУчетВРозницеОстатки.СебестоимостьОстаток * ТаблицаДокумента.СуммаВал / СуммовойУчетВРозницеОстатки.СуммаВалОстаток КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ 0
	|			КОНЕЦ > 0.005
	|			ИЛИ ВЫБОР
	|				КОГДА СуммовойУчетВРозницеОстатки.СуммаВалОстаток - ТаблицаДокумента.СуммаВал = 0
	|					ТОГДА СуммовойУчетВРозницеОстатки.СебестоимостьОстаток
	|				КОГДА СуммовойУчетВРозницеОстатки.СуммаВалОстаток <> 0
	|					ТОГДА ВЫРАЗИТЬ(СуммовойУчетВРозницеОстатки.СебестоимостьОстаток * ТаблицаДокумента.СуммаВал / СуммовойУчетВРозницеОстатки.СуммаВалОстаток КАК ЧИСЛО(15, 2))
	|				ИНАЧЕ 0
	|			КОНЕЦ < -0.005)";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаСуммовойУчетВРознице.Организация КАК Организация,
	|	ВременнаяТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаСуммовойУчетВРознице.Валюта КАК Валюта
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ВременнаяТаблицаСуммовойУчетВРознице";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СуммовойУчетВРознице");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СуммовойУчетВРозницеОстатки.Организация КАК Организация,
	|	СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СуммовойУчетВРозницеОстатки.СчетУчета КАК СчетУчета,
	|	СуммовойУчетВРозницеОстатки.Валюта КАК Валюта,
	|	СУММА(СуммовойУчетВРозницеОстатки.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(СуммовойУчетВРозницеОстатки.СуммаВалОстаток) КАК СуммаВалОстаток
	|ПОМЕСТИТЬ ВременнаяТаблицаОстаткиПослеПроведения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВременнаяТаблица.Организация КАК Организация,
	|		ВременнаяТаблица.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ВременнаяТаблица.Валюта КАК Валюта,
	|		ВременнаяТаблица.СчетУчета КАК СчетУчета,
	|		ВременнаяТаблица.СуммаДляОстатка КАК СуммаОстаток,
	|		ВременнаяТаблица.СуммаВалДляОстатка КАК СуммаВалОстаток
	|	ИЗ
	|		ВременнаяТаблицаСуммовойУчетВРознице КАК ВременнаяТаблица
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОстатки.Организация,
	|		ТаблицаОстатки.СтруктурнаяЕдиница,
	|		ТаблицаОстатки.Валюта,
	|		ТаблицаОстатки.СтруктурнаяЕдиница.СчетУчетаВРознице,
	|		ЕСТЬNULL(ТаблицаОстатки.СуммаОстаток, 0),
	|		ЕСТЬNULL(ТаблицаОстатки.СуммаВалОстаток, 0)
	|	ИЗ
	|		РегистрНакопления.СуммовойУчетВРознице.Остатки(
	|				&МоментВремени,
	|				(Организация, СтруктурнаяЕдиница, Валюта) В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВременнаяТаблицаСуммовойУчетВРознице.Организация,
	|						ВременнаяТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница,
	|						ВременнаяТаблицаСуммовойУчетВРознице.Валюта
	|					ИЗ
	|						ВременнаяТаблицаСуммовойУчетВРознице)) КАК ТаблицаОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокумента.Организация,
	|		ДвиженияДокумента.СтруктурнаяЕдиница,
	|		ДвиженияДокумента.Валюта,
	|		ДвиженияДокумента.СтруктурнаяЕдиница.СчетУчетаВРознице,
	|		ВЫБОР
	|			КОГДА ДвиженияДокумента.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокумента.Сумма, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокумента.Сумма, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокумента.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокумента.СуммаВал, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокумента.СуммаВал, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.СуммовойУчетВРознице КАК ДвиженияДокумента
	|	ГДЕ
	|		ДвиженияДокумента.Регистратор = &Ссылка
	|		И ДвиженияДокумента.Период <= &ПериодКонтроля) КАК СуммовойУчетВРозницеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	СуммовойУчетВРозницеОстатки.Организация,
	|	СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница,
	|	СуммовойУчетВРозницеОстатки.Валюта,
	|	СуммовойУчетВРозницеОстатки.СчетУчета
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Валюта,
	|	СчетУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	1 КАК НомерСтроки,
	|	&ПериодКонтроля КАК Дата,
	|	ТаблицаСуммовойУчетВРознице.Организация КАК Организация,
	|	ТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ТаблицаОстатки.СуммаВалОстаток, 0) * КурсыВалютКассаСрезПоследних.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассаСрезПоследних.Кратность) - ЕСТЬNULL(ТаблицаОстатки.СуммаОстаток, 0) КАК СуммаКурсовойРазницы,
	|	ТаблицаСуммовойУчетВРознице.Валюта КАК Валюта,
	|	ТаблицаСуммовойУчетВРознице.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсовыхРазницСуммовойУчетВРознице
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ТаблицаСуммовойУчетВРознице
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаОстаткиПослеПроведения КАК ТаблицаОстатки
	|		ПО ТаблицаСуммовойУчетВРознице.Организация = ТаблицаОстатки.Организация
	|			И ТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница = ТаблицаОстатки.СтруктурнаяЕдиница
	|			И ТаблицаСуммовойУчетВРознице.Валюта = ТаблицаОстатки.Валюта
	|			И ТаблицаСуммовойУчетВРознице.СчетУчета = ТаблицаОстатки.СчетУчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|						ВременнаяТаблицаСуммовойУчетВРознице.Валюта
	|					ИЗ
	|						ВременнаяТаблицаСуммовойУчетВРознице)) КАК КурсыВалютКассаСрезПоследних
	|		ПО ТаблицаСуммовойУчетВРознице.Валюта = КурсыВалютКассаСрезПоследних.Валюта
	|ГДЕ
	|	(ЕСТЬNULL(ТаблицаОстатки.СуммаВалОстаток, 0) * КурсыВалютКассаСрезПоследних.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассаСрезПоследних.Кратность) - ЕСТЬNULL(ТаблицаОстатки.СуммаОстаток, 0) >= 0.005
	|			ИЛИ ЕСТЬNULL(ТаблицаОстатки.СуммаВалОстаток, 0) * КурсыВалютКассаСрезПоследних.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассаСрезПоследних.Кратность) - ЕСТЬNULL(ТаблицаОстатки.СуммаОстаток, 0) <= -0.005)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.ВидДвижения КАК ВидДвижения,
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Валюта КАК Валюта,
	|	ТаблицаДокумента.Сумма КАК Сумма,
	|	ТаблицаДокумента.СуммаВал КАК СуммаВал,
	|	0 КАК Себестоимость,
	|	ТаблицаДокумента.СодержаниеПроводки КАК СодержаниеПроводки,
	|	НЕОПРЕДЕЛЕНО КАК ДокументПродажи
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаДокумента.НомерСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Валюта,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	0,
	|	0,
	|	&КурсоваяРазница,
	|	НЕОПРЕДЕЛЕНО
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницСуммовойУчетВРознице КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаДокумента.НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.СтруктурнаяЕдиница,
	|	ТаблицаДокумента.Валюта,
	|	0,
	|	0,
	|	ТаблицаДокумента.Себестоимость,
	|	&Себестоимость,
	|	&Ссылка
	|ИЗ
	|	ВременнаяТаблицаСебестоимость КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВременнаяТаблицаОстаткиПослеПроведения";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСуммовойУчетВРознице", МассивРезультатов[2].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаПоступлениеВКассу.ВалютаДенежныхСредств);
	Запрос.УстановитьПараметр("ВалютаДоговораКредитаЗайма", ДокументСсылкаПоступлениеВКассу.ДоговорКредитаЗайма.ВалютаРасчетов);
	
	Запрос.УстановитьПараметр("ИспользоватьСтатьиДДС", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиДвиженияДенежныхСредств"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаУчета, &ВалютаДС, &ВалютаДоговораКредитаЗайма)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ТаблицаДокумента.Ссылка.ВидОперации КАК ВидОперации,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.ЭтоДоговорОбслуживания
	|				И ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК ДоговорОбслуживанияНаправлениеДеятельности,
	|	ТаблицаДокумента.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ТаблицаДокумента.Ссылка.Касса КАК Касса,
	|	ТаблицаДокумента.Ссылка.Касса.СчетУчета КАК БанковскийСчетКассаСчетУчета,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиДДС
	|				ИЛИ ТаблицаДокумента.СтатьяДДС = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Ссылка.Статья
	|		ИНАЧЕ ТаблицаДокумента.СтатьяДДС
	|	КОНЕЦ КАК Статья,
	|	ТаблицаДокумента.Ссылка.Корреспонденция КАК Корреспонденция,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ПризнакАванса КАК ПризнакАванса,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаУчета,
	|	СУММА(ТаблицаДокумента.СуммаРасчетов) КАК СуммаРасчетов,
	|	СУММА(ТаблицаДокумента.СуммаПлатежа) КАК СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.ПоступлениеДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ПоступлениеДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.ПеремещениеДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ПеремещениеДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТаблицаДокумента.СчетНаОплату.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.СчетНаОплату
	|		КОГДА ТаблицаДокумента.Заказ.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетНаОплатуДляПлатежногоКалендаря,
	|	ТаблицаДокумента.СчетНаОплату КАК СчетНаОплату,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.ТипСуммы,
	|	ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма,
	|	ТаблицаДокумента.Ссылка.Подотчетник КАК Сотрудник,
	|	ТаблицаДокумента.Ссылка.СчетОрганизации КАК СчетОрганизации,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчета
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|	КОНЕЦ КАК СчетУчетаПоТипуСуммы,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ""Оплата основного долга по займу""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ""Оплата процентов по займу""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ""Оплата комиссии по займу""
	|	КОНЕЦ КАК СодержаниеПоТипуСуммы,
	|	ТаблицаДокумента.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПодотчетника)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ТОГДА ТаблицаДокумента.Ссылка.Корреспонденция
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|			ТОГДА ТаблицаДокумента.Ссылка.КассаККМ
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА ТаблицаДокумента.Ссылка.СтруктурнаяЕдиница
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА ТаблицаДокумента.Ссылка.СчетОрганизации
	|	КОНЕЦ КАК АналитикаДДС
	|ПОМЕСТИТЬ ВременнаяТаблицаРасшифровкаПлатежа
	|ИЗ
	|	Документ.ПоступлениеВКассу.РасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.Документ,
	|	ТаблицаДокумента.Ссылка.ВидОперации,
	|	ТаблицаДокумента.Ссылка.Контрагент,
	|	ТаблицаДокумента.Договор,
	|	ТаблицаДокумента.Ссылка.Касса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПоставщика)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПокупателя)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ,
	|	ТаблицаДокумента.ПризнакАванса,
	|	ТаблицаДокумента.Ссылка.Дата,
	|	ТаблицаДокумента.Договор.ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.ПоступлениеДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ПоступлениеДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.ПеремещениеДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ПеремещениеДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТаблицаДокумента.СчетНаОплату.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.СчетНаОплату
	|		КОГДА ТаблицаДокумента.Заказ.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетНаОплату,
	|	ТаблицаДокумента.Ссылка.Касса.СчетУчета,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Ссылка.Корреспонденция,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.ТипСуммы,
	|	ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма,
	|	ТаблицаДокумента.Ссылка.Подотчетник,
	|	ТаблицаДокумента.Ссылка.СчетОрганизации,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчета
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ""Возврат основного долга по займу""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ""Возврат процентов по займу""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ""Возврат комиссии по займу""
	|	КОНЕЦ,
	|	ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.ЭтоДоговорОбслуживания
	|				И ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ТаблицаДокумента.Ссылка,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ""Оплата основного долга по займу""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ""Оплата процентов по займу""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ""Оплата комиссии по займу""
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПодотчетника)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ТОГДА ТаблицаДокумента.Ссылка.Корреспонденция
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|			ТОГДА ТаблицаДокумента.Ссылка.КассаККМ
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА ТаблицаДокумента.Ссылка.СтруктурнаяЕдиница
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА ТаблицаДокумента.Ссылка.СчетОрганизации
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиДДС
	|				ИЛИ ТаблицаДокумента.СтатьяДДС = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Ссылка.Статья
	|		ИНАЧЕ ТаблицаДокумента.СтатьяДДС
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.ВидОперации КАК ВидОперации,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.Касса КАК Касса,
	|	ТаблицаДокумента.КассаККМ КАК КассаККМ,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|	КОНЕЦ КАК Сумма,
	|	ТаблицаДокумента.СуммаДокумента КАК СуммаВал,
	|	ТаблицаДокумента.Касса.СчетУчета КАК КассаСчетУчета,
	|	ТаблицаДокумента.КассаККМ.СчетУчета КАК КассаККМСчетУчета,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.СтруктурнаяЕдиница.СчетУчетаВРознице КАК СтруктурнаяЕдиницаСчетУчетаВРознице,
	|	ТаблицаДокумента.СтруктурнаяЕдиница.СчетУчетаНаценки КАК СтруктурнаяЕдиницаСчетУчетаНаценки,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж КАК НаправлениеДеятельностиСчетУчетаВыручкиОтПродаж,
	|	ТаблицаДокумента.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж КАК НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	ТаблицаДокумента.Корреспонденция КАК Корреспонденция,
	|	ТаблицаДокумента.Корреспонденция.ТипСчета КАК КорреспонденцияТипСчета,
	|	ТаблицаДокумента.Подотчетник КАК Подотчетник,
	|	ТаблицаДокумента.Подотчетник.СчетРасчетовСПодотчетниками КАК ПодотчетникСчетРасчетовСПодотчетниками,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ТаблицаДокумента.АналитикаПрочихДоходовИРасходов КАК АналитикаДоходовИРасходов,
	|	ТаблицаДокумента.СчетОрганизации,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.Курс,
	|	ТаблицаДокумента.Кратность,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ОтПодотчетника)
	|			ТОГДА ТаблицаДокумента.Подотчетник
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.Прочее)
	|			ТОГДА ТаблицаДокумента.Корреспонденция
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|			ТОГДА ТаблицаДокумента.Контрагент
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ТОГДА ТаблицаДокумента.Контрагент
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником)
	|			ТОГДА ТаблицаДокумента.Подотчетник
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручка)
	|			ТОГДА ТаблицаДокумента.КассаККМ
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РозничнаяВыручкаСуммовойУчет)
	|			ТОГДА ТаблицаДокумента.СтруктурнаяЕдиница
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПолучениеНаличныхВБанке)
	|			ТОГДА ТаблицаДокумента.СчетОрганизации
	|	КОНЕЦ КАК АналитикаДДС
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.ПоступлениеВКассу КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка";
	
	Запрос.ВыполнитьПакет();
	
	// Формирование таблиц движений по разделам учета.
	СформироватьТаблицаДенежныеСредства(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДенежныеСредстваВКассахККМ(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПодотчетниками(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСуммовойУчетВРознице(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	// Прочие расчеты
	СформироватьТаблицаРасчетыСПрочимиКонтрагентами(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	// Конец Прочие расчеты
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПлатежныйКалендарь(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	СформироватьТаблицаУправленческий(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства);
	
	// Рублевые суммы документов
	Если ДокументСсылкаПоступлениеВКассу.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя Тогда
		РублевыеСуммыДокументов.ДобавитьДвиженияВТаблицуРасчетов(
			ДокументСсылкаПоступлениеВКассу.РасшифровкаПлатежа,
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасчетыСПокупателями,
			ДокументСсылкаПоступлениеВКассу,
			СтруктураДополнительныеСвойства,
			Истина);
	КонецЕсли;
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаПоступлениеВКассу, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ Константы.КонтролироватьОстаткиПриПроведении.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Возврат;
	КонецЕсли;
	// Конец МобильноеПриложение
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы содержат записи, необходимо выполнить контроль
	// отрицательных остатков.
	Если СтруктураВременныеТаблицы.ДвиженияДенежныеСредстваИзменение
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПодотчетникамиИзменение
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПокупателямиИзменение
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияСуммовойУчетВРозницеИзменение
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияДенежныеСредстваВКассахККМИзменение Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияДенежныеСредстваИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияДенежныеСредстваИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса КАК БанковскийСчетКассаПредставление,
		|	ДвиженияДенежныеСредстваИзменение.Валюта КАК ВалютаПредставление,
		|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств КАК ТипДенежныхСредствПредставление,
		|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств КАК ТипДенежныхСредств,
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалИзменение + ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаВалОстаток, 0) КАК ОстатокДенежныхСредств,
		|	ДвиженияДенежныеСредстваИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияДенежныеСредстваИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияДенежныеСредстваИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалИзменение КАК СуммаВалИзменение
		|ИЗ
		|	ДвиженияДенежныеСредстваИзменение КАК ДвиженияДенежныеСредстваИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредства.Остатки(&МоментКонтроля, ) КАК ДенежныеСредстваОстатки
		|		ПО ДвиженияДенежныеСредстваИзменение.Организация = ДенежныеСредстваОстатки.Организация
		|			И ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств = ДенежныеСредстваОстатки.ТипДенежныхСредств
		|			И ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса = ДенежныеСредстваОстатки.БанковскийСчетКасса
		|			И ДвиженияДенежныеСредстваИзменение.Валюта = ДенежныеСредстваОстатки.Валюта
		|ГДЕ
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаВалОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПодотчетникамиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Сотрудник КАК СотрудникПредставление,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Валюта КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Документ КАК ДокументПредставление,
		|	ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) КАК ОстатокУПодотчетногоЛица,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалИзменение КАК СуммаВалИзменение
		|ИЗ
		|	ДвиженияРасчетыСПодотчетникамиИзменение КАК ДвиженияРасчетыСПодотчетникамиИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПодотчетниками.Остатки(&МоментКонтроля, ) КАК РасчетыСПодотчетникамиОстатки
		|		ПО ДвиженияРасчетыСПодотчетникамиИзменение.Организация = РасчетыСПодотчетникамиОстатки.Организация
		|			И ДвиженияРасчетыСПодотчетникамиИзменение.Сотрудник = РасчетыСПодотчетникамиОстатки.Сотрудник
		|			И ДвиженияРасчетыСПодотчетникамиИзменение.Валюта = РасчетыСПодотчетникамиОстатки.Валюта
		|			И ДвиженияРасчетыСПодотчетникамиИзменение.Документ = РасчетыСПодотчетникамиОстатки.Документ
		|ГДЕ
		|	(ТИПЗНАЧЕНИЯ(РасчетыСПодотчетникамиОстатки.Документ) = ТИП(Документ.АвансовыйОтчет)
		|				И ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) > 0
		|			ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСПодотчетникамиОстатки.Документ) <> ТИП(Документ.АвансовыйОтчет)
		|				И ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПокупателямиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПокупателямиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Договор.ВалютаРасчетов КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Документ КАК ДокументПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетовПредставление,
		|	ИСТИНА КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаОстаткаЗадолженности,
		|	-(ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0)) КАК СуммаНепогашенныхАвансов,
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПокупателямиИзменение КАК ДвиженияРасчетыСПокупателямиИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПокупателями.Остатки(&МоментКонтроля, ) КАК РасчетыСПокупателямиОстатки
		|		ПО ДвиженияРасчетыСПокупателямиИзменение.Организация = РасчетыСПокупателямиОстатки.Организация
		|			И ДвиженияРасчетыСПокупателямиИзменение.Контрагент = РасчетыСПокупателямиОстатки.Контрагент
		|			И ДвиженияРасчетыСПокупателямиИзменение.Договор = РасчетыСПокупателямиОстатки.Договор
		|			И ДвиженияРасчетыСПокупателямиИзменение.Документ = РасчетыСПокупателямиОстатки.Документ
		|			И ДвиженияРасчетыСПокупателямиИзменение.Заказ = РасчетыСПокупателямиОстатки.Заказ
		|			И ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов = РасчетыСПокупателямиОстатки.ТипРасчетов
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|				ТОГДА ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) > 0
		|			ИНАЧЕ ЕСТЬNULL(РасчетыСПокупателямиОстатки.СуммаВалОстаток, 0) < 0
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияСуммовойУчетВРозницеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияСуммовойУчетВРозницеИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница.РозничныйВидЦен.ВалютаЦены КАК ВалютаПредставление,
		|	ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалИзменение + ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) КАК ОстатокВРознице,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьПередЗаписью КАК СебестоимостьПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьПриЗаписи КАК СебестоимостьПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьИзменение КАК СебестоимостьИзменение
		|ИЗ
		|	ДвиженияСуммовойУчетВРозницеИзменение КАК ДвиженияСуммовойУчетВРозницеИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.СуммовойУчетВРознице.Остатки(&МоментКонтроля, ) КАК СуммовойУчетВРозницеОстатки
		|		ПО ДвиженияСуммовойУчетВРозницеИзменение.Организация = СуммовойУчетВРозницеОстатки.Организация
		|			И ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница = СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница
		|ГДЕ
		|	ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.КассаККМ КАК КассаККМПредставление,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.КассаККМ.ВалютаДенежныхСредств КАК ВалютаПредставление,
		|	ЕСТЬNULL(ДенежныеСредстваВКассахККМОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(ДенежныеСредстваВКассахККМОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалИзменение + ЕСТЬNULL(ДенежныеСредстваВКассахККМОстатки.СуммаВалОстаток, 0) КАК ОстатокДенежныхСредств,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалИзменение КАК СуммаВалИзменение
		|ИЗ
		|	ДвиженияДенежныеСредстваВКассахККМИзменение КАК ДвиженияДенежныеСредстваВКассахККМИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредстваВКассахККМ.Остатки(&МоментКонтроля, ) КАК ДенежныеСредстваВКассахККМОстатки
		|		ПО ДвиженияДенежныеСредстваВКассахККМИзменение.Организация = ДенежныеСредстваВКассахККМОстатки.Организация
		|			И ДвиженияДенежныеСредстваВКассахККМИзменение.КассаККМ = ДенежныеСредстваВКассахККМОстатки.КассаККМ
		|ГДЕ
		|	ЕСТЬNULL(ДенежныеСредстваВКассахККМОстатки.СуммаВалОстаток, 0) < 0
		|	И ДенежныеСредстваВКассахККМОстатки.КассаККМ.ТипКассы <> ЗНАЧЕНИЕ(Перечисление.ТипыКассККМ.АвтономнаяККМ)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			ИЛИ НЕ МассивРезультатов[1].Пустой()
			ИЛИ НЕ МассивРезультатов[2].Пустой()
			ИЛИ НЕ МассивРезультатов[3].Пустой()
			ИЛИ НЕ МассивРезультатов[4].Пустой() Тогда
			ДокументОбъектПоступлениеВКассу = ДокументСсылкаПоступлениеВКассу.ПолучитьОбъект()
		КонецЕсли;
		
		// Отрицательный остаток по денежным средствам.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструДенежныеСредства(ДокументОбъектПоступлениеВКассу, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с подотчетными лицами.
		Если НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыСПодотчетниками(ДокументОбъектПоступлениеВКассу, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с покупателями.
		Если НЕ МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыСПокупателями(ДокументОбъектПоступлениеВКассу, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по суммовому учету в рознице.
		Если НЕ МассивРезультатов[3].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструСуммовойУчетВРознице(ДокументОбъектПоступлениеВКассу, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по кассе ККМ.
		Если НЕ МассивРезультатов[4].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструДенежныеСредстваВКассахККМ(ДокументОбъектПоступлениеВКассу, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует поле в том числе для печатной формы.
//
// Параметры:      
//	ВыборкаДляПечати - Выборка из результата запроса.
//
// Возвращаемое значение: 
//  Строка       - текст поля "в том числе".
//
Функция ПолучитьТекстВТомЧисле(ВыборкаДляПечати, РасшифровкаПлатежа = 21)
	
	ТабНДС = РасшифровкаПлатежа.Выгрузить();

	ТабНДС.Свернуть("СтавкаНДС", "СуммаНДС");

	ТекстСуммаНДС = "";
	
	Если ТабНДС.Количество() > 0 Тогда
		
		Для каждого СтрокаНДС ИЗ ТабНДС цикл
			ТекстСуммаНДС = ТекстСуммаНДС
						  + Символы.ПС
						  + "НДС ("
						  + ?(НЕ ЗначениеЗаполнено(СтрокаНДС.СтавкаНДС),
							"без налога",
							СтрокаНДС.СтавкаНДС)
						  + ") "
						  + Формат(СтрокаНДС.СуммаНДС, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00")
						  + " "
						  + ВыборкаДляПечати.ВалютаПредставление;
		КонецЦикла;
		
	Иначе
		ТекстСуммаНДС = ТекстСуммаНДС
					  + Символы.ПС
					  + "НДС (без налога) "
					  + Формат(0, "ЧЦ=15; ЧДЦ=2; ЧРД=-; ЧН=0-00")
					  + " "
					  + ВыборкаДляПечати.ВалютаПредставление;
	КонецЕсли;

	ТекстСуммаНДС = Сред(ТекстСуммаНДС, 2);
	
	Возврат ТекстСуммаНДС;
	
КонецФункции

// Функция формирует поле СуммаРубКоп для печатной формы.
//
Функция РубКоп(Сумма)
	
	Руб = Цел(Сумма);
	Коп = ОКР(100 * (Сумма - Руб), 0, 1);
	СуммаРубКоп = "" + Руб + " руб. " + Цел(Коп/10) + (Коп - 10 * Цел(Коп / 10)) + " коп.";
	Возврат СуммаРубКоп;
	
КонецФункции // РубКоп()

// Функция формирует печатную форму документа по указанному макету.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
//  ИмяМакета    - Строка, имя макета печатной формы.
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_ПоступлениеВКассу";
	
	ПервыйДокумент = Истина;
	
	Для каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		
		Если ТекущийДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.Прочее
		 ИЛИ ТекущийДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ПокупкаВалюты Тогда
			СтрокаКредитСубСчет = "ПоступлениеВКассу.Корреспонденция.Код КАК КорСчет,
								  |НЕОПРЕДЕЛЕНО КАК КорСчетАвансов";
		ИначеЕсли ТекущийДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПодотчетника Тогда
			СтрокаКредитСубСчет = "ПоступлениеВКассу.Подотчетник.СчетРасчетовСПодотчетниками.Код КАК КорСчет,
								  |НЕОПРЕДЕЛЕНО КАК КорСчетАвансов";
		ИначеЕсли ТекущийДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя Тогда
			СтрокаКредитСубСчет = "ПоступлениеВКассу.Контрагент.СчетУчетаРасчетовСПокупателем.Код КАК КорСчет,
								  |ПоступлениеВКассу.Контрагент.СчетУчетаАвансовПокупателя.Код КАК КорСчетАвансов";
		ИначеЕсли ТекущийДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПоставщика Тогда
			СтрокаКредитСубСчет = "ПоступлениеВКассу.Контрагент.СчетУчетаРасчетовСПоставщиком.Код КАК КорСчет,
								  |ПоступлениеВКассу.Контрагент.СчетУчетаАвансовПоставщику.Код КАК КорСчетАвансов";
		ИначеЕсли ТекущийДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты Тогда
			СтрокаКредитСубСчет = "ПоступлениеВКассу.Корреспонденция.Код КАК КорСчет,
								  |НЕОПРЕДЕЛЕНО КАК КорСчетАвансов";
		Иначе
			СтрокаКредитСубСчет = "НЕОПРЕДЕЛЕНО КАК КорСчет,
								  |НЕОПРЕДЕЛЕНО КАК КорСчетАвансов";
		КонецЕсли;
		                                                                  
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПоступлениеВКассу.Ссылка КАК Ссылка,
		|	ПоступлениеВКассу.Номер КАК Номер,
		|	ПоступлениеВКассу.Дата КАК ДатаДокумента,
		|	ПоступлениеВКассу.Организация КАК Организация,
		|	ПоступлениеВКассу.Организация.ЮридическоеФизическоеЛицо КАК ЮрФизЛицо,
		|	ПоступлениеВКассу.Организация.Префикс КАК Префикс,
		|	ПоступлениеВКассу.Организация.КодПоОКПО КАК ОрганизацияПоОКПО,
		|	ПоступлениеВКассу.Организация.НаименованиеПолное КАК ПредставлениеОрганизации,
		|	ПоступлениеВКассу.Касса.СчетУчета.Код КАК КодДебета,
		|	ПоступлениеВКассу.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
		|	ПРЕДСТАВЛЕНИЕ(ПоступлениеВКассу.ВалютаДенежныхСредств) КАК ВалютаПредставление,
		|	ПоступлениеВКассу.ПринятоОт КАК ПринятоОт,
		|	ПоступлениеВКассу.Основание КАК Основание,
		|	ПоступлениеВКассу.Приложение КАК Приложение,
		|	ПоступлениеВКассу.СуммаДокумента КАК СуммаДокумента,
		|	" + СтрокаКредитСубСчет + "
		|ИЗ
		|	Документ.ПоступлениеВКассу КАК ПоступлениеВКассу
		|ГДЕ
		|	ПоступлениеВКассу.Ссылка = &ТекущийДокумент";
		
		ВыборкаДляПечати = Запрос.Выполнить().Выбрать();
		ВыборкаДляПечати.Следующий();
		
		Валютный = ВыборкаДляПечати.ВалютаДенежныхСредств <> Константы.НациональнаяВалюта.Получить();
		
		ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_ПоступлениеВКассу_ПоступлениеВКассу";
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.ПоступлениеВКассу.ПФ_MXL_ПКО");
		ОбластьМакета = Макет.ПолучитьОбласть("Шапка");
		ОбластьМакета.Параметры.Заполнить(ВыборкаДляПечати);
		ОбластьМакета.Параметры.Сумма = Формат(ВыборкаДляПечати.СуммаДокумента, "ЧЦ=15; ЧДЦ=2") + ?(Валютный, " " + СокрЛП(ВыборкаДляПечати.ВалютаДенежныхСредств), "");
		ОбластьМакета.Параметры.СубСчет = СокрЛП(ВыборкаДляПечати.КорСчет) + ?(ЗначениеЗаполнено(ВыборкаДляПечати.КорСчетАвансов), "," + СокрЛП(ВыборкаДляПечати.КорСчетАвансов), "");
		Руководители = УправлениеНебольшойФирмойСервер.ОтветственныеЛицаОрганизационнойЕдиницы(ВыборкаДляПечати.Организация, ВыборкаДляПечати.ДатаДокумента);
		ОбластьМакета.Параметры.ФИОГлавногоБухгалтера = Руководители.ФИОГлавногоБухгалтера;
		ОбластьМакета.Параметры.ФИОКассира = Руководители.ФИОКассира;
		
		Если ВыборкаДляПечати.ДатаДокумента < Дата('20110101') Тогда
			НомерДокумента = УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(ВыборкаДляПечати.Номер, ВыборкаДляПечати.Префикс);
		Иначе
			НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(ВыборкаДляПечати.Номер, Истина, Истина);
		КонецЕсли;
		
		ОбластьМакета.Параметры.СуммаРубКоп = ?(Валютный, Формат(ВыборкаДляПечати.СуммаДокумента, "ЧЦ=15; ЧДЦ=2") + " " + СокрЛП(ВыборкаДляПечати.ВалютаДенежныхСредств), РубКоп(ВыборкаДляПечати.СуммаДокумента));
		ОбластьМакета.Параметры.НомерДокумента = НомерДокумента;
		
		ОбластьМакета.Параметры.СуммаПрописью = УправлениеНебольшойФирмойСервер.ФорматироватьСуммуПрописьюПлатежногоДокумента(
			ВыборкаДляПечати.СуммаДокумента,
			ВыборкаДляПечати.ВалютаДенежныхСредств,
			Ложь
		);
		
		ОбластьМакета.Параметры.ВТомЧисле = ПолучитьТекстВТомЧисле(ВыборкаДляПечати, ТекущийДокумент.РасшифровкаПлатежа);
		
		ТабличныйДокумент.Вывести(ОбластьМакета);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ВыборкаДляПечати.Ссылка);
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПКО") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "ПКО", "Приходный кассовый ордер", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПКО";
	КомандаПечати.Представление = НСтр("ru = 'КО-1 (Приходный кассовый ордер)'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#Область ПрочиеРасчеты

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПрочимиКонтрагентами(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("УчетРасчетовПоПрочимОперациям", НСтр("ru='Учет расчетов по прочим операциям'"));
	Запрос.УстановитьПараметр("Комментарий", НСтр("ru='Уменьшение долга контрагента'"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = НЕОПРЕДЕЛЕНО
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов КАК Валюта,
	|	ВременнаяТаблицаШапка.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Дата,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК Сумма,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВал,
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК СуммаДляОстатка,
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	&УчетРасчетовПоПрочимОперациям КАК СодержаниеПроводки,
	|	&Комментарий КАК Комментарий,
	|	ВременнаяТаблицаШапка.Корреспонденция КАК СчетУчета,
	|	ВременнаяТаблицаШапка.Касса КАК Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Период
	|ПОМЕСТИТЬ ВременнаяТаблицаПрочиеРасчеты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ПрочиеРасчеты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов,
	|	ВременнаяТаблицаШапка.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = НЕОПРЕДЕЛЕНО
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВременнаяТаблицаШапка.Корреспонденция,
	|	ВременнаяТаблицаШапка.Касса,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаПрочиеРасчеты.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаПрочиеРасчеты.Организация КАК Организация,
	|	ВременнаяТаблицаПрочиеРасчеты.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаПрочиеРасчеты.Договор КАК Договор
	|ИЗ
	|	ВременнаяТаблицаПрочиеРасчеты КАК ВременнаяТаблицаПрочиеРасчеты";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПрочимиКонтрагентами");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыПоПрочимОперациям(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПрочимиКонтрагентами", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПрочимиКонтрагентами()

Процедура СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаПоступлениеВКассу, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("УчетРасчетовПоКредитамИЗаймам", НСтр("ru='Поступление по кредиту'"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаПоступлениеВКассу);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаПоступлениеВКассу.ВалютаДенежныхСредств);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ПоступлениеВКассу.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.КредитПолученный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику)
	|	КОНЕЦ КАК ВидДоговора,
	|	ПоступлениеВКассу.Дата КАК Дата,
	|	ПоступлениеВКассу.Дата КАК Период,
	|	&УчетРасчетовПоКредитамИЗаймам КАК СодержаниеПроводки,
	|	ПоступлениеВКассу.Контрагент КАК Контрагент,
	|	ПоступлениеВКассу.СуммаДокумента КАК СуммаПлатежа,
	|	ВЫРАЗИТЬ(ПоступлениеВКассу.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютДоговора.Кратность / (КурсыВалютДоговора.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолгВал,
	|	ВЫРАЗИТЬ(ПоступлениеВКассу.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолг,
	|	ВЫРАЗИТЬ(ПоступлениеВКассу.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютДоговора.Кратность / (КурсыВалютДоговора.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолгВалДляОстатка,
	|	ВЫРАЗИТЬ(ПоступлениеВКассу.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолгДляОстатка,
	|	0 КАК ПроцентыВал,
	|	0 КАК Проценты,
	|	0 КАК ПроцентыВалДляОстатка,
	|	0 КАК ПроцентыДляОстатка,
	|	0 КАК КомиссияВал,
	|	0 КАК Комиссия,
	|	0 КАК КомиссияВалДляОстатка,
	|	0 КАК КомиссияДляОстатка,
	|	ВЫРАЗИТЬ(ПоступлениеВКассу.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютДоговора.Кратность / (КурсыВалютДоговора.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ПоступлениеВКассу.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ПоступлениеВКассу.ДоговорКредитаЗайма КАК ДоговорКредитаЗайма,
	|	ПоступлениеВКассу.ДоговорКредитаЗайма.ВалютаРасчетов КАК Валюта,
	|	ПоступлениеВКассу.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ПоступлениеВКассу.ДоговорКредитаЗайма.СчетУчета КАК СчетУчета,
	|	ПоступлениеВКассу.Касса,
	|	ЛОЖЬ КАК УдержаноИзЗарплаты
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыПоКредитамИЗаймам
	|ИЗ
	|	Документ.ПоступлениеВКассу КАК ПоступлениеВКассу
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютДоговора
	|		ПО (КурсыВалютДоговора.Валюта = ПоступлениеВКассу.ДоговорКредитаЗайма.ВалютаРасчетов)
	|ГДЕ
	|	ПоступлениеВКассу.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|	И ПоступлениеВКассу.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.РасчетыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.КредитПолученный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.СодержаниеПоТипуСуммы,
	|	ТаблицаДокумента.Сотрудник,
	|	ТаблицаДокумента.СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СуммаРасчетов,
	|	ТаблицаДокумента.СуммаУчета,
	|	ТаблицаДокумента.ДоговорКредитаЗайма,
	|	ТаблицаДокумента.ДоговорКредитаЗайма.ВалютаРасчетов,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.ДоговорКредитаЗайма.СчетУчета,
	|	ТаблицаДокумента.Касса,
	|	ЛОЖЬ
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийПоступлениеВКассу.ВозвратЗаймаСотрудником)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма КАК ДоговорКредитаЗайма
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам КАК ВременнаяТаблицаРасчетыПоКредитамИЗаймам";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоКредитамИЗаймам");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыПоКредитамИЗаймам(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоКредитамИЗаймам", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
