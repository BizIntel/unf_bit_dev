
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСПереопределяемый.ОбработкаЗаполненияДокумента(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПустаяСтрока(Идентификатор) Тогда
		Идентификатор = Строка(Новый УникальныйИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СтатусОбработки = Перечисления.СтатусыОбработкиВозвратаИзТорговогоЗалаЕГАИС.Новый;
	
	Идентификатор = "";
	ИдентификаторЕГАИС = "";
	ДатаРегистрацииДвижений = '00010101';
	
	Товары.ЗагрузитьКолонку(Новый Массив(Товары.Количество()), "СправкаБ");
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ТаблицаДвижений = ПолучитьТаблицуДвижений();
	
	РегистрыНакопления.ОстаткиАлкогольнойПродукцииЕГАИС.ЗаписатьТаблицуДвижений(ТаблицаДвижений, Движения, Отказ);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьТаблицуДвижений()
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("ПустаяДата", '00010101');
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.Дата КАК Период,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка КАК Регистратор,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.ОрганизацияЕГАИС КАК ОрганизацияЕГАИС,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.НомерСтроки КАК НомерСтроки,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.АлкогольнаяПродукция КАК АлкогольнаяПродукция,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.СправкаБ КАК СправкаБ,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Количество КАК СвободныйОстаток,
	|	0 КАК Количество
	|ИЗ
	|	Документ.ВозвратИзТорговогоЗалаЕГАИС.Товары КАК ВозвратИзТорговогоЗалаЕГАИСТовары
	|ГДЕ
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка = &Ссылка
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.СтатусОбработки = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиВозвратаИзТорговогоЗалаЕГАИС.ПереданВЕГАИС)
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Количество <> 0
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.ОрганизацияЕГАИС.УчитыватьОстаткиАлкогольнойПродукции
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.ДатаРегистрацииДвижений,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.ОрганизацияЕГАИС,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.НомерСтроки,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.АлкогольнаяПродукция,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.СправкаБ,
	|	0,
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Количество
	|ИЗ
	|	Документ.ВозвратИзТорговогоЗалаЕГАИС.Товары КАК ВозвратИзТорговогоЗалаЕГАИСТовары
	|ГДЕ
	|	ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка = &Ссылка
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.СтатусОбработки = ЗНАЧЕНИЕ(Перечисление.СтатусыОбработкиВозвратаИзТорговогоЗалаЕГАИС.ПереданВЕГАИС)
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.ДатаРегистрацииДвижений <> &ПустаяДата
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Количество <> 0
	|	И ВозвратИзТорговогоЗалаЕГАИСТовары.Ссылка.ОрганизацияЕГАИС.УчитыватьОстаткиАлкогольнойПродукции
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

#КонецОбласти

#КонецЕсли