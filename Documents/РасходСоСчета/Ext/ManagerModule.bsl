#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДенежныеСредства(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("РасходДенежныхСредств", НСтр("ru = 'Расход денежных средств'"));
	Запрос.УстановитьПараметр("ПеремещениеДенежныхСредств", НСтр("ru = 'Перемещение денежных средств'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ИспользуютсяПереводыВПути",РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата,СтруктураДополнительныеСвойства.ДляПроведения.Организация));
	
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаРасходСоСчета.ВалютаДенежныхСредств);
	
	Если ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		Итог = ДокументСсылкаРасходСоСчета.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	Иначе
		Итог = 0;
	КонецЕсли;
	ОстатокСуммы = ДокументСсылкаРасходСоСчета.СуммаДокумента - Итог;
	Запрос.УстановитьПараметр("ПлатежныйКалендарь", ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"));
	Запрос.УстановитьПараметр("ОстатокСуммы", ОстатокСуммы);
	Запрос.УстановитьПараметр("УчетВалютныхОпераций", ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|				ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			ТОГДА &ПеремещениеДенежныхСредств
	|		ИНАЧЕ &РасходДенежныхСредств
	|	КОНЕЦ КАК СодержаниеПроводки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные) КАК ТипДенежныхСредств,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.БанковскийСчет КАК БанковскийСчетКасса,
	|	ТаблицаДокумента.АналитикаДДС КАК Аналитика,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК Валюта,
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(&ОстатокСуммы) КАК СуммаВал,
	|	-СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаДляОстатка,
	|	-СУММА(&ОстатокСуммы) КАК СуммаВалДляОстатка,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета КАК СчетУчета
	|ПОМЕСТИТЬ ВременнаяТаблицаДенежныеСредства
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|ГДЕ
	|	(ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|				И &ИспользуютсяПереводыВПути)
	|	И &ОстатокСуммы > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|				ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			ТОГДА &ПеремещениеДенежныхСредств
	|		ИНАЧЕ &РасходДенежныхСредств
	|	КОНЕЦ,
	|	ТаблицаДокумента.АналитикаДДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	&РасходДенежныхСредств,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.АналитикаДДС,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность)),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность)),
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	(ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|				И &ИспользуютсяПереводыВПути)
	|	И &ПлатежныйКалендарь
	|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.АналитикаДДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	&РасходДенежныхСредств,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета),
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	(ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты)
	|			ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю))
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МАКСИМУМ(ВыплатаЗаработнойПлаты.НомерСтроки),
	|	&РасходДенежныхСредств,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ВыплатаЗаработнойПлаты.Ссылка.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ВыплатаЗаработнойПлаты.Ссылка.Статья,
	|	ВыплатаЗаработнойПлаты.Ссылка.БанковскийСчет,
	|	ПлатежнаяВедомостьСотрудники.Сотрудник,
	|	ВыплатаЗаработнойПлаты.Ссылка.ВалютаДенежныхСредств,
	|	СУММА(ВЫРАЗИТЬ(ПлатежнаяВедомостьСотрудники.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))),
	|	СУММА(ПлатежнаяВедомостьСотрудники.СуммаПлатежа),
	|	-СУММА(ВЫРАЗИТЬ(ПлатежнаяВедомостьСотрудники.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))),
	|	-СУММА(ПлатежнаяВедомостьСотрудники.СуммаПлатежа),
	|	ВыплатаЗаработнойПлаты.Ссылка.БанковскийСчет.СчетУчета
	|ИЗ
	|	Документ.ПлатежнаяВедомость.Сотрудники КАК ПлатежнаяВедомостьСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходСоСчета.ВыплатаЗаработнойПлаты КАК ВыплатаЗаработнойПлаты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|			ПО ВыплатаЗаработнойПлаты.Ссылка.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|		ПО ПлатежнаяВедомостьСотрудники.Ссылка = ВыплатаЗаработнойПлаты.Ведомость
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВыплатаЗаработнойПлаты.Ссылка = &Ссылка
	|	И ВыплатаЗаработнойПлаты.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Зарплата)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыплатаЗаработнойПлаты.Ссылка.Дата,
	|	ВыплатаЗаработнойПлаты.Ссылка.Статья,
	|	ВыплатаЗаработнойПлаты.Ссылка.БанковскийСчет,
	|	ВыплатаЗаработнойПлаты.Ссылка.ВалютаДенежныхСредств,
	|	ВыплатаЗаработнойПлаты.Ссылка.БанковскийСчет.СчетУчета,
	|	ПлатежнаяВедомостьСотрудники.Сотрудник
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	&ПеремещениеДенежныхСредств,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.СчетКонтрагента,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))),
	|	СУММА(&ОстатокСуммы),
	|	-СУММА(ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))),
	|	-СУММА(&ОстатокСуммы),
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|	И НЕ &ИспользуютсяПереводыВПути
	|	И &ОстатокСуммы > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ТаблицаДокумента.СчетКонтрагента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	&ПеремещениеДенежныхСредств,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.СчетКонтрагента,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность)),
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность)),
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа),
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|	И НЕ &ИспользуютсяПереводыВПути
	|	И &ПлатежныйКалендарь
	|	И ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Статья,
	|	ВременнаяТаблицаРасшифровкаПлатежа.СчетКонтрагента,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчетКассаСчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТипДенежныхСредств,
	|	БанковскийСчетКасса,
	|	Валюта,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаДенежныеСредства.Организация КАК Организация,
	|	ВременнаяТаблицаДенежныеСредства.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ВременнаяТаблицаДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ВременнаяТаблицаДенежныеСредства.Валюта КАК Валюта
	|ИЗ
	|	ВременнаяТаблицаДенежныеСредства КАК ВременнаяТаблицаДенежныеСредства";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДенежныеСредства");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыДенежныеСредства(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДенежныеСредства", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДенежныеСредства()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПодотчетниками(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеДолгаПодотчетника", НСтр("ru = 'Возникновение долга подотчетника'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	&ВозникновениеДолгаПодотчетника КАК СодержаниеПроводки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.БанковскийСчет КАК БанковскийСчет,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Документ = ЗНАЧЕНИЕ(Документ.АвансовыйОтчет.ПустаяСсылка)
	|			ТОГДА &Ссылка
	|		ИНАЧЕ ТаблицаДокумента.Документ
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Подотчетник КАК Сотрудник,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК Валюта,
	|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ТаблицаДокумента.СуммаДокумента) КАК СуммаВал,
	|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаДляОстатка,
	|	СУММА(ТаблицаДокумента.СуммаДокумента) КАК СуммаВалДляОстатка,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Документ = ЗНАЧЕНИЕ(Документ.АвансовыйОтчет.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Подотчетник.СчетРасчетовСПодотчетниками
	|		ИНАЧЕ ТаблицаДокумента.Подотчетник.СчетРасчетовПоПерерасходу
	|	КОНЕЦ КАК СчетУчета
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПодотчетниками
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта =&ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.Подотчетник,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Документ = ЗНАЧЕНИЕ(Документ.АвансовыйОтчет.ПустаяСсылка)
	|			ТОГДА &Ссылка
	|		ИНАЧЕ ТаблицаДокумента.Документ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Документ = ЗНАЧЕНИЕ(Документ.АвансовыйОтчет.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Подотчетник.СчетРасчетовСПодотчетниками
	|		ИНАЧЕ ТаблицаДокумента.Подотчетник.СчетРасчетовПоПерерасходу
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Сотрудник,
	|	Валюта,
	|	Документ,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Валюта КАК Валюта,
	|	ВременнаяТаблицаРасчетыСПодотчетниками.Документ КАК Документ
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПодотчетниками КАК ВременнаяТаблицаРасчетыСПодотчетниками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПодотчетниками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПодотчетниками(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПодотчетниками", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПодотчетниками()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеАвансаПоставщику", "Возникновение аванса поставщику");
	Запрос.УстановитьПараметр("ПогашениеОбязательствПередПоставщиком", "Погашение обязательств перед поставщиком");
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет КАК БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ КАК ТипРасчетов,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Дата,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК Сумма,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВал,
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК СуммаДляОстатка,
	|	-СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПоставщику
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПоставщиком
	|	КОНЕЦ КАК СчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов КАК Валюта,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ВозникновениеАвансаПоставщику
	|		ИНАЧЕ &ПогашениеОбязательствПередПоставщиком
	|	КОНЕЦ КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПоставщиками
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПоставщику
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПоставщиком
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ВозникновениеАвансаПоставщику
	|		ИНАЧЕ &ПогашениеОбязательствПередПоставщиком
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПоставщиками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ВременнаяТаблицаРасчетыСПоставщиками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщиками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПоставщиками(Запрос.МенеджерВременныхТаблиц, Ложь, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПоставщиками()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПогашениеАвансаПокупателя", НСтр("ru = 'Погашение аванса покупателя'"));
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПокупателя", НСтр("ru = 'Возникновение обязательств покупателя'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет КАК БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ КАК ТипРасчетов,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Дата,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК Сумма,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВал,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК СуммаДляОстатка,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПокупателя
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПокупателем
	|	КОНЕЦ КАК СчетУчета,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов КАК Валюта,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ПогашениеАвансаПокупателя
	|		ИНАЧЕ &ВозникновениеОбязательствПокупателя
	|	КОНЕЦ КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПокупателями
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаАвансовПокупателя
	|		ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.СчетУчетаРасчетовСПокупателем
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ПризнакАванса
	|			ТОГДА &ПогашениеАвансаПокупателя
	|		ИНАЧЕ &ВозникновениеОбязательствПокупателя
	|	КОНЕЦ
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПокупателями.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПокупателями.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПокупателями.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПокупателями.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПокупателями.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПокупателями.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями КАК ВременнаяТаблицаРасчетыСПокупателями";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПокупателями");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПокупателями(Запрос.МенеджерВременныхТаблиц, Ложь, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПокупателями", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПокупателями()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПерсоналом(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПогашениеОбязательствПередПерсоналом", НСтр("ru='Погашение обязательств перед персоналом'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПлатежнаяВедомость.Ссылка
	|ИЗ
	|	Документ.ПлатежнаяВедомость КАК ПлатежнаяВедомость
	|ГДЕ
	|	ПлатежнаяВедомость.Ссылка В
	|			(ВЫБРАТЬ
	|				ВыплатаЗаработнойПлаты.Ведомость
	|			ИЗ
	|				Документ.РасходСоСчета.ВыплатаЗаработнойПлаты КАК ВыплатаЗаработнойПлаты
	|			ГДЕ
	|				ВыплатаЗаработнойПлаты.Ссылка = &Ссылка)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("Документ.ПлатежнаяВедомость");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МАКСИМУМ(ВыплатаЗаработнойПлаты.НомерСтроки) КАК НомерСтроки,
	|	&Организация КАК Организация,
	|	ВыплатаЗаработнойПлаты.Ссылка.Дата КАК Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ПлатежнаяВедомостьСотрудники.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ПлатежнаяВедомостьСотрудники.Сотрудник КАК Сотрудник,
	|	ПлатежнаяВедомостьСотрудники.Ссылка.ВалютаРасчетов КАК Валюта,
	|	ПлатежнаяВедомостьСотрудники.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	СУММА(ВЫРАЗИТЬ(ПлатежнаяВедомостьСотрудники.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ПлатежнаяВедомостьСотрудники.СуммаРасчетов) КАК СуммаВал,
	|	-СУММА(ВЫРАЗИТЬ(ПлатежнаяВедомостьСотрудники.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаДляОстатка,
	|	-СУММА(ПлатежнаяВедомостьСотрудники.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	ПлатежнаяВедомостьСотрудники.Сотрудник.СчетРасчетовСПерсоналом КАК СчетУчета,
	|	&ПогашениеОбязательствПередПерсоналом КАК СодержаниеПроводки,
	|	ВыплатаЗаработнойПлаты.Ссылка.БанковскийСчет КАК БанковскийСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПерсоналом
	|ИЗ
	|	Документ.ПлатежнаяВедомость.Сотрудники КАК ПлатежнаяВедомостьСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.РасходСоСчета.ВыплатаЗаработнойПлаты КАК ВыплатаЗаработнойПлаты
	|			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|			ПО ВыплатаЗаработнойПлаты.Ссылка.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|		ПО ПлатежнаяВедомостьСотрудники.Ссылка = ВыплатаЗаработнойПлаты.Ведомость
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта = &ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ВыплатаЗаработнойПлаты.Ссылка = &Ссылка
	|	И ВыплатаЗаработнойПлаты.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Зарплата)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВыплатаЗаработнойПлаты.Ссылка.Дата,
	|	ПлатежнаяВедомостьСотрудники.Ссылка.СтруктурнаяЕдиница,
	|	ПлатежнаяВедомостьСотрудники.Сотрудник,
	|	ПлатежнаяВедомостьСотрудники.Ссылка.ВалютаРасчетов,
	|	ПлатежнаяВедомостьСотрудники.Ссылка.ПериодРегистрации,
	|	ПлатежнаяВедомостьСотрудники.Сотрудник.СчетРасчетовСПерсоналом,
	|	ВыплатаЗаработнойПлаты.Ссылка.БанковскийСчет
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Сотрудник,
	|	Валюта,
	|	ПериодРегистрации,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПерсоналом.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПерсоналом.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаРасчетыСПерсоналом.Сотрудник КАК Сотрудник,
	|	ВременнаяТаблицаРасчетыСПерсоналом.Валюта КАК Валюта,
	|	ВременнаяТаблицаРасчетыСПерсоналом.ПериодРегистрации КАК ПериодРегистрации
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПерсоналом КАК ВременнаяТаблицаРасчетыСПерсоналом";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПерсоналом");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПерсоналом(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПерсоналом", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПерсоналом()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ОтражениеРасходов", НСтр("ru = 'Отражение расходов'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА ТаблицаДокумента.Подразделение
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ТОГДА ТаблицаДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Корреспонденция КАК СчетУчета,
	|	ТаблицаДокумента.АналитикаПрочихДоходовИРасходов КАК Аналитика,
	|	&ОтражениеРасходов КАК СодержаниеПроводки,
	|	0 КАК СуммаДоходов,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютРасчетов.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК СуммаРасходов,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютРасчетов.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И (ТаблицаДокумента.Корреспонденция.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ИЛИ ТаблицаДокумента.Корреспонденция.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы))
	|	И (ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ИЛИ ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику))
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницДенежныеСредства КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПодотчетниками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПерсоналом КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницПрочиеРасчеты КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаДоходыИРасходы = РегистрыНакопления.ДоходыИРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", ТаблицаДоходыИРасходы);
	
	Выборка = РезультатЗапроса.Выбрать();
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		НоваяСтрока.Активность = Истина;
	КонецЦикла; 
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	Запрос.УстановитьПараметр("Содержание", НСтр("ru = 'Списание денежных средств на произвольный счет'"));
	Запрос.УстановитьПараметр("СодержаниеСнятиеНаличных", НСтр("ru = 'Оприходование денежных средств в кассу'"));
	Запрос.УстановитьПараметр("ОплатаНалога", НСтр("ru='Оплата налога'"));
	Запрос.УстановитьПараметр("СодержаниеОплатаОтБанкаЭквайрера", НСтр("ru = 'Оплата от банка-эквайрера'"));
	Запрос.УстановитьПараметр("СодержаниеКомиссияБанкаЭквайрера", НСтр("ru = 'Комиссия банка-эквайрера'"));
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ИспользуютсяПереводыВПути",РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата,СтруктураДополнительныеСвойства.ДляПроведения.Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаДокумента.Корреспонденция КАК СчетДт,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Корреспонденция.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютРасчетов.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫБОР КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|	ТОГДА  ВЫРАЗИТЬ(&СодержаниеСнятиеНаличных КАК СТРОКА(100))
	|	ИНАЧЕ ВЫРАЗИТЬ(&Содержание КАК СТРОКА(100)) КОНЕЦ КАК Содержание
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта = &ВалютаУчета) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			И &ИспользуютсяПереводыВПути)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.ВидНалога.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидНалога.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидНалога.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаДокумента
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютРасчетов.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)),
	|	ВЫРАЗИТЬ(&ОплатаНалога КАК СТРОКА(100))
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта = &ВалютаУчета) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПодотчетниками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПодотчетниками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПокупателями КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	7,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	8,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	9,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницДенежныеСредства КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	10,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПерсоналом КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	11,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыСПерсоналом КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	12,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчетаЭквайринг,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаЭквайринг.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаЭквайринг.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СуммаУчета,
	|	ВЫРАЗИТЬ(&СодержаниеОплатаОтБанкаЭквайрера КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	13,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетЗатратЭквайринг,
	|	ТаблицаДокумента.СчетУчетаЭквайринг,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетЗатратЭквайринг.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаЭквайринг.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетЗатратЭквайринг.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаРасчетовКомиссии
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаЭквайринг.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаРасчетовКомиссии
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СуммаУчетаКомиссии,
	|	ВЫРАЗИТЬ(&СодержаниеКомиссияБанкаЭквайрера КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты)
	|	И ТаблицаДокумента.СуммаРасчетовКомиссии <> 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	14,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	15,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницРасчетыПоКредитамИЗаймам КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	16,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СчетУчета
	|		ИНАЧЕ ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеРасходы)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ЗНАЧЕНИЕ(ПланСчетов.Управленческий.ПрочиеДоходы)
	|		ИНАЧЕ ТаблицаДокумента.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы < 0
	|				И ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	ВременнаяТаблицаКурсовыхРазницПрочиеРасчеты КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	17,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчета,
	|	ТаблицаДокумента.БанковскийСчет.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.ВалютаДенежныхСредств
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.БанковскийСчет.СчетУчета.Валютный
	|			ТОГДА ТаблицаДокумента.СуммаПлатежа
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	ТаблицаДокумента.СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаПрочиеРасчеты КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";

	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаРасходСоСчета.ВалютаДенежныхСредств);
	
	Если ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		Итог = ДокументСсылкаРасходСоСчета.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	Иначе
		Итог = 0;
	КонецЕсли;
	ОстатокСуммы = ДокументСсылкаРасходСоСчета.СуммаДокумента - Итог;
	Запрос.УстановитьПараметр("ПлатежныйКалендарь", ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"));
	Запрос.УстановитьПараметр("ОстатокСуммы", ОстатокСуммы);
	Запрос.УстановитьПараметр("УчетВалютныхОпераций", ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ТаблицаДокумента.ДоговорОбслуживанияНаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	0 КАК СуммаДоходов,
	|	ТаблицаДокумента.СуммаУчета КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|	И (ТаблицаДокумента.ПризнакАванса
	|			ИЛИ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	0,
	|	Таблица.СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные КАК Таблица
	|ГДЕ
	|	Таблица.СуммаРасходов > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья,
	|	0,
	|	ВЫРАЗИТЬ(&ОстатокСуммы * КурсыВалютРасчетов.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2))
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчетаСрезПоследних
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютРасчетов.Валюта
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Зарплата)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги))
	|	И &ОстатокСуммы > 0
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.НаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.НаправлениеДеятельности
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья,
	|	0,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Зарплата)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги))
	|	И ТаблицаДокумента.СуммаПлатежа > 0
	|	И &ПлатежныйКалендарь
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ТаблицаДокумента.ДоговорОбслуживанияНаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья,
	|	-ТаблицаДокумента.СуммаУчета,
	|	0
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|	И (ТаблицаДокумента.ПризнакАванса
	|			ИЛИ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	-Таблица.СуммаДоходов,
	|	0
	|ИЗ
	|	ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные КАК Таблица
	|ГДЕ
	|	Таблица.СуммаДоходов > 0";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	0 КАК СуммаДоходов,
	|	ТаблицаДокумента.СуммаУчета КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|	И НЕ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И ТаблицаДокумента.ПризнакАванса
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья,
	|	-ТаблицаДокумента.СуммаУчета,
	|	0
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|	И НЕ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И ТаблицаДокумента.ПризнакАванса
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	Запрос.УстановитьПараметр("Период", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	
	// Формирование таблицы с суммами к списанию.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	СУММА(ТаблицаДокумента.СуммаУчета) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|	И НЕ ТаблицаДокумента.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И НЕ ТаблицаДокумента.ПризнакАванса
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДоходыИРасходыОтложенные");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Организация", "Организация");
	ЭлементБлокировки.ИспользоватьИзИсточникаДанных("Документ", "Документ");
	Блокировка.Заблокировать();
	
	ТаблицаСуммыКСписанию = РезультатЗапроса.Выгрузить();
	
	// Формирование таблицы с остатками.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Период КАК Период,
	|	ДоходыИРасходыОтложенныеОстатки.Организация КАК Организация,
	|	ДоходыИРасходыОтложенныеОстатки.Документ КАК Документ,
	|	ДоходыИРасходыОтложенныеОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка) КАК Статья,
	|	0 КАК СуммаДоходов,
	|	0 КАК СуммаРасходов,
	|	-СУММА(ДоходыИРасходыОтложенныеОстатки.СуммаДоходовОстаток) КАК СуммаДоходовОстаток,
	|	СУММА(ДоходыИРасходыОтложенныеОстатки.СуммаРасходовОстаток) КАК СуммаРасходовОстаток
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДоходыИРасходыОтложенныеОстатки.Организация КАК Организация,
	|		ДоходыИРасходыОтложенныеОстатки.Документ КАК Документ,
	|		ДоходыИРасходыОтложенныеОстатки.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|		ДоходыИРасходыОтложенныеОстатки.СуммаДоходовОстаток КАК СуммаДоходовОстаток,
	|		ДоходыИРасходыОтложенныеОстатки.СуммаРасходовОстаток КАК СуммаРасходовОстаток
	|	ИЗ
	|		РегистрНакопления.ДоходыИРасходыОтложенные.Остатки(
	|				,
	|				Организация = &Организация
	|					И Документ В
	|						(ВЫБРАТЬ
	|							ТаблицаДокумента.Документ
	|						ИЗ
	|							ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|						ГДЕ
	|							(ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|								ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)))) КАК ДоходыИРасходыОтложенныеОстатки
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДокументаДоходыИРасходыОтложенные.Организация,
	|		ДвиженияДокументаДоходыИРасходыОтложенные.Документ,
	|		ДвиженияДокументаДоходыИРасходыОтложенные.НаправлениеДеятельности,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаДоходыИРасходыОтложенные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаДоходов, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаДоходов, 0)
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ДвиженияДокументаДоходыИРасходыОтложенные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаРасходов, 0)
	|			ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаДоходыИРасходыОтложенные.СуммаРасходов, 0)
	|		КОНЕЦ
	|	ИЗ
	|		РегистрНакопления.ДоходыИРасходыОтложенные КАК ДвиженияДокументаДоходыИРасходыОтложенные
	|	ГДЕ
	|		ДвиженияДокументаДоходыИРасходыОтложенные.Регистратор = &Ссылка) КАК ДоходыИРасходыОтложенныеОстатки
	|
	|СГРУППИРОВАТЬ ПО
	|	ДоходыИРасходыОтложенныеОстатки.Организация,
	|	ДоходыИРасходыОтложенныеОстатки.Документ,
	|	ДоходыИРасходыОтложенныеОстатки.НаправлениеДеятельности
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ";
	
	ТаблицаСуммыОстатки = Запрос.Выполнить().Выгрузить();

	ТаблицаСуммыОстатки.Индексы.Добавить("Документ");
	
	// Расчет сумм списания.
	Для каждого СтрокаСуммыКСписанию Из ТаблицаСуммыКСписанию Цикл
		СуммаКСписанию = СтрокаСуммыКСписанию.СуммаКСписанию;
		Отбор = Новый Структура("Документ", СтрокаСуммыКСписанию.Документ);
		МассивСтрокСуммыОстатки = ТаблицаСуммыОстатки.НайтиСтроки(Отбор);
		Для каждого СтрокаСуммыОстатки Из МассивСтрокСуммыОстатки Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаСуммыОстатки.СуммаРасходовОстаток < СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаРасходов = СтрокаСуммыОстатки.СуммаРасходовОстаток;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = СуммаКСписанию - СтрокаСуммыОстатки.СуммаРасходовОстаток;
			ИначеЕсли СтрокаСуммыОстатки.СуммаРасходовОстаток >= СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаРасходов = СуммаКСписанию;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Формирование таблицы с суммами к списанию.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	СУММА(ТаблицаДокумента.СуммаУчета) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|	И НЕ ТаблицаДокумента.ПризнакАванса
	|
	|СГРУППИРОВАТЬ ПО
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.Статья"; 
	
	ТаблицаСуммыКСписанию = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаСуммыКСписанию Из ТаблицаСуммыКСписанию Цикл
		СуммаКСписанию = СтрокаСуммыКСписанию.СуммаКСписанию;
		Отбор = Новый Структура("Документ", СтрокаСуммыКСписанию.Документ);
		МассивСтрокСуммыОстатки = ТаблицаСуммыОстатки.НайтиСтроки(Отбор);
		Для каждого СтрокаСуммыОстатки Из МассивСтрокСуммыОстатки Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаСуммыОстатки.СуммаДоходовОстаток < СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаДоходов = СтрокаСуммыОстатки.СуммаДоходовОстаток;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = СуммаКСписанию - СтрокаСуммыОстатки.СуммаДоходовОстаток;
			ИначеЕсли СтрокаСуммыОстатки.СуммаДоходовОстаток >= СуммаКСписанию Тогда
				СтрокаСуммыОстатки.СуммаДоходов = СуммаКСписанию;
				СтрокаСуммыОстатки.Статья = СтрокаСуммыКСписанию.Статья;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Формирование временной таблицы с суммами, статьями и направлениями
	// деятельности. Понадобится для формирования движений по доходам и расходам
	// кассовым методом.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	Таблица.Статья КАК Статья,
	|	Таблица.СуммаДоходов КАК СуммаДоходов,
	|	Таблица.СуммаРасходов КАК СуммаРасходов,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица
	|ГДЕ
	|	(Таблица.СуммаДоходов > 0
	|			ИЛИ Таблица.СуммаРасходов > 0)";
	
	Запрос.УстановитьПараметр("Таблица", ТаблицаСуммыОстатки);
	
	Запрос.Выполнить();
	
	// Формирование таблицы для записи в регистр.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	Таблица.Статья КАК Статья,
	|	-Таблица.СуммаДоходов КАК СуммаДоходов,
	|	Таблица.СуммаРасходов КАК СуммаРасходов,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности
	|ИЗ
	|	ВременнаяТаблицаТаблицаДоходыИРасходыОтложенные КАК Таблица";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()  

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыПоНалогам(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", 		ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("ВалютаУчета",	Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДокумента",ДокументСсылкаРасходСоСчета.ВалютаДенежныхСредств);
	Запрос.УстановитьПараметр("Организация",	СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени",	Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ОплатаНалога",	НСтр("ru = 'Оплата налога'"));
	Запрос.УстановитьПараметр("ПустаяДата", Дата('00010101'));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ КурсыВалют
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаУчета, &ВалютаДокумента)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ ПЕРВЫЕ 1
	|	НачислениеНалоговНалоги.СрокУплаты
	|ПОМЕСТИТЬ ВременнаяТаблицаСрокУплаты
	|ИЗ
	|	Документ.РасходСоСчета КАК РасходСоСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.НачислениеНалогов.Налоги КАК НачислениеНалоговНалоги
	|		ПО (РасходСоСчета.Ссылка = &Ссылка)
	|			И (НачислениеНалоговНалоги.Ссылка = РасходСоСчета.ДокументОснование
	|				ИЛИ НачислениеНалоговНалоги.Ссылка = РасходСоСчета.ДокументОснование.ДокументОснование)
	|			И РасходСоСчета.ВидНалога = НачислениеНалоговНалоги.ВидНалога
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Дата КАК Период,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.ВидНалога КАК ВидНалога,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсВалютыДокумента.Курс * КурсВалютыУчета.Кратность / (КурсВалютыУчета.Курс * КурсВалютыДокумента.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ТаблицаДокумента.ВидНалога.СчетУчета КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СрокУплаты = &ПустаяДата
	|			ТОГДА ТаблицаСрокУплаты.СрокУплаты
	|	ИНАЧЕ ТаблицаДокумента.СрокУплаты
	|КОНЕЦ КАК СрокУплаты,
	|	&ОплатаНалога КАК СодержаниеПроводки
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыДокумента
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсВалютыДокумента.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ КурсыВалют КАК КурсВалютыУчета
	|		ПО (КурсВалютыУчета.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСрокУплаты КАК ТаблицаСрокУплаты
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоНалогам", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыПоНалогам()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаПлатежныйКалендарь(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ИспользоватьПлатежныйКалендарь", Константы.ФункциональнаяОпцияПлатежныйКалендарь.Получить());
	Запрос.УстановитьПараметр("ИспользуютсяПереводыВПути",РегистрыСведений.ПрименениеПереводовВПути.ИспользуютсяПереводыВПути(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата,СтруктураДополнительныеСвойства.ДляПроведения.Организация));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные) КАК ТипДенежныхСредств,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден) КАК СтатусУтвержденияПлатежа,
	|	ТаблицаДокумента.БанковскийСчет КАК БанковскийСчетКасса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря КАК СчетНаОплату,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|				ТОГДА -ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ -ТаблицаДокумента.СуммаПлатежа
	|		КОНЕЦ) КАК СуммаОплаты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|	И ТаблицаДокумента.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Зарплата)
	|	И (ТаблицаДокумента.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			ИЛИ &ИспользуютсяПереводыВПути)
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2 КАК Порядок,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные) КАК ТипДенежныхСредств,
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден) КАК СтатусУтвержденияПлатежа,
	|	ТаблицаДокумента.СчетКонтрагента КАК БанковскийСчетКасса,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ КАК Валюта,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря КАК СчетНаОплату,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ТаблицаДокумента.СуммаПлатежа
	|		КОНЕЦ) КАК СуммаОплаты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|	И ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|		И НЕ &ИспользуютсяПереводыВПути
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Статья,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.СчетКонтрагента,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ ТаблицаДокумента.ВалютаДенежныхСредств
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетНаОплатуДляПлатежногоКалендаря
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки),
	|	ТаблицаДокумента.Ссылка.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Ссылка.Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден),
	|	ТаблицаДокумента.Ссылка.БанковскийСчет,
	|	ТаблицаДокумента.Ссылка.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.ДокументПланирования,
	|	СУММА(-ТаблицаДокумента.СуммаПлатежа)
	|ИЗ
	|	Документ.РасходСоСчета.ВыплатаЗаработнойПлаты КАК ТаблицаДокумента
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|	И ТаблицаДокумента.Ссылка = &Ссылка
	|	И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Зарплата)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.ДокументПланирования,
	|	ТаблицаДокумента.Ссылка.Дата,
	|	ТаблицаДокумента.Ссылка.Статья,
	|	ТаблицаДокумента.Ссылка.БанковскийСчет,
	|	ТаблицаДокумента.Ссылка.ВалютаДенежныхСредств
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Статья,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыДенежныхСредств.БезНаличные),
	|	ЗНАЧЕНИЕ(Перечисление.СтатусыУтвержденияПлатежей.Утвержден),
	|	ТаблицаДокумента.БанковскийСчет,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	НЕОПРЕДЕЛЕНО,
	|	-ТаблицаДокумента.СуммаДокумента
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			КОЛИЧЕСТВО(ЕСТЬNULL(ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки, 0)) КАК Количество
	|		ИЗ
	|			ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа) КАК ВложенныйЗапрос
	|		ПО (ИСТИНА)
	|ГДЕ
	|	&ИспользоватьПлатежныйКалендарь
	|	И ТаблицаДокумента.Ссылка = &Ссылка
	|	И ВложенныйЗапрос.Количество = 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаПлатежныйКалендарь", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПлатежныйКалендарь()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.СчетНаОплату КАК СчетНаОплату,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаАванса,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.СчетНаОплату.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаОплаты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСчета
	|		ПО ТаблицаДокумента.СчетНаОплату.ВалютаДокумента = КурсыВалютСчета.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ВестиУчетОплатыПоСчетам
	|	И ТаблицаДокумента.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплату.ПустаяСсылка)
	|	И ТаблицаДокумента.СчетНаОплату <> ЗНАЧЕНИЕ(Документ.СчетНаОплатуПоставщика.ПустаяСсылка)
	|	И ТаблицаДокумента.СчетНаОплату <> НЕОПРЕДЕЛЕНО
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.СчетНаОплату,
	|	ТаблицаДокумента.ВидОперации
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	&Организация,
	|	ТаблицаДокумента.Заказ,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ПризнакАванса
	|				ТОГДА 0
	|			КОГДА ТаблицаДокумента.ВалютаДенежныхСредств = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаПлатежа
	|			КОГДА ТаблицаДокумента.ВалютаРасчетов = ТаблицаДокумента.Заказ.ВалютаДокумента
	|				ТОГДА ТаблицаДокумента.СуммаРасчетов
	|			ИНАЧЕ ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютСчета.Кратность / (КурсыВалютСчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))
	|		КОНЕЦ) * ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|			ТОГДА -1
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютСчета
	|		ПО ТаблицаДокумента.Заказ.ВалютаДокумента = КурсыВалютСчета.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютКассы
	|		ПО ТаблицаДокумента.ВалютаДенежныхСредств = КурсыВалютКассы.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ВестиУчетОплатыПоСчетам
	|	И (ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Заказ) = ТИП(Документ.ЗаказПокупателя)
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ИЛИ ТИПЗНАЧЕНИЯ(ТаблицаДокумента.Заказ) = ТИП(Документ.ЗаказПоставщику)
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка))
	|	И (ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|			ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Заказ,
	|	ТаблицаДокумента.ВидОперации
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаОплатаСчетовИЗаказов", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаОплатаСчетовИЗаказов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьНДФЛРасчетыНалоговыхАгентовСБюджетом(ДокументСсылкаРасходИзКассы, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСНДФЛ.Период,
	|	РасчетыСНДФЛ.Организация,
	|	РасчетыСНДФЛ.ВидДвижения,
	|	РасчетыСНДФЛ.ПериодРегистрации,
	|	РасчетыСНДФЛ.Сумма
	|ИЗ
	|	ВТРасчетыСНДФЛ КАК РасчетыСНДФЛ";
	
	
	// Обрабатываем НДФЛ
	ВыборкаПоНДФЛ = Запрос.Выполнить().Выбрать();
	ТаблицаПоНДФЛ = Новый ТаблицаЗначений();
	ТаблицаПоНДФЛ.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата"));
	ТаблицаПоНДФЛ.Колонки.Добавить("Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПоНДФЛ.Колонки.Добавить("ВидДвижения",Новый ОписаниеТипов("ВидДвиженияНакопления"));
	ТаблицаПоНДФЛ.Колонки.Добавить("ПериодРегистрации",Новый ОписаниеТипов("Дата"));
	ТаблицаПоНДФЛ.Колонки.Добавить("Сотрудник",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаПоНДФЛ.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число"),Новый КвалификаторыЧисла(15,2));
	
	Если ВыборкаПоНДФЛ.Следующий() Тогда
		СтрокиРаспределения = Сотрудники.РаспределитьПеречисленныйНалогПоФизическимЛицам(ДокументСсылкаРасходИзКассы, ДокументСсылкаРасходИзКассы.Организация,ВыборкаПоНДФЛ.Период , ВыборкаПоНДФЛ.Сумма, ВыборкаПоНДФЛ.ПериодРегистрации);
		Для Каждого СтрокаТЗ Из СтрокиРаспределения Цикл
			НоваяСтрока = ТаблицаПоНДФЛ.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоНДФЛ, , "Сумма");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		КонецЦикла;
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНДФЛРасчетыНалоговыхАгентовСБюджетом", ТаблицаПоНДФЛ);
	
КонецПроцедуры // СформироватьНДФЛРасчетыНалоговыхАгентовСБюджетом()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьРасчетыСФондамиПоСтраховымВзносам(ДокументСсылкаРасходИзКассы, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСФондамиПоСтраховымВзносам.Период,
	|	РасчетыСФондамиПоСтраховымВзносам.Организация,
	|	РасчетыСФондамиПоСтраховымВзносам.ВидДвижения,
	|	РасчетыСФондамиПоСтраховымВзносам.ЭтоСтраховыеВзносы,
	|	РасчетыСФондамиПоСтраховымВзносам.ВидОбязательногоСтрахованияСотрудников,
	|	РасчетыСФондамиПоСтраховымВзносам.Сумма
	|ИЗ
	|	ВТРасчетыСФондамиПоСтраховымВзносам КАК РасчетыСФондамиПоСтраховымВзносам";
	
	
	// Обрабатываем НДФЛ
	ВыборкаПоВзносам = Запрос.Выполнить().Выбрать();
	ТаблицаПоВзносам = Новый ТаблицаЗначений();
	ТаблицаПоВзносам.Колонки.Добавить("Период",Новый ОписаниеТипов("Дата"));
	ТаблицаПоВзносам.Колонки.Добавить("Организация",Новый ОписаниеТипов("СправочникСсылка.Организации"));
	ТаблицаПоВзносам.Колонки.Добавить("ВидДвижения",Новый ОписаниеТипов("ВидДвиженияНакопления"));
	ТаблицаПоВзносам.Колонки.Добавить("ЭтоСтраховыеВзносы",Новый ОписаниеТипов("Булево"));
	ТаблицаПоВзносам.Колонки.Добавить("ВидОбязательногоСтрахованияСотрудников",Новый ОписаниеТипов("ПеречислениеСсылка.ВидыОбязательногоСтрахованияСотрудников"));
	ТаблицаПоВзносам.Колонки.Добавить("Сотрудник",Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаПоВзносам.Колонки.Добавить("Сумма",Новый ОписаниеТипов("Число"),Новый КвалификаторыЧисла(15,2));
	
	Если ВыборкаПоВзносам.Следующий() Тогда
		СтрокиРаспределения = Сотрудники.РаспределитьПеречисленныйВзносПоФизическимЛицам(ДокументСсылкаРасходИзКассы, ДокументСсылкаРасходИзКассы.Организация,ВыборкаПоВзносам.Период , ВыборкаПоВзносам.Сумма, ВыборкаПоВзносам.ВидОбязательногоСтрахованияСотрудников);
		Для Каждого СтрокаТЗ Из СтрокиРаспределения Цикл
			НоваяСтрока = ТаблицаПоВзносам.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаПоВзносам, , "Сумма");
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЗ);
		КонецЦикла;
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСФондамиПоСтраховымВзносам", ТаблицаПоВзносам);
	
КонецПроцедуры // СформироватьРасчетыСФондамиПоСтраховымВзносам()

#Область Биллинг

// Формирует таблицу значений, содержащую данные для проведения по регистру ВыполнениеДоговоровОбслуживания.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаВыполнениеДоговораОбслуживания(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Если НЕ ЗначениеЗаполнено(ДокументСсылкаРасходСоСчета.НаправлениеДеятельности)
		ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьБиллинг")
		ИЛИ НЕ Константы.БиллингВестиУчетРасходовПоДоговорамОбслуживания.Получить() Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыполнениеДоговораОбслуживания", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаШапка.Дата КАК Период,
	|	ДоговорыКонтрагентов.Ссылка КАК Договор,
	|	ВременнаяТаблицаШапка.Корреспонденция КАК ОбъектДоговораОбслуживания,
	|	ИСТИНА КАК УказанаСтоимость,
	|	1 КАК Количество,
	|	ВременнаяТаблицаШапка.СуммаПоДоговоруОбслуживания КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО ВременнаяТаблицаШапка.НаправлениеДеятельности = ДоговорыКонтрагентов.ДоговорОбслуживанияНаправлениеДеятельности
	|ГДЕ
	|	(ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|			ИЛИ ВременнаяТаблицаШапка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы))
	|	И ДоговорыКонтрагентов.ЭтоДоговорОбслуживания";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаВыполнениеДоговораОбслуживания", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

#КонецОбласти

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ОрганизацияДляРегОтчетности", ДокументСсылкаРасходСоСчета.Организация);
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаРасходСоСчета.ВалютаДенежныхСредств);
	Запрос.УстановитьПараметр("ВалютаДоговораКредитаЗайма", ДокументСсылкаРасходСоСчета.ДоговорКредитаЗайма.ВалютаРасчетов);
	Если НЕ ЗначениеЗаполнено(ДокументСсылкаРасходСоСчета.НаправлениеДеятельности)
		ИЛИ НЕ ПолучитьФункциональнуюОпцию("ИспользоватьБиллинг")
		ИЛИ НЕ Константы.БиллингВестиУчетРасходовПоДоговорамОбслуживания.Получить() Тогда
		Запрос.УстановитьПараметр("ВалютаДоговораОбслуживания", Справочники.Валюты.ПустаяСсылка());
	Иначе
		ДоговорОбслуживания = Справочники.ДоговорыКонтрагентов.НайтиПоРеквизиту("ДоговорОбслуживанияНаправлениеДеятельности", ДокументСсылкаРасходСоСчета.НаправлениеДеятельности);
		Запрос.УстановитьПараметр("ВалютаДоговораОбслуживания", ?(ЗначениеЗаполнено(ДоговорОбслуживания), ДоговорОбслуживания.ВалютаРасчетов, Справочники.Валюты.ПустаяСсылка()));
	КонецЕсли;
	
	Запрос.УстановитьПараметр("ОбъединятьНакопительнуюИСтраховуюЧасть", ?(ЗначениеЗаполнено(ДокументСсылкаРасходСоСчета.ДокументОснование), ДокументСсылкаРасходСоСчета.ДокументОснование.Дата >= УчетСтраховыхВзносов.ДатаОбъединенияСтраховойИНакопительнойЧастейВзносовПФР(), Истина));
	Запрос.УстановитьПараметр("ПериодРегистрацииНДФЛ", НачалоМесяца(ДобавитьМесяц(ДокументСсылкаРасходСоСчета.СрокУплаты, -1)));
	
	СтраховыеВидыВзаиморасчетов = Новый Массив();
	СтраховыеВидыВзаиморасчетов.Добавить(Справочники.ВидыНалогов.ПФРНакопительнаяСотрудники);
	СтраховыеВидыВзаиморасчетов.Добавить(Справочники.ВидыНалогов.ПФРСтраховаяСотрудники);
	СтраховыеВидыВзаиморасчетов.Добавить(Справочники.ВидыНалогов.ФОМССотрудники);
	СтраховыеВидыВзаиморасчетов.Добавить(Справочники.ВидыНалогов.ФСССотрудники);
	СтраховыеВидыВзаиморасчетов.Добавить(Справочники.ВидыНалогов.ФССТравматизмСотрудники);
	
	Запрос.УстановитьПараметр("СтраховыеВидыВзаиморасчетов",СтраховыеВидыВзаиморасчетов);
	
	Запрос.УстановитьПараметр("ИспользоватьСтатьиДДС", ПолучитьФункциональнуюОпцию("ИспользоватьСтатьиДвиженияДенежныхСредств"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаУчета, &ВалютаДС, &ВалютаДоговораКредитаЗайма, &ВалютаДоговораОбслуживания)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ТаблицаДокумента.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДокумента.Ссылка.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ТаблицаДокумента.Ссылка.ВидОперации КАК ВидОперации,
	|	ТаблицаДокумента.Ссылка.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.ЭтоДоговорОбслуживания
	|				И ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК ДоговорОбслуживанияНаправлениеДеятельности,
	|	ТаблицаДокумента.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ТаблицаДокумента.Ссылка.БанковскийСчет КАК БанковскийСчет,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиДДС
	|				ИЛИ ТаблицаДокумента.СтатьяДДС = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Ссылка.Статья
	|		ИНАЧЕ ТаблицаДокумента.СтатьяДДС
	|	КОНЕЦ КАК Статья,
	|	ТаблицаДокумента.Ссылка.Корреспонденция КАК Корреспонденция,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ПризнакАванса КАК ПризнакАванса,
	|	ТаблицаДокумента.Ссылка.Дата КАК Дата,
	|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежа * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаУчета,
	|	СУММА(ТаблицаДокумента.СуммаРасчетов) КАК СуммаРасчетов,
	|	СУММА(ТаблицаДокумента.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ТаблицаДокумента.СуммаРасчетов + ТаблицаДокумента.СуммаРасчетовКомиссии) КАК СуммаРасчетовДляЭквайринга,
	|	СУММА(ТаблицаДокумента.СуммаПлатежа + ТаблицаДокумента.СуммаПлатежаКомиссии) КАК СуммаПлатежаДляЭквайринга,
	|	СУММА(ВЫРАЗИТЬ((ТаблицаДокумента.СуммаПлатежа + ТаблицаДокумента.СуммаПлатежаКомиссии) * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаУчетаДляЭквайринга,
	|	СУММА(ВЫРАЗИТЬ(ТаблицаДокумента.СуммаПлатежаКомиссии * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2))) КАК СуммаУчетаКомиссии,
	|	СУММА(ТаблицаДокумента.СуммаПлатежаКомиссии) КАК СуммаПлатежаКомиссии,
	|	СУММА(ТаблицаДокумента.СуммаРасчетовКомиссии) КАК СуммаРасчетовКомиссии,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.РасходДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.РасходДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.ПеремещениеДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ПеремещениеДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТаблицаДокумента.СчетНаОплату.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.СчетНаОплату
	|		КОГДА ТаблицаДокумента.Заказ.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК СчетНаОплатуДляПлатежногоКалендаря,
	|	ТаблицаДокумента.СчетНаОплату КАК СчетНаОплату,
	|	ТаблицаДокумента.Ссылка.БанковскийСчет.СчетУчета КАК БанковскийСчетКассаСчетУчета,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Ссылка.ЭквайринговыйТерминал.СчетУчета КАК СчетУчетаЭквайринг,
	|	ТаблицаДокумента.Ссылка.ЭквайринговыйТерминал.СчетЗатрат КАК СчетЗатратЭквайринг,
	|	ТаблицаДокумента.Ссылка.ЭквайринговыйТерминал,
	|	ТаблицаДокумента.ТипСуммы,
	|	ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма,
	|	ТаблицаДокумента.Ссылка.Подотчетник КАК Сотрудник,
	|	ТаблицаДокумента.Ссылка.СчетКонтрагента КАК СчетКонтрагента,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчета
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|	КОНЕЦ КАК СчетУчетаПоТипуСуммы,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ""Оплата основного долга по кредиту""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ""Оплата процентов по кредиту""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ""Оплата комиссии по кредиту""
	|	КОНЕЦ КАК СодержаниеПоТипуСуммы,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)
	|			ТОГДА ТаблицаДокумента.Ссылка.ВидНалога
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|				ИЛИ ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ТОГДА ТаблицаДокумента.Ссылка.Корреспонденция
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|			ТОГДА ТаблицаДокумента.Ссылка.СчетКонтрагента
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			ТОГДА ТаблицаДокумента.Ссылка.Касса
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Контрагент
	|	КОНЕЦ КАК АналитикаДДС,
	|	ТаблицаДокумента.Ссылка.НаправлениеДеятельности
	|ПОМЕСТИТЬ ВременнаяТаблицаРасшифровкаПлатежа
	|ИЗ
	|	Документ.РасходСоСчета.РасшифровкаПлатежа КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Ссылка.ВалютаДенежныхСредств,
	|	ТаблицаДокумента.Документ,
	|	ТаблицаДокумента.Ссылка.ВидОперации,
	|	ТаблицаДокумента.Ссылка.Контрагент,
	|	ТаблицаДокумента.Договор,
	|	ТаблицаДокумента.Ссылка.БанковскийСчет,
	|	ТаблицаДокумента.Ссылка.СчетКонтрагента,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Заказ = НЕОПРЕДЕЛЕНО
	|				И ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Покупателю)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Поставщику)
	|				И ТаблицаДокумента.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|		ИНАЧЕ ТаблицаДокумента.Заказ
	|	КОНЕЦ,
	|	ТаблицаДокумента.ПризнакАванса,
	|	ТаблицаДокумента.Ссылка,
	|	ТаблицаДокумента.Договор.ВалютаРасчетов,
	|	ТаблицаДокумента.Ссылка.Корреспонденция,
	|	ТаблицаДокумента.Ссылка.Дата,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.РасходДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.РасходДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТИПЗНАЧЕНИЯ(ТаблицаДокумента.ДокументПланирования) = ТИП(Документ.ПеремещениеДСПлан)
	|				И ТаблицаДокумента.ДокументПланирования <> ЗНАЧЕНИЕ(Документ.ПеремещениеДСПлан.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.ДокументПланирования
	|		КОГДА ТаблицаДокумента.СчетНаОплату.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.СчетНаОплату
	|		КОГДА ТаблицаДокумента.Заказ.ЗапланироватьОплату
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетНаОплату,
	|	ТаблицаДокумента.Ссылка.БанковскийСчет.СчетУчета,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПокупателя,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Ссылка.Контрагент.СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДоговорам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиРасчетыПоЗаказам,
	|	ТаблицаДокумента.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам,
	|	ТаблицаДокумента.Ссылка.ЭквайринговыйТерминал,
	|	ТаблицаДокумента.Ссылка.ЭквайринговыйТерминал.СчетУчета,
	|	ТаблицаДокумента.Ссылка.ЭквайринговыйТерминал.СчетЗатрат,
	|	ТаблицаДокумента.ТипСуммы,
	|	ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма,
	|	ТаблицаДокумента.Ссылка.Подотчетник,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчета
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.Ссылка.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ""Оплата основного долга по кредиту""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ""Оплата процентов по кредиту""
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ""Оплата комиссии по кредиту""
	|	КОНЕЦ,
	|	ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Договор.ЭтоДоговорОбслуживания
	|				И ТаблицаДокумента.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)
	|			ТОГДА ТаблицаДокумента.Ссылка.ВидНалога
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|				ИЛИ ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ТОГДА ТаблицаДокумента.Ссылка.Корреспонденция
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ТОГДА ТаблицаДокумента.Ссылка.Контрагент
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ТОГДА ТаблицаДокумента.Ссылка.Подотчетник
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|			ТОГДА ТаблицаДокумента.Ссылка.СчетКонтрагента
	|		КОГДА ТаблицаДокумента.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			ТОГДА ТаблицаДокумента.Ссылка.Касса
	|		ИНАЧЕ ТаблицаДокумента.Ссылка.Контрагент
	|	КОНЕЦ,
	|	ТаблицаДокумента.Ссылка.НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА НЕ &ИспользоватьСтатьиДДС
	|				ИЛИ ТаблицаДокумента.СтатьяДДС = ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ПустаяСсылка)
	|			ТОГДА ТаблицаДокумента.Ссылка.Статья
	|		ИНАЧЕ ТаблицаДокумента.СтатьяДДС
	|	КОНЕЦ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.ВидОперации КАК ВидОперации,
	|	ТаблицаДокумента.Дата КАК Дата,
	|	&Организация КАК Организация,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	ВЫРАЗИТЬ(ТаблицаДокумента.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ТаблицаДокумента.СуммаДокумента КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА &ВалютаДоговораОбслуживания <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|					И &ВалютаДоговораОбслуживания = ТаблицаДокумента.ВалютаДенежныхСредств
	|				ТОГДА ТаблицаДокумента.СуммаДокумента
	|			КОГДА &ВалютаДоговораОбслуживания <> ЗНАЧЕНИЕ(Справочник.Валюты.ПустаяСсылка)
	|					И &ВалютаДоговораОбслуживания <> ТаблицаДокумента.ВалютаДенежныхСредств
	|				ТОГДА ТаблицаДокумента.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчетаСрезПоследних.Кратность * КурсыВалютДоговорОбслуживания.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КурсыВалютКассы.Кратность * КурсыВалютДоговорОбслуживания.Курс)
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаПоДоговоруОбслуживания,
	|	ТаблицаДокумента.Ссылка.БанковскийСчет.СчетУчета КАК БанковскийСчетКассаСчетУчета,
	|	ТаблицаДокумента.ВидНалога КАК ВидНалога,
	|	ТаблицаДокумента.ВидНалога.СчетУчета КАК ВидНалогаСчетУчета,
	|	ТаблицаДокумента.Подразделение КАК Подразделение,
	|	ТаблицаДокумента.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж КАК НаправлениеДеятельностиСчетУчетаВыручкиОтПродаж,
	|	ТаблицаДокумента.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж КАК НаправлениеДеятельностиСчетУчетаСебестоимостиПродаж,
	|	ТаблицаДокумента.Заказ КАК Заказ,
	|	ТаблицаДокумента.Корреспонденция КАК Корреспонденция,
	|	ТаблицаДокумента.Корреспонденция.ТипСчета КАК КорреспонденцияТипСчета,
	|	ТаблицаДокумента.Подотчетник КАК Подотчетник,
	|	ТаблицаДокумента.Подотчетник.СчетРасчетовСПерсоналом КАК ПодотчетникСчетРасчетовСперсоналом,
	|	ТаблицаДокумента.Подотчетник.СчетРасчетовСПодотчетниками КАК ПодотчетникСчетРасчетовСПодотчетниками,
	|	ТаблицаДокумента.Документ КАК Документ,
	|	ТаблицаДокумента.ДокументОснование КАК ДокументОснование,
	|	ТаблицаДокумента.АналитикаПрочихДоходовИРасходов КАК АналитикаДоходовИРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК УчетПрочихРасчетов,
	|	ТаблицаДокумента.БанковскийСчет,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику)
	|			ТОГДА ТаблицаДокумента.Подотчетник
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Налоги)
	|			ТОГДА ТаблицаДокумента.ВидНалога
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.Прочее)
	|				ИЛИ ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.НаРасходы)
	|			ТОГДА ТаблицаДокумента.Корреспонденция
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|			ТОГДА ТаблицаДокумента.Контрагент
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ТОГДА ТаблицаДокумента.Контрагент
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|			ТОГДА ТаблицаДокумента.Подотчетник
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет)
	|			ТОГДА ТаблицаДокумента.СчетКонтрагента
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных)
	|			ТОГДА ТаблицаДокумента.Касса
	|		ИНАЧЕ ТаблицаДокумента.Контрагент
	|	КОНЕЦ КАК АналитикаДДС,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.СчетКонтрагента
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.РасходСоСчета КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютДоговорОбслуживания
	|		ПО (КурсыВалютДоговорОбслуживания.Валюта = &ВалютаДоговораОбслуживания)
	|ГДЕ
	|	ТаблицаДокумента.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходСоСчета.Дата КАК Период,
	|	&ОрганизацияДляРегОтчетности КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА РасходСоСчета.ДокументОснование.Дата ЕСТЬ NULL
	|			ТОГДА &ПериодРегистрацииНДФЛ
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(РасходСоСчета.ДокументОснование.Дата, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодРегистрации,
	|	РасходСоСчета.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТРасчетыСНДФЛ
	|ИЗ
	|	ВременнаяТаблицаШапка КАК РасходСоСчета
	|ГДЕ
	|	РасходСоСчета.Сумма <> 0
	|	И РасходСоСчета.ВидНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалогов.НДФЛСотрудники)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходСоСчета.Дата КАК Период,
	|	&ОрганизацияДляРегОтчетности КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ИСТИНА КАК ЭтоСтраховыеВзносы,
	|	ВЫБОР
	|		КОГДА РасходСоСчета.ВидНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалогов.ПФРНакопительнаяСотрудники)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПФРНакопительная)
	|		КОГДА РасходСоСчета.ВидНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалогов.ПФРСтраховаяСотрудники)
	|			ТОГДА ВЫБОР
	|					КОГДА &ОбъединятьНакопительнуюИСтраховуюЧасть
	|						ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПФРПоСуммарномуТарифу)
	|					ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ПФРСтраховая)
	|				КОНЕЦ
	|		КОГДА РасходСоСчета.ВидНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалогов.ФОМССотрудники)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ФФОМС)
	|		КОГДА РасходСоСчета.ВидНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалогов.ФСССотрудники)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ФСС)
	|		КОГДА РасходСоСчета.ВидНалога = ЗНАЧЕНИЕ(Справочник.ВидыНалогов.ФССТравматизмСотрудники)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОбязательногоСтрахованияСотрудников.ФССНесчастныеСлучаи)
	|	КОНЕЦ КАК ВидОбязательногоСтрахованияСотрудников,
	|	РасходСоСчета.Сумма КАК Сумма
	|ПОМЕСТИТЬ ВТРасчетыСФондамиПоСтраховымВзносам
	|ИЗ
	|	ВременнаяТаблицаШапка КАК РасходСоСчета
	|ГДЕ
	|	РасходСоСчета.Сумма <> 0
	|	И РасходСоСчета.ВидНалога В(&СтраховыеВидыВзаиморасчетов)";
	
	Запрос.Выполнить();
	
	// Формирование таблиц движений по разделам учета.
	СформироватьТаблицаДенежныеСредства(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПодотчетниками(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПокупателями(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПерсоналом(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	// Прочие расчеты
	СформироватьТаблицаРасчетыСПрочимиКонтрагентами(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	// Конец Прочие расчеты
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыПоНалогам(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаПлатежныйКалендарь(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаОплатаСчетовИЗаказов(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	// Эквайринг
	СформироватьТаблицаРасчетыПоЭквайрингу(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетодЭквайринг(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыЭквайринг(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	// Конец Эквайринг
	СформироватьТаблицаУправленческий(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьНДФЛРасчетыНалоговыхАгентовСБюджетом(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	СформироватьРасчетыСФондамиПоСтраховымВзносам(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	// Биллинг
	СформироватьТаблицаВыполнениеДоговораОбслуживания(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаРасходСоСчета, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ Константы.КонтролироватьОстаткиПриПроведении.Получить() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы содержат записи, необходимо выполнить контроль
	// отрицательных остатков.
	Если СтруктураВременныеТаблицы.ДвиженияДенежныеСредстваИзменение
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПодотчетникамиИзменение
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыПоЭквайрингуИзменение // Эквайринг
	 ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПоставщикамиИзменение Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияДенежныеСредстваИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияДенежныеСредстваИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса КАК БанковскийСчетКассаПредставление,
		|	ДвиженияДенежныеСредстваИзменение.Валюта КАК ВалютаПредставление,
		|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств КАК ТипДенежныхСредствПредставление,
		|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств КАК ТипДенежныхСредств,
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалИзменение + ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаВалОстаток, 0) КАК ОстатокДенежныхСредств,
		|	ДвиженияДенежныеСредстваИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияДенежныеСредстваИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияДенежныеСредстваИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалИзменение КАК СуммаВалИзменение
		|ИЗ
		|	ДвиженияДенежныеСредстваИзменение КАК ДвиженияДенежныеСредстваИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ДенежныеСредства.Остатки(&МоментКонтроля, ) КАК ДенежныеСредстваОстатки
		|		ПО ДвиженияДенежныеСредстваИзменение.Организация = ДенежныеСредстваОстатки.Организация
		|			И ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств = ДенежныеСредстваОстатки.ТипДенежныхСредств
		|			И ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса = ДенежныеСредстваОстатки.БанковскийСчетКасса
		|			И ДвиженияДенежныеСредстваИзменение.Валюта = ДенежныеСредстваОстатки.Валюта
		|ГДЕ
		|	ЕСТЬNULL(ДенежныеСредстваОстатки.СуммаВалОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПодотчетникамиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Сотрудник КАК СотрудникПредставление,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Валюта КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.Документ КАК ДокументПредставление,
		|	ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) КАК ОстатокУПодотчетногоЛица,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПодотчетникамиИзменение.СуммаВалИзменение КАК СуммаВалИзменение
		|ИЗ
		|	ДвиженияРасчетыСПодотчетникамиИзменение КАК ДвиженияРасчетыСПодотчетникамиИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПодотчетниками.Остатки(&МоментКонтроля, ) КАК РасчетыСПодотчетникамиОстатки
		|		ПО ДвиженияРасчетыСПодотчетникамиИзменение.Организация = РасчетыСПодотчетникамиОстатки.Организация
		|			И ДвиженияРасчетыСПодотчетникамиИзменение.Сотрудник = РасчетыСПодотчетникамиОстатки.Сотрудник
		|			И ДвиженияРасчетыСПодотчетникамиИзменение.Валюта = РасчетыСПодотчетникамиОстатки.Валюта
		|			И ДвиженияРасчетыСПодотчетникамиИзменение.Документ = РасчетыСПодотчетникамиОстатки.Документ
		|ГДЕ
		|	(ТИПЗНАЧЕНИЯ(РасчетыСПодотчетникамиОстатки.Документ) = ТИП(Документ.АвансовыйОтчет)
		|				И ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) > 0
		|			ИЛИ ТИПЗНАЧЕНИЯ(РасчетыСПодотчетникамиОстатки.Документ) <> ТИП(Документ.АвансовыйОтчет)
		|				И ЕСТЬNULL(РасчетыСПодотчетникамиОстатки.СуммаВалОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПоставщикамиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Договор.ВалютаРасчетов КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Документ КАК ДокументПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетовПредставление,
		|	ИСТИНА КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаОстаткаЗадолженности,
		|	-(ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0)) КАК СуммаНепогашенныхАвансов,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПоставщикамиИзменение КАК ДвиженияРасчетыСПоставщикамиИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Остатки(&МоментКонтроля, ) КАК РасчетыСПоставщикамиОстатки
		|		ПО ДвиженияРасчетыСПоставщикамиИзменение.Организация = РасчетыСПоставщикамиОстатки.Организация
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Контрагент = РасчетыСПоставщикамиОстатки.Контрагент
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Договор = РасчетыСПоставщикамиОстатки.Договор
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Документ = РасчетыСПоставщикамиОстатки.Документ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Заказ = РасчетыСПоставщикамиОстатки.Заказ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = РасчетыСПоставщикамиОстатки.ТипРасчетов
		|ГДЕ
		|	ВЫБОР
		|			КОГДА ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|				ТОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) > 0
		|			ИНАЧЕ ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) < 0
		|		КОНЕЦ
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыПоЭквайрингуИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидОперацииЭквайринга КАК ТипДенежныхСредствПредставление,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ЭквайринговыйТерминал.БанковскийСчетЭквайринг.ВалютаДенежныхСредств КАК ВалютаПредставление,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ЭквайринговыйТерминал КАК ЭквайринговыйТерминалПредставление,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ДатаПлатежа КАК ДатаПлатежаПредставление,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.Документ КАК ДокументПредставление,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.Заказ КАК ЗаказПредставление,
		|	ИСТИНА КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияПередЗаписью КАК КомиссияПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияПриЗаписи КАК КомиссияПриЗаписи,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияИзменение КАК КомиссияИзменение,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалПередЗаписью КАК КомиссияВалПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалПриЗаписи КАК КомиссияВалПриЗаписи,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалИзменение КАК КомиссияВалИзменение,
		|	ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.СуммаВалОстаток, 0) + ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалПриЗаписи КАК СуммаОстаткаЗадолженности,
		|	ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.КомиссияВалОстаток, 0) + ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалПриЗаписи КАК СуммаОстаткаКомиссии,
		|	ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.КомиссияОстаток, 0) КАК КомиссияОстаток,
		|	ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.КомиссияВалОстаток, 0) КАК КомиссияВалОстаток,
		|	РасчетыПоЭквайрингуОстатки.ВидПлатежнойКарты КАК ВидПлатежнойКартыПредставление,
		|	РасчетыПоЭквайрингуОстатки.НомерПлатежнойКарты КАК НомерПлатежнойКартыПредставление
		|ИЗ
		|	ДвиженияРасчетыПоЭквайрингуИзменение КАК ДвиженияРасчетыПоЭквайрингуИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыПоЭквайрингу.Остатки(&МоментКонтроля, ) КАК РасчетыПоЭквайрингуОстатки
		|		ПО ДвиженияРасчетыПоЭквайрингуИзменение.ВидОперацииЭквайринга = РасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.Организация = РасчетыПоЭквайрингуОстатки.Организация
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.ЭквайринговыйТерминал = РасчетыПоЭквайрингуОстатки.ЭквайринговыйТерминал
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.ДатаПлатежа = РасчетыПоЭквайрингуОстатки.ДатаПлатежа
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.Документ = РасчетыПоЭквайрингуОстатки.Документ
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.Заказ = РасчетыПоЭквайрингуОстатки.Заказ
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.ВидПлатежнойКарты = РасчетыПоЭквайрингуОстатки.ВидПлатежнойКарты
		|			И ДвиженияРасчетыПоЭквайрингуИзменение.НомерПлатежнойКарты = РасчетыПоЭквайрингуОстатки.НомерПлатежнойКарты
		|ГДЕ
		|	(&УдалениеПроведения
		|				И (ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.СуммаВалОстаток, 0) < 0
		|					ИЛИ ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.КомиссияВалОстаток, 0) < 0)
		|			ИЛИ НЕ &УдалениеПроведения
		|				И (ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.СуммаВалОстаток, 0) <> 0
		|					ИЛИ ЕСТЬNULL(РасчетыПоЭквайрингуОстатки.КомиссияВалОстаток, 0) <> 0))
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		Запрос.УстановитьПараметр("УдалениеПроведения", УдалениеПроведения);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			ИЛИ НЕ МассивРезультатов[1].Пустой()
			ИЛИ НЕ МассивРезультатов[2].Пустой()
			ИЛИ НЕ МассивРезультатов[3].Пустой() Тогда
			ДокументОбъектРасходСоСчета = ДокументСсылкаРасходСоСчета.ПолучитьОбъект()
		КонецЕсли;
		
		// Отрицательный остаток по денежным средствам.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструДенежныеСредства(ДокументОбъектРасходСоСчета, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с подотчетными лицами.
		Если НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыСПодотчетниками(ДокументОбъектРасходСоСчета, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с поставщиками.
		Если НЕ МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыСПоставщиками(ДокументОбъектРасходСоСчета, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Эквайринг
		// Отрицательный остаток расчетов по эквайрингу.
		Если НЕ МассивРезультатов[3].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыПоЭквайрингу(ДокументОбъектРасходСоСчета, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		// Конец Эквайринг
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#Область Эквайринг

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыПоЭквайрингу(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Если ДокументСсылкаРасходСоСчета.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоЭквайрингу", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ОплатаПлатежнойКартой", "Оплата от банка-эквайрера");
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ВозвратОплатыНаПлатежныеКарты", Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты);
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям", ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям"));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаРасходСоСчета.ВалютаДенежныхСредств);
	
	// Блокировка
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходСоСчета.ЭквайринговыйТерминал,
	|	&Организация
	|ИЗ
	|	Документ.РасходСоСчета КАК РасходСоСчета
	|ГДЕ
	|	РасходСоСчета.Ссылка = &Ссылка
	|	И РасходСоСчета.ВидОперации = &ВозвратОплатыНаПлатежныеКарты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МИНИМУМ(РасходСоСчетаЭквайринговыеОперации.ДатаПлатежа) КАК ДатаНачала,
	|	МАКСИМУМ(РасходСоСчетаЭквайринговыеОперации.ДатаПлатежа) КАК ДатаОкончания
	|ИЗ
	|	Документ.РасходСоСчета.ЭквайринговыеОперации КАК РасходСоСчетаЭквайринговыеОперации
	|ГДЕ
	|	РасходСоСчетаЭквайринговыеОперации.Ссылка = &Ссылка";

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоЭквайрингу");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = МассивРезультатов[0];
	
	Для каждого КолонкаРезультатЗапроса из МассивРезультатов[0].Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	
	Если ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") Тогда
		Выборка = МассивРезультатов[1].Выбрать();
		Выборка.Следующий();
		Если Выборка.ДатаНачала <> Null И Выборка.ДатаНачала <> '00010101' Тогда
			ДиапазонДатПлатежа = Новый Диапазон(Выборка.ДатаНачала, Выборка.ДатаОкончания);
			ЭлементБлокировки.УстановитьЗначение("ДатаПлатежа", ДиапазонДатПлатежа);
		КонецЕсли;
	КонецЕсли;
	
	Блокировка.Заблокировать();
	// Конец Блокировка
	
	// Валюта расчетов и валюта платежа при эквайринговых операциях совпадает.
	// В документе не отображается информация о сумме платежа для вида операции "Продажи по платежным картам".
	// В этом случае достаточно для всех регистров использовать сумму расчетов.
	
	Если ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал,
		|	ПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал.СчетУчета КАК СчетУчетаЭквайринг,
		|	ПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал.СчетЗатрат КАК СчетЗатратЭквайринг,
		|	ПараметрыУчетаЭквайринговыхОпераций.НаправлениеДеятельностиЗатраты КАК НаправлениеДеятельностиЗатраты,
		|	ПараметрыУчетаЭквайринговыхОпераций.ПодразделениеЗатраты КАК ПодразделениеЗатраты,
		|	ПараметрыУчетаЭквайринговыхОпераций.НаправлениеДеятельностиЗатраты КАК НаправлениеДеятельностиЗатраты1
		|ПОМЕСТИТЬ ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций
		|ИЗ
		|	Документ.РасходСоСчета КАК ПараметрыУчетаЭквайринговыхОпераций
		|ГДЕ
		|	ПараметрыУчетаЭквайринговыхОпераций.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	РасходСоСчетаЭквайринговыеОперации.НомерСтроки,
		|	РасходСоСчетаЭквайринговыеОперации.СуммаРасчетов + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовВозврата КАК СуммаВал,
		|	РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссии + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссииВозврата КАК КомиссияВал,
		|	ВЫРАЗИТЬ((РасходСоСчетаЭквайринговыеОперации.СуммаРасчетов + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовВозврата) * КурсыВалютБанковскогоСчета.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютБанковскогоСчета.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
		|	ВЫРАЗИТЬ((РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссии + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссииВозврата) * КурсыВалютБанковскогоСчета.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютБанковскогоСчета.Кратность) КАК ЧИСЛО(15, 2)) КАК Комиссия,
		|	ВЫБОР
		|		КОГДА &РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям
		|			ТОГДА РасходСоСчетаЭквайринговыеОперации.Документ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Документ,
		|	ВЫБОР
		|		КОГДА &РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям
		|			ТОГДА РасходСоСчетаЭквайринговыеОперации.Заказ
		|		ИНАЧЕ НЕОПРЕДЕЛЕНО
		|	КОНЕЦ КАК Заказ,
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка.ЭквайринговыйТерминал,
		|	ВЫБОР
		|		КОГДА &РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям
		|			ТОГДА РасходСоСчетаЭквайринговыеОперации.ДатаПлатежа
		|		ИНАЧЕ ДАТАВРЕМЯ(1, 1, 1)
		|	КОНЕЦ КАК ДатаПлатежа,
		|	РасходСоСчетаЭквайринговыеОперации.НомерПлатежнойКарты,
		|	РасходСоСчетаЭквайринговыеОперации.ВидПлатежнойКарты,
		|	&Организация,
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка.Контрагент КАК Эквайрер,
		|	ВЫБОР
		|		КОГДА РасходСоСчетаЭквайринговыеОперации.Документ ССЫЛКА Документ.ОперацияПоПлатежнымКартам
		|			ТОГДА РасходСоСчетаЭквайринговыеОперации.Документ.ВидОперации
		|		КОГДА РасходСоСчетаЭквайринговыеОперации.СуммаРасчетов > 0
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя)
		|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю)
		|	КОНЕЦ КАК ВидОперацииЭквайринга,
		|	&ОплатаПлатежнойКартой КАК СодержаниеПроводки,
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка.Дата КАК Период,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетУчетаЭквайринг,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетЗатратЭквайринг,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.НаправлениеДеятельностиЗатраты КАК НаправлениеДеятельности,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетУчетаЭквайринг.ТипСчета КАК СчетУчетаЭквайрингТипСчета,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетЗатратЭквайринг.ТипСчета КАК СчетЗатратЭквайрингТипСчета,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.ПодразделениеЗатраты КАК Подразделение
		|ИЗ
		|	Документ.РасходСоСчета.ЭквайринговыеОперации КАК РасходСоСчетаЭквайринговыеОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчета
		|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютБанковскогоСчета
		|		ПО (КурсыВалютБанковскогоСчета.Валюта = &ВалютаДС)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций КАК ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций
		|		ПО РасходСоСчетаЭквайринговыеОперации.Ссылка.ЭквайринговыйТерминал = ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал
		|ГДЕ
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка = &Ссылка";
	Иначе
		Запрос.Текст =
		"ВЫБРАТЬ
		|	ПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал,
		|	ПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал.СчетУчета КАК СчетУчетаЭквайринг,
		|	ПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал.СчетЗатрат КАК СчетЗатратЭквайринг,
		|	ПараметрыУчетаЭквайринговыхОпераций.НаправлениеДеятельностиЗатраты КАК НаправлениеДеятельностиЗатраты,
		|	ПараметрыУчетаЭквайринговыхОпераций.ПодразделениеЗатраты КАК ПодразделениеЗатраты,
		|	ПараметрыУчетаЭквайринговыхОпераций.НаправлениеДеятельностиЗатраты КАК НаправлениеДеятельностиЗатраты1
		|ПОМЕСТИТЬ ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций
		|ИЗ
		|	Документ.РасходСоСчета КАК ПараметрыУчетаЭквайринговыхОпераций
		|ГДЕ
		|	ПараметрыУчетаЭквайринговыхОпераций.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
		|	РасходСоСчетаЭквайринговыеОперации.НомерСтроки,
		|	-(РасходСоСчетаЭквайринговыеОперации.СуммаРасчетов + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовВозврата) КАК СуммаВал,
		|	РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссии + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссииВозврата КАК КомиссияВал,
		|	-(ВЫРАЗИТЬ((РасходСоСчетаЭквайринговыеОперации.СуммаРасчетов + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовВозврата) * КурсыВалютБанковскогоСчета.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютБанковскогоСчета.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
		|	ВЫРАЗИТЬ((РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссии + РасходСоСчетаЭквайринговыеОперации.СуммаРасчетовКомиссииВозврата) * КурсыВалютБанковскогоСчета.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютБанковскогоСчета.Кратность) КАК ЧИСЛО(15, 2)) КАК Комиссия,
		|	ЗНАЧЕНИЕ(Документ.ОперацияПоПлатежнымКартам.ПустаяСсылка) КАК Документ,
		|	НЕОПРЕДЕЛЕНО КАК Заказ,
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка.ЭквайринговыйТерминал,
		|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаПлатежа,
		|	"""" КАК НомерПлатежнойКарты,
		|	"""" КАК ВидПлатежнойКарты,
		|	&Организация,
		|	НЕОПРЕДЕЛЕНО КАК Эквайрер,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ПустаяСсылка) КАК ВидОперацииЭквайринга,
		|	&ОплатаПлатежнойКартой КАК СодержаниеПроводки,
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка.Дата КАК Период,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетУчетаЭквайринг,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетЗатратЭквайринг,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.НаправлениеДеятельностиЗатраты КАК НаправлениеДеятельности,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетУчетаЭквайринг.ТипСчета КАК СчетУчетаЭквайрингТипСчета,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.СчетЗатратЭквайринг.ТипСчета КАК СчетЗатратЭквайрингТипСчета,
		|	ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.ПодразделениеЗатраты КАК Подразделение
		|ИЗ
		|	Документ.РасходСоСчета.ЭквайринговыеОперации КАК РасходСоСчетаЭквайринговыеОперации
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчета
		|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютБанковскогоСчета
		|		ПО (КурсыВалютБанковскогоСчета.Валюта = &ВалютаДС)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций КАК ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций
		|		ПО РасходСоСчетаЭквайринговыеОперации.Ссылка.ЭквайринговыйТерминал = ВременнаяТаблицаПараметрыУчетаЭквайринговыхОпераций.ЭквайринговыйТерминал
		|ГДЕ
		|	РасходСоСчетаЭквайринговыеОперации.Ссылка = &Ссылка";
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоЭквайрингу", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаПлатежныйКалендарь()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетодЭквайринг(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Если Не СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов Или
		ДокументСсылкаРасходСоСчета.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетодЭквайринг", Новый ТаблицаЗначений);
		Возврат;
	КонецЕсли;
	
	ТаблицаРасчетыПоЭквайрингу = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасчетыПоЭквайрингу;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	Запрос.УстановитьПараметр("ТаблицаРасчетыПоЭквайрингу", ТаблицаРасчетыПоЭквайрингу);
	Запрос.УстановитьПараметр("МоментКонтроля", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля, ВидГраницы.Включая));

	// Установка исключительной блокировки контролируемых остатков запасов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыПоЭквайрингу.Организация,
	|	РасчетыПоЭквайрингу.Документ
	|ПОМЕСТИТЬ ВременнаяТаблицаИсточникДанныхДляБлокировки
	|ИЗ
	|	&ТаблицаРасчетыПоЭквайрингу КАК РасчетыПоЭквайрингу
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаИсточникДанныхДляБлокировки.Организация,
	|	ВременнаяТаблицаИсточникДанныхДляБлокировки.Документ
	|ИЗ
	|	ВременнаяТаблицаИсточникДанныхДляБлокировки КАК ВременнаяТаблицаИсточникДанныхДляБлокировки
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаИсточникДанныхДляБлокировки.Документ,
	|	ВременнаяТаблицаИсточникДанныхДляБлокировки.Организация";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДоходыИРасходыКассовыйМетодЭквайринг");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков.
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицаРасчетыПоЭквайрингу.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
		|	ТаблицаРасчетыПоЭквайрингу.Документ КАК Документ,
		|	ТаблицаРасчетыПоЭквайрингу.ДатаПлатежа КАК ДатаПлатежа,
		|	ТаблицаРасчетыПоЭквайрингу.Сумма КАК Сумма,
		|	ТаблицаРасчетыПоЭквайрингу.СуммаВал КАК СуммаВал,
		|	ТаблицаРасчетыПоЭквайрингу.Комиссия КАК Комиссия,
		|	ТаблицаРасчетыПоЭквайрингу.КомиссияВал КАК КомиссияВал,
		|	ТаблицаРасчетыПоЭквайрингу.ВидОперацииЭквайринга КАК ВидОперацииЭквайринга,
		|	ТаблицаРасчетыПоЭквайрингу.НаправлениеДеятельности КАК НаправлениеДеятельности
		|ПОМЕСТИТЬ ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам
		|ИЗ
		|	&ТаблицаРасчетыПоЭквайрингу КАК ТаблицаРасчетыПоЭквайрингу
		|
		|ИНДЕКСИРОВАТЬ ПО
		|	ЭквайринговыйТерминал,
		|	Документ,
		|	ДатаПлатежа
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.Документ,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.Организация,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.НаправлениеДеятельности,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.Статья,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаДоходовОстаток,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаРасходовОстаток,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаКомиссииДоходовОстаток,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаКомиссииРасходовОстаток,
		|	ДоходыИРасходыКассовыйМетодЭквайрингОстатки.ВидОперацииЭквайринга
		|ПОМЕСТИТЬ ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки
		|ИЗ
		|	РегистрНакопления.ДоходыИРасходыКассовыйМетодЭквайринг.Остатки(
		|			&МоментКонтроля,
		|			Документ В
		|				(ВЫБРАТЬ
		|					ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.Документ
		|				ИЗ
		|					ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам КАК ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам)) КАК ДоходыИРасходыКассовыйМетодЭквайрингОстатки
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ДоходыИРасходыКассовыйМетодЭквайринг.Документ,
		|	ДоходыИРасходыКассовыйМетодЭквайринг.Организация,
		|	ДоходыИРасходыКассовыйМетодЭквайринг.НаправлениеДеятельности,
		|	ДоходыИРасходыКассовыйМетодЭквайринг.Статья,
		|	ВЫБОР
		|		КОГДА ДоходыИРасходыКассовыйМетодЭквайринг.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаДоходов, 0)
		|		ИНАЧЕ -ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаДоходов, 0)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДоходыИРасходыКассовыйМетодЭквайринг.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаРасходов, 0)
		|		ИНАЧЕ -ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаРасходов, 0)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДоходыИРасходыКассовыйМетодЭквайринг.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаКомиссииДоходов, 0)
		|		ИНАЧЕ -ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаКомиссииДоходов, 0)
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ДоходыИРасходыКассовыйМетодЭквайринг.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
		|			ТОГДА ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаКомиссииРасходов, 0)
		|		ИНАЧЕ -ЕСТЬNULL(ДоходыИРасходыКассовыйМетодЭквайринг.СуммаКомиссииРасходов, 0)
		|	КОНЕЦ,
		|	ДоходыИРасходыКассовыйМетодЭквайринг.ВидОперацииЭквайринга
		|ИЗ
		|	РегистрНакопления.ДоходыИРасходыКассовыйМетодЭквайринг КАК ДоходыИРасходыКассовыйМетодЭквайринг
		|ГДЕ
		|	ДоходыИРасходыКассовыйМетодЭквайринг.Регистратор = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.Документ,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.Организация,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.НаправлениеДеятельности,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.Статья,
		|	-СУММА(ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаДоходовОстаток) КАК СуммаДоходовОстаток,
		|	СУММА(ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаРасходовОстаток) КАК СуммаРасходовОстаток,
		|	СУММА(ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаКомиссииДоходовОстаток) КАК СуммаКомиссииДоходовОстаток,
		|	СУММА(ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.СуммаКомиссииРасходовОстаток) КАК СуммаКомиссииРасходовОстаток,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.ВидОперацииЭквайринга
		|ИЗ
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки КАК ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.Документ,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.Организация,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.НаправлениеДеятельности,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.Статья,
		|	ВременнаяТаблицаДоходыИРасходыКассовыйМетодЭквайрингОстатки.ВидОперацииЭквайринга
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.Документ КАК Документ,
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.ДатаПлатежа,
		|	СУММА(ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.Сумма) КАК Сумма,
		|	СУММА(ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.Комиссия) КАК Комиссия,
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.НаправлениеДеятельности КАК НаправлениеДеятельностиЗатраты,
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.ВидОперацииЭквайринга,
		|	ВЫБОР
		|		КОГДА ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.ВидОперацииЭквайринга = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК ЭтоВозврат
		|ИЗ
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам КАК ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.Документ,
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.ДатаПлатежа,
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.ВидОперацииЭквайринга,
		|	ВременнаяТаблицаЗачтенаОплатаПоПлатежнымКартам.НаправлениеДеятельности";
	
	МРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаДоходыИРасходыКассовыйМетодЭквайринг = РегистрыНакопления.ДоходыИРасходыКассовыйМетодЭквайринг.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	ТаблицаДоходыИРасходыКассовыйМетод = РегистрыНакопления.ДоходыИРасходыКассовыйМетод.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	ТаблицаДоходыИРасходыКассовыйМетодЭквайринг.Колонки.Удалить("Активность");
	ТаблицаДоходыИРасходыКассовыйМетод.Колонки.Удалить("Активность");
	
	ТаблицаДоходыИРасходыЭквайринг = МРезультатов[2].Выгрузить();
	ВыборкаИтоги = МРезультатов[3].Выбрать();
	
	// По данным о зачтенных суммах перенесем данные из регистра ДоходыИРасходыКассовыйМетодЭквайринг в регистр ТаблицаДоходыИРасходыЭквайринг.
	Пока ВыборкаИтоги.Следующий() Цикл
		
		ПараметрыОтбора = Новый Структура("Документ, ВидОперацииЭквайринга", ВыборкаИтоги.Документ, ВыборкаИтоги.ВидОперацииЭквайринга);
		ТаблицаРезультатПоиска = ТаблицаДоходыИРасходыЭквайринг.НайтиСтроки(ПараметрыОтбора);
		МожноЗачесть = ВыборкаИтоги.Сумма;
		МожноЗачестьКомиссию = ВыборкаИтоги.Комиссия;
		
		Если ЗначениеЗаполнено(ТаблицаРезультатПоиска) Тогда
			Для Каждого ТекущаяСтрока Из ТаблицаРезультатПоиска Цикл
				
				// Сумма оплаты (вместе с комиссией).
				Если (МожноЗачесть > 0 И ТекущаяСтрока.СуммаДоходовОстаток > 0) Тогда
					
					НоваяСтрокаЭквайринг = ТаблицаДоходыИРасходыКассовыйМетодЭквайринг.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаЭквайринг, ТекущаяСтрока);
					НоваяСтрокаЭквайринг.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяСтрокаЭквайринг.Период = СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата;
					
					Если МожноЗачесть >= ТекущаяСтрока.СуммаДоходовОстаток Тогда
						НоваяСтрокаЭквайринг.СуммаДоходов = -ТекущаяСтрока.СуммаДоходовОстаток;
						МожноЗачесть = МожноЗачесть - ТекущаяСтрока.СуммаДоходовОстаток;
					Иначе
						НоваяСтрокаЭквайринг.СуммаДоходов = -МожноЗачесть;
						МожноЗачесть = 0;
					КонецЕсли;
					
					НоваяСтрока = ТаблицаДоходыИРасходыКассовыйМетод.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, НоваяСтрокаЭквайринг);
					Если ВыборкаИтоги.ЭтоВозврат Тогда
						НоваяСтрока.СуммаДоходов = НоваяСтрока.СуммаДоходов;
					КонецЕсли;
					
				КонецЕсли;
				
				// Сумма комиссии.
				Если МожноЗачестьКомиссию > 0 И ТекущаяСтрока.СуммаКомиссииДоходовОстаток > 0 Тогда
					НоваяСтрокаЭквайринг = ТаблицаДоходыИРасходыКассовыйМетодЭквайринг.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаЭквайринг, ТекущаяСтрока);
					НоваяСтрокаЭквайринг.ВидДвижения = ВидДвиженияНакопления.Расход;
					НоваяСтрокаЭквайринг.Период = СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата;
					
					Если МожноЗачестьКомиссию > ТекущаяСтрока.СуммаКомиссииДоходовОстаток Тогда
						НоваяСтрокаЭквайринг.СуммаКомиссииДоходов = ТекущаяСтрока.СуммаКомиссииДоходовОстаток;
						МожноЗачестьКомиссию = МожноЗачестьКомиссию - ТекущаяСтрока.СуммаКомиссииДоходовОстаток;
					Иначе
						НоваяСтрокаЭквайринг.СуммаКомиссииДоходов = МожноЗачестьКомиссию;
						МожноЗачестьКомиссию = 0;
					КонецЕсли;
				
					НоваяСтрока = ТаблицаДоходыИРасходыКассовыйМетод.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрока, НоваяСтрокаЭквайринг);
					НоваяСтрока.СуммаДоходов = 0;
					НоваяСтрока.СуммаРасходов = НоваяСтрокаЭквайринг.СуммаКомиссииДоходов;
					НоваяСтрока.НаправлениеДеятельности = ДокументСсылкаРасходСоСчета.НаправлениеДеятельностиЗатраты;
				КонецЕсли;
				
				Если МожноЗачесть = 0 и МожноЗачестьКомиссию = 0 Тогда
					Прервать;
				КонецЕсли;
				
			КонецЦикла;
		КонецЕсли;
		
		// Ситуация может возникнуть, когда поступление на счет оформляется РАНЬШЕ чем операция оплаты, которая указан в ТЧ "ЭквайринговыеОперации".
		// Данная ситуация обрабатываевтся перед записью текущего документа на сервере.
		// Возможны и другие ситуации.
		Если МожноЗачестьКомиссию > 0 Или МожноЗачесть <> 0 Тогда
			
			Продолжить;
			
			Если МожноЗачесть > 0 Тогда
				
				НоваяСтрокаЭквайринг = ТаблицаДоходыИРасходыКассовыйМетодЭквайринг.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаЭквайринг, ВыборкаИтоги);
				НоваяСтрокаЭквайринг.Организация = СтруктураДополнительныеСвойства.ДляПроведения.Организация;
				НоваяСтрокаЭквайринг.ВидДвижения = ВидДвиженияНакопления.Расход;
				НоваяСтрокаЭквайринг.Период = СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата;
				
				НоваяСтрокаЭквайринг.СуммаДоходов = -МожноЗачесть;
				НоваяСтрокаЭквайринг.Комментарий = "Сумма не распределилась по остаткам";
				
				НоваяСтрока = ТаблицаДоходыИРасходыКассовыйМетод.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НоваяСтрокаЭквайринг);
				Если ВыборкаИтоги.ЭтоВозврат Тогда
					НоваяСтрока.СуммаДоходов = НоваяСтрока.СуммаДоходов;
				КонецЕсли;
				
			КонецЕсли;
					
			Если МожноЗачестьКомиссию > 0 Тогда
				
				НоваяСтрокаЭквайринг = ТаблицаДоходыИРасходыКассовыйМетодЭквайринг.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаЭквайринг, ВыборкаИтоги);
				НоваяСтрокаЭквайринг.Организация = СтруктураДополнительныеСвойства.ДляПроведения.Организация;
				НоваяСтрокаЭквайринг.ВидДвижения = ВидДвиженияНакопления.Расход;
				НоваяСтрокаЭквайринг.Период = СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата;
				
				НоваяСтрокаЭквайринг.СуммаКомиссииДоходов = МожноЗачестьКомиссию;
				НоваяСтрокаЭквайринг.Комментарий = "Сумма не распределилась по остаткам";
				
				НоваяСтрока = ТаблицаДоходыИРасходыКассовыйМетод.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, НоваяСтрокаЭквайринг);
				НоваяСтрока.СуммаДоходов = 0;
				НоваяСтрока.СуммаРасходов = НоваяСтрокаЭквайринг.СуммаКомиссииДоходов;
				НоваяСтрока.НаправлениеДеятельности = ВыборкаИтоги.НаправлениеДеятельностиЗатраты;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетодЭквайринг", ТаблицаДоходыИРасходыКассовыйМетодЭквайринг);
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", ТаблицаДоходыИРасходыКассовыйМетод);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыЭквайринг(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	ТаблицаРасчетыПоЭквайрингу = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРасчетыПоЭквайрингу;
	ТаблицаДоходыИРасходы = РегистрыНакопления.ДоходыИРасходы.СоздатьНаборЗаписей().ВыгрузитьКолонки();
	
	Для каждого ТекущаяСтрокаРасчетов Из ТаблицаРасчетыПоЭквайрингу Цикл
	
		Если ТекущаяСтрокаРасчетов.Комиссия = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Движение = ТаблицаДоходыИРасходы.Добавить();
		Движение.Период = СтруктураДополнительныеСвойства.ДляПроведения.Дата;
		Движение.Организация = СтруктураДополнительныеСвойства.ДляПроведения.Организация;
		Движение.СчетУчета = ТекущаяСтрокаРасчетов.СчетЗатратЭквайринг;
		Движение.Аналитика = ТекущаяСтрокаРасчетов.Эквайрер;
		Если ТекущаяСтрокаРасчетов.СчетЗатратЭквайрингТипСчета = ПредопределенноеЗначение("Перечисление.ТипыСчетов.Расходы") Тогда
			Движение.НаправлениеДеятельности = ТекущаяСтрокаРасчетов.НаправлениеДеятельности;
			Движение.ЗаказПокупателя = ТекущаяСтрокаРасчетов.Заказ;
			Движение.СтруктурнаяЕдиница = ТекущаяСтрокаРасчетов.Подразделение;
		Иначе
			Движение.НаправлениеДеятельности = ПредопределенноеЗначение("Справочник.НаправленияДеятельности.Прочее");
			Движение.ЗаказПокупателя = Неопределено;
			Движение.СтруктурнаяЕдиница = Неопределено;
		КонецЕсли;
		Движение.СуммаРасходов = ТекущаяСтрокаРасчетов.Комиссия;
		Движение.СодержаниеПроводки = НСтр("ru = 'Комиссия банка-эквайера'");
		Движение.Активность = Истина;
		
	КонецЦикла;
	
	Если ТаблицаДоходыИРасходы.Количество() > 0 Тогда
		Если СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Количество() = 0 Тогда
			СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", ТаблицаДоходыИРасходы);
		Иначе
			
			Для Каждого ТекущаяСтрока Из ТаблицаДоходыИРасходы Цикл
				НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрока);
			КонецЦикла;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

#КонецОбласти

#Область ПрочиеРасчеты

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПрочимиКонтрагентами(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("УчетРасчетовПоПрочимОперациям", НСтр("ru='Учет расчетов по прочим операциям'"));
	Запрос.УстановитьПараметр("Комментарий", НСтр("ru='Увеличение долга контрагента'"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки КАК НомерСтроки,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = НЕОПРЕДЕЛЕНО
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов КАК Валюта,
	|	ВременнаяТаблицаШапка.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Дата,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаПлатежа) КАК СуммаПлатежа,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК Сумма,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВал,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаУчета) КАК СуммаДляОстатка,
	|	СУММА(ВременнаяТаблицаРасшифровкаПлатежа.СуммаРасчетов) КАК СуммаВалДляОстатка,
	|	&УчетРасчетовПоПрочимОперациям КАК СодержаниеПроводки,
	|	&Комментарий КАК Комментарий,
	|	ВременнаяТаблицаШапка.Корреспонденция КАК СчетУчета,
	|	ВременнаяТаблицаШапка.БанковскийСчет КАК БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата КАК Период
	|ПОМЕСТИТЬ ВременнаяТаблицаПрочиеРасчеты
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ВременнаяТаблицаРасшифровкаПлатежа
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|		ПО (ВременнаяТаблицаШапка.УчетПрочихРасчетов)
	|ГДЕ
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасшифровкаПлатежа.НомерСтроки,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Контрагент,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Договор,
	|	ВременнаяТаблицаРасшифровкаПлатежа.ВалютаРасчетов,
	|	ВременнаяТаблицаШапка.ВалютаДенежныхСредств,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|							ИЛИ ВременнаяТаблицаРасшифровкаПлатежа.Документ = НЕОПРЕДЕЛЕНО
	|						ТОГДА &Ссылка
	|					ИНАЧЕ ВременнаяТаблицаРасшифровкаПлатежа.Документ
	|				КОНЕЦ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасшифровкаПлатежа.ВестиРасчетыПоЗаказам
	|			ТОГДА ВременнаяТаблицаРасшифровкаПлатежа.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ,
	|	ВременнаяТаблицаШапка.Корреспонденция,
	|	ВременнаяТаблицаШапка.БанковскийСчет,
	|	ВременнаяТаблицаРасшифровкаПлатежа.Дата";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаПрочиеРасчеты.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаПрочиеРасчеты.Организация КАК Организация,
	|	ВременнаяТаблицаПрочиеРасчеты.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаПрочиеРасчеты.Договор КАК Договор
	|ИЗ
	|	ВременнаяТаблицаПрочиеРасчеты КАК ВременнаяТаблицаПрочиеРасчеты";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПрочимиКонтрагентами");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыПоПрочимОперациям(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПрочимиКонтрагентами", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПрочимиКонтрагентами()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаРасходСоСчета, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("УчетРасчетовПоЗаймамСотрудникам", НСтр("ru='Выдача займа сотруднику'"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходСоСчета);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", ДокументСсылкаРасходСоСчета.ВалютаДенежныхСредств);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА РасходСоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.КредитПолученный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику)
	|	КОНЕЦ КАК ВидДоговора,
	|	РасходСоСчета.Дата КАК Дата,
	|	РасходСоСчета.Дата КАК Период,
	|	&УчетРасчетовПоЗаймамСотрудникам КАК СодержаниеПроводки,
	|	РасходСоСчета.Подотчетник КАК Контрагент,
	|	РасходСоСчета.СуммаДокумента КАК СуммаПлатежа,
	|	ВЫРАЗИТЬ(РасходСоСчета.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютДоговора.Кратность / (КурсыВалютДоговора.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолгВал,
	|	ВЫРАЗИТЬ(РасходСоСчета.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолг,
	|	ВЫРАЗИТЬ(РасходСоСчета.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютДоговора.Кратность / (КурсыВалютДоговора.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолгВалДляОстатка,
	|	ВЫРАЗИТЬ(РасходСоСчета.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК ОсновнойДолгДляОстатка,
	|	0 КАК ПроцентыВал,
	|	0 КАК Проценты,
	|	0 КАК ПроцентыВалДляОстатка,
	|	0 КАК ПроцентыДляОстатка,
	|	0 КАК КомиссияВал,
	|	0 КАК Комиссия,
	|	0 КАК КомиссияВалДляОстатка,
	|	0 КАК КомиссияДляОстатка,
	|	ВЫРАЗИТЬ(РасходСоСчета.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютДоговора.Кратность / (КурсыВалютДоговора.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(РасходСоСчета.СуммаДокумента * КурсыВалютКассы.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютКассы.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	РасходСоСчета.ДоговорКредитаЗайма КАК ДоговорКредитаЗайма,
	|	РасходСоСчета.ДоговорКредитаЗайма.ВалютаРасчетов КАК Валюта,
	|	РасходСоСчета.ВалютаДенежныхСредств КАК ВалютаДенежныхСредств,
	|	РасходСоСчета.ДоговорКредитаЗайма.СчетУчета КАК СчетУчета,
	|	ЛОЖЬ КАК УдержаноИзЗарплаты,
	|	РасходСоСчета.БанковскийСчет
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыПоКредитамИЗаймам
	|ИЗ
	|	Документ.РасходСоСчета КАК РасходСоСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютКассы
	|		ПО (КурсыВалютКассы.Валюта = &ВалютаДС)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютДоговора
	|		ПО (КурсыВалютДоговора.Валюта = РасходСоСчета.ДоговорКредитаЗайма.ВалютаРасчетов)
	|ГДЕ
	|	РасходСоСчета.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику)
	|	И РасходСоСчета.Ссылка = &Ссылка
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	&Организация,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.КредитПолученный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику)
	|	КОНЕЦ,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.СодержаниеПоТипуСуммы,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.СуммаПлатежа,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаРасчетов
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.СуммаУчета
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СуммаРасчетов,
	|	ТаблицаДокумента.СуммаУчета,
	|	ТаблицаДокумента.ДоговорКредитаЗайма,
	|	ТаблицаДокумента.ДоговорКредитаЗайма.ВалютаРасчетов,
	|	ТаблицаДокумента.ВалютаДенежныхСредств,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.ОсновнойДолг)
	|			ТОГДА ТаблицаДокумента.ДоговорКредитаЗайма.СчетУчета
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ТаблицаДокумента.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|		КОГДА ТаблицаДокумента.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ТаблицаДокумента.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ЛОЖЬ,
	|	ТаблицаДокумента.БанковскийСчет
	|ИЗ
	|	ВременнаяТаблицаРасшифровкаПлатежа КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам)";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с подотчетными лицами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма КАК ДоговорКредитаЗайма
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоКредитамИЗаймам КАК ВременнаяТаблицаРасчетыПоКредитамИЗаймам";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоКредитамИЗаймам");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыПоКредитамИЗаймам(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоКредитамИЗаймам", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли