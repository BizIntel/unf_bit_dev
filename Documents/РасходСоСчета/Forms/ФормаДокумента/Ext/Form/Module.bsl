
#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("СозданПоКомандеИзФормыСписка") Тогда
		СозданПоКомандеИзФормыСписка = Параметры.СозданПоКомандеИзФормыСписка;
	Иначе
		СозданПоКомандеИзФормыСписка = Ложь;
	КонецЕсли;
	
	ФормаСоздана = Истина;
	
	// Предопределенные значения
	ВариантЗаполненияРасшифровкиАвтоматически = ПредопределенноеЗначение("Перечисление.ВариантыЗаполненияРасшифровкиПлатежа.Автоматически");
	ВариантЗаполненияРасшифровкиСПомощником = ПредопределенноеЗначение("Перечисление.ВариантыЗаполненияРасшифровкиПлатежа.СПомощником");
	ВариантЗаполненияРасшифровкиВручную = ПредопределенноеЗначение("Перечисление.ВариантыЗаполненияРасшифровкиПлатежа.Вручную");
	
	ВидОперацииПоставщику = Перечисления.ВидыОперацийРасходСоСчета.Поставщику;
	ВидОперацииЭквайринг = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты;
	ВидОперацииПокупателю = Перечисления.ВидыОперацийРасходСоСчета.Покупателю;
	
	УчетПоКомпании = Константы.УчетПоКомпании.Получить();
	// Конец Предопределенные значения
	
	// Зачет долгов
	Если Объект.ВариантЗаполненияРасшифровки.Пустая() ИЛИ
		(Объект.Ссылка.Пустая() И Параметры.Свойство("Основание") И ЗначениеЗаполнено(Параметры.Основание)) Тогда
		Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную;
	ИначеЕсли Объект.Ссылка.Пустая() И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		
		ИмяКлючаОбъекта = СтрЗаменить(ИмяФормы,".","");
		СохраненноеЗначение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+"_ВариантЗаполненияРасшифровки");
		Если ЗначениеЗаполнено(СохраненноеЗначение) Тогда
			Объект.ВариантЗаполненияРасшифровки = СохраненноеЗначение;
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаполнитьИнформациюОСуммеИВалютеОснования();
	// Конец Зачет долгов
	
	Если Объект.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.Зарплата
	   И Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
	КонецЕсли;
	
	// ФО Использовать подсистему Зарплата.
	УстановитьВидимостьОтФОИспользоватьПодсистемуЗарплата();
	
	Если Объект.Ссылка.Пустая()
		И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования) Тогда
		ЗаполнитьДоговор(Параметры);
		// Статья может прийти из шаблона документа или из документа планирования и тогда её заполнять не нужно.
		Если НЕ (Параметры.ЗначенияЗаполнения.Свойство("ШаблонДокумента") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.ШаблонДокумента) И
			Параметры.ЗначенияЗаполнения.Свойство("Статья") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Статья))
			И НЕ ((ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.РасходДСПлан") ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ПеремещениеДСПлан"))
			И НЕ Параметры.Основание.СтатьяДвиженияДенежныхСредств.Пустая())
			Тогда
			
			УстановитьСтатьюДДС();
			Если НЕ Объект.Контрагент.Пустая() Тогда
				УстановитьСтатьиДДСВРасшифровкеПлатежа();
			КонецЕсли;
			
		ИначеЕсли ((ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.РасходДСПлан") ИЛИ ТипЗнч(Параметры.Основание) = Тип("ДокументСсылка.ПеремещениеДСПлан"))
			И НЕ Параметры.Основание.СтатьяДвиженияДенежныхСредств.Пустая()) Тогда
			
			Если Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее
				И (Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата
				ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги
				ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты) Тогда
				Объект.УчитыватьВНУ = Истина;
			Иначе
				Объект.УчитыватьВНУ = СтатьяУчитываетсяВНУ(Объект.Статья, Объект.Организация, Объект.Дата);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
		ЭтоАванс = Ложь;
	Иначе
		ЭтоАванс = Объект.РасшифровкаПлатежа[0].ПризнакАванса;
	КонецЕсли;
	
	Если ЭтоАванс Тогда
		ПризнакАванса = Перечисления.ДаНет.Да;
	Иначе
		ПризнакАванса = Перечисления.ДаНет.Нет;
	КонецЕсли;
	
	// Установка реквизитов формы.
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);
	СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Объект.ВалютаДенежныхСредств));
	
	Курс = ?(
		СтруктураПоВалюте.Курс = 0,
		1,
		СтруктураПоВалюте.Курс
	);
	Кратность = ?(
		СтруктураПоВалюте.Курс = 0,
		1,
		СтруктураПоВалюте.Кратность
	);
	
	УстановитьВидимостьКурсаВалюты(Объект.ВалютаДенежныхСредств);
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка)
		И НЕ ЗначениеЗаполнено(Параметры.Основание)
		И НЕ ЗначениеЗаполнено(Параметры.ЗначениеКопирования)
		И НЕ Параметры.Свойство("ДокументОснование") Тогда
		
		Если Не ЗначениеЗаполнено(Объект.ВидОперации) Тогда
			Объект.ВидОперации = ВидОперацииПоставщику;
		КонецЕсли;
		
		ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС();
	Иначе
		УстановитьВидимостьНалогообложениеНДС();
	КонецЕсли;
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = Объект.Организация.СтавкаНДСПоУмолчанию;
	ИначеЕсли Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
	
	ВидОперации = Объект.ВидОперации;
	ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств;
	Корреспонденция = Объект.Корреспонденция;
	
	// Счет фактура и документ-основание
	Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Объект.ДокументОснование);
	Элементы.ДокументОснованиеНадписьАвто.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Объект.ДокументОснование);
	Элементы.ДокументОснованиеНадписьВручную.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Объект.ДокументОснование);
	Элементы.ДокументОснованиеНадписьСПомощником.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Объект.ДокументОснование);
	// Конец Счет фактура и документ-основание
	
	ДатаДокумента = Объект.Дата;
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДата();
	КонецЕсли;
	
	ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов = Константы.ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов.Получить();
	
	// Зачет долгов
	Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаЗаполнениеСПомощником;
	ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную Тогда
		Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаРучноеЗаполнение;
	КонецЕсли;
	
	ОбновитьИнформациюОбАвансеВРасшифровкеПлатежа();
	ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	
	ИсходнаяСуммаРавнаНулю = (Объект.СуммаДокумента = 0);
	// Конец Зачет долгов
	
	ВестиРасчетыПоДокументам = Объект.Контрагент.ВестиРасчетыПоДокументам;
	
	УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции();
	УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации(Истина);
	УстановитьВидимостьИДоступность();
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги Тогда
		Элементы.НаправлениеДеятельностиНалоги.Видимость = ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов;
	КонецЕсли;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата Тогда
		Элементы.ВыплатыЗаработнойПлатыНаправлениеДеятельности.Видимость = ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов;
	КонецЕсли;
	
	// Заполнение табличной части при вводе документа из рабочего места.
	Если ТипЗнч(Параметры.ЗначенияЗаполнения) = Тип("Структура")
	   И Параметры.ЗначенияЗаполнения.Свойство("ЗаполнитьРасшифровкаПлатежа")
	   И Параметры.ЗначенияЗаполнения.ЗаполнитьРасшифровкаПлатежа Тогда
		
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
			СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтрокаТабличнойЧасти.СтавкаНДС.Ставка + 100) / 100);
		
	КонецЕсли;
	
	УстановитьВидимостьРеквизитовРасчетов();
	
	ОбменСGoogle.ПодключитьОбработчикиСобытияАвтоподбор(ЭтаФорма);
	
	// Эквайринг
	Если Объект.ВидОперации = ВидОперацииЭквайринг Тогда
		ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
		Для Каждого ТекущаяСтрока Из Объект.ЭквайринговыеОперации Цикл
			ТекущаяСтрока.СуммаПлатежаИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетовВозврата
								+ТекущаяСтрока.СуммаРасчетовКомиссии+ТекущаяСтрока.СуммаРасчетовКомиссииВозврата-ТекущаяСтрока.СуммаРасчетов;
		КонецЦикла;
		СуммаПлатежаЭквайринг = Объект.ЭквайринговыеОперации.Итог("СуммаПлатежаИтогоПоСтроке");
		ТипСчетаЗатратШапка = Объект.ЭквайринговыйТерминал.СчетЗатрат.ТипСчета;
		НастроитьВидимостьЭлементовВЗависимостиОтТипаСчетаЗатрат();
	КонецЕсли;
	// Конец Эквайринг
	
	// Остатки ДС и взаиморасчетов на форме
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	ОбновитьВидимостьИОстаткиДС(Объект.БанковскийСчет);
	
	УстановитьСвязиПараметровВыбораДоступныеТипы();
	
	ОтчетыУНФ.ПриСозданииНаСервереФормыСвязанногоОбъекта(ЭтотОбъект);
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
	// СтандартныеПодсистемы.Печать
	УправлениеПечатью.ПриСозданииНаСервере(ЭтаФорма, Элементы.ГруппаВажныеКоманды);
	// Конец СтандартныеПодсистемы.Печать
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = УправлениеСвойствамиПереопределяемый.ЗаполнитьДополнительныеПараметры(Объект, "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства
	
	ОпределитьВидимостьНастроекУчетаВНалогообложении();
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события ПриЧтенииНаСервере.
//
&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// Зачет долгов
	Если ФормаСоздана Тогда
		НастроитьЭлементыРаспределенияДолговНаСервере(Истина);
	КонецЕсли;
	// Конец Зачет долгов
	
	ВестиРасчетыПоДокументам = Объект.Контрагент.ВестиРасчетыПоДокументам;
	
	ДатыЗапретаИзменения.ОбъектПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры // ПриЧтенииНаСервере()

// Процедура - обработчик события ПослеЗаписи.
//
&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если СозданПоКомандеИзФормыСписка Тогда
		ПараметрыОповещения = Новый Структура("Ссылка", Объект.Ссылка);
		Оповестить("Запись_РасходСоСчета", ПараметрыОповещения);
	КонецЕсли;
	
	// Оповещение об оплате.
	ОповеститьОбОплатеСчета = Ложь;
	ОповеститьОбОплатеЗаказа = Ложь;
	
	Для каждого ТекСтрока Из Объект.РасшифровкаПлатежа Цикл
		ОповеститьОбОплатеСчета = ?(
			ОповеститьОбОплатеСчета,
			ОповеститьОбОплатеСчета,
			ЗначениеЗаполнено(ТекСтрока.СчетНаОплату)
		);
		ОповеститьОбОплатеЗаказа = ?(
			ОповеститьОбОплатеЗаказа,
			ОповеститьОбОплатеЗаказа,
			ЗначениеЗаполнено(ТекСтрока.Заказ)
		);
	КонецЦикла;
	
	Если ОповеститьОбОплатеСчета Тогда
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплату"));
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
	КонецЕсли;
	
	Если ОповеститьОбОплатеЗаказа Тогда
		Оповестить("ОповещениеОбОплатеЗаказа");
	КонецЕсли;
	
	Оповестить("ОповещениеОбИзмененииДолга");
	
	ОбновитьОтображениеДанных();
	
КонецПроцедуры // ПослеЗаписи()

// Процедура - обработчик события ПриОткрытии.
// В процедуре устанавливается текущая страница в зависимости от операции.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УстановитьТекущуюСтраницу();
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры // ПриОткрытии()

// Процедура - обработчик события формы ПередЗаписью
//
&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Зачет долгов с помощником
	// Удалим строку с авансом, если он = 0, и строки с нулевыми суммами.
	Если ВидОперации = ВидОперацииПоставщику Тогда
		Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		//Если Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаРаспределениеСПомощником Тогда
			
			Если СуммаПлатежаАванс = 0 Тогда
				
				СтрокаАванса = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдентификаторСтрокиАванса);
				Если СтрокаАванса <> Неопределено Тогда
					Объект.РасшифровкаПлатежа.Удалить(СтрокаАванса.НомерСтроки - 1);
				КонецЕсли;
				
			ИначеЕсли ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение И
				Объект.ДоговорДляЗачетаАвансаСПомощником.Пустая() Тогда
				
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = "Не выбран договор для учета аванса. Документ не проведен!";
				Сообщение.Поле = "Объект.ДоговорДляЗачетаАвансаСПомощником";
				Сообщение.Сообщить();
				Отказ = Истина;
				Возврат;
				
			КонецЕсли;
			
			Индекс = Объект.РасшифровкаПлатежа.Количество() - 1;
			Пока Индекс >= 0 Цикл
				ТекущаяСтрока = Объект.РасшифровкаПлатежа[Индекс];
				Если ТекущаяСтрока.СуммаРасчетов = 0 И ТекущаяСтрока.СуммаПлатежа = 0 Тогда
					Объект.РасшифровкаПлатежа.Удалить(Индекс);
				КонецЕсли;
				
				Индекс = Индекс - 1;
			КонецЦикла;
		ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
			
			Объект.РасшифровкаПлатежа.Очистить();
			ДобавитьСтрокуАвансаНаКлиенте();
		КонецЕсли;
	КонецЕсли;
	// Конец Зачет долгов с помощником
	
	// СтандартныеПодсистемы.ОценкаПроизводительности
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "Проведение"+ РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	КонецЕсли;
	// СтандартныеПодсистемы.ОценкаПроизводительности
	
КонецПроцедуры // ПередЗаписью()

// Процедура обработчик события ПередЗаписьюНаСервере.
//
&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПараметрыЗаписи.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
		
		ТекстСообщения = "";
		Если ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
			ПроверитьСоответствиеДоговораУсловиямДокумента(Объект.ДоговорыАвтоЗачетаДолгов, ТекстСообщения, Объект.Ссылка, Объект.Организация, Объект.Контрагент, Объект.ВидОперации, Отказ, Объект.ДоговорКредитаЗайма);
		Иначе
			ПроверитьСоответствиеДоговораУсловиямДокумента(Объект.РасшифровкаПлатежа, ТекстСообщения, Объект.Ссылка, Объект.Организация, Объект.Контрагент, Объект.ВидОперации, Отказ, Объект.ДоговорКредитаЗайма);
		КонецЕсли;
		
		Если ТекстСообщения <> "" Тогда
			
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ?(Отказ, НСтр("ru = 'Документ не проведен! '") + ТекстСообщения, ТекстСообщения);
			Сообщение.Сообщить();
			
			Если Отказ Тогда
				Возврат;
			КонецЕсли;
			
		КонецЕсли;
		
		// Эквайринг
		Если ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") И ТекущийОбъект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты Тогда
			// Дата документа должна быть больше даты операции.
			Для Каждого ТекущаяСтрока Из ТекущийОбъект.ЭквайринговыеОперации Цикл
				Если ТекущаяСтрока.Документ <> Неопределено И ТекущаяСтрока.Документ.Дата >= ТекущийОбъект.Дата Тогда
					Сообщение = Новый СообщениеПользователю;
					Сообщение.Текст = НСтр("ru = 'Дата операции больше или равна дате документа!'");
					Сообщение.Поле = "ЭквайринговыеОперации["+(ТекущаяСтрока.НомерСтроки-1)+"].Документ";
					Сообщение.ПутьКДанным = "Объект";
					Сообщение.Сообщить();
					
					Отказ = Истина;
				КонецЕсли;
			КонецЦикла;
			
			Если Отказ Тогда
				Сообщение = Новый СообщениеПользователю;
				Сообщение.Текст = НСтр("ru = 'Дата документа должна быть больше даты всех операций, которые указаны в табличной части ""Эквайринговые операции"". Документ не проведен!'");
				Сообщение.Сообщить();
				
				Отказ = Истина;
				Возврат;
			КонецЕсли;
		КонецЕсли;
		// Конец Эквайринг
		
	КонецЕсли;
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтаФорма, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	// Обсуждения
	ТекущийОбъект.ДополнительныеСвойства.Вставить("Модифицированность",Модифицированность);
	// Конец Обсуждения
	
КонецПроцедуры // ПередЗаписьюНаСервере()

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбменСGoogle.УвеличитьЗначениеСчетчикаПодсказок(ЭтаФорма);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаОповещения формы.
//
&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Контрагент" Тогда
		Если ЗначениеЗаполнено(Параметр)
		   И Объект.Контрагент = Параметр Тогда
			УстановитьВидимостьРеквизитовРасчетов();
		КонецЕсли;
	КонецЕсли;
	
	// Прочие расчеты
	Если ИмяСобытия = "Запись_ПланСчетовУправленческий" Тогда
		Если ЗначениеЗаполнено(Параметр)
		   И Объект.Корреспонденция = Параметр Тогда
			УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		КонецЕсли;
	КонецЕсли;
	// Конец Прочие расчеты
	
	// Обсуждения
	ОбсужденияКлиент.ОбработкаОповещения(ИмяСобытия, Параметр, Источник, ЭтаФорма, Объект.Ссылка);
	// Конец Обсуждения
	
	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр) Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры //ОбработкаОповещения()

// Процедура - обработчик события ОбработкаПроверкиЗаполненияНаСервере формы.
//
&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтаФорма, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

#Область ДокументОснование

&НаСервереБезКонтекста
Функция ПолучитьСписокДляВыбораДокументаОснования()
	
	СписокОснований = Новый СписокЗначений;
	
	СписокОснований.Добавить("Документ.ПриходнаяНакладная.ФормаВыбора", "Приходная накладная");
	СписокОснований.Добавить("Документ.НачислениеНалогов.ФормаВыбора", "Начисление налогов");
	СписокОснований.Добавить("Документ.ПлатежноеПоручение.ФормаВыбора", "Платежное поручение");
	СписокОснований.Добавить("Документ.ЗаказПоставщику.ФормаВыбора", "Заказ поставщику");
	СписокОснований.Добавить("Документ.СчетНаОплатуПоставщика.ФормаВыбора", "Счет на оплату (полученный)");
	СписокОснований.Добавить("Документ.ДополнительныеРасходы.ФормаВыбора", "Дополнительные расходы");
	СписокОснований.Добавить("Документ.ПлатежнаяВедомость.ФормаВыбора", "Платежная ведомость");
	Если ПолучитьФункциональнуюОпцию("ПередачаТоваровНаКомиссию") Тогда
		СписокОснований.Добавить("Документ.ОтчетКомиссионера.ФормаВыбора", "Отчет комиссионера");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ПриемТоваровНаКомиссию") Тогда
		СписокОснований.Добавить("Документ.ОтчетКомитенту.ФормаВыбора", "Отчет комитенту");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		СписокОснований.Добавить("Документ.ПеремещениеДСПлан.ФормаВыбора", "Перемещение денег (план)");
		СписокОснований.Добавить("Документ.РасходДСПлан.ФормаВыбора", "Заявка на расход денег");
	КонецЕсли;
	Если ПолучитьФункциональнуюОпцию("КредитыИЗаймы") Тогда
		СписокОснований.Добавить("Документ.НачисленияПоКредитамИЗаймам.ФормаВыбора", "Начисления по кредитам и займам");
		СписокОснований.Добавить("Документ.ДоговорКредитаИЗайма.ФормаВыбора", "Договор кредита (займа)");
	КонецЕсли;
	
	СписокОснований.СортироватьПоПредставлению(НаправлениеСортировки.Возр);
	
	Возврат СписокОснований;
	
КонецФункции

&НаКлиенте
Процедура ДокументОснованиеНадписьОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Если НавигационнаяСсылкаФорматированнойСтроки = "удалить" Тогда
		Объект.ДокументОснование = Неопределено;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Элементы.ДокументОснованиеНадписьАвто.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Элементы.ДокументОснованиеНадписьВручную.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Элементы.ДокументОснованиеНадписьСПомощником.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(Неопределено);
		Модифицированность = Истина;
		
		ДокументОснованиеВалютаПредставление = "";
		ДокументОснованиеСумма = 0;
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "заполнить" Тогда
		ЗаполнитьПоОснованиюНачало();
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "выбрать" Тогда
		
		//Выбрать основание
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыборТипаОснованияЗавершение", ЭтотОбъект);
		ПоказатьВыборИзМеню(ОписаниеОповещения, ПолучитьСписокДляВыбораДокументаОснования(), Элемент);
		
	ИначеЕсли НавигационнаяСсылкаФорматированнойСтроки = "открыть" Тогда
		
		Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) тогда
			возврат;
		КонецЕсли;
		
		РаботаСФормойДокументаКлиент.ОткрытьФормуДокументаПоТипу(Объект.ДокументОснование);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВыборТипаОснованияЗавершение(ВыбИмяФормы, Параметры) Экспорт
	
	Если ВыбИмяФормы<>Неопределено Тогда
		
		СтруктураПараметровОтбора = Новый Структура();
		Для каждого элОтбора Из ПараметрыВыбораДокументаОснования Цикл
			ИмяПоляОтбора = СтрЗаменить(элОтбора.Имя,"Отбор.","");
			СтруктураПараметровОтбора.Вставить(ИмяПоляОтбора, элОтбора.Значение);
		КонецЦикла;
		_ОповещениеОЗакрытии = Новый ОписаниеОповещения("ВыбратьОснованиеЗавершение", ЭтотОбъект);
		ОткрытьФорму(ВыбИмяФормы.Значение, СтруктураПараметровОтбора, ЭтотОбъект, ,,,_ОповещениеОЗакрытии);
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьОснованиеЗавершение(ВыбЗначение, Параметры) Экспорт

	Если ВыбЗначение<>Неопределено Тогда
		Объект.ДокументОснование = ВыбЗначение;
		Элементы.ДокументОснованиеНадпись.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Элементы.ДокументОснованиеНадписьАвто.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Элементы.ДокументОснованиеНадписьВручную.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Элементы.ДокументОснованиеНадписьСПомощником.Заголовок = РаботаСФормойДокументаКлиентСервер.СформироватьНадписьДокументОснование(ВыбЗначение);
		Модифицированность = Истина;
		
		ЗаполнитьПоОснованиюНачало();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюНачало() Экспорт

	ОписаниеОповещения = Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект);
	ПоказатьВопрос(
		ОписаниеОповещения, 
		НСтр("ru = 'Заполнить документ по выбранному основанию?'"), 
		РежимДиалогаВопрос.ДаНет, 0);		

КонецПроцедуры

#КонецОбласти

// Процедура - обработчик события ПриИзменении поля ввода Контрагент.
//
&НаКлиенте
Процедура КонтрагентПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И 
		(Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником 
		ИЛИ
		Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически)Тогда
		Объект.РасшифровкаПлатежа.Очистить();
		ДобавитьСтрокуАвансаНаКлиенте();
		ПересчитатьИтогиПриИзмененииАвансаНаКлиенте(Истина);
	КонецЕсли;
	
	СтруктураДанные = ПолучитьДанныеКонтрагентПриИзменении(Объект.Контрагент, Объект.Организация, Объект.Дата);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда 
		
		Объект.РасшифровкаПлатежа[0].Договор = СтруктураДанные.Договор;
		
		Если ЗначениеЗаполнено(Объект.РасшифровкаПлатежа[0].Договор) Тогда
			Объект.РасшифровкаПлатежа[0].Курс = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс
			);
			Объект.РасшифровкаПлатежа[0].Кратность = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
			);
		КонецЕсли;
		
		Объект.РасшифровкаПлатежа[0].Курс = ?(
			Объект.РасшифровкаПлатежа[0].Курс = 0,
			1,
			Объект.РасшифровкаПлатежа[0].Курс
		);
		Объект.РасшифровкаПлатежа[0].Кратность = ?(
			Объект.РасшифровкаПлатежа[0].Кратность = 0,
			1,
			Объект.РасшифровкаПлатежа[0].Кратность
		);
		
		Объект.РасшифровкаПлатежа[0].СуммаРасчетов = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа,
			Курс,
			Объект.РасшифровкаПлатежа[0].Курс,
			Кратность,
			Объект.РасшифровкаПлатежа[0].Кратность
		);
		
	КонецЕсли;
	
	// Статьи ДДС
	УстановитьСтатьиДДСВРасшифровкеПлатежа();
	// Конец Статьи ДДС
	
	// Прочие расчеты
	Если СтруктураДанные.Свойство("ДоговорКредитаЗаймаПоУмолчанию") Тогда
		Объект.ДоговорКредитаЗайма = СтруктураДанные.ДоговорКредитаЗаймаПоУмолчанию;
		ОбработатьИзменениеДоговораКредитаИлиЗайма();
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты") Тогда
		
		Объект.Корреспонденция = СтруктураДанные.КорреспонденцияПрочихРасчетов;
		
	КонецЕсли;
	// Конец Прочие расчеты
	
	Объект.ДоговорДляЗачетаАвансаСПомощником = СтруктураДанные.Договор;
	СоздатьЭлементыДоговоровЗачетаДолгов();
	
	Если ЗначениеЗаполнено(СтруктураДанные.СтатьяДДСПоУмолчанию) Тогда
		Объект.Статья = СтруктураДанные.СтатьяДДСПоУмолчанию;
		Объект.УчитыватьВНУ = СтруктураДанные.УчитыватьВНУ;
	КонецЕсли;
	
	ВестиРасчетыПоДокументам = СтруктураДанные.ВестиРасчетыПоДокументам;
	Если ВестиРасчетыПоДокументам И Объект.ВидОперации = ВидОперацииПокупателю Тогда
		Для Каждого ТекущаяСтрокаРасшифровки Из Объект.РасшифровкаПлатежа Цикл
			ТекущаяСтрокаРасшифровки.ПризнакАванса = Ложь;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // КонтрагентПриИзменении()

// Процедура - обработчик события ВидОперацииПриИзменении.
// Управляет страницами при изменении вида операции документа.
//
&НаКлиенте
Процедура ВидОперацииПриИзменении(Элемент)
	
	ВидОперацииПередИзменением = ВидОперации;
	ВидОперации = Объект.ВидОперации;
	
	Если ВидОперации <> ВидОперацииПередИзменением Тогда
		УстановитьТекущуюСтраницу();
		ОчиститьРеквизитыНеОтносящиесяКОперации();
		ВидОперацииПриИзмененииНаСервере();
		Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВидОперацииПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Дата.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ДатаПриИзменении(Элемент)
	
	// Обработка события изменения даты.
	ДатаПередИзменением = ДатаДокумента;
	ДатаДокумента = Объект.Дата;
	Если Объект.Дата <> ДатаПередИзменением Тогда
		СтруктураДанные = ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением);
		Если СтруктураДанные.РазностьДат <> 0 Тогда
			Объект.Номер = "";
		КонецЕсли;
		
		// Помощник зачета долгов
		Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
			ПерезаполнитьПолеДатаДокБольшеДатыДокументаВТаблицеДолгов();
		КонецЕсли;
		// Конец Помощник зачета долгов
		
		// Обновление курсов расчетов
		СтруктураДанные.Вставить("ОбновитьКурсыРасчетов", (Объект.РасшифровкаПлатежа.Количество() > 0 И Объект.РасшифровкаПлатежа.Итог("Курс") > 0 И
			Объект.РасшифровкаПлатежа.Количество() <> Объект.РасшифровкаПлатежа.Итог("Курс")));
		СтруктураДанные.Вставить("ОбновитьКурсыРасчетовТекстВопроса", НСтр("ru = 'Изменилась дата документа. Установить курс расчетов в соответствии с курсом валюты договора?'"));
		// Конец Обновление курсов расчетов
		
		ТекстСообщения = НСтр("ru = 'Изменился курс валюты банковского счета. Пересчитать суммы документа?'");
		ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры // ДатаПриИзменении()

// Процедура перезаполняет значение поля  в таблице долгов.
// В процедуре определяется ситуация, когда при изменении своей даты документ 
// оказывается в другом периоде нумерации документов, и в этом случае
// присваивает документу новый уникальный номер.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ПерезаполнитьПолеДатаДокБольшеДатыДокументаВТаблицеДолгов()
	
	Для Каждого ТекущаяСтрока Из ТаблицаДолгов Цикл
		Если ЗначениеЗаполнено(ТекущаяСтрока.Документ) Тогда
			ТекущаяСтрока.ДатаДокБольшеДатыДокумента = (КонецДня(ТекущаяСтрока.ДатаДокумента) > КонецДня(Объект.Дата));
		Иначе
			ТекущаяСтрока.ДатаДокБольшеДатыДокумента = Ложь;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода Организация.
// В процедуре осуществляется очистка номера документа,
// а также производится установка параметров функциональных опций формы.
// Переопределяет соответствующий параметр формы.
//
&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	
	// Обработка события изменения организации.
	Объект.Номер = "";
	СтруктураДанные = ПолучитьДанныеОрганизацияПриИзменении();
	Компания = СтруктураДанные.Компания;
	Объект.БанковскийСчет = СтруктураДанные.БанковскийСчет;
	Объект.СчетКонтрагента = СтруктураДанные.СчетКонтрагента;
	
	ВалютаДенежныхСредствПередИзменением = ВалютаДенежныхСредств;
	Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств;
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		ЗаполнитьДолги(Истина);
	КонецЕсли;
	
	УстановитьНастройкиУчетаВНалогообложении();
	
	// Если валюта не изменилась, то ничего не делаем.
	Если ВалютаДенежныхСредств = ВалютаДенежныхСредствПередИзменением Тогда
		Возврат;
	КонецЕсли;
	
	Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из Объект.РасшифровкаПлатежа Цикл
			ТекущаяСтрока.Договор = Неопределено;
		КонецЦикла;
	КонецЕсли;
	Если Объект.ДоговорыАвтоЗачетаДолгов.Количество() > 0 Тогда
		Для Каждого ТекущаяСтрока Из Объект.ДоговорыАвтоЗачетаДолгов Цикл
			ТекущаяСтрока.Договор = Неопределено;
		КонецЦикла;
	КонецЕсли;
	
	ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	
КонецПроцедуры // ОрганизацияПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода БанковскийСчет.
//
&НаКлиенте
Процедура БанковскийСчетПриИзменении(Элемент)
	
	ПредыдущаяВалюдаДС = Объект.ВалютаДенежныхСредств;
	
	СтруктураДанные = ПолучитьДанныеБанковскийСчетПриИзменении(
		Объект.Дата,
		Объект.БанковскийСчет,
		Объект.СчетКонтрагента
	);
	
	Если Объект.ВидОперации = ВидОперацииЭквайринг Тогда
		Объект.СуммаДокумента = 0;
		Объект.СуммаКомиссииДокумента = 0;
		Объект.ЭквайринговыеОперации.Очистить();
		СуммаПлатежаЭквайринг = Объект.ЭквайринговыеОперации.Итог("СуммаПлатежаИтогоПоСтроке");
	КонецЕсли;
	
	Если Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаДенежныхСредств Тогда
		Возврат;
	КонецЕсли;
	
	Объект.СчетКонтрагента					= СтруктураДанные.СчетКонтрагента;
	Объект.ВалютаДенежныхСредств			= СтруктураДанные.ВалютаДенежныхСредств;
	ВалютаДенежныхСредств 					= СтруктураДанные.ВалютаДенежныхСредств;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Зарплата") Тогда
		ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Список ""Платежные ведомости"" будет очищен.'");
		ПоказатьПредупреждение(Новый ОписаниеОповещения("БанковскийСчетПриИзмененииЗавершение", ЭтотОбъект, Новый Структура("СтруктураДанные, ТекстСообщения", СтруктураДанные, ТекстСообщения)), ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	БанковскийСчетПриИзмененииФрагмент(СтруктураДанные);
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И НЕ ЗначениеЗаполнено(ПредыдущаяВалюдаДС) И НЕ Объект.ВалютаДенежныхСредств.Пустая() Тогда
		ЗаполнитьДолги(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетПриИзмененииЗавершение(ДополнительныеПараметры) Экспорт
	
	СтруктураДанные = ДополнительныеПараметры.СтруктураДанные;
	ТекстСообщения = ДополнительныеПараметры.ТекстСообщения;
	
	Объект.ВыплатаЗаработнойПлаты.Очистить();
	
	БанковскийСчетПриИзмененииФрагмент(СтруктураДанные);

КонецПроцедуры

&НаКлиенте
Процедура БанковскийСчетПриИзмененииФрагмент(Знач СтруктураДанные)
	
	Перем ТекстСообщения;
	
	ТекстСообщения = НСтр("ru = 'Изменилась валюта банковского счета. Пересчитать суммы документа?'");
	ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения);
	
КонецПроцедуры // БанковскийСчетПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода Корреспонденция.
//
&НаКлиенте
Процедура КорреспонденцияПриИзменении(Элемент)
	
	Если Корреспонденция <> Объект.Корреспонденция Тогда
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Истина);
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Корреспонденция = Объект.Корреспонденция;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода СуммаДокумента.
//
&НаКлиенте
Процедура СуммаДокументаПриИзменении(Элемент)
	
	ИсходнаяСуммаРавнаНулю = (Объект.СуммаДокумента = 0);
	
	СуммаДокументаПриИзмененииФрагмент();
	
КонецПроцедуры // СуммаДокументаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода СуммаДокумента.
//
&НаКлиенте
Процедура СуммаДокументаПриИзмененииФрагмент()
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 И
		НЕ (Объект.ВидОперации = ВидОперацииПоставщику И 
		Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником) Тогда
		
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента;
		
		// Эквайринг
		Если Объект.ВидОперации = ВидОперацииЭквайринг Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаПлатежа + Объект.СуммаКомиссииДокумента;
		КонецЕсли;
		// Конец Эквайринг
		
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
			СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
		
		РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
	Если Объект.ВидОперации = ВидОперацииПоставщику Тогда
		
		Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
			Если Объект.СуммаДокумента = ТаблицаДолгов.Итог("ДолгВалПлатежа") Тогда
				ЗачестьДолгиАвтоматическиФрагмент();
			Иначе
				ПересчитатьИтогиПриИзмененииАвансаНаСервере(Истина);
			КонецЕсли;
		ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически И Объект.ДоговорыАвтоЗачетаДолгов.Количество() = 1 Тогда
			Объект.ДоговорыАвтоЗачетаДолгов[0].СуммаПлатежа = Объект.СуммаДокумента;
			ПересчитатьИтогиПриИзмененииАвансаНаСервере();
		Иначе
			ПересчитатьИтогиПриИзмененииАвансаНаСервере();
		КонецЕсли;
		
	Иначе
		
		СуммаПлатежаДляУсловногоОформления = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		
	КонецЕсли;
	
	//ПересчитатьИтогиПриИзмененииАвансаНаСервере(Истина);
	
КонецПроцедуры // СуммаДокументаПриИзменении()

&НаСервере
Процедура УстановитьВидиомостьДокументаОснования(НоваяВидимость)
	
	Элементы.ДокументОснованиеНадпись.Видимость = НоваяВидимость;
	Элементы.ДокументОснованиеНадписьАвто.Видимость = НоваяВидимость;
	Элементы.ДокументОснованиеНадписьВручную.Видимость = НоваяВидимость;
	Элементы.ДокументОснованиеНадписьСПомощником.Видимость = НоваяВидимость;
	
КонецПроцедуры

// Процедура устанавливает видимость элементов в зависимости от вида операции.
//
&НаСервере
Процедура УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации(ПриСозданииНаСервере = Ложь)
	
	Элементы.РасшифровкаПлатежаСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	Элементы.РасшифровкаПлатежаПрочиеРасчетыСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	Элементы.РасшифровкаПлатежаПрочиеРасчетыСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	Элементы.РасшифровкаПлатежаРасчетыПоКредитамСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	
	Элементы.Подвал.Видимость = Истина;
	Элементы.СтраницыРасчетовСКонтрагентомПодвал.Видимость = Ложь;
	
	Элементы.РасчетыСКонтрагентом.Видимость = Ложь;
	Элементы.РасчетыСПодотчетником.Видимость = Ложь;
	Элементы.РасчетыПрочие.Видимость = Ложь;
	Элементы.ВыплатыЗаработнойПлаты.Видимость = Ложь;
	Элементы.РасчетыПоНалогам.Видимость = Ложь;
	
	Элементы.НалогообложениеНДС.Видимость = Ложь;
	Элементы.ГруппаКонтрагент.Видимость = Ложь;
	Элементы.Контрагент.Видимость = Ложь;
	Элементы.СчетКонтрагента.Видимость = Ложь;
	Элементы.Подотчетник.Видимость = Ложь;
	
	// Эквайринг
	Элементы.РасчетыПоЭквайрингу.Видимость = Ложь;
	Элементы.ЭквайринговыйТерминал.Видимость = Ложь;
	Элементы.СуммаПлатежаЭквайринг.Видимость = Ложь;
	Элементы.СуммаКомиссииДокумента.Видимость = Ложь;
	УстановитьВидиомостьДокументаОснования(Истина);
	Элементы.СуммаДокумента.Заголовок = "Сумма";
	// Конец Эквайринг
	
	// Прочие расчеты
	Элементы.РасчетыПоКредитам.Видимость = Ложь;
	Элементы.РасчетыПоКредитам.Заголовок = "Расчеты по кредитам";
	Элементы.ДоговорЗаймаСотруднику.Видимость = Ложь;
	Элементы.ЗаполнитьПоДоговоруЗайма.Видимость = Ложь;
	Элементы.ДоговорКредита.Видимость = Ложь;
	Элементы.ЗаполнитьПоДоговоруКредита.Видимость = Ложь;
	Элементы.ГруппаИнформацияПоДоговору.Видимость = Ложь;
	Элементы.Сотрудник.Видимость = Ложь;
	// Конец Прочие расчеты
	
	//Элементы.СтатьяСуммаДокумента.ОтметкаНезаполненного = Ложь;
	Элементы.Корреспонденция.Заголовок = "Корреспонденция";
	Элементы.СуммаДокумента.КнопкаВыпадающегоСписка = Ложь;
	
	Элементы.СчетПолучателя.Видимость = Ложь;
	Элементы.Касса.Видимость = Ложь;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику") Тогда
		
		Элементы.РасчетыСКонтрагентом.Видимость = Истина;
		Элементы.РасшифровкаПлатежаПодбор.Видимость = Истина;
		Элементы.РасшифровкаПлатежаЗаполнитьРасшифровку.Видимость = Истина;
		
		Элементы.ГруппаКонтрагент.Видимость = Истина;
		Элементы.Контрагент.Видимость = Истина;
		Элементы.Контрагент.Заголовок = "Поставщик";
		Элементы.СчетКонтрагента.Видимость = Истина;
		Элементы.НалогообложениеНДС.Видимость = Истина;
		
		НовыйМассив = Новый Массив();
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент");
		НовыйМассив.Добавить(НоваяСвязь);
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.РасшифровкаПлатежаСчетНаОплату.СвязиПараметровВыбора = НовыеСвязи;
		
		Элементы.СуммаПлатежа.Видимость = Истина;
		Элементы.СуммаПлатежа.Заголовок = НСтр("ru='Разнесено'");
		Элементы.СуммаРасчетов.Видимость = НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		
		Элементы.СуммаРазнесеноВручную.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		Элементы.СуммаРасчетовРазнесеноВручную.Видимость = НЕ Элементы.СуммаРазнесеноВручную.Видимость;
		
		Элементы.СуммаНДС.Видимость = Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		// Подвал
		Элементы.Подвал.Видимость = Ложь;
		Элементы.СтраницыРасчетовСКонтрагентомПодвал.Видимость = Истина;
		Элементы.СуммаРазнесеноВручную.Заголовок = "Итого разнесено вручную";
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю") Тогда
		
		Элементы.РасчетыСКонтрагентом.Видимость = Истина;
		Элементы.РасшифровкаПлатежаПодбор.Видимость = Ложь;
		Элементы.РасшифровкаПлатежаЗаполнитьРасшифровку.Видимость = Ложь;
		
		Элементы.ГруппаКонтрагент.Видимость = Истина;
		Элементы.Контрагент.Видимость = Истина;
		Элементы.Контрагент.Заголовок = "Покупатель";
		
		Элементы.СчетКонтрагента.Видимость = Истина;
		Элементы.НалогообложениеНДС.Видимость = Истина;
		
		НовыйМассив = Новый Массив();
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Контрагент", "Объект.Контрагент");
		НовыйМассив.Добавить(НоваяСвязь);
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.РасшифровкаПлатежаСчетНаОплату.СвязиПараметровВыбора = НовыеСвязи;
		
		Элементы.СуммаПлатежа.Видимость = Истина;
		Элементы.СуммаПлатежа.Заголовок = НСтр("ru='Сумма платежа'");
		Элементы.СуммаРасчетов.Видимость = НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		
		Элементы.СуммаРазнесеноВручную.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		Элементы.СуммаРасчетовРазнесеноВручную.Видимость = НЕ Элементы.СуммаРазнесеноВручную.Видимость;
		
		Элементы.СуммаНДС.Видимость = Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаРучноеЗаполнение;
		Элементы.РасчетыСКонтрагентом.Заголовок = "Зачет долгов";
		
		// Подвал
		Элементы.Подвал.Видимость = Ложь;
		Элементы.СтраницыРасчетовСКонтрагентомПодвал.Видимость = Истина;
		Элементы.СуммаРазнесеноВручную.Заголовок = "Итого разнесено";
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику") Тогда
		
		Элементы.РасчетыСПодотчетником.Видимость = Истина;
		Элементы.Подотчетник.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежа.Заголовок = ?(ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"), НСтр("ru='Сумма (план)'"), НСтр("ru='Сумма платежа'"));
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		
		Элементы.РасчетыСПодотчетникомРасшифровкаПлатежаСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Зарплата") Тогда
		
		Элементы.ВыплатыЗаработнойПлаты.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = Ложь;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Истина;
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Налоги") Тогда
		
		Элементы.РасчетыПоНалогам.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежа.Заголовок = ?(ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"), НСтр("ru='Сумма (план)'"), НСтр("ru='Сумма платежа'"));
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		
		Элементы.РасчетыСПодотчетникомРасшифровкаПлатежаСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
	// Эквайринг
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты") Тогда
		
		Элементы.ДокументОснованиеНадписьАвто.Видимость = Ложь;
		Элементы.РасчетыСКонтрагентом.Видимость = Ложь;
		
		Элементы.ГруппаКонтрагент.Видимость = Истина;
		Элементы.Контрагент.Видимость = Истина;
		Элементы.Контрагент.Заголовок = "Банк-эквайрер";
		Элементы.СчетКонтрагента.Видимость = Истина;
		
		Элементы.ЭквайринговыйТерминал.Видимость = Истина;
		
		Элементы.НалогообложениеНДС.Видимость = Истина;
		
		Элементы.РасчетыПоЭквайрингу.Видимость = Истина;
		
		Если ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") Тогда
			Элементы.СуммаПлатежа.Видимость = Ложь;
			Элементы.СуммаРасчетов.Видимость = Ложь;
			Элементы.СуммаНДС.Видимость = Ложь;
			Элементы.СуммаПлатежаЭквайринг.Видимость = Истина;
			
			Если НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
				Элементы.СуммаПлатежаЭквайринг.Заголовок = "Сумма расчетов";
			Иначе
				Элементы.СуммаПлатежаЭквайринг.Заголовок = "Сумма платежа";
			КонецЕсли;
			
			Элементы.ЭквайринговыеОперации.Видимость = Истина;
			Элементы.ЭквайринговыеОперацииЗаполнитьЭквайринговыеОперацииСВыборомПериода.Видимость = Истина;
			Элементы.СуммаДокумента.Заголовок = "Сумма";
			Элементы.СуммаКомиссииДокумента.Видимость = Ложь;
		Иначе
			Элементы.СуммаПлатежа.Видимость = Истина;
			Элементы.СуммаПлатежа.Заголовок = ?(ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"), НСтр("ru='Сумма (план)'"), НСтр("ru='Сумма платежа'"));
			Элементы.СуммаРасчетов.Видимость = Ложь;
			Элементы.СуммаНДС.Видимость = Ложь;
			Элементы.СуммаПлатежаЭквайринг.Видимость = Ложь;
			
			Элементы.ЭквайринговыеОперации.Видимость = Ложь;
			Элементы.ЭквайринговыеОперацииЗаполнитьЭквайринговыеОперацииСВыборомПериода.Видимость = Ложь;
			Элементы.СуммаДокумента.Заголовок = "Сумма/комиссия";
			Элементы.СуммаКомиссииДокумента.Видимость = Истина;
		КонецЕсли;
		
		Элементы.РасчетыСКонтрагентом.Видимость = Ложь;
		
		Если ТипСчетаЗатратШапка = Перечисления.ТипыСчетов.Расходы Тогда
			Элементы.ПодразделениеЗатраты.Видимость = Истина;
			Элементы.НаправлениеДеятельностиЗатраты.Видимость = Истина;
		Иначе
			Элементы.ПодразделениеЗатраты.Видимость = Ложь;
			Элементы.НаправлениеДеятельностиЗатраты.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
	// Конец Эквайринг
	// Прочие расчеты
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Прочее") Тогда
		
		Элементы.РасчетыПрочие.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежа.Заголовок = ?(ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"), НСтр("ru='Сумма (план)'"), НСтр("ru='Сумма платежа'"));
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		
		Элементы.РасчетыСПодотчетникомРасшифровкаПлатежаСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		Элементы.РасшифровкаПлатежаПрочиеРасчеты.Видимость = Ложь;
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных") Тогда
		
		Элементы.Касса.Видимость = Истина;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		Элементы.РасшифровкаПлатежаПрочиеРасчеты.Видимость = Ложь;
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет") Тогда
		
		Элементы.СчетПолучателя.Видимость = Истина;
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		Элементы.РасшифровкаПлатежаПрочиеРасчеты.Видимость = Ложь;
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.НаРасходы") Тогда
		
		Элементы.РасчетыПрочие.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежа.Заголовок = ?(ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"), НСтр("ru='Сумма (план)'"), НСтр("ru='Сумма платежа'"));
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаРасчетов.Видимость = Ложь;
		
		Элементы.РасчетыСПодотчетникомРасшифровкаПлатежаСуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		Элементы.РасшифровкаПлатежаПрочиеРасчеты.Видимость = Ложь;
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		
		Элементы.Корреспонденция.Заголовок = "Статья расходов";
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты") Тогда
		
		Элементы.РасчетыПрочие.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		Элементы.СуммаПлатежа.Заголовок = НСтр("ru='Сумма платежа'");
		Элементы.СуммаРасчетов.Видимость = НЕ ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
		Элементы.ГруппаКонтрагент.Видимость = Истина;
		Элементы.Контрагент.Видимость = Истина;
		Элементы.Контрагент.Заголовок = "Контрагент";
		Элементы.РасшифровкаПлатежаПрочиеРасчеты.Видимость = Истина;
		Элементы.РасшифровкаПлатежаПрочиеРасчетыДоговор.Видимость = Объект.Контрагент.ВестиРасчетыПоДоговорам;
		УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(Ложь);
		
		Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
			Идентификатор = Объект.РасшифровкаПлатежа[0].ПолучитьИдентификатор();
			Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущаяСтрока = Идентификатор;
		КонецЕсли;
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику") Тогда
		
		Элементы.Сотрудник.Видимость = Истина;
		Элементы.РасчетыПоКредитам.Заголовок = "Расчеты по займам";
		Элементы.РасчетыПоКредитам.Видимость = Истина;
		Элементы.РасшифровкаПлатежаРасчетыПоКредитам.Видимость = Ложь;
		
		Элементы.ДокументОснованиеНадписьАвто.Видимость = Ложь;
		
		Элементы.ДоговорЗаймаСотруднику.Видимость = Истина;
		Элементы.ЗаполнитьПоДоговоруЗайма.Видимость = Истина;
		
		ЗаполнитьИнформациюПоКредитуЗаймуНаСервере();
		
		Элементы.ГруппаИнформацияПоДоговору.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежаВалюта.Видимость = Элементы.СуммаПлатежа.Видимость;
		Элементы.СуммаПлатежа.Заголовок = НСтр("ru='Сумма платежа'");
		Элементы.СуммаРасчетов.Видимость = Ложь;
		
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
	ИначеЕсли ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам") Тогда
		
		Элементы.РасчетыПоКредитам.Видимость = Истина;
		Элементы.ГруппаКонтрагент.Видимость = Истина;
		Элементы.Контрагент.Видимость = Истина;
		Элементы.Контрагент.Заголовок = "Банк";
		Элементы.НалогообложениеНДС.Видимость = Истина;
		Элементы.РасшифровкаПлатежаРасчетыПоКредитам.Видимость = Истина;
		
		Элементы.ДокументОснованиеНадписьАвто.Видимость = Ложь;
		
		Элементы.ДоговорКредита.Видимость = Истина;
		Элементы.ЗаполнитьПоДоговоруКредита.Видимость = Истина;
		
		ЗаполнитьИнформациюПоКредитуЗаймуНаСервере();
		
		Элементы.ГруппаИнформацияПоДоговору.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
		Элементы.СуммаПлатежа.Заголовок = НСтр("ru='Сумма платежа'");
		Элементы.СуммаРасчетов.Видимость = Истина;
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
	// Конец Прочие расчеты
	Иначе
		
		Элементы.РасчетыПрочие.Видимость = Истина;
		
		Элементы.СуммаПлатежа.Видимость = ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь");
		Элементы.СуммаПлатежа.Заголовок = ?(ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь"), НСтр("ru='Сумма (план)'"), НСтр("ru='Сумма платежа'"));
		Элементы.СуммаРасчетов.Видимость = Ложь;
		Элементы.СуммаНДС.Видимость = Ложь;
		Элементы.ВыплатаЗаработнойПлатыИтогСуммаПлатежа.Видимость = Ложь;
		
	КонецЕсли;
	
	УстановитьВидимостьДокументыПланирования();
	
	НастроитьЭлементыРаспределенияДолговНаСервере(ПриСозданииНаСервере);
	ОпределитьВидимостьНастроекУчетаВНалогообложении();
	
КонецПроцедуры // УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации()

// Процедура выполняет действия при изменении договора контрагента.
//
&НаКлиенте
Процедура ОбработатьИзменениеДоговораКонтрагента(СтрокаТабличнойЧасти)
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
		СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор,
			СтрокаТабличнойЧасти.ДокументПланирования,
			СтрокаТабличнойЧасти.СтатьяДДС
		);
		СтрокаТабличнойЧасти.Курс = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
		);
		
		СтрокаТабличнойЧасти.СтатьяДДС = СтруктураДанные.СтатьяДДСПоУмолчанию;
	Иначе
		СтрокаТабличнойЧасти.СтатьяДДС = ПолучитьСтатьюДДСПоУмолчаниюДляСтрокиРасшифровки(СтрокаТабличнойЧасти.Договор,
			СтрокаТабличнойЧасти.ДокументПланирования,
			СтрокаТабличнойЧасти.СтатьяДДС);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
		СтрокаТабличнойЧасти.СуммаПлатежа,
		Курс,
		СтрокаТабличнойЧасти.Курс,
		Кратность,
		СтрокаТабличнойЧасти.Кратность
	);
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

#Область ОбновитьКурсыРасчетов

// Процедура выполняет обновление курса расчётов в ТЧ РасшифровкаПлатежа и пересчет суммы расчетов при изменении даты документа.
//
&НаСервере
Процедура ОбновитьКурсыРасчетовРасшифровкиПлатежаНаСервере()
	
	Для Каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
			
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
				Объект.Дата,
				СтрокаТабличнойЧасти.Договор
			);
			СтрокаТабличнойЧасти.Курс = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Курс
			);
			СтрокаТабличнойЧасти.Кратность = ?(
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
				1,
				СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
			);
		КонецЕсли;
		
		СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
	КонецЦикла;
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		ЗаполнитьСуммовыеПоказателиТаблицыДолговПоРасшифровкеПлатежа();
	ИначеЕсли Объект.ВидОперации = ВидОперацииПоставщику Тогда
		ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

// Процедура выполняет обновление курса расчётов в ТЧ РасшифровкаПлатежа и пересчет суммы расчетов при изменении даты документа.
// Вызывается при положительном ответе пользователя на вопрос о необходимости обновления курса и пересчета.
//
&НаКлиенте
Процедура ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы(КнопкаРезультат, ДополнительныеПараметры) Экспорт

	Если КнопкаРезультат = КодВозвратаДиалога.Да Тогда
		ОбновитьКурсыРасчетовРасшифровкиПлатежаНаСервере();
	КонецЕсли;

КонецПроцедуры // ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммыЗавершение()

&НаКлиенте
Процедура ДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Подотчетник.Пустая() Тогда
		УправлениеНебольшойФирмойКлиент.СообщитьОбОшибке(Объект,НСтр("ru = 'Сначала нужно выбрать подотчетника'"),,,"Объект.Подотчетник");
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

// Процедура выполняет действия при начале выбора договора контрагента.
//
&НаКлиенте
Процедура ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка, Знач ТекущийДоговор = Неопределено)
	
	Если ТекущийДоговор = Неопределено Тогда
		Если Элементы.РасшифровкаПлатежа.ТекущиеДанные = Неопределено Тогда
			Возврат;
		Иначе
			ТекущийДоговор = Элементы.РасшифровкаПлатежа.ТекущиеДанные.Договор;
		КонецЕсли;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, ТекущийДоговор, Объект.ВидОперации);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

// Процедура заполняет строку ТЧ РасшифровкаПлатежа данными документа расчетов.
//
&НаКлиенте
Процедура ОбработатьВыборДокументаРасчетов(ДанныеДокумента)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	Если ТипЗнч(ДанныеДокумента) = Тип("Структура") Тогда
		
		СтрокаТабличнойЧасти.Документ = ДанныеДокумента.Документ;
		СтрокаТабличнойЧасти.Заказ = ДанныеДокумента.Заказ;
		СтрокаТабличнойЧасти.СчетНаОплату = ДанныеДокумента.СчетНаОплату;
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтрокаТабличнойЧасти.Договор = ДанныеДокумента.Договор;
			ОбработатьИзменениеДоговораКонтрагента(СтрокаТабличнойЧасти);
		КонецЕсли;
		
		ВыполнитьДействияПриИзмененииДокументаРасчетов();
		
		Модифицированность = Истина;
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьВыборДокументаРасчетов()

// Процедура определяет признак аванса в зависимости от типа документа расчетов.
//
&НаКлиенте
Процедура ВыполнитьДействияПриИзмененииДокументаРасчетов()
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю") Тогда
		
		Если ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ПоступлениеВКассу")
			ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ПоступлениеНаСчет")
			ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ОперацияПоПлатежнымКартам") Тогда
			
			СтрокаТабличнойЧасти.ПризнакАванса = Истина;
			
		Иначе
			
			СтрокаТабличнойЧасти.ПризнакАванса = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьДействияПриИзмененииДокументаРасчетов()

// Процедура заполняет расшифровку платежа.
//
&НаСервере
Процедура ЗаполнитьРасшифровкуПлатежа(ТекущийОбъект = Неопределено)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.ЗаполнитьРасшифровкуПлатежа();
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
КонецПроцедуры // ЗаполнитьРасшифровкуПлатежа()

&НаКлиенте
Процедура НалогообложениеНДСПриИзменении(Элемент)
	
	ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура СтатьяПриИзменении(Элемент)
	Если ЗначениеЗаполнено(Объект.Статья) Тогда
		Объект.УчитыватьВНУ = СтатьяУчитываетсяВНУ(Объект.Статья, Объект.Организация, Объект.Дата);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетрыСКонтрагентом(Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокВзаиморасчетовОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетВзаиморасчетрыСКонтрагентом(Объект.Контрагент);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствНажатие(Элемент)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.БанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ОстатокДенежныхСредствОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	РаботаСФормойДокументаКлиент.ОткрытьОтчетДенежныеСредства(Объект.БанковскийСчет);
	
КонецПроцедуры

&НаКлиенте
Процедура ВариантЗаполненияРасшифровкиПриИзменении(Элемент)
	
	НастроитьЭлементыРаспределенияДолговНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаАвтоПодбор(Элемент, Текст, ДанныеВыбора, ПараметрыПолученияДанных, Ожидание, СтандартнаяОбработка)
	
	Элемент.СписокВыбора.Очистить();
	Если Объект.ВидОперации = ВидОперацииПоставщику Тогда
		
		СуммаИтог = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		
		Если СуммаИтог <> 0 И СуммаИтог <> Объект.СуммаДокумента
			И Объект.ВариантЗаполненияРасшифровки <> ВариантЗаполненияРасшифровкиАвтоматически
			Тогда
			НадписьВСкобках = ?(Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную, 
				НСтр("ru = 'разнесено вручную'"), 
				НСтр("ru = 'зачтено + аванс'"));
			
			Элемент.СписокВыбора.Добавить(СуммаИтог, ""+СуммаИтог+" ("+НадписьВСкобках+")");
		КонецЕсли;
		
		Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
			СуммаИтогЗачесть = ТаблицаДолгов.Итог("ДолгВалПлатежа");
			Если СуммаИтогЗачесть <> 0 И СуммаИтогЗачесть <> Объект.СуммаДокумента Тогда
				Элемент.СписокВыбора.Добавить(СуммаИтогЗачесть, ""+СуммаИтогЗачесть+НСтр("ru = ' (текущий долг)'"));
			КонецЕсли;
		ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
			СуммаИтогЗачесть = Объект.ДоговорыАвтоЗачетаДолгов.Итог("СуммаПлатежа");
			Если СуммаИтогЗачесть <> 0 И СуммаИтогЗачесть <> Объект.СуммаДокумента Тогда
				Элемент.СписокВыбора.Добавить(СуммаИтогЗачесть, ""+СуммаИтогЗачесть+НСтр("ru = ' (разнесено по договорам)'"));
			КонецЕсли;
		КонецЕсли;
		
		Если ОстатокВзаиморасчетов < 0 И (-ОстатокВзаиморасчетов) <> Объект.СуммаДокумента Тогда
			Элемент.СписокВыбора.Добавить(-ОстатокВзаиморасчетов, ""+(-ОстатокВзаиморасчетов)+НСтр("ru = ' (мы должны)'"));
		КонецЕсли;
		
		Если ОстатокДенежныхСредств > 0 И (ОстатокДенежныхСредств) <> Объект.СуммаДокумента Тогда
			Элемент.СписокВыбора.Добавить(ОстатокДенежныхСредств, ""+(ОстатокДенежныхСредств)+НСтр("ru = ' (остаток на счете)'"));
		КонецЕсли;
		
		Если Элемент.СписокВыбора.Количество() = 0 Тогда
			Элемент.СписокВыбора.Добавить(Объект.СуммаДокумента, НСтр("ru = 'Нет данных для заполнения'"));
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокаПоискаПоДоговоруПриИзменении(Элемент)
	
	Если Элементы.СнятьОтборПоДоговору.Доступность Тогда
		
		Для каждого ЭлементОтбораСтрок Из Элементы.ТаблицаДолгов.ОтборСтрок Цикл
			ВыполнитьПоискПоДоговоруНаСервере(ЭлементОтбораСтрок.Ключ);
			Прервать;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
	
КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.НазначениеПлатежа", НСтр("ru='Назначение платежа'"));
	СтатистикаИспользованияФормКлиент.ПриИнтерактивномДействии(ЭтотОбъект, Элемент, "НачалоВыбора");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыРасшифровкаПлатежа

// Процедура - обработчик события ПередУдалением табличной части РасшифровкаПлатежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() <= 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаПередУдалением()

// Процедура - обработчик события ПослеУдаления табличной части РасшифровкаПлатежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПослеУдаления(Элемент)
	
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаДоговор.
// Устанавливает курс и кратность валюты договора.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДоговорПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	ОбработатьИзменениеДоговораКонтрагента(СтрокаТабличнойЧасти);
	
КонецПроцедуры // РасшифровкаПлатежаДоговорПриИзменении()

// Процедура - обработчик события НачалоВыбора поля ввода РасшифровкаПлатежаДоговор.
// Устанавливает курс и кратность валюты договора.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаВидРасчетов.
// Очищает реквизит документ если тип расчетов - "Аванс".
//
&НаКлиенте
Процедура РасшифровкаПлатежаПризнакАвансаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику") Тогда
		Если СтрокаТабличнойЧасти.ПризнакАванса Тогда
			СтрокаТабличнойЧасти.Документ = Неопределено;
		Иначе
			СтрокаТабличнойЧасти.ДокументПланирования = Неопределено;
		КонецЕсли;
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю")
		И ВестиРасчетыПоДокументам Тогда
		Если ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ПоступлениеВКассу")
		 ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ПоступлениеНаСчет")
		 ИЛИ ТипЗнч(СтрокаТабличнойЧасти.Документ) = Тип("ДокументСсылка.ОперацияПоПлатежнымКартам") Тогда
			СтрокаТабличнойЧасти.ПризнакАванса = Истина;
			ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данного типа документа расчетов признак аванса всегда установлен!'"));
		ИначеЕсли ТипЗнч(СтрокаТабличнойЧасти.Документ) <> Тип("ДокументСсылка.Взаимозачет") Тогда
			СтрокаТабличнойЧасти.ПризнакАванса = Ложь;
			ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Для данного типа документа расчетов нельзя установить признак аванса!'"));
		КонецЕсли;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса , СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры // РасшифровкаПлатежаПризнакАвансаПриИзменении()

// Процедура - обработчик события НачалоВыбора поля ввода РасшифровкаПлатежаДокумент.
// Передает в параметры текущее значение реквизита.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДокументНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	Если СтрокаТабличнойЧасти.ПризнакАванса
		И Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику") Тогда
		
		ПоказатьПредупреждение(, НСтр("ru = 'Для вида расчета с признаком ""Аванс"" документом расчетов будет текущий!'"));
		
	Иначе
		
		ЭтоРасчетыСПокупателями = Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю");
		
		СтруктураОтбор = Новый Структура();
		СтруктураОтбор.Вставить("Контрагент", Объект.Контрагент);
		
		Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
			СтруктураОтбор.Вставить("Договор", СтрокаТабличнойЧасти.Договор);
		КонецЕсли;
		Если НЕ УчетПоКомпании Тогда
			СтруктураОтбор.Вставить("Организация", Объект.Организация);
		КонецЕсли;
		
		СтруктураПараметры = Новый Структура("Отбор, ЭтоРасчетыСПокупателями, ТипДокумента",
			СтруктураОтбор,
			ЭтоРасчетыСПокупателями,
			ТипЗнч(Объект.Ссылка)
		);
		
		ОткрытьФорму("ОбщаяФорма.ФормаВыборДокументаРасчетов", СтруктураПараметры, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаДокументНачалоВыбора()

// Процедура - обработчик события ОбработкаВыбора поля ввода РасшифровкаПлатежаДокумент.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДокументОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ОбработатьВыборДокументаРасчетов(ВыбранноеЗначение);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля вв РасшифровкаПлатежаСуммаРасчетов.
// Расчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСуммаРасчетовПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаСуммаРасчетовПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаКурс.
// Расчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаКурсПриИзменении(Элемент)
	
	РасшифровкаПлатежаКурсПриИзмененииФрагмент(Элементы.РасшифровкаПлатежа.ТекущиеДанные);
	
КонецПроцедуры // РасшифровкаПлатежаКурсПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаКурс.
// Расчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаКурсПриИзмененииФрагмент(ТекущиеДанные)
	
	Если ТекущиеДанные.СуммаПлатежа = 0 И ТекущиеДанные.СуммаРасчетов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуПлатежаНаКлиенте(ТекущиеДанные, "Курс");
		
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаКурсПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаКратность.
// Расчитывает сумму платежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаКратностьПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	Если ТекущиеДанные.СуммаПлатежа = 0 И ТекущиеДанные.СуммаРасчетов = 0 Тогда
		Возврат;
	КонецЕсли;
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежа.ТекущиеДанные, "Кратность");
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
КонецПроцедуры // РасшифровкаПлатежаКратностьПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаСуммаПлатежа.
// Расчитывает курс и кратность валюты расчетов и сумму НДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаКлиенте(СтрокаТабличнойЧасти);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
		
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры // РасшифровкаПлатежаСуммаПлатежаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаСтавкаНДС.
// Расчитывает сумму НДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры // РасшифровкаПлатежаСтавкаНДСПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаДокумент.
//
&НаКлиенте
Процедура РасшифровкаПлатежаДокументПриИзменении(Элемент)
	
	ВыполнитьДействияПриИзмененииДокументаРасчетов();
	
КонецПроцедуры // РасшифровкаПлатежаДокументПриИзменении() 

// Процедура - обработчик события ПриИзменении поля ввода ВыплатаЗаработнойПлатыВедомость.
//
&НаКлиенте
Процедура ВыплатаЗаработнойПлатыВедомостьПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.ВыплатаЗаработнойПлаты.ТекущиеДанные;
	СтрокаТабличнойЧасти.СуммаПлатежа = ПолучитьДанныеВыплатаЗаработнойПлатыВедомостьПриИзменении(СтрокаТабличнойЧасти.Ведомость);
	
КонецПроцедуры // ВыплатаЗаработнойПлатыВедомостьПриИзменении()

&НаКлиенте
Процедура РасшифровкаПлатежаСчетНаОплатуПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ВидОперацииПоставщику Тогда
		РасшифровкаПлатежаСчетНаОплатуПриИзмененииФрагмент(Элементы.РасшифровкаПлатежа.ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаСчетНаОплатуПриИзмененииФрагмент(ТекущиеДанныеИдентификатор)
	
	ТекущиеДанные = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	Если Объект.Контрагент.ВестиРасчетыПоЗаказам Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.СчетНаОплату) И 
			НЕ ЗначениеЗаполнено(ТекущиеДанные.Заказ) И
			ТипЗнч(ТекущиеДанные.СчетНаОплату) = Тип("ДокументСсылка.СчетНаОплатуПоставщика") И
			ТипЗнч(ТекущиеДанные.СчетНаОплату.ДокументОснование) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			ТекущиеДанные.Заказ = ТекущиеДанные.СчетНаОплату.ДокументОснование;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаЗаказПриИзменении(Элемент)
	
	Если Объект.ВидОперации = ВидОперацииПоставщику Тогда
		РасшифровкаПлатежаЗаказПриИзмененииФрагмент(Элементы.РасшифровкаПлатежа.ТекущиеДанные.ПолучитьИдентификатор());
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура РасшифровкаПлатежаЗаказПриИзмененииФрагмент(ТекущиеДанныеИдентификатор)
	
	ТекущиеДанные = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ТекущиеДанныеИдентификатор);
	Если Объект.Контрагент.ВестиУчетОплатыПоСчетам Тогда
		Если ЗначениеЗаполнено(ТекущиеДанные.Заказ) И 
			НЕ ЗначениеЗаполнено(ТекущиеДанные.СчетНаОплату) И
			ТипЗнч(ТекущиеДанные.Заказ) = Тип("ДокументСсылка.ЗаказПоставщику") Тогда
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ РАЗРЕШЕННЫЕ
			|	СчетНаОплатуПоставщика.Ссылка КАК СчетНаОплату
			|ИЗ
			|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика
			|ГДЕ
			|	СчетНаОплатуПоставщика.ДокументОснование = &ДокументОснование";
			
			Запрос.УстановитьПараметр("ДокументОснование", ТекущиеДанные.Заказ);
			Выборка = Запрос.Выполнить().Выбрать();
			
			Если Выборка.Количество() = 1
				И Выборка.Следующий() Тогда
				
				ТекущиеДанные.СчетНаОплату = Выборка.СчетНаОплату;
				
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаСуммаПлатежа.
// Расчитывает курс и кратность валюты расчетов и сумму НДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаКлиенте(СтрокаТабличнойЧасти, ИмяКолонки = "СуммаПлатежа")
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор
		);
		
	Если СтрокаТабличнойЧасти.СуммаПлатежа = 0 Тогда
		СтрокаТабличнойЧасти.СуммаРасчетов = 0;
		СтрокаТабличнойЧасти.Курс = СтруктураДанные.ДоговорВалютаКурсКратность.Курс;
	ИначеЕсли Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаРасчетов Тогда
		СтрокаТабличнойЧасти.СуммаРасчетов = СтрокаТабличнойЧасти.СуммаПлатежа;
	ИначеЕсли СтрокаТабличнойЧасти.СуммаРасчетов = 0 Тогда
		СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
	Иначе
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс,
			СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
		);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	
КонецПроцедуры // РасшифровкаПлатежаСуммаПлатежаПриИзменении()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаСуммаПлатежа.
// Расчитывает курс и кратность валюты расчетов и сумму НДС.
//
&НаСервере
Процедура РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(СтрокаТабличнойЧасти, ДолгВалДоговора = 0, ДолгВалПлатежа = 0)
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор
		);
		
	Если СтрокаТабличнойЧасти.СуммаПлатежа = 0 Тогда
		СтрокаТабличнойЧасти.СуммаРасчетов = 0;
		СтрокаТабличнойЧасти.Курс = СтруктураДанные.ДоговорВалютаКурсКратность.Курс;
	ИначеЕсли Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаРасчетов Тогда
		СтрокаТабличнойЧасти.СуммаРасчетов = СтрокаТабличнойЧасти.СуммаПлатежа;
	ИначеЕсли СтрокаТабличнойЧасти.СуммаРасчетов = 0 Тогда
		Если ДолгВалДоговора <> 0 И ДолгВалПлатежа = СтрокаТабличнойЧасти.СуммаПлатежа Тогда
			СтрокаТабличнойЧасти.СуммаРасчетов = ДолгВалДоговора;
		Иначе
			СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаПлатежа,
				Курс,
				СтрокаТабличнойЧасти.Курс,
				Кратность,
				СтрокаТабличнойЧасти.Кратность
			);
		КонецЕсли;
	Иначе
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс,
			СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
		);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	
КонецПроцедуры // РасшифровкаПлатежаСуммаПлатежаПриИзменении()

&НаКлиенте
Процедура РасчетыСПодотчетникомРасшифровкаПлатежаСуммаПлатежаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДокументыПланированияРасшифровкаПлатежа.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.СуммаРасчетов = ТекущаяСтрока.СуммаПлатежа;
	КонецЕсли;
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаДокументПланированияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументПланирования) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасчетыСПодотчетникомРасшифровкаПлатежаСчетНаОплатуПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.ДокументыПланированияРасшифровкаПлатежа.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументПланирования) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамДокументПланированияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументПланирования) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыДокументПланированияПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеДокументаПланированияПриИзменении(ТекущиеДанные.ДокументПланирования);
	
	Если ЗначениеЗаполнено(ТекущиеДанные.ДокументПланирования) Тогда
		ТекущиеДанные.СтатьяДДС = СтруктураДанных.СтатьяДДС;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыДолгов

&НаКлиенте
Процедура ТаблицаДолговВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "ТаблицаДолговДокумент" Тогда
		ТекущиеДанные = Элементы.ТаблицаДолгов.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.Документ) Тогда
			ПараметрыОткрытия = Новый Структура("Ключ", ТекущиеДанные.Документ);
			ОткрытьФорму("Документ."+ПолучитьИмяТипаДокумента(ТекущиеДанные.Документ)+".ФормаОбъекта", ПараметрыОткрытия);
		КонецЕсли;
	ИначеЕсли Поле.Имя = "ТаблицаДолговЗаказ" ИЛИ Поле.Имя = "ТаблицаДолговЗаказКогдаНетУчетаПоДокументам" Тогда
		ТекущиеДанные = Элементы.ТаблицаДолгов.ТекущиеДанные;
		Если ЗначениеЗаполнено(ТекущиеДанные.Заказ) Тогда
			ПараметрыОткрытия = Новый Структура("Ключ", ТекущиеДанные.Заказ);
			Если ТипЗнч(ТекущиеДанные.Заказ) = Тип("ДокументСсылка.ЗаказПокупателя") Тогда
				ОткрытьФорму("Документ.ЗаказПокупателя.ФормаОбъекта", ПараметрыОткрытия);
			Иначе
				ОткрытьФорму("Документ.ЗаказПоставщику.ФормаОбъекта", ПараметрыОткрытия);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговВыбранПриИзменении(Элемент)
	
	ТаблицаДолговВыбранПриИзмененииЗавершение(Элементы.ТаблицаДолгов.ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

&НаСервере
Процедура ТаблицаДолговВыбранПриИзмененииЗавершение(ИдентификаторТаблицыДолгов)
	
	ТекущаяСтрокаДолгов = ТаблицаДолгов.НайтиПоИдентификатору(ИдентификаторТаблицыДолгов);
	
	Если ТекущаяСтрокаДолгов.Выбран Тогда // Возможно, требуется добавить новую строку.
		ПриИзмененииВТаблицеДолгов("Выбран", ТекущаяСтрокаДолгов.ПолучитьИдентификатор());
	Иначе
		ТекущаяСтрокаРасшифровки = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ТекущаяСтрокаДолгов.ИдентификаторСтрокиРасшифровкиПлатежа);
		Если ТекущаяСтрокаРасшифровки <> Неопределено Тогда
			ТекущаяСтрокаРасшифровки.СуммаРасчетов = 0;
			ТекущаяСтрокаРасшифровки.СуммаПлатежа = 0;
			ТекущаяСтрокаРасшифровки.СуммаНДС = 0;
		КонецЕсли;
		
		ТекущаяСтрокаДолгов.СуммаРасчетов = 0;
		ТекущаяСтрокаДолгов.Зачесть = 0;
		ТекущаяСтрокаДолгов.ЗачестьВыбрано = 0;
		
		ПересчитатьИтогиПриИзмененииАвансаНаСервере(Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговЗачестьПриИзменении(Элемент)
	
	ТекущаяСтрокаДолгов = Элементы.ТаблицаДолгов.ТекущиеДанные;
	
	ТекущаяСтрокаДолгов.Выбран = Истина;
	ПриИзмененииВТаблицеДолгов("Зачесть", ТекущаяСтрокаДолгов.ПолучитьИдентификатор(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговСуммаРасчетовПриИзменении(Элемент)
	
	ТекущаяСтрокаДолгов = Элементы.ТаблицаДолгов.ТекущиеДанные;
	
	ТекущаяСтрокаДолгов.Выбран = Истина;
	ПриИзмененииВТаблицеДолгов("СуммаРасчетов", ТекущаяСтрокаДолгов.ПолучитьИдентификатор(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговКурсПриИзменении(Элемент)
	
	ТаблицаДолговКурсПриИзмененииФрагмент(Элементы.ТаблицаДолгов.ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговКурсПриИзмененииФрагмент(ТекущаяСтрокаДолгов)
	
	ТекущаяСтрокаДолгов.Выбран = Истина;
	ПриИзмененииВТаблицеДолгов("Курс", ТекущаяСтрокаДолгов.ПолучитьИдентификатор(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговКратностьПриИзменении(Элемент)
	
	ТекущаяСтрокаДолгов = Элементы.ТаблицаДолгов.ТекущиеДанные;
	
	ТекущаяСтрокаДолгов.Выбран = Истина;
	ПриИзмененииВТаблицеДолгов("Кратность", ТекущаяСтрокаДолгов.ПолучитьИдентификатор(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговСтавкаНДСПриИзменении(Элемент)
	
	ТекущаяСтрокаДолгов = Элементы.ТаблицаДолгов.ТекущиеДанные;
	
	ТекущаяСтрокаДолгов.Выбран = Истина;
	ПриИзмененииВТаблицеДолгов("СтавкаНДС", ТекущаяСтрокаДолгов.ПолучитьИдентификатор(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаДолговСчетПриИзменении(Элемент)
	
	ТекущаяСтрокаДолгов = Элементы.ТаблицаДолгов.ТекущиеДанные;
	ТекущаяСтрокаДолгов.Выбран = Истина;
	ПриИзмененииВТаблицеДолгов("СчетНаОплату", ТекущаяСтрокаДолгов.ПолучитьИдентификатор(), Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ВидНалогаПриИзменении(Элемент)
	Если Объект.ВидНалога = ПредопределенноеЗначение("Справочник.ВидыНалогов.Патент") Тогда
		Объект.УчитыватьВНУ = Ложь;
	КонецЕсли;
	ЗаполнитьНазначениеПлатежаНаСервере();
КонецПроцедуры

#Область ВспомогательныеПроцедурыПересчета

&НаСервере
Процедура ПересчитатьСуммуРасчетовВСпискеДолгов(ТекущаяСтрокаРасшифровки, ТекущаяСтрокаДолгов, ИмяКолонки, ДолгВалДоговора = 0, ДолгВалПлатежа = 0)
	
	ТекущаяСтрокаРасшифровки.СуммаПлатежа = ТекущаяСтрокаДолгов.Зачесть;
	
	РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(ТекущаяСтрокаРасшифровки, ДолгВалДоговора, ДолгВалПлатежа);
	
	ТекущаяСтрокаДолгов.СуммаРасчетов = ТекущаяСтрокаРасшифровки.СуммаРасчетов;
	ТекущаяСтрокаДолгов.Курс = ТекущаяСтрокаРасшифровки.Курс;
	// Обновим кратность на случай, если пользователь обнулил курс и кратность, когда сумма расчетов и сумма платежа были = 0.
	ТекущаяСтрокаДолгов.Кратность = ТекущаяСтрокаРасшифровки.Кратность;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьСуммуПлатежаВСпискеДолгов(ТекущаяСтрокаРасшифровки, ТекущаяСтрокаДолгов, ИмяКолонки)
	
	ТекущаяСтрокаРасшифровки.СуммаРасчетов = ТекущаяСтрокаДолгов.СуммаРасчетов;
	
	РассчитатьСуммуПлатежаНаСервере(ТекущаяСтрокаРасшифровки);
	
	ТекущаяСтрокаДолгов.Зачесть = ТекущаяСтрокаРасшифровки.СуммаПлатежа;
	ТекущаяСтрокаДолгов.ЗачестьВыбрано = ?(ТекущаяСтрокаДолгов.Выбран, ТекущаяСтрокаРасшифровки.СуммаПлатежа, 0);
	ТекущаяСтрокаДолгов.Курс = ТекущаяСтрокаРасшифровки.Курс;
	// Обновим кратность на случай, если пользователь обнулил курс и кратность, когда сумма расчетов и сумма платежа были = 0.
	ТекущаяСтрокаДолгов.Кратность = ТекущаяСтрокаРасшифровки.Кратность;
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик события Выполнить команды Подбор.
// Открывает форму подбора долгообразующих документов.
//
&НаКлиенте
Процедура Подбор(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.Контрагент) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале контрагента!'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
	   И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале банковский счет!'"));
		Возврат;
	КонецЕсли;
	
	АдресРасшифровкаПлатежаВХранилище = ПоместитьРасшифровкаПлатежаВХранилище();
	
	ПараметрыПодбора = Новый Структура(
		"АдресРасшифровкаПлатежаВХранилище,
		|Компания,
		|Дата,
		|Контрагент,
		|Ссылка,
		|ВидОперации,
		|ВалютаДенежныхСредств,
		|СуммаДокумента",
		АдресРасшифровкаПлатежаВХранилище,
		Компания,
		Объект.Дата,
		Объект.Контрагент,
		Объект.Ссылка,
		Объект.ВидОперации,
		Объект.ВалютаДенежныхСредств,
		Объект.СуммаДокумента
	);
	
	Результат = Неопределено;

	
	ОткрытьФорму("ОбщаяФорма.ФормаПодбораДолговПоставщикам", ПараметрыПодбора,,,,, Новый ОписаниеОповещения("ПодборЗавершение", ЭтотОбъект, Новый Структура("АдресРасшифровкаПлатежаВХранилище", АдресРасшифровкаПлатежаВХранилище)));
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	АдресРасшифровкаПлатежаВХранилище = ДополнительныеПараметры.АдресРасшифровкаПлатежаВХранилище;
	
	
	Результат = Результат1;
	Если Результат = КодВозвратаДиалога.OK Тогда
		
		ПолучитьРасшифровкаПлатежаИзХранилища(АдресРасшифровкаПлатежаВХранилище);
		ИмяТабличнойЧасти = "РасшифровкаПлатежа";
		Для каждого СтрокаРасшифровкаПлатежа Из Объект.РасшифровкаПлатежа Цикл
			Если НЕ ЗначениеЗаполнено(СтрокаРасшифровкаПлатежа.СтавкаНДС) Тогда
				СтрокаРасшифровкаПлатежа.СтавкаНДС = СтавкаНДСПоУмолчанию;
			КонецЕсли;
			РассчитатьСуммуПлатежаНаКлиенте(СтрокаРасшифровкаПлатежа);
		КонецЦикла;
		
		УстановитьТекущуюСтраницу();
		
		Если Объект.РасшифровкаПлатежа.Количество() = 1 ИЛИ ИсходнаяСуммаРавнаНулю Тогда
			Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // Подбор()

// Процедура вызывается при нажатии кнопки "ЗаполнитьПоОснованию" командной панели
// табличного поля.
//
&НаКлиенте
Процедура ЗаполнитьПоОснованию(Команда)
	
	Если НЕ ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		ПоказатьПредупреждение(Неопределено, Нстр("ru='Не выбран документ основание!'"));
		Возврат;
	КонецЕсли;
	
	Если (ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеДСПлан")
		ИЛИ ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.РасходДСПлан"))
		И НЕ ДокументУтвержден(Объект.ДокументОснование) Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя ввести перемещение денег на основании неутвержденного планового документа!'");
	КонецЕсли;
	
	Ответ = Неопределено;
	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьПоОснованиюЗавершение", ЭтотОбъект), НСтр("ru = 'Документ будет полностью перезаполнен по ""Документу-основанию""! Продолжить выполнение операции?'"), РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПоОснованиюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	Если Ответ = КодВозвратаДиалога.Да Тогда
		
		Объект.БанковскийСчет = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		
		Объект.РасшифровкаПлатежа.Очистить();
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		
		ЗаполнитьПоДокументу(Объект.ДокументОснование);
		
		Если Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Зарплата")
			И Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
			Объект.РасшифровкаПлатежа.Добавить();
			Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КонецЕсли;
		
		ВидОперации = Объект.ВидОперации;
		ВалютаДенежныхСредств = Объект.ВалютаДенежныхСредств;
		ДатаДокумента = Объект.Дата;
		
		УстановитьТекущуюСтраницу();
		
		ЗаполнитьПоОснованиюЗавершениеНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьПоОснованию()

&НаСервере
Процедура ЗаполнитьПоОснованиюЗавершениеНаСервере()
	
	ВидОперацииПриИзмененииНаСервере(Ложь);
	
	ЗаполнитьДоговор();
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки <> ВариантЗаполненияРасшифровкиВручную Тогда
		Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную;
		НастроитьЭлементыРаспределенияДолговНаСервере();
	КонецЕсли;
	
	Если НЕ (ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.РасходДСПлан")
		ИЛИ ТипЗнч(Объект.ДокументОснование) = Тип("ДокументСсылка.ПеремещениеДСПлан")) Тогда
		
		УстановитьСтатьюДДС();
		Если НЕ Объект.Контрагент.Пустая() Тогда
			УстановитьСтатьиДДСВРасшифровкеПлатежа();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик команды ЗаполнитьРасшифровку.
//
&НаКлиенте
Процедура ЗаполнитьРасшифровку(Команда)
	
	Если Объект.СуммаДокумента = 0 Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru='Укажите вначале сумму документа.'"));
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
	   И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале банковский счет!'"));
		Возврат;
	КонецЕсли;
	
	Ответ = Неопределено;

	
	ПоказатьВопрос(Новый ОписаниеОповещения("ЗаполнитьРасшифровкуЗавершение", ЭтотОбъект), 
		НСтр("ru='Расшифровка будет полностью перезаполнена. Продолжить?'"),
		РежимДиалогаВопрос.ДаНет
	);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьРасшифровкуЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Ответ = Результат;
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Объект.РасшифровкаПлатежа.Очистить();
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику") Тогда
		
		ЗаполнитьРасшифровкуПлатежа();
		
	КонецЕсли;
	
	УстановитьТекущуюСтраницу();
	
КонецПроцедуры // ЗаполнитьРасшифровку()

&НаКлиенте
Процедура ОбновитьКурсВТекущейСтрокеРасшифровки(Команда)
	
	ТекущиеДанные = Элементы.РасшифровкаПлатежа.ТекущиеДанные;
	СтруктураДанных = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(Объект.Дата, ТекущиеДанные.Договор);
	ТекущиеДанные.Курс = СтруктураДанных.ДоговорВалютаКурсКратность.Курс;
	ТекущиеДанные.Кратность = СтруктураДанных.ДоговорВалютаКурсКратность.Кратность;
	РасшифровкаПлатежаКурсПриИзмененииФрагмент(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗачестьДолгиАвтоматически(Команда)
	
	ЗачестьДолгиАвтоматическиФрагмент();
	
КонецПроцедуры

&НаСервере
Процедура ЗачестьДолгиАвтоматическиФрагмент()
	
	Для Каждого ТекущаяСтрока Из ТаблицаДолгов Цикл
		Если ТекущаяСтрока.Выбран Тогда
			ТекущаяСтрока.Выбран = Ложь;
			ТаблицаДолговВыбранПриИзмененииЗавершение(ТекущаяСтрока.ПолучитьИдентификатор());
		КонецЕсли;
	КонецЦикла;
	// Отметка у всех строк снята, а значит аванс = сумме документа!
	// Эта строка нужна для случая, когда в строке не было отмечено ни одной строки.
	// Если отмечена хоть одна строка, то сумма аванса будет обновлена при снятии отметки.
	СуммаПлатежаАванс = Объект.СуммаДокумента;
	
	НужноРаспределить = Объект.СуммаДокумента;
	Для Каждого ТекущаяСтрока Из ТаблицаДолгов Цикл
		
		Если ТекущаяСтрока.ДолгВалПлатежа <= 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Если НужноРаспределить <= 0 Тогда
			Прервать;
		Иначе
			ТекущаяСтрока.Зачесть = Мин(НужноРаспределить, ТекущаяСтрока.ДолгВалПлатежа);
			ТекущаяСтрока.Выбран = Истина;
			ТекущаяСтрока.ЗачестьВыбрано = ТекущаяСтрока.Зачесть;
			
			ТаблицаДолговВыбранПриИзмененииЗавершение(ТекущаяСтрока.ПолучитьИдентификатор());
			
			НужноРаспределить = НужноРаспределить - ТекущаяСтрока.Зачесть;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоДоговору(Команда)
	
	СписокПодменю = Новый СписокЗначений;
	СписокПодменю.Добавить("Договор", "Фильтр по договору");
	СписокПодменю.Добавить("ДолгВалДоговора", "Фильтр по сумме долга");
	СписокПодменю.Добавить("Документ", "Фильтр по документу");
	СписокПодменю.Добавить("Заказ", "Фильтр по заказу");
	
	ПоказатьВыборИзМеню(Новый ОписаниеОповещения("УстановитьОтборПоДоговоруЗавершение", ЭтотОбъект), СписокПодменю, Элементы.УстановитьОтборПоДоговору);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьОтборПоДоговоруЗавершение(РезультатЗавершения, ПараметрыЗавершения) Экспорт
	
	Если РезультатЗавершения <> Неопределено Тогда
		ВыполнитьПоискПоДоговоруНаСервере(РезультатЗавершения.Значение);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьОтборПоДоговору(Команда)
	СнятьОтборПоДоговоруНаСервере();
КонецПроцедуры

&НаСервере
Процедура СнятьОтборПоДоговоруНаСервере()
	
	Элементы.ТаблицаДолгов.ОтборСтрок = Неопределено;
	Элементы.СнятьОтборПоДоговору.Доступность = Ложь;
	Элементы.УстановитьОтборПоДоговору.Доступность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТаблицаДолгов(Команда)
	
	Если Объект.Контрагент.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале контрагента!'"));
		Возврат;
	КонецЕсли;
	Если Объект.Организация.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Укажите вначале организацию!'"));
		Возврат;
	КонецЕсли;
	
	ЗаполнитьДолги(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ПолучениеДанныхССервера

// Получает набор данных с сервера для процедуры ВалютаДенежныхСредствПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеБанковскийСчетПриИзменении(Дата, БанковскийСчет, СчетКонтрагента)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", БанковскийСчет.ВалютаДенежныхСредств)
		)
	);
	
	СтруктураДанные.Вставить(
		"ВалютаДенежныхСредств",
		БанковскийСчет.ВалютаДенежныхСредств
	);
	
	СтруктураДанные.Вставить(
		"СчетКонтрагента",
		?(ЗначениеЗаполнено(СчетКонтрагента) И СчетКонтрагента.ВалютаДенежныхСредств = БанковскийСчет.ВалютаДенежныхСредств, СчетКонтрагента, Неопределено)
	);
	
	ОбновитьВидимостьИОстаткиДС(Объект.БанковскийСчет);
	
	УстановитьЗаголовкиКолонокТабличныхЧастей(СтруктураДанные.ВалютаДенежныхСредств);
	ОбновитьДолгиВПомощникеЗачетаДолгов(СтруктураДанные);
	
	УстановитьВидимостьКурсаВалюты(БанковскийСчет.ВалютаДенежныхСредств);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеБанковскийСчетПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(Дата, Договор, ДокументПланирования = Неопределено, СтатьяДДС = Неопределено)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"ДоговорВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", Договор.ВалютаРасчетов)
		)
	);
	СтруктураДанные.Вставить("ВалютаРасчетов", Договор.ВалютаРасчетов);
	// Статья в заявке на расход (РасходДСПЛан) приоритетнее.
	Если ЗначениеЗаполнено(ДокументПланирования) Тогда
		СтруктураДанные.Вставить("СтатьяДДСПоУмолчанию", СтатьяДДС);
	Иначе
		СтруктураДанные.Вставить("СтатьяДДСПоУмолчанию", Договор.СтатьяДДСПоУмолчанию);
	КонецЕсли;
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьСтатьюДДСПоУмолчаниюДляСтрокиРасшифровки(Договор, ДокументПланирования = Неопределено, СтатьяДДС = Неопределено)
	
	// Статья в заявке на расход (РасходДСПЛан) приоритетнее.
	Если ЗначениеЗаполнено(ДокументПланирования) Тогда
		Возврат СтатьяДДС;
	ИначеЕсли ЗначениеЗаполнено(Договор) Тогда
		Возврат Договор.СтатьяДДСПоУмолчанию;
	Иначе
		Возврат Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции // ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении()

&НаСервере
Процедура УстановитьСтатьиДДСВРасшифровкеПлатежа();
	
	Для Каждого ТекущаяСтрока Из Объект.РасшифровкаПлатежа Цикл
		Если Объект.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам Тогда
			ТекущаяСтрока.СтатьяДДС = ПолучитьСтатьюДДСПоУмолчаниюДляСтрокиРасшифровки(ТекущаяСтрока.Договор,
				ТекущаяСтрока.ДокументПланирования,
				ТекущаяСтрока.СтатьяДДС);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеДатаПриИзменении(ДатаПередИзменением)
	
	РазностьДат = УправлениеНебольшойФирмойСервер.ПроверитьНомерДокумента(Объект.Ссылка, Объект.Дата, ДатаПередИзменением);
	ВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Объект.ВалютаДенежныхСредств));
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"РазностьДат",
		РазностьДат
	);
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		ВалютаКурсКратность
	);
	
	ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС();
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеДатаПриИзменении()

// Получает набор данных с сервера для процедуры ДоговорПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеОрганизацияПриИзменении()
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"Компания",
		УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация)
	);
	
	СтруктураДанные.Вставить(
		"БанковскийСчет",
		?(ЗначениеЗаполнено(Объект.БанковскийСчет) И Объект.БанковскийСчет.Владелец = Объект.Организация, Объект.БанковскийСчет, Объект.Организация.БанковскийСчетПоУмолчанию)
	);
	
	СтруктураДанные.Вставить(
		"ВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Объект.Дата,
			Новый Структура("Валюта", СтруктураДанные.БанковскийСчет.ВалютаДенежныхСредств)
		)
	);
	
	СтруктураДанные.Вставить(
		"ВалютаДенежныхСредств",
		СтруктураДанные.БанковскийСчет.ВалютаДенежныхСредств
	);
	
	СтруктураДанные.Вставить(
		"СчетКонтрагента",
		?(ЗначениеЗаполнено(Объект.СчетКонтрагента) И СтруктураДанные.БанковскийСчет.ВалютаДенежныхСредств = Объект.СчетКонтрагента.ВалютаДенежныхСредств, Объект.СчетКонтрагента, Справочники.БанковскиеСчета.ПустаяСсылка())
	);
	
	ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС();
	
	ОбновитьВидимостьИОстаткиДС(СтруктураДанные.БанковскийСчет);
	
	УстановитьЗаголовкиКолонокТабличныхЧастей(СтруктураДанные.ВалютаДенежныхСредств);
	ОбновитьДолгиВПомощникеЗачетаДолгов(СтруктураДанные);
	
	УстановитьВидимостьКурсаВалюты(СтруктураДанные.БанковскийСчет.ВалютаДенежныхСредств);
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеОрганизацияПриИзменении()

// Получает набор данных с сервера для процедуры ВыплатаЗаработнойПлатыВедомостьПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеВыплатаЗаработнойПлатыВедомостьПриИзменении(Ведомость)
	
	Возврат Ведомость.Сотрудники.Итог("СуммаПлатежа");
	
КонецФункции // ПолучитьДанныеВыплатаЗаработнойПлатыВедомостьПриИзменении()

// Получает набор данных с сервера для процедуры ТаблицаДолговСчетПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСчетаНаОплатуПриИзменении(СчетНаОплату)

	СтруктураВозврат = Новый Структура("Сумма, Валюта", 0, "");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СчетНаОплатуПоставщика.СуммаДокумента,
		|	СчетНаОплатуПоставщика.ВалютаДокумента
		|ИЗ
		|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика
		|ГДЕ
		|	СчетНаОплатуПоставщика.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", СчетНаОплату);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Если ВыборкаДетальныеЗаписи.Следующий() Тогда
		СтруктураВозврат.Вставить("Сумма", ВыборкаДетальныеЗаписи.СуммаДокумента);
		СтруктураВозврат.Вставить("Валюта", УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(ВыборкаДетальныеЗаписи.ВалютаДокумента));
	КонецЕсли;
	
	Возврат СтруктураВозврат;
	
КонецФункции // ПолучитьДанныеСчетаНаОплатуПриИзменении()

// Получает набор данных с сервера для процедуры СотрудникПриИзменении.
//
&НаСервереБезКонтекста
Функция ПолучитьДанныеСотрудникПриИзменении(Сотрудник, Дата, Организация)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("ДоговорКредитаЗайма", Документы.ДоговорКредитаИЗайма.ПолучитьДоговорКредитаИЗаймаПоУмолчаниюПоОрганизацииВидуДоговора(Сотрудник, Организация));
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеПодотчетникПриИзменении()

&НаСервереБезКонтекста
Функция ПолучитьДанныеДокументаПланированияПриИзменении(ДокументПланирования)
	
	СтруктураДанных = Новый Структура("СтатьяДДС", Справочники.СтатьиДвиженияДенежныхСредств.ПустаяСсылка());
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасходДСПлан.СтатьяДвиженияДенежныхСредств
		|ИЗ
		|	Документ.РасходДСПлан КАК РасходДСПлан
		|ГДЕ
		|	РасходДСПлан.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументПланирования);
	
	Если ТипЗнч(ДокументПланирования) = Тип("ДокументСсылка.ПеремещениеДСПлан") Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Документ.РасходДСПлан КАК РасходДСПлан", "Документ.ПеремещениеДСПлан КАК РасходДСПлан");
	КонецЕсли;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		СтруктураДанных.Вставить("СтатьяДДС", Выборка.СтатьяДвиженияДенежныхСредств);
	КонецЕсли;
	
	Возврат СтруктураДанных;
	
КонецФункции

// Получает набор данных с сервера для процедуры КонтрагентПриИзменении.
//
&НаСервере
Функция ПолучитьДанныеКонтрагентПриИзменении(Контрагент, Организация, Дата)
	
	ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить(
		"Договор",
		ДоговорПоУмолчанию
	);
	
	СтруктураДанные.Вставить(
		"ДоговорВалютаКурсКратность",
		РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
			Дата,
			Новый Структура("Валюта", ДоговорПоУмолчанию.ВалютаРасчетов)
		)
	);
	
	СтруктураДанные.Вставить(
		"ВестиРасчетыПоДокументам",
		Контрагент.ВестиРасчетыПоДокументам
	);
	
	// Прочие расчеты
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам Тогда
		ДоговорКредитаЗаймаПоУмолчанию = ПолучитьДоговорКредитаЗаймаПоУмолчанию(Объект.Ссылка, Контрагент, Организация, Объект.ВидОперации);
		СтруктураДанные.Вставить(
			"ДоговорКредитаЗаймаПоУмолчанию",
			ДоговорКредитаЗаймаПоУмолчанию
		);
	КонецЕсли;
	
	СтруктураДанные.Вставить(
			"КорреспонденцияПрочихРасчетов",
			Контрагент.СчетУчетаРасчетовСПоставщиком
		);
	// Конец Прочие расчеты
	
	УстановитьВидимостьРеквизитовРасчетов();
	Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		ЗаполнитьДолги(Истина);
	КонецЕсли;
	
	СтруктураДанные.Вставить("СтатьяДДСПоУмолчанию", Контрагент.СтатьяДДСПоУмолчанию);
	Если ЗначениеЗаполнено(Контрагент.СтатьяДДСПоУмолчанию) Тогда
		СтруктураДанные.Вставить("УчитыватьВНУ", СтатьяУчитываетсяВНУ(Контрагент.СтатьяДДСПоУмолчанию, Организация, Дата));
	Иначе
		СтруктураДанных = ПолучитьДанныеСтатьиДДС();
		СтруктураДанные.Вставить("СтатьяДДСПоУмолчанию", СтруктураДанных.Статья);
		СтруктураДанные.Вставить("УчитыватьВНУ", СтруктураДанных.УчитыватьВНУ);
	КонецЕсли;
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	
	Возврат СтруктураДанные;
	
КонецФункции // ПолучитьДанныеКонтрагентПриИзменении()

#КонецОбласти

// Процедура обработки изменения поля Вид операции на сервере.
//
&НаСервере
Процедура УстановитьСтатьюДДСПриСменеВидаОперации()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
		И (Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей) Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику
		И (Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам) Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
	ИначеЕсли (Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей
		ИЛИ Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам) Тогда
		Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее;
	КонецЕсли;
	
	Если Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее
		И (Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты) Тогда
		Объект.УчитыватьВНУ = Истина;
	Иначе
		Если ЗначениеЗаполнено(Объект.Статья) Тогда
			Объект.УчитыватьВНУ = Объект.Статья.УчитыватьВНУ;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // УстановитьСтатьюДДСПриСменеВидаОперации()

// Процедура обработки изменения поля Вид операции на сервере.
//
&НаСервере
Процедура УстановитьСтатьюДДС()
	
	СтруктураДанных = ПолучитьДанныеСтатьиДДС();
	
	Объект.Статья = СтруктураДанных.Статья;
	Объект.УчитыватьВНУ = СтруктураДанных.УчитыватьВНУ;
	
КонецПроцедуры // УстановитьСтатьюДДС()

&НаСервере
Функция ПолучитьДанныеСтатьиДДС()
	
	СтруктураДляВозврата = Новый Структура;
	
	Если НЕ Объект.Контрагент.Пустая() И НЕ Объект.Контрагент.СтатьяДДСПоУмолчанию.Пустая() Тогда
		СтруктураДляВозврата.Вставить("Статья", Объект.Контрагент.СтатьяДДСПоУмолчанию);
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю Тогда
		СтруктураДляВозврата.Вставить("Статья", Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей);
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
		СтруктураДляВозврата.Вставить("Статья", Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам);
	Иначе
		СтруктураДляВозврата.Вставить("Статья", Справочники.СтатьиДвиженияДенежныхСредств.Прочее);
	КонецЕсли;
	
	Если Объект.Статья = Справочники.СтатьиДвиженияДенежныхСредств.Прочее
		И (Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты) Тогда
		СтруктураДляВозврата.Вставить("УчитыватьВНУ", Истина);
	Иначе
		Если ЗначениеЗаполнено(Объект.Статья) Тогда
			СтруктураДляВозврата.Вставить("УчитыватьВНУ", Объект.Статья.УчитыватьВНУ);
		Иначе
			СтруктураДляВозврата.Вставить("УчитыватьВНУ", Ложь);
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтруктураДляВозврата;
	
КонецФункции // ПолучитьДанныеСтатьиДДС()

&НаСервере
Процедура ЗаполнитьИнформациюОСуммеИВалютеОснования()
	
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		Попытка
			ДокументОснованиеСумма = Объект.ДокументОснование.СуммаДокумента;
			ДокументОснованиеВалютаПредставление = Объект.ДокументОснование.ВалютаДокумента.СимвольноеПредставление;
		Исключение
			// Нет реквизита "СуммаДокумента" или "ВалютаДокумента".
			ДокументОснованиеСумма = 0;
			ДокументОснованиеВалютаПредставление = "";
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

// Процедура вызывает обработку заполнения документа по основанию.
//
&НаСервере
Процедура ЗаполнитьПоДокументу(ДокОснование)
	
	Документ = РеквизитФормыВЗначение("Объект");
	Документ.Заполнить(ДокОснование);
	ЗначениеВРеквизитФормы(Документ, "Объект");
	Модифицированность = Истина;
	
	УстановитьВидимостьНалогообложениеНДС();
	УстановитьВидимостьРеквизитовРасчетов();
	
	Компания = УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация);
	СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Объект.ВалютаДенежныхСредств));
	Курс = ?(
		СтруктураПоВалюте.Курс = 0,
		1,
		СтруктураПоВалюте.Курс
	);
	Кратность = ?(
		СтруктураПоВалюте.Курс = 0,
		1,
		СтруктураПоВалюте.Кратность
	);
	
	ЗаполнитьИнформациюОСуммеИВалютеОснования();
	
КонецПроцедуры // ЗаполнитьПоДокументу()

// Функция помещает табличную часть РасшифровкаРасчетов во временное хранилище
// и возвращает адрес
//
&НаСервере
Функция ПоместитьРасшифровкаПлатежаВХранилище()
	
	Возврат ПоместитьВоВременноеХранилище(
		Объект.РасшифровкаПлатежа.Выгрузить(,
			"Договор,
			|ПризнакАванса,
			|Документ,
			|Заказ,
			|СуммаРасчетов,
			|Курс,
			|Кратность"
		),
		УникальныйИдентификатор
	);
	
КонецФункции // ПоместитьРасшифровкаПлатежаВХранилище()

// Функция получает табличную часть РасшифровкаРасчетов из временного хранилища.
//
&НаСервере
Процедура ПолучитьРасшифровкаПлатежаИзХранилища(АдресРасшифровкаПлатежаВХранилище, Очищать = Истина)
	
	ТаблицаРасшифровкаПлатежа = ПолучитьИзВременногоХранилища(АдресРасшифровкаПлатежаВХранилище);
	Если Очищать Тогда
		Объект.РасшифровкаПлатежа.Очистить();
	КонецЕсли;
	Для каждого СтрокаРасшифровкаПлатежа Из ТаблицаРасшифровкаПлатежа Цикл
		Строка = Объект.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаРасшифровкаПлатежа);
		Если НЕ Строка.Договор.Пустая() Тогда
			Строка.СтатьяДДС = Строка.Договор.СтатьяДДСПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПолучитьРасшифровкаПлатежаИзХранилища()

// Выполняет пересчет сумм по валюте табличной части документа после изменения
// банковского счета или кассы.
//
&НаСервере
Процедура ПересчитатьСуммыДокумента(Курс, Кратность, ПересчитатьСуммуПлатежа, ДополнительныеПараметры)
	
	Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
		Если СтрокаТабличнойЧасти.Договор.ВалютаРасчетов = Объект.ВалютаДенежныхСредств Тогда
			Если ПересчитатьСуммуПлатежа Тогда
				СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаРасчетов;
				СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
				РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
			Иначе
				СтрокаТабличнойЧасти.СуммаРасчетов = СтрокаТабличнойЧасти.СуммаПлатежа;
			КонецЕсли;
			Продолжить;
		КонецЕсли;
		Если ПересчитатьСуммуПлатежа Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				Курс,
				СтрокаТабличнойЧасти.Кратность,
				Кратность
			);
			СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
			РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
		Иначе
			СтрокаТабличнойЧасти.Курс = ?(
				СтрокаТабличнойЧасти.Курс = 0,
				1,
				СтрокаТабличнойЧасти.Курс
			);
			СтрокаТабличнойЧасти.Кратность = ?(
				СтрокаТабличнойЧасти.Кратность = 0,
				1,
				СтрокаТабличнойЧасти.Кратность
			);
			СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаПлатежа,
				Курс,
				СтрокаТабличнойЧасти.Курс,
				Кратность,
				СтрокаТабличнойЧасти.Кратность
			);
		КонецЕсли;
	КонецЦикла;
	
	// Если нужно, то пересчитаем суммы в ТЧ ДоговорыАвтоЗачетаДолгов.
	Если ПересчитатьСуммуПлатежа И Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
		Для каждого СтрокаТабличнойЧасти Из Объект.ДоговорыАвтоЗачетаДолгов Цикл
			СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаПлатежа,
				ДополнительныеПараметры.КурсПередИзменением,
				Курс,
				ДополнительныеПараметры.КратностьПередИзменением,
				Кратность
			);
		КонецЦикла;
		Объект.СуммаДокумента = Объект.ДоговорыАвтоЗачетаДолгов.Итог("СуммаПлатежа");
	ИначеЕсли ПересчитатьСуммуПлатежа Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	КонецЕсли;
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		ЗаполнитьСуммовыеПоказателиТаблицыДолговПоРасшифровкеПлатежа();
	Иначе
		ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	КонецЕсли;
	
КонецПроцедуры // ПересчитатьСуммыДокумента()

&НаСервере
Процедура ЗаполнитьСуммовыеПоказателиТаблицыДолговПоРасшифровкеПлатежа()

	Для Каждого ТекущаяСтрокаДолгов Из ТаблицаДолгов Цикл
		ТекущаяСтрокаРасшифровки = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ТекущаяСтрокаДолгов.ИдентификаторСтрокиРасшифровкиПлатежа);
		Если ТекущаяСтрокаРасшифровки <> Неопределено Тогда
			ТекущаяСтрокаДолгов.СуммаРасчетов = ТекущаяСтрокаРасшифровки.СуммаРасчетов;
			ТекущаяСтрокаДолгов.Курс = ТекущаяСтрокаРасшифровки.Курс;
			ТекущаяСтрокаДолгов.Кратность = ТекущаяСтрокаРасшифровки.Кратность;
			ТекущаяСтрокаДолгов.Зачесть = ТекущаяСтрокаРасшифровки.СуммаПлатежа;
			ТекущаяСтрокаДолгов.ЗачестьВыбрано = ?(ТекущаяСтрокаДолгов.Выбран, ТекущаяСтрокаДолгов.Зачесть, 0);
		КонецЕсли;
	КонецЦикла;

	СуммаЗачтено = ТаблицаДолгов.Итог("ЗачестьВыбрано");
	ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	
	Если Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		ОбновитьДолгиВПомощникеЗачетаДолгов();
	КонецЕсли;
	
КонецПроцедуры

// Выполняет пересчет сумм по валюте денежных средств.
//
&НаКлиенте
Процедура ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств(СтруктураДанные, ТекстСообщения)
	
	КурсПередИзменением = Курс;
	КратностьПередИзменением = Кратность;
	
	Если ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		Курс = ?(
			СтруктураДанные.ВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ВалютаКурсКратность.Курс
		);
		Кратность = ?(
			СтруктураДанные.ВалютаКурсКратность.Кратность = 0,
			1,	
			СтруктураДанные.ВалютаКурсКратность.Кратность
		);
	КонецЕсли;
	
	// Если курс валюты не изменился или не заполнена валюта денежных средств
	// или документ не заполнен, то ничего не делаем.
	Если (Курс = КурсПередИзменением
		И Кратность = КратностьПередИзменением)
	 ИЛИ (НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств))
	 ИЛИ (Объект.РасшифровкаПлатежа.Итог("СуммаРасчетов") = 0
	 И НЕ ЗначениеЗаполнено(Объект.СуммаДокумента)) Тогда
		
		// Обновить курсы расчетов
		// Если в расшифровке платежа есть курс <> 1, то предложим его перезаполнить.
		Если НЕ (Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически) И
			СтруктураДанные.Свойство("ОбновитьКурсыРасчетов") И СтруктураДанные.ОбновитьКурсыРасчетов Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы", ЭтотОбъект), СтруктураДанные.ОбновитьКурсыРасчетовТекстВопроса, РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
		// Конец Обновить курсы расчетов
		
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КурсПередИзменением", КурсПередИзменением);
	ДополнительныеПараметры.Вставить("КратностьПередИзменением", КратностьПередИзменением);
	
	// Обновить курсы расчетов
	Если СтруктураДанные.Свойство("ОбновитьКурсыРасчетов") Тогда
		ДополнительныеПараметры.Вставить("ОбновитьКурсыРасчетов", СтруктураДанные.ОбновитьКурсыРасчетов);
		ДополнительныеПараметры.Вставить("ОбновитьКурсыРасчетовТекстВопроса", СтруктураДанные.ОбновитьКурсыРасчетовТекстВопроса);
	КонецЕсли;
	// Конец Обновить курсы расчетов
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОпределитьНеобходимостьПересчетаСуммПриИзмененииКурса", ЭтотОбъект, ДополнительныеПараметры);
	ПоказатьВопрос(ОписаниеОповещения, ТекстСообщения, РежимДиалогаВопрос.ДаНет, 0);
	
КонецПроцедуры // ПересчитатьСуммыПриИзмененииКурсаВалютыДенежныхСредств()

// Процедура-обработчик ответа на вопрос о пересчете документа после изменения курса валюты
//
&НаКлиенте
Процедура ОпределитьНеобходимостьПересчетаСуммПриИзмененииКурса(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия = КодВозвратаДиалога.Да Тогда
		
		КурсПередИзменением = ДополнительныеПараметры.КурсПередИзменением;
		КратностьПередИзменением = ДополнительныеПараметры.КратностьПередИзменением;
		
		Если Объект.РасшифровкаПлатежа.Количество() > 0
		   И Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Зарплата") Тогда // для вида операции "Зарплата" пересчитывается только шапка.
			Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю")
			 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику") Тогда
				ПересчитатьСуммыДокумента(Курс, Кратность, Истина, ДополнительныеПараметры);
			// Прочие расчеты
			ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику")
				ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты") Тогда
				ПересчитатьСуммыДокумента(Курс, Кратность, Истина, ДополнительныеПараметры);
			// Конец Прочие расчеты
			Иначе
				СуммаДокументаРавнаИтогуСуммыПлатежа = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа") = Объект.СуммаДокумента;
				
				Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл // для видов операций с планируемыми платежами пересчитываем сумму плана.
					СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
						СтрокаТабличнойЧасти.СуммаПлатежа,
						КурсПередИзменением,
						Курс,
						КратностьПередИзменением,
						Кратность
					);
					
					РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
				КонецЦикла;
					
				Если СуммаДокументаРавнаИтогуСуммыПлатежа Тогда
					Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
				Иначе
					Объект.СуммаДокумента = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
						Объект.СуммаДокумента,
						КурсПередИзменением,
						Курс,
						КратностьПередИзменением,
						Кратность
					);
				КонецЕсли;
				
				ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
				
			КонецЕсли;
		Иначе
			Объект.СуммаДокумента = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
				Объект.СуммаДокумента,
				КурсПередИзменением,
				Курс,
				КратностьПередИзменением,
				Кратность
			);
		КонецЕсли;
	Иначе
		Если Объект.РасшифровкаПлатежа.Количество() > 0 Тогда
			ПересчитатьСуммыДокумента(Курс, Кратность, Ложь, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
	// Обновить курсы расчетов
	Если НЕ (Объект.ВидОперации = ВидОперацииПоставщику И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически) Тогда
		Если ДополнительныеПараметры.Свойство("ОбновитьКурсыРасчетов")  И ДополнительныеПараметры.ОбновитьКурсыРасчетов Тогда
			ПоказатьВопрос(Новый ОписаниеОповещения("ОбновитьКурсВРасшифровкеПлатежаИПересчитатьСуммы", ЭтотОбъект),
				ДополнительныеПараметры.ОбновитьКурсыРасчетовТекстВопроса,
				РежимДиалогаВопрос.ДаНет);
		КонецЕсли;
	КонецЕсли;
	// Конец Обновить курсы расчетов
	
КонецПроцедуры // ОпределитьНеобходимостьПересчетаСуммПриИзмененииКурса()

// Выполняем пересчет суммы платежа в переданной строке табличной части.
//
&НаКлиенте
Процедура РассчитатьСуммуПлатежаНаКлиенте(СтрокаТабличнойЧасти, ИмяКолонки = "")
	
	СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор
		);
		
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		?(СтруктураДанные.ДоговорВалютаКурсКратность.Курс =0, 1, СтруктураДанные.ДоговорВалютаКурсКратность.Курс),
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	Если СтрокаТабличнойЧасти.СуммаРасчетов = 0 Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = 0;
		СтрокаТабличнойЧасти.Курс = СтруктураДанные.ДоговорВалютаКурсКратность.Курс;
	ИначеЕсли Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаРасчетов Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаРасчетов;
	ИначеЕсли СтрокаТабличнойЧасти.СуммаПлатежа = 0 ИЛИ
		(ИмяКолонки = "Курс" ИЛИ ИмяКолонки = "Кратность") Тогда
		Если СтрокаТабличнойЧасти.Курс = 0 Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = 0;
		Иначе
			СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				Курс,
				СтрокаТабличнойЧасти.Кратность,
				Кратность
			);
		КонецЕсли;
	Иначе
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс, //СтрокаТабличнойЧасти.Курс,
			СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	
КонецПроцедуры // РассчитатьСуммуПлатежаНаКлиенте()

// Выполняем пересчет суммы платежа в переданной строке табличной части.
//
&НаСервере
Процедура РассчитатьСуммуПлатежаНаСервере(СтрокаТабличнойЧасти, ИмяКолонки = "")
	
	СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор
		);
		
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		?(СтруктураДанные.ДоговорВалютаКурсКратность.Курс =0, 1, СтруктураДанные.ДоговорВалютаКурсКратность.Курс),
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	Если СтрокаТабличнойЧасти.СуммаРасчетов = 0 Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = 0;
		СтрокаТабличнойЧасти.Курс = СтруктураДанные.ДоговорВалютаКурсКратность.Курс;
	ИначеЕсли Объект.ВалютаДенежныхСредств = СтруктураДанные.ВалютаРасчетов Тогда
		СтрокаТабличнойЧасти.СуммаПлатежа = СтрокаТабличнойЧасти.СуммаРасчетов;
	ИначеЕсли СтрокаТабличнойЧасти.СуммаПлатежа = 0 ИЛИ
		(ИмяКолонки = "Курс" ИЛИ ИмяКолонки = "Кратность") Тогда
		Если СтрокаТабличнойЧасти.Курс = 0 Тогда
			СтрокаТабличнойЧасти.СуммаПлатежа = 0;
		Иначе
			СтрокаТабличнойЧасти.СуммаПлатежа = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
				СтрокаТабличнойЧасти.СуммаРасчетов,
				СтрокаТабличнойЧасти.Курс,
				Курс,
				СтрокаТабличнойЧасти.Кратность,
				Кратность
			);
		КонецЕсли;
	Иначе
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс, //СтрокаТабличнойЧасти.Курс,
			СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.СуммаРасчетов = 0 ИЛИ СтрокаТабличнойЧасти.СуммаПлатежа = 0,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
	КонецЕсли;
	
	СтрокаТабличнойЧасти.Аванс = ?(СтрокаТабличнойЧасти.ПризнакАванса, СтрокаТабличнойЧасти.СуммаПлатежа, 0);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти);
	ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	
КонецПроцедуры // РассчитатьСуммуПлатежаНаСервере()

// Выполняет пересчет сумм по валюте табличной части документа после изменения
// банковского счета или кассы.
//
&НаКлиенте
Процедура РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтавкаНДС + 100) / 100);
	
КонецПроцедуры // РассчитатьСуммуНДС()

// Выполняет пересчет сумм по валюте табличной части документа после изменения
// банковского счета или кассы.
//
&НаСервере
Процедура РассчитатьСуммуНДСНаСервере(СтрокаТабличнойЧасти)
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтавкаНДС + 100) / 100);
	
КонецПроцедуры // РассчитатьСуммуНДС()

// Процедура устанавливает видимость реквизитов расчетов.
//
&НаСервере
Процедура УстановитьВидимостьРеквизитовРасчетов()
	
	КонтрагентВестиРасчетыПоДоговорам = Объект.Контрагент.ВестиРасчетыПоДоговорам;
	
	Элементы.РасшифровкаПлатежаДоговор.Видимость = КонтрагентВестиРасчетыПоДоговорам;
	Элементы.ГруппаДоговоров.Видимость = КонтрагентВестиРасчетыПоДоговорам;
	Элементы.РасшифровкаПлатежаДокумент.Видимость = Объект.Контрагент.ВестиРасчетыПоДокументам;
	Элементы.РасшифровкаПлатежаЗаказ.Видимость = Объект.Контрагент.ВестиРасчетыПоЗаказам;
	Элементы.РасшифровкаПлатежаСчетНаОплату.Видимость = Объект.Контрагент.ВестиУчетОплатыПоСчетам;
	
	// Прочие расчеты
	Элементы.РасшифровкаПлатежаПрочиеРасчетыДоговор.Видимость = КонтрагентВестиРасчетыПоДоговорам;
	// Конец Прочие расчеты
	
	// Помощник зачета долгов
	Элементы.ТаблицаДолговДокумент.Видимость = Объект.Контрагент.ВестиРасчетыПоДокументам;
	Элементы.ТаблицаДолговГруппаСуммаДокумента.Видимость = Объект.Контрагент.ВестиРасчетыПоДокументам;
	Элементы.ТаблицаДолговЗаказ.Видимость = Объект.Контрагент.ВестиРасчетыПоЗаказам И Объект.Контрагент.ВестиРасчетыПоДокументам;
	Элементы.ТаблицаДолговЗаказКогдаНетУчетаПоДокументам.Видимость = Объект.Контрагент.ВестиРасчетыПоЗаказам И НЕ Объект.Контрагент.ВестиРасчетыПоДокументам;
	Элементы.ТаблицаДолговГруппаСуммаЗаказа.Видимость = Объект.Контрагент.ВестиРасчетыПоЗаказам;
	Элементы.ТаблицаДолговСчетНаОплату.Видимость = Объект.Контрагент.ВестиУчетОплатыПоСчетам;
	Элементы.ТаблицаДолговГруппаСуммаСчета.Видимость = Объект.Контрагент.ВестиУчетОплатыПоСчетам;
	Элементы.ТаблицаДолговДоговор.Видимость = КонтрагентВестиРасчетыПоДоговорам;
	Элементы.ДоговорДляЗачетаАвансаСПомощником.Видимость = КонтрагентВестиРасчетыПоДоговорам;
	// Когда только по договорам, то сделаем ширину поля с договором больше.
	Если НЕ Объект.Контрагент.ВестиРасчетыПоЗаказам И НЕ Объект.Контрагент.ВестиРасчетыПоДокументам И
		КонтрагентВестиРасчетыПоДоговорам Тогда
		Элементы.ТаблицаДолговДоговор.Ширина = 25 + Элементы.ТаблицаДолговДокумент.Ширина;
	Иначе
		Элементы.ТаблицаДолговДоговор.Ширина = 25;
	КонецЕсли;
	// Конец Помощник зачета долгов
	
	Элементы.РасшифровкаПлатежаДоговорВалютаРасчетовСимвольноеПредставление.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	Элементы.РасшифровкаПлатежаПрочиеРасчетыДоговорВалютаРасчетовСимвольноеПредставление.Видимость = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	
КонецПроцедуры // УстановитьВидимостьРеквизитовРасчетов()

&НаСервере
Процедура УстановитьВидимостьКурсаВалюты(ВалютаДенежныхСредств)
	
	Элементы.Курс.Видимость = (ВалютаДенежныхСредств <> Константы.НациональнаяВалюта.Получить());
	
КонецПроцедуры

// Процедура обработки изменения поля Вид операции на сервере.
//
&НаСервере
Процедура ВидОперацииПриИзмененииНаСервере(ЗаполнятьНалогообложение = Истина)
	
	УстановитьСвязиПараметровВыбораДоступныеТипы();
	
	// Прочие расчеты
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ПрочиеРасчеты
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.СнятиеНаличных
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		Объект.РасшифровкаПлатежа[0].СтавкаНДС = СтавкаНДСПоУмолчанию;
		СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
	// Конец Прочие расчеты
	ИначеЕсли ЗаполнятьНалогообложение Тогда
		ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС();
	КонецЕсли;
	
	УстановитьВидимостьЭлементовВЗависимостиОтВидаОперации();
	УстановитьСтатьюДДСПриСменеВидаОперации();
	
КонецПроцедуры // ВидОперацииПриИзмененииНаСервере()

// Процедура заполняет ставку НДС по умолчанию.
//
&НаСервере
Процедура ЗаполнитьСтавкуНДСПоУмолчанию()
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = Объект.Организация.СтавкаНДСПоУмолчанию;
	ИначеЕсли Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
	
КонецПроцедуры // ЗаполнитьСтавкуНДСПоУмолчанию()

// Процедура заполняет Ставку НДС в табличной части по системе налогообложения
// организации.
//
&НаСервере
Процедура ЗаполнитьСтавкуНДСПоОрганизациияНалогообложениеНДС()
	
	НалогообложениеПередИзменением = Объект.НалогообложениеНДС;
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю Тогда
		
		Объект.НалогообложениеНДС = УправлениеНебольшойФирмойСервер.НалогообложениеНДС(Объект.Организация,, Объект.Дата);
		
	Иначе
		
		Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
		
	КонецЕсли;
	
	Если (Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику)
		И НЕ НалогообложениеПередИзменением = Объект.НалогообложениеНДС Тогда
		
		ЗаполнитьСтавкуНДСПоНалогообложениеНДС();
		
	Иначе
		
		ЗаполнитьСтавкуНДСПоУмолчанию();
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьСтавкуНДСПоНалогообложениеНДС()

// Процедура устанавливает видимость поля Налогообложение.
//
&НаСервере
Процедура УстановитьВидимостьДокументыПланирования()
	
	Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата
		ИЛИ НЕ ПолучитьФункциональнуюОпцию("ПлатежныйКалендарь") Тогда
		Элементы.ДокументыПланирования.Видимость = Ложь;
	// Эквайринг
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты Тогда
		Если ПолучитьФункциональнуюОпцию("РазноситьОплатуОтЭквайрераПоЭквайринговымОперациям") Тогда
			Элементы.ДокументыПланирования.Видимость = Ложь;
		Иначе
			Элементы.ДокументыПланирования.Видимость = Истина;
		КонецЕсли;
	// Прочие расчеты
	ИначеЕсли Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ПрочиеРасчеты
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам Тогда
		Элементы.ДокументыПланирования.Видимость = Ложь;
	// Конец Прочие расчеты
	Иначе
		Элементы.ДокументыПланирования.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры // УстановитьВидимостьНалогообложениеНДС()

// Процедура заполняет Ставку НДС в табличной части по системе налогообложения.
// 
&НаСервере
Процедура ЗаполнитьСтавкуНДСПоНалогообложениеНДС(ВосстанавливатьСтавкиНДС = Истина)
	
	ЗаполнитьСтавкуНДСПоУмолчанию();
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Истина;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Истина;
			Элементы.СуммаНДС.Видимость = Истина;
			
		КонецЕсли;
		
		СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
		
		Если ВосстанавливатьСтавкиНДС Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
				СтрокаТабличнойЧасти.СтавкаНДС = Объект.Организация.СтавкаНДСПоУмолчанию;
				СтрокаТабличнойЧасти.СуммаНДС = СтрокаТабличнойЧасти.СуммаПлатежа - (СтрокаТабличнойЧасти.СуммаПлатежа) / ((СтавкаНДС + 100) / 100);
			КонецЦикла;
		КонецЕсли;
		
	Иначе
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Ложь;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Ложь;
			Элементы.СуммаНДС.Видимость = Ложь;
			
		КонецЕсли;
		
		Если ВосстанавливатьСтавкиНДС Тогда
			Для каждого СтрокаТабличнойЧасти Из Объект.РасшифровкаПлатежа Цикл
				СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
				СтрокаТабличнойЧасти.СуммаНДС = 0;
			КонецЦикла;
		Конецесли;
		
	КонецЕсли;
	
	УстановитьВидимостьДокументыПланирования();
	
КонецПроцедуры // ЗаполнитьСтавкуНДСПоНалогообложениеНДС()

&НаСервере
// Процедура устанавливает видимость поля Налогообложение.
//
Процедура УстановитьВидимостьНалогообложениеНДС()
	
	Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Истина;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Истина;
			
		КонецЕсли;
		
		СтавкаНДСПоУмолчанию = Объект.Организация.СтавкаНДСПоУмолчанию;
		
	Иначе
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
			
			Элементы.РасшифровкаПлатежаСтавкаНДС.Видимость = Ложь;
			Элементы.РасшифровкаПлатежаСуммаНДС.Видимость = Ложь;
			
		КонецЕсли;
		
		Если Объект.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
		Иначе
			СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
		КонецЕсли;
		
	КонецЕсли;
	СтавкаНДСПоУмолчаниюЧисло = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтавкаНДСПоУмолчанию);
	
КонецПроцедуры // УстановитьВидимостьНалогообложениеНДС()

// Процедура устанавливает видимость реквизитов формы от опции
// Использовать подсистему Зарплата.
//
// Параметры:
// Нет.
//
&НаСервере
Процедура УстановитьВидимостьОтФОИспользоватьПодсистемуЗарплата()
	
	СписокВидовОпераций = ДвиженияДенежныхСредствВызовСервера.ПолучитьСписокВидовОперацийРасходДСБанк();
	Элементы.ВидОперации.СписокВыбора.ЗагрузитьЗначения(СписокВидовОпераций.ВыгрузитьЗначения());
	
КонецПроцедуры // УстановитьВидимостьОтФОИспользоватьПодсистемуЗарплата()

// Проверяет соответствие реквизитов договора "Организация" и "ВидДоговора" условиям документа.
//
&НаСервереБезКонтекста
Процедура ПроверитьСоответствиеДоговораУсловиямДокумента(Знач ТЧРасшифровкаПлатежа, ТекстСообщения, Документ, Организация, Контрагент, ВидОперации, Отказ, ДоговорКредитаЗайма)
	
	Если Не УправлениеНебольшойФирмойПовтИсп.ТребуетсяКонтрольДоговоровКонтрагентов()
		ИЛИ Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		
		Возврат;
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	// Прочие расчеты
	Если ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам Тогда
		
		СписокВидовДоговора = Новый СписокЗначений;
		СписокВидовДоговора.Добавить(Перечисления.ВидыДоговоровКредитаИЗайма.КредитПолученный);
		
		Если Не МенеджерСправочника.ДоговорСоответствуетУсловиямДокумента(ТекстСообщения, ДоговорКредитаЗайма, Организация, Контрагент, СписокВидовДоговора)
			И Константы.НеПроводитьДокументыСНекорректнымиДоговорами.Получить() Тогда
			
			Отказ = Истина;
			
		КонецЕсли;
		
	Иначе
	// Конец Прочие расчеты
	
		СписокВидовДоговора = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
		
		Для каждого СтрокаТабличнойЧасти Из ТЧРасшифровкаПлатежа Цикл
			
			Если Не МенеджерСправочника.ДоговорСоответствуетУсловиямДокумента(ТекстСообщения, СтрокаТабличнойЧасти.Договор, Организация, Контрагент, СписокВидовДоговора)
				И Константы.НеПроводитьДокументыСНекорректнымиДоговорами.Получить() Тогда
				
				Отказ = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

// Получает структуру параметров формы выбора договора контрагента.
//
&НаСервереБезКонтекста
Функция ПолучитьПараметрыФормыВыбора(Документ, Организация, Контрагент, Договор, ВидОперации)
	
	СписокВидовДоговоров = Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("КонтролироватьВыборДоговора", Контрагент.ВестиРасчетыПоДоговорам);
	ПараметрыФормы.Вставить("Контрагент", Контрагент);
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("ВидыДоговоров", СписокВидовДоговоров);
	ПараметрыФормы.Вставить("ТекущаяСтрока", Договор);
	
	Возврат ПараметрыФормы;
	
КонецФункции

// Получает договор по умолчанить в зависимости от способа ведения расчетов.
//
&НаСервереБезКонтекста
Функция ПолучитьДоговорПоУмолчанию(Документ, Контрагент, Организация, ВидОперации)
	
	Если Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		Возврат Контрагент.ДоговорПоУмолчанию;
	КонецЕсли;
	
	МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
	
	СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(Документ, ВидОперации);
	ДоговорПоУмолчанию = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорПоУмолчанию;
	
КонецФункции

// Проверяет утвержден документ или нет.
//
&НаСервереБезКонтекста
Функция ДокументУтвержден(ДокументОснование)
	
	Возврат ДокументОснование.СтатусУтвержденияПлатежа = Перечисления.СтатусыУтвержденияПлатежей.Утвержден;
	
КонецФункции // ДокументУтвержден()

// Заполняет договор.
//
Процедура ЗаполнитьДоговор(Параметры = Неопределено)
	
	Если Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику") И
		Объект.ВидОперации <> ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам") Тогда
		
		Если ЗначениеЗаполнено(Объект.Контрагент)
			И Объект.РасшифровкаПлатежа.Количество() > 0
			И (Параметры = Неопределено ИЛИ (Параметры <> Неопределено И НЕ ЗначениеЗаполнено(Параметры.ДокументОснование))) Тогда
			Если НЕ ЗначениеЗаполнено(Объект.РасшифровкаПлатежа[0].Договор) Тогда
				Объект.РасшифровкаПлатежа[0].Договор = Объект.Контрагент.ДоговорПоУмолчанию;
			КонецЕсли;
			Если ЗначениеЗаполнено(Объект.РасшифровкаПлатежа[0].Договор) Тогда
				ДоговорВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(Объект.Дата, Новый Структура("Валюта", Объект.РасшифровкаПлатежа[0].Договор.ВалютаРасчетов));
				Объект.РасшифровкаПлатежа[0].Курс = ?(ДоговорВалютаКурсКратность.Курс = 0, 1, ДоговорВалютаКурсКратность.Курс);
				Объект.РасшифровкаПлатежа[0].Кратность = ?(ДоговорВалютаКурсКратность.Кратность = 0, 1, ДоговорВалютаКурсКратность.Кратность);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
Конецпроцедуры

&НаСервереБезКонтекста
Функция СтатьяУчитываетсяВНУ(Статья, Организация, Дата)
	СистемаНалогообложенияСтруктура = РегистрыСведений.СистемыНалогообложенияОрганизаций.ОпределитьСистемуНалогообложенияОрганизации(Организация, Дата);
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Статья, "УчитыватьВНУ") И СистемаНалогообложенияСтруктура.ПлательщикУСН;
КонецФункции

&НаСервере
Процедура ОбновитьВидимостьИОстаткиВзаиморасчетов()
	
	Элементы.ОстатокВзаиморасчетов.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(Объект.Контрагент);
	ОстатокВзаиморасчетов = 0;
	
	Если НЕ Элементы.ОстатокВзаиморасчетов.Видимость Тогда
		//Только для новых объектов
		Элементы.Контрагент.АвтоМаксимальнаяШирина = Истина;
		Возврат;
	КонецЕсли;
	
	Элементы.Контрагент.АвтоМаксимальнаяШирина = Ложь;
	Элементы.Контрагент.МаксимальнаяШирина = 22;
	Элементы.ОстатокВзаиморасчетов.Заголовок = РаботаСКонтрагентамиУНФ.ЗаголовокНадписиВзаиморасчетов(Объект.Контрагент, ОстатокВзаиморасчетов);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьВидимостьИОстаткиДС(БанковскийСчет = Неопределено)
	
	Если БанковскийСчет = Неопределено Тогда
		ТекущийБанковскийСчет = Объект.БанковскийСчет;
	Иначе
		ТекущийБанковскийСчет = БанковскийСчет;
	КонецЕсли;
	
	Элементы.ОстатокДенежныхСредств.Видимость = НЕ ЗначениеЗаполнено(Объект.Ссылка) И ЗначениеЗаполнено(ТекущийБанковскийСчет);
	ОстатокДенежныхСредств = 0;

	Если НЕ Элементы.ОстатокДенежныхСредств.Видимость Тогда
		//Только для новых объектов
		Элементы.БанковскийСчет.АвтоМаксимальнаяШирина = Истина;
		возврат;
	КонецЕсли;
	
	Элементы.БанковскийСчет.АвтоМаксимальнаяШирина = Ложь;
	Элементы.БанковскийСчет.МаксимальнаяШирина = 22;
	Элементы.ОстатокДенежныхСредств.Заголовок = ДвиженияДенежныхСредствВызовСервера.ЗаголовокНадписиОстатковДС(ТекущийБанковскийСчет, Объект.Организация, ОстатокДенежныхСредств);
	
КонецПроцедуры

// Процедура заполняет таблицу долгов.
//
&НаСервере
Процедура ЗаполнитьДолги(ПересчитыватьСлужебныеДанные)
	
	// Не будем в табличную часть помещать документы, дата которых больше даты текущего документа.
	Если Объект.Контрагент.Пустая() Тогда
		ТаблицаДолгов.Очистить();
		ОпределитьТекущуюСтраницуПомощника();
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыСПоставщикамиОстатки.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.Организация КАК Организация,
	|	РасчетыСПоставщикамиОстатки.Договор КАК Договор,
	|	РасчетыСПоставщикамиОстатки.Документ КАК Документ,
	|	РасчетыСПоставщикамиОстатки.Заказ КАК Заказ,
	|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток
	|ПОМЕСТИТЬ РасчетыСПоставщикамиОстатки
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками.Остатки(
	|			,
	|			Организация = &Организация
	|				И Контрагент = &Контрагент
	|				И ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)) КАК РасчетыСПоставщикамиОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ДвиженияДокументаРасчетыСПоставщиками.Контрагент,
	|	ДвиженияДокументаРасчетыСПоставщиками.Организация,
	|	ДвиженияДокументаРасчетыСПоставщиками.Договор,
	|	ДвиженияДокументаРасчетыСПоставщиками.Документ,
	|	ДвиженияДокументаРасчетыСПоставщиками.Заказ,
	|	ВЫБОР
	|		КОГДА ДвиженияДокументаРасчетыСПоставщиками.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.СуммаВал, 0)
	|		ИНАЧЕ ЕСТЬNULL(ДвиженияДокументаРасчетыСПоставщиками.СуммаВал, 0)
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасчетыСПоставщиками КАК ДвиженияДокументаРасчетыСПоставщиками
	|ГДЕ
	|	ДвиженияДокументаРасчетыСПоставщиками.Регистратор = &Ссылка
	|	И ДвиженияДокументаРасчетыСПоставщиками.Организация = &Организация
	|	И ДвиженияДокументаРасчетыСПоставщиками.Контрагент = &Контрагент
	|	И ДвиженияДокументаРасчетыСПоставщиками.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Заказ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасчетыСПоставщикамиОстатки.Контрагент КАК Контрагент,
	|	РасчетыСПоставщикамиОстатки.Организация КАК Организация,
	|	РасчетыСПоставщикамиОстатки.Договор КАК Договор,
	|	РасчетыСПоставщикамиОстатки.Документ КАК Документ,
	|	РасчетыСПоставщикамиОстатки.Заказ КАК Заказ,
	|	СУММА(РасчетыСПоставщикамиОстатки.СуммаВалОстаток) КАК ДолгВалДоговора,
	|	РасчетыСПоставщикамиОстатки.Документ.Дата КАК ДокументДата,
	|	КурсыВалютРасчетов.Курс КАК Курс,
	|	КурсыВалютРасчетов.Кратность КАК Кратность,
	|	0 КАК Вниз,
	|	""+"" КАК Добавить
	|ПОМЕСТИТЬ ВременнаяТаблицаДолгов
	|ИЗ
	|	РасчетыСПоставщикамиОстатки КАК РасчетыСПоставщикамиОстатки
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(КОНЕЦПЕРИОДА(&Период, ДЕНЬ), ) КАК КурсыВалютРасчетов
	|		ПО РасчетыСПоставщикамиОстатки.Договор.ВалютаРасчетов = КурсыВалютРасчетов.Валюта
	|
	|СГРУППИРОВАТЬ ПО
	|	РасчетыСПоставщикамиОстатки.Контрагент,
	|	РасчетыСПоставщикамиОстатки.Организация,
	|	РасчетыСПоставщикамиОстатки.Договор,
	|	РасчетыСПоставщикамиОстатки.Документ,
	|	РасчетыСПоставщикамиОстатки.Заказ,
	|	РасчетыСПоставщикамиОстатки.Документ.Дата,
	|	КурсыВалютРасчетов.Курс,
	|	КурсыВалютРасчетов.Кратность
	|
	|ИМЕЮЩИЕ
	|	СУММА(РасчетыСПоставщикамиОстатки.СуммаВалОстаток) > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СчетНаОплатуПоставщика.Ссылка) КАК КоличествоСчетов,
	|	СчетНаОплатуПоставщика.ДокументОснование КАК ЗаказПоставщику
	|ПОМЕСТИТЬ ВременнаяТаблицаКолвоСчетовПоЗаказу
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РасчетыСПоставщикамиОстатки КАК РасчетыСПоставщикамиОстатки
	|		ПО СчетНаОплатуПоставщика.ДокументОснование = РасчетыСПоставщикамиОстатки.Заказ
	|			И (СчетНаОплатуПоставщика.ДокументОснование <> НЕОПРЕДЕЛЕНО)
	|			И (НЕ СчетНаОплатуПоставщика.ДокументОснование ЕСТЬ NULL )
	|			И (СчетНаОплатуПоставщика.ДокументОснование <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка))
	|ГДЕ
	|	СчетНаОплатуПоставщика.Проведен
	|
	|СГРУППИРОВАТЬ ПО
	|	СчетНаОплатуПоставщика.ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СчетНаОплатуПоставщика.Ссылка КАК СчетНаОПлату,
	|	СчетНаОплатуПоставщика.ДокументОснование КАК Заказ
	|ПОМЕСТИТЬ ВременнаяТаблицаСчетаНаОплату
	|ИЗ
	|	Документ.СчетНаОплатуПоставщика КАК СчетНаОплатуПоставщика
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаКолвоСчетовПоЗаказу КАК ВременнаяТаблицаКолвоСчетовПоЗаказу
	|		ПО СчетНаОплатуПоставщика.ДокументОснование = ВременнаяТаблицаКолвоСчетовПоЗаказу.ЗаказПоставщику
	|			И (ВременнаяТаблицаКолвоСчетовПоЗаказу.КоличествоСчетов = 1)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаДолгов.Организация,
	|	ВременнаяТаблицаДолгов.Договор КАК Договор,
	|	ВременнаяТаблицаДолгов.Документ КАК Документ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаДолгов.Заказ ЕСТЬ NULL 
	|				ИЛИ ВременнаяТаблицаДолгов.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|				ИЛИ ВременнаяТаблицаДолгов.Заказ = ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ВременнаяТаблицаДолгов.Заказ
	|	КОНЕЦ КАК Заказ,
	|	ВременнаяТаблицаДолгов.Заказ.Дата КАК ДатаЗаказа,
	|	ВременнаяТаблицаДолгов.ДолгВалДоговора,
	|	ВременнаяТаблицаДолгов.ДокументДата КАК ДатаДокумента,
	|	ВременнаяТаблицаДолгов.Курс,
	|	ВременнаяТаблицаДолгов.Кратность,
	|	ВременнаяТаблицаДолгов.Вниз,
	|	ВременнаяТаблицаДолгов.Добавить,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВременнаяТаблицаДолгов.Документ) = ТИП(Документ.ОтчетПереработчика)
	|			ТОГДА ВременнаяТаблицаДолгов.Документ.Сумма
	|		КОГДА ТИПЗНАЧЕНИЯ(ВременнаяТаблицаДолгов.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ВременнаяТаблицаДолгов.Документ.СуммаУчета
	|		ИНАЧЕ ВременнаяТаблицаДолгов.Документ.СуммаДокумента
	|	КОНЕЦ КАК СуммаДокумента,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ВременнаяТаблицаДолгов.Документ) = ТИП(Документ.РасходИзКассы)
	|				ИЛИ ТИПЗНАЧЕНИЯ(ВременнаяТаблицаДолгов.Документ) = ТИП(Документ.РасходСоСчета)
	|			ТОГДА ВременнаяТаблицаДолгов.Документ.ВалютаДенежныхСредств.СимвольноеПредставление
	|		КОГДА ТИПЗНАЧЕНИЯ(ВременнаяТаблицаДолгов.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ВалютаУчета.Значение.СимвольноеПредставление
	|		ИНАЧЕ ВременнаяТаблицаДолгов.Документ.ВалютаДокумента.СимвольноеПредставление
	|	КОНЕЦ КАК ВалютаДокумента,
	|	ВременнаяТаблицаДолгов.ДолгВалДоговора * ВременнаяТаблицаДолгов.Курс * КурсыВалютСрезПоследних.Кратность / (ВременнаяТаблицаДолгов.Кратность * КурсыВалютСрезПоследних.Курс) КАК ДолгВалПлатежа,
	|	&СтавкаНДС,
	|	ВременнаяТаблицаДолгов.Заказ.СуммаДокумента КАК СуммаЗаказа,
	|	ВременнаяТаблицаДолгов.Заказ.ВалютаДокумента.СимвольноеПредставление КАК ВалютаЗаказа,
	|	-1 КАК ИдентификаторСтрокиРасшифровкиПлатежа,
	|	ВременнаяТаблицаДолгов.Договор.ВалютаРасчетов.СимвольноеПредставление КАК ВалютаДоговора,
	|	ВременнаяТаблицаДолгов.Договор.ВалютаРасчетов КАК ВалютаДоговораСсылка,
	|	ВременнаяТаблицаДолгов.Курс КАК КурсИсходный,
	|	ВременнаяТаблицаСчетаНаОплату.СчетНаОПлату,
	|	ВременнаяТаблицаСчетаНаОплату.СчетНаОПлату.ВалютаДокумента.СимвольноеПредставление КАК ВалютаСчета,
	|	ВременнаяТаблицаСчетаНаОплату.СчетНаОПлату.СуммаДокумента КАК СуммаСчета,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаДолгов.Контрагент.ВестиРасчетыПоДокументам
	|				И КОНЕЦПЕРИОДА(ВременнаяТаблицаДолгов.ДокументДата, ДЕНЬ) > КОНЕЦПЕРИОДА(&Период, ДЕНЬ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДатаДокБольшеДатыДокумента,
	|	ВЫБОР
	|		КОГДА НЕ ВременнаяТаблицаДолгов.Контрагент.ВестиРасчетыПоДокументам
	|				И ВременнаяТаблицаДолгов.Контрагент.ВестиРасчетыПоЗаказам
	|				И КОНЕЦПЕРИОДА(ВременнаяТаблицаДолгов.Заказ.Дата, ДЕНЬ) > КОНЕЦПЕРИОДА(&Период, ДЕНЬ)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ДатаЗаказаБольшеДатыДокумента
	|ИЗ
	|	ВременнаяТаблицаДолгов КАК ВременнаяТаблицаДолгов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаСчетаНаОплату КАК ВременнаяТаблицаСчетаНаОплату
	|		ПО ВременнаяТаблицаДолгов.Заказ = ВременнаяТаблицаСчетаНаОплату.Заказ,
	|	Константа.ВалютаУчета КАК ВалютаУчета,
	|	РегистрСведений.КурсыВалют.СрезПоследних(КОНЕЦПЕРИОДА(&Период, ДЕНЬ), Валюта = &Валюта) КАК КурсыВалютСрезПоследних
	|
	|УПОРЯДОЧИТЬ ПО
	|	Документ,
	|	Заказ,
	|	Договор
	|АВТОУПОРЯДОЧИВАНИЕ";
	
	Запрос.УстановитьПараметр("Организация", Компания);
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("Период", ?(Объект.Дата = '00010101', ТекущаяДатаСеанса(), Объект.Дата));
	Запрос.УстановитьПараметр("Валюта", Объект.ВалютаДенежныхСредств);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("СтавкаНДС", СтавкаНДСПоУмолчанию);
	
	НуженОтборПоДоговорам = УправлениеНебольшойФирмойПовтИсп.ТребуетсяКонтрольДоговоровКонтрагентов();
	Если Объект.Контрагент.ВестиРасчетыПоДоговорам
	   И НуженОтборПоДоговорам Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "// ТекстДоговорОтбор", "И Договор.ВидДоговора В (&СписокВидовДоговоров)");
		Запрос.УстановитьПараметр("СписокВидовДоговоров", Справочники.ДоговорыКонтрагентов.ПолучитьСписокВидовДоговораДляДокумента(Объект.Ссылка, ВидОперации));
	КонецЕсли;
	
	ТЗДолгов = Запрос.Выполнить().Выгрузить();
	ТаблицаДолгов.Загрузить(ТЗДолгов);
	
	Для Каждого ТекущаяСтрока Из ТаблицаДолгов Цикл
		ДолгВалДоговораСтрока = ТекущаяСтрока.ДолгВалДоговора;
		ДолгВалДоговораСтрока = СтрЗаменить(ДолгВалДоговораСтрока, Символы.НПП, "");
		ТекущаяСтрока.ДолгВалДоговораСтрока = ДолгВалДоговораСтрока;
	КонецЦикла;
	
	Элементы.ГруппаФильтры.Видимость = ТаблицаДолгов.Количество() > 5;
	Если НЕ Элементы.ГруппаФильтры.Видимость Тогда
		Элементы.ТаблицаДолгов.ОтборСтрок = Неопределено;
	КонецЕсли;
	
	Если ПересчитыватьСлужебныеДанные Тогда
		РаспределитьРасшифровкуПоТаблицеДолгов();
		ПересчитатьИтогиПриИзмененииАвансаНаСервере();
	КонецЕсли;
	
	ОпределитьТекущуюСтраницуПомощника();
	
КонецПроцедуры // ЗаполнитьДолги()

&НаКлиенте
Процедура ПересчитатьИтогиПриИзмененииАвансаНаКлиенте(ОбновитьВидимостьАвансовогоДоговора = Ложь)
	
	СуммаПлатежаАванс = Объект.РасшифровкаПлатежа.Итог("Аванс");
	СуммаПлатежаДляУсловногоОформления = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	СуммаЗачтено = СуммаПлатежаДляУсловногоОформления - СуммаПлатежаАванс;
	СуммаРазнесено = ТаблицаДолгов.Итог("ЗачестьВыбрано") + СуммаПлатежаАванс;
	
	Если ОбновитьВидимостьАвансовогоДоговора Тогда
		НастроитьДоступностьСуммыИДоговораАванса();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПересчитатьИтогиПриИзмененииАвансаНаСервере(ОбновитьСтрокуАванса = Ложь)
	
	Если ОбновитьСтрокуАванса Тогда
		ОбновитьСуммаВСтрокеАванса();
	КонецЕсли;
	
	СуммаПлатежаАванс = Объект.РасшифровкаПлатежа.Итог("Аванс");
	СуммаПлатежаДляУсловногоОформления = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
	СуммаЗачтено = СуммаПлатежаДляУсловногоОформления - СуммаПлатежаАванс;
	СуммаРазнесено = ТаблицаДолгов.Итог("ЗачестьВыбрано") + СуммаПлатежаАванс;
	
	РассчитатьСуммуНДСПоУмолчаниюНаСервере();
	
	НастроитьДоступностьСуммыИДоговораАванса();
	
КонецПроцедуры

&НаСервере
Процедура РассчитатьСуммуНДСПоУмолчаниюНаСервере()
	
	ИтогоРазнесено = Объект.ДоговорыАвтоЗачетаДолгов.Итог("СуммаПлатежа");
	//СуммаНДСПоУмолчанию = Объект.СуммаДокумента - (Объект.СуммаДокумента) / ((СтавкаНДСПоУмолчаниюЧисло + 100) / 100);
	СуммаНДСПоУмолчанию = ИтогоРазнесено - (ИтогоРазнесено) / ((СтавкаНДСПоУмолчаниюЧисло + 100) / 100);
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуНДСПоУмолчаниюНаКлиенте()
	
	ИтогоРазнесено = Объект.ДоговорыАвтоЗачетаДолгов.Итог("СуммаПлатежа");
	СуммаНДСПоУмолчанию = ИтогоРазнесено - (ИтогоРазнесено) / ((СтавкаНДСПоУмолчаниюЧисло + 100) / 100);
	
КонецПроцедуры

&НаСервере
Процедура НастроитьДоступностьСуммыИДоговораАванса()
	
	ДоступностьПредыдущееЗначение = Элементы.ГруппаСПомощникомИтогиАванс.Доступность;
	Элементы.ГруппаСПомощникомИтогиАванс.Доступность = Не (СуммаПлатежаАванс = 0);
	// Возможно, что фокус ушел из таблицы долгов.
	Если ДоступностьПредыдущееЗначение <> Элементы.ГруппаСПомощникомИтогиАванс.Доступность Тогда
		ТекущийЭлемент = Элементы.ТаблицаДолгов;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьВидимостьНастроекУчетаВНалогообложении()
	
	Если РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Объект.Организация) Тогда
		Элементы.ГруппаУчетВНУ.Видимость = Ложь;
		
		Возврат;
	Иначе
		Элементы.ГруппаУчетВНУ.Видимость = Истина;
	КонецЕсли;
	
	СистемаНалогообложенияСтруктура = РегистрыСведений.СистемыНалогообложенияОрганизаций.ОпределитьСистемуНалогообложенияОрганизации(Объект.Организация, Объект.Дата);
	
	ПлательщикУСН =  СистемаНалогообложенияСтруктура.ПлательщикУСН;
	
	Если Не ПлательщикУСН Тогда
		Элементы.ГруппаУчетВНУ.Видимость = Ложь;
	Иначе
		Если Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Поставщику
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Покупателю
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Прочее
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.НаРасходы
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.ПрочиеРасчеты 
			ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Зарплата Тогда
			Элементы.УчитыватьВНУ.Видимость = Истина;
		Иначе
			Элементы.УчитыватьВНУ.Видимость = Ложь;
		КонецЕсли;
		
		Элементы.ГруппаУчетВНУ.Видимость = Элементы.УчитыватьВНУ.Видимость;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНастройкиУчетаВНалогообложении()
	
	ОпределитьВидимостьНастроекУчетаВНалогообложении();
	Если ЗначениеЗаполнено(Объект.Статья) Тогда
		Объект.УчитыватьВНУ = СтатьяУчитываетсяВНУ(Объект.Статья, Объект.Организация, Объект.Дата);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНазначениеПлатежаНаСервере()
	Перем ВидНалога;
	
	Если ЗначениеЗаполнено(Объект.Дата) Тогда
		Период = Объект.Дата;
	Иначе
		Период = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.ВидНалога) Тогда
		ВидНалога = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.ВидНалога, "ВидНалога");
	КонецЕсли;
	
	ПоказателиНалоговогоПериода = ПлатежиВБюджетКлиентСервер.ПоказателиНалоговогоПериода(Объект.Организация, ВидНалога, Период);
	
	Если ПустаяСтрока(Объект.НазначениеПлатежа) Тогда
		Объект.НазначениеПлатежа = Справочники.ВидыНалогов.НазначениеПлатежа(
			Объект.ВидНалога,,
			Объект.Организация,
			Макс(ПоказателиНалоговогоПериода.Период, Период),
			ПоказателиНалоговогоПериода.ПредставлениеНалоговогоПериода);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область УправлениеВнешнимВидомФормы

// Процедура устанавливает текущую страницу в зависимости от вида операции.
//
&НаКлиенте
Процедура УстановитьТекущуюСтраницу()
	
	КоличествоСтрок = Объект.РасшифровкаПлатежа.Количество();
	
	Если КоличествоСтрок = 0 Тогда
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		КоличествоСтрок = 1;
	КонецЕсли;
	
КонецПроцедуры // УстановитьТекущуюСтраницу()

// Процедура очищает реквизиты, которые ранее могли быть заполнены, но не
// относятся к текущей операции.
//
&НаКлиенте
Процедура ОчиститьРеквизитыНеОтносящиесяКОперации()
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику")
	 ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Объект.Подразделение = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Подотчетнику") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подразделение = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Зарплата") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.Подразделение = Неопределено;
		Если НЕ ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов Тогда
			Объект.НаправлениеДеятельности = Неопределено;
		КонецЕсли;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.РасшифровкаПлатежа.Очистить();
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Прочее")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.СнятиеНаличных")
		ИЛИ Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПереводНаДругойСчет") Тогда
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.НаРасходы") Тогда
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Налоги") Тогда
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.Корреспонденция = Неопределено;
		Объект.Подразделение = Неопределено;
		Если НЕ ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов Тогда
			Объект.НаправлениеДеятельности = Неопределено;
		КонецЕсли;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	// Эквайринг
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВозвратОплатыНаПлатежныеКарты") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Объект.Подразделение = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.СтатьяДДС = Неопределено;
		КонецЦикла;
	// Конец Эквайринг
	// Прочие расчеты
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подразделение = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.Подразделение = Неопределено;
		Объект.НаправлениеДеятельности = Неопределено;
		Объект.Заказ = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Для каждого СтрокаТаблицы Из Объект.РасшифровкаПлатежа Цикл
			СтрокаТаблицы.Договор = Неопределено;
			СтрокаТаблицы.ПризнакАванса = Ложь;
			СтрокаТаблицы.Документ = Неопределено;
			СтрокаТаблицы.Заказ = Неопределено;
			СтрокаТаблицы.СчетНаОплату = Неопределено;
			СтрокаТаблицы.СтавкаНДС = Неопределено;
			СтрокаТаблицы.СуммаНДС = Неопределено;
		КонецЦикла;
		Объект.ЭквайринговыеОперации.Очистить();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты") Тогда
		Объект.Корреспонденция = Неопределено;
		Объект.Контрагент = Неопределено;
		Объект.СчетКонтрагента = Неопределено;
		Объект.Подотчетник = Неопределено;
		Объект.Документ = Неопределено;
		Объект.ВидНалога = Неопределено;
		Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		Объект.ДоговорКредитаЗайма = Неопределено;
		Объект.Заказ = Неопределено;
		Объект.ВыплатаЗаработнойПлаты.Очистить();
		Объект.РасшифровкаПлатежа.Очистить();
		Объект.РасшифровкаПлатежа.Добавить();
		Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
		Объект.ЭквайринговыеОперации.Очистить();
	// Конец Прочие расчеты
	КонецЕсли;
	
	Корреспонденция = Объект.Корреспонденция;
	
КонецПроцедуры // ОчиститьРеквизитыНеОтносящиесяКОперации()

// Процедура устанавливает связи параметров выбора и доступные типы.
//
&НаСервере
Процедура УстановитьСвязиПараметровВыбораДоступныеТипы()
	
	// Прочие расчеты
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ПрочиеРасчеты") Тогда
		УстановитьПараметрыВыбораПоУчетуПрочихРасчетовНаСервереДляЭлементаСчетаКорреспонденции();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.НаРасходы") Тогда
		УстановитьПараметрыВыбораНаРасходыНаСервереДляЭлементаСчетаКорреспонденции();
	Иначе
		УстановитьПараметрыВыбораПоМетаданнымДляЭлементаСчетаКорреспонденции();
	КонецЕсли;
	// Конец Прочие расчеты
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Поставщику") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("ДокументСсылка.ДополнительныеРасходы"));
		Массив.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		Массив.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетПереработчика"));
		Массив.Добавить(Тип("ДокументСсылка.Взаимозачет"));
		
		ДопустимыеТипы = Новый ОписаниеТипов(Массив, ,);
		Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПоставщику", ,);
		Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплатуПоставщика", , );
		Элементы.РасшифровкаПлатежаСчетНаОплату.ОграничениеТипа = ДопустимыеТипы;
		
		Элементы.РасшифровкаПлатежаДокумент.Подсказка = "Оплачиваемый документ отгрузки товаров, работ и услуг контрагентом";
		
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.Покупателю") Тогда
		
		Массив = Новый Массив();
		Массив.Добавить(Тип("ДокументСсылка.ПоступлениеВКассу"));
		Массив.Добавить(Тип("ДокументСсылка.ПоступлениеНаСчет"));
		Массив.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
		Массив.Добавить(Тип("ДокументСсылка.Взаимозачет"));
		Массив.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
		Массив.Добавить(Тип("ДокументСсылка.ОтчетОПереработке"));
		Массив.Добавить(Тип("ДокументСсылка.ПередачаВА"));
		Массив.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		Массив.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
		
		ДопустимыеТипы = Новый ОписаниеТипов(Массив, ,);
		Элементы.РасшифровкаПлатежаДокумент.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.ЗаказПокупателя", , );
		Элементы.РасшифровкаПлатежаЗаказ.ОграничениеТипа = ДопустимыеТипы;
		
		ДопустимыеТипы = Новый ОписаниеТипов("ДокументСсылка.СчетНаОплату", , );
		Элементы.РасшифровкаПлатежаСчетНаОплату.ОграничениеТипа = ДопустимыеТипы;
		
		Элементы.РасшифровкаПлатежаДокумент.Подсказка = "Документ расчетов с контрагентом, по которому осуществляется возврат денежных средств";
		
	КонецЕсли;
	
КонецПроцедуры // УстановитьСвязиПараметровВыбораДоступныеТипы()

// Процедура устанавливает устанавливает видимость реквизитов в зависимости от корреспонденции.
//
&НаСервере
Процедура УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции(ОчиститьАналитику = Ложь)
	
	Если Объект.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Расходы Тогда
		Элементы.НаправлениеДеятельности.Видимость = Истина;
		Элементы.Подразделение.Видимость = Истина;
		Элементы.Заказ.Видимость = Истина;
		Если НЕ ЗначениеЗаполнено(Объект.Подразделение) Тогда
			Пользователь = Пользователи.ТекущийПользователь();
			ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновноеПодразделение");
			Объект.Подразделение = ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение);
		КонецЕсли;
	Иначе
		Если Объект.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.Налоги // для ввода на основании
		 ИЛИ (Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Налоги
		 И НЕ ФункциональнаяОпцияКассовыйМетодУчетаДоходовИРасходов) Тогда
			Объект.НаправлениеДеятельности = Неопределено;
		КонецЕсли;
		Объект.Подразделение = Неопределено;
		Объект.Заказ = Неопределено;
		Элементы.НаправлениеДеятельности.Видимость = Ложь;
		Элементы.Подразделение.Видимость = Ложь;
		Элементы.Заказ.Видимость = Ложь;
			
	КонецЕсли;
	
	// Прочие расчеты
	Если (Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.Прочее 
		ИЛИ Объект.ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.НаРасходы) Тогда
		
		АналитикаСчета = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Корреспонденция, "АналитикаДоходовИРасходов");
		
		Если ОчиститьАналитику И ЗначениеЗаполнено(АналитикаСчета) Тогда
			Элементы.АналитикаПрочихДоходовИРасходов.ОграничениеТипа = Новый ОписаниеТипов(АналитикаСчета);
			Элементы.АналитикаПрочихДоходовИРасходов.Заголовок = Строка(Элементы.АналитикаПрочихДоходовИРасходов.ОграничениеТипа);
			Элементы.АналитикаПрочихДоходовИРасходов.Доступность = Истина;
			Элементы.АналитикаПрочихДоходовИРасходов.Видимость = Истина;
			Элементы.АналитикаПрочихДоходовИРасходов.ПодсказкаВвода = "";
			Объект.АналитикаПрочихДоходовИРасходов = Новый(АналитикаСчета);
		ИначеЕсли ЗначениеЗаполнено(АналитикаСчета) Тогда
			Элементы.АналитикаПрочихДоходовИРасходов.ОграничениеТипа = Новый ОписаниеТипов(АналитикаСчета);
			Элементы.АналитикаПрочихДоходовИРасходов.Заголовок = Строка(Элементы.АналитикаПрочихДоходовИРасходов.ОграничениеТипа);
			Элементы.АналитикаПрочихДоходовИРасходов.Доступность = Истина;
			Элементы.АналитикаПрочихДоходовИРасходов.Видимость = Истина;
			Элементы.АналитикаПрочихДоходовИРасходов.ПодсказкаВвода = "";
		Иначе // Очистить и не заполнена аналитика.
			Элементы.АналитикаПрочихДоходовИРасходов.Заголовок = "Аналитика расходов";
			Элементы.АналитикаПрочихДоходовИРасходов.ПодсказкаВвода = "<Не настроена у этого счета>";
			Элементы.АналитикаПрочихДоходовИРасходов.Доступность = Ложь;
			Элементы.АналитикаПрочихДоходовИРасходов.Видимость = Истина;
			Объект.АналитикаПрочихДоходовИРасходов = Неопределено;
		КонецЕсли;
		
	Иначе
		Элементы.АналитикаПрочихДоходовИРасходов.Видимость = Ложь;
	КонецЕсли;
	// Конец Прочие расчеты
	
КонецПроцедуры // УстановитьВидимостьРеквизитовВЗависимостиОтКорреспонденции()

// Процедура устанавливает доступность элементов формы.
//
// Параметры:
//  Нет.
//
&НаСервере
Процедура УстановитьВидимостьИДоступность(ИзмененВидОперации = Ложь)
	
	// Документ основание.
	НовыйМассив = Новый Массив();
	НовыеПараметры = Новый ФиксированныйМассив(НовыйМассив);
	ПараметрыВыбораДокументаОснования = НовыеПараметры;
	// Конец Документ основание.
	
КонецПроцедуры // УстановитьВидимостьИДоступность()

// Процедура устанавливает доступность и подсказку ввода элемента ДоговорЗаймаСотруднику формы.
//
&НаСервере
Процедура НастроитьЭлементДоговораЗайма()
	
	Элементы.ДоговорЗаймаСотруднику.Доступность = НЕ Объект.Подотчетник.Пустая();
	Если Элементы.ДоговорЗаймаСотруднику.Доступность Тогда
		Элементы.ДоговорЗаймаСотруднику.ПодсказкаВвода = "";
	Иначе
		Элементы.ДоговорЗаймаСотруднику.ПодсказкаВвода = НСтр("ru = 'Чтобы выбрать договор, выберите сотрудника'");
	КонецЕсли;
	
	Элементы.ДоговорКредита.Доступность = НЕ Объект.Контрагент.Пустая();
	Если Элементы.ДоговорКредита.Доступность Тогда
		Элементы.ДоговорКредита.ПодсказкаВвода = "";
	Иначе
		Элементы.ДоговорКредита.ПодсказкаВвода = НСтр("ru = 'Чтобы выбрать договор, выберите банк'");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗаполнениеОбъектов

&НаКлиенте
Процедура СохранитьДокументКакШаблон(Параметр) Экспорт
	
	ЗаполнениеОбъектовУНФКлиент.СохранитьДокументКакШаблон(Объект, ОтображаемыеРеквизиты(), Параметр);
	
КонецПроцедуры

&НаСервере
Функция ОтображаемыеРеквизиты()
	
	Возврат ЗаполнениеОбъектовУНФ.ОтображаемыеРеквизиты(ЭтаФорма);
	
КонецФункции

#КонецОбласти

#Область ПрочиеРасчеты

#Область ПроцедурыОбработчикиСобытийРеквизитовШапки

// Процедура - обработчик события ПриИзменении поля ввода ДоговорЗаймаСотруднику.
//
&НаКлиенте
Процедура ДоговорЗаймаСотрудникуПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКредитаИлиЗайма();
	
КонецПроцедуры

// Процедура заполняет реквизиты документа в соответствии с реквизитами договора.
//
&НаКлиенте
Процедура ОбработатьИзменениеДоговораКредитаИлиЗайма()
	
	ДанныеДоговораЗаймаСотруднику = ДоговорКредитаЗаймаПриИзмененииНаСервере(Объект.ДоговорКредитаЗайма, Объект.Дата);
	
	ЗаполнитьИнформациюПоКредитуЗаймуНаСервере();
	
КонецПроцедуры

// Функция возвращает структуру с реквизитами договора кредита (займа).
//
&НаСервереБезКонтекста
Функция ДоговорКредитаЗаймаПриИзмененииНаСервере(ДоговорКредитаЗайма, Дата)
	
	СтруктураДанные = Новый Структура;
	
	СтруктураДанные.Вставить("ВалютаРасчетов", ДоговорКредитаЗайма.ВалютаРасчетов);
	СтруктураДанные.Вставить("Контрагент", ДоговорКредитаЗайма.Контрагент);
	СтруктураДанные.Вставить("Сотрудник", ДоговорКредитаЗайма.Сотрудник);
	СтруктураДанные.Вставить("ЭтоДоговорЗайма", ДоговорКредитаЗайма.ВидДоговора = Перечисления.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику);
		
	Возврат СтруктураДанные;
	
КонецФункции

// Процедура - обработчик события ПриИзменении поля ввода Сотрудник.
//
&НаКлиенте
Процедура СотрудникПриИзменении(Элемент)
	
	СтруктураДанные = ПолучитьДанныеСотрудникПриИзменении(Объект.Подотчетник, Объект.Дата, Объект.Организация);
	
	Объект.ДоговорКредитаЗайма = СтруктураДанные.ДоговорКредитаЗайма;
	ОбработатьИзменениеДоговораКредитаИлиЗайма();
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийРеквизитовТабличнойЧасти

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаПрочиеРасчеты.СуммаРасчетов.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыСуммаРасчетовПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;

КонецПроцедуры

// Процедура - обработчик события ПередУдалением элемента РасшифровкаПлатежаПрочиеРасчеты (ТЧ).
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаПрочиеРасчеты.Курс.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыКурсПриИзменении(Элемент)
		
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаПрочиеРасчеты.Кратность.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыКратностьПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаПрочиеРасчеты.СуммаПлатежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.СуммаРасчетов = 0,
		1,
		СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
	);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаПрочиеРасчеты.Договор.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыДоговорПриИзменении(Элемент)
	
	ОбработатьИзменениеДоговораКонтрагентаПрочиеРасчеты();
	
КонецПроцедуры

// Процедура выполняет действия при изменении договора контрагента.
//
&НаКлиенте
Процедура ОбработатьИзменениеДоговораКонтрагентаПрочиеРасчеты()
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	
	Если ЗначениеЗаполнено(СтрокаТабличнойЧасти.Договор) Тогда
		СтруктураДанные = ПолучитьДанныеРасшифровкаПлатежаДоговорПриИзменении(
			Объект.Дата,
			СтрокаТабличнойЧасти.Договор,
			СтрокаТабличнойЧасти.ДокументПланирования,
			СтрокаТабличнойЧасти.СтатьяДДС
		);
		СтрокаТабличнойЧасти.Курс = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Курс
		);
		СтрокаТабличнойЧасти.Кратность = ?(
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность = 0,
			1,
			СтруктураДанные.ДоговорВалютаКурсКратность.Кратность
		);
		
		СтрокаТабличнойЧасти.СтатьяДДС = СтруктураДанные.СтатьяДДСПоУмолчанию;
	Иначе
		СтрокаТабличнойЧасти.СтатьяДДС = Неопределено;
	КонецЕсли;
	
	СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
		СтрокаТабличнойЧасти.СуммаПлатежа,
		Курс,
		СтрокаТабличнойЧасти.Курс,
		Кратность,
		СтрокаТабличнойЧасти.Кратность
	);
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

// Процедура - обработчик события НачалоВыбора поля ввода РасшифровкаПлатежаПрочиеРасчеты.Договор.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Контрагент.Пустая() Тогда
		СтандартнаяОбработка = Ложь;
		
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала выберите контрагента'");
		Сообщение.Поле = "Объект.Контрагент";
		Сообщение.Сообщить();
		
		Возврат;
	КонецЕсли;
	
	ОбработатьНачалоВыбораДоговораКонтрагентаПрочиеРасчеты(Элемент, СтандартнаяОбработка);
	
КонецПроцедуры

// Процедура выполняет действия при начале выбора договора контрагента.
//
&НаКлиенте
Процедура ОбработатьНачалоВыбораДоговораКонтрагентаПрочиеРасчеты(Элемент, СтандартнаяОбработка)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	Если СтрокаТабличнойЧасти = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = ПолучитьПараметрыФормыВыбора(Объект.Ссылка, Объект.Организация, Объект.Контрагент, СтрокаТабличнойЧасти.Договор, Объект.ВидОперации);
	Если ПараметрыФормы.КонтролироватьВыборДоговора Тогда
		
		СтандартнаяОбработка = Ложь;
		ОткрытьФорму("Справочник.ДоговорыКонтрагентов.Форма.ФормаВыбора", ПараметрыФормы, Элемент);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработатьИзменениеДоговораКонтрагента()

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаПрочиеРасчеты.СтавкаНДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаПрочиеРасчетыСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаПрочиеРасчеты.ТекущиеДанные;
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);

КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаРасчетыПоКредитам.СуммаРасчетов.
//
&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамСуммаРасчетовПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаРасчетыПоКредитам.СуммаРасчетов.
//
&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамКурсПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаРасчетыПоКредитам.Кратность.
//
&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамКратностьПриИзменении(Элемент)
	
	РассчитатьСуммуПлатежаНаКлиенте(Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные);
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаРасчетыПоКредитам.СтавкаНДС.
//
&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамСтавкаНДСПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные;
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении поля ввода РасшифровкаПлатежаРасчетыПоКредитам.СуммаПлатежа.
//
&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамСуммаПлатежаПриИзменении(Элемент)
	
	СтрокаТабличнойЧасти = Элементы.РасшифровкаПлатежаРасчетыПоКредитам.ТекущиеДанные;
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.Курс = 0,
		1,
		СтрокаТабличнойЧасти.Курс
	);
	СтрокаТабличнойЧасти.Кратность = ?(
		СтрокаТабличнойЧасти.Кратность = 0,
		1,
		СтрокаТабличнойЧасти.Кратность
	);
	
	СтрокаТабличнойЧасти.Курс = ?(
		СтрокаТабличнойЧасти.СуммаРасчетов = 0,
		1,
		СтрокаТабличнойЧасти.СуммаПлатежа / СтрокаТабличнойЧасти.СуммаРасчетов * Курс
	);
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
		СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
	КонецЕсли;
	
	РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
	
КонецПроцедуры

// Процедура - обработчик события ПередУдалением табличной части РасшифровкаПлатежаРасчетыПоКредитам.
//
&НаКлиенте
Процедура РасшифровкаПлатежаРасчетыПоКредитамПередУдалением(Элемент, Отказ)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыДействияКоманд

// Процедура - обработчик команды ЗаполнитьПоДоговоруЗайма. Заполняет сумму документа остатком невыплаченной суммы.
//
&НаСервере
Процедура ЗаполнитьПоДоговоруЗаймаНаСервере()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасчетыПоКредитамИЗаймамОбороты.ДоговорКредитаЗайма.ВалютаРасчетов КАК ВалютаРасчетов,
	|	РасчетыПоКредитамИЗаймамОбороты.ОсновнойДолгВалПриход
	|ПОМЕСТИТЬ ВременнаяТаблицаВыданныеРанееСуммы
	|ИЗ
	|	РегистрНакопления.РасчетыПоКредитамИЗаймам.Обороты(
	|			,
	|			,
	|			,
	|			ДоговорКредитаЗайма = &ДоговорКредитаЗайма
	|				И Организация = &Организация) КАК РасчетыПоКредитамИЗаймамОбороты
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма.ВалютаРасчетов,
	|	ВЫБОР
	|		КОГДА РасчетыПоКредитамИЗаймам.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|		ИНАЧЕ РасчетыПоКредитамИЗаймам.ОсновнойДолгВал
	|	КОНЕЦ
	|ИЗ
	|	РегистрНакопления.РасчетыПоКредитамИЗаймам КАК РасчетыПоКредитамИЗаймам
	|ГДЕ
	|	РасчетыПоКредитамИЗаймам.Регистратор = &Ссылка
	|	И РасчетыПоКредитамИЗаймам.ДоговорКредитаЗайма = &ДоговорКредитаЗайма
	|	И РасчетыПоКредитамИЗаймам.Организация = &Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов,
	|	СУММА(ВременнаяТаблицаВыданныеРанееСуммы.ОсновнойДолгВалПриход) КАК ОсновнойДолгВалПриход,
	|	ДоговорКредитаЗайма.СуммаДокумента
	|ИЗ
	|	ВременнаяТаблицаВыданныеРанееСуммы КАК ВременнаяТаблицаВыданныеРанееСуммы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДоговорКредитаИЗайма КАК ДоговорКредитаЗайма
	|		ПО ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов = ДоговорКредитаЗайма.ВалютаРасчетов
	|ГДЕ
	|	ДоговорКредитаЗайма.Ссылка = &ДоговорКредитаЗайма
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов,
	|	ДоговорКредитаЗайма.СуммаДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(, Валюта = &ВалютаРасчетов) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(, Валюта = &ВалютаДенежныхСредств) КАК КурсыВалютСрезПоследних";
	
	Запрос.УстановитьПараметр("ДоговорКредитаЗайма", Объект.ДоговорКредитаЗайма);
	Запрос.УстановитьПараметр("Организация", Компания);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("ВалютаРасчетов", Объект.ДоговорКредитаЗайма.ВалютаРасчетов);
	Запрос.УстановитьПараметр("ВалютаДенежныхСредств", Объект.ВалютаДенежныхСредств);
	
	МРезультатов = Запрос.ВыполнитьПакет();
	
	Выборка = МРезультатов[1].Выбрать();
	
	ВыборкаВалютаДоговора = МРезультатов[2].Выбрать();
	Если ВыборкаВалютаДоговора.Следующий() Тогда
		КурсДоговора = ВыборкаВалютаДоговора.Курс;
		КратностьДоговора = ВыборкаВалютаДоговора.Кратность;
	Иначе
		КурсДоговора = 1;
		КратностьДоговора = 1;
	КонецЕсли;
	
	ВыборкаВалютыДС = МРезультатов[3].Выбрать();
	Если ВыборкаВалютыДС.Следующий() Тогда
		КурсДокумента = ВыборкаВалютыДС.Курс;
		КратностьДокумента = ВыборкаВалютыДС.Кратность;
	Иначе
		КурсДокумента = 1;
		КратностьДокумента = 1;
	КонецЕсли;
	
	Если Выборка.Следующий() Тогда
		ТекстСообщения = "";
		Если Выборка.СуммаДокумента < Выборка.ОсновнойДолгВалПриход Тогда
			ТекстСообщения = "По договору займа уже выдано "+Выборка.ОсновнойДолгВалПриход+" ("+Выборка.ВалютаРасчетов+").";
		ИначеЕсли Выборка.СуммаДокумента = Выборка.ОсновнойДолгВалПриход Тогда
			ТекстСообщения = "По договору займа уже выдана вся сумма "+Выборка.ОсновнойДолгВалПриход+" ("+Выборка.ВалютаРасчетов+").";
		Иначе
			Объект.СуммаДокумента = (Выборка.СуммаДокумента - Выборка.ОсновнойДолгВалПриход) * КурсДоговора * КратностьДокумента / (КратностьДоговора * КурсДокумента);
		КонецЕсли;
		
		Если ТекстСообщения <> "" Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле = "ДоговорКредитаЗайма";
			Сообщение.Сообщить();
		КонецЕсли;
	Иначе
		Объект.СуммаДокумента = Объект.ДоговорКредитаЗайма.СуммаДокумента * КурсДоговора * КратностьДокумента / (КратностьДоговора * КурсДокумента);
	КонецЕсли;
	
	Модифицированность = Истина;
	
КонецПроцедуры

// Процедура - обработчик команды ЗаполнитьПоДоговоруЗайма.
//
&НаКлиенте
Процедура ЗаполнитьПоДоговоруЗайма(Команда)
	
	Если Объект.ДоговорКредитаЗайма.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, "Выберите договор");
		Возврат;
	КонецЕсли;
	
	ЗаполнитьПоДоговоруЗаймаНаСервере();
	СуммаДокументаПриИзменении(Элементы.СуммаДокумента);
	
КонецПроцедуры

// Процедура - обработчик команды ЗаполнитьПоДоговоруКредита.
//
&НаКлиенте
Процедура ЗаполнитьПоДоговоруКредита(Команда)
	
	Если Объект.ДоговорКредитаЗайма.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, "Выберите договор");
		Возврат;
	КонецЕсли;
	
	АдресРасшифровкиПлатежаВХранилище = ПоместитьРасшифровкаПлатежаВХранилище();
	ПараметрыОтбора = Новый Структура("
		|АдресРасшифровкиПлатежаВХранилище,
		|Организация,
		|Регистратор,
		|ИдентификаторФормыДокумента,
		|ВидОперации,
		|Дата,
		|Валюта,
		|ДоговорКредитаЗайма,
		|СуммаДокумента,
		|Контрагент,
		|СтавкаНДСПоУмолчанию,
		|СуммаПлатежа,
		|Курс,
		|Кратность,
		|Сотрудник",
		АдресРасшифровкиПлатежаВХранилище,
		Объект.Организация,
		Объект.Ссылка,
		УникальныйИдентификатор,
		Объект.ВидОперации,
		Объект.Дата,
		Объект.ВалютаДенежныхСредств,
		Объект.ДоговорКредитаЗайма,
		Объект.СуммаДокумента,
		Объект.Контрагент,
		СтавкаНДСПоУмолчанию,
		Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа"),
		Курс,
		Кратность,
		Объект.Подотчетник);
	
	ОткрытьФорму("ОбщаяФорма.ФормаЗаполненияРасшифровкиПлатежаПоКредитамИЗаймам", 
						ПараметрыОтбора,
						ЭтаФорма,,,, Новый ОписаниеОповещения("ЗаполнитьПоДоговоруКредитаЗавершение", ЭтотОбъект));
	
КонецПроцедуры

// Процедура - обработчик команды ЗаполнитьПоДоговоруКредита. Завершающая часть, которая вызывается после выбора данных заполнения.
//
&НаКлиенте
Процедура ЗаполнитьПоДоговоруКредитаЗавершение(РезультатЗаполнения, ПараметрыЗавершения) Экспорт

	Если ТипЗнч(РезультатЗаполнения) = Тип("Структура") Тогда
		ЗаполнитьСуммуДокумента = Ложь;
		Если РезультатЗаполнения.Свойство("ОчищатьТабличнуюЧастьПриЗаполнении") И РезультатЗаполнения.ОчищатьТабличнуюЧастьПриЗаполнении Тогда
			Объект.РасшифровкаПлатежа.Очистить();
			ЗаполнитьСуммуДокумента = Истина;
		КонецЕсли;
		Если РезультатЗаполнения.Свойство("АдресРасшифровкиПлатежаВХранилище") Тогда
			ПолучитьРасшифровкаПлатежаИзХранилища(РезультатЗаполнения.АдресРасшифровкиПлатежаВХранилище, Ложь);
			
			Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
				Объект.СуммаДокумента = Объект.РасшифровкаПлатежа[0].СуммаПлатежа;
			КонецЕсли;
		КонецЕсли;
		Если ЗаполнитьСуммуДокумента Тогда
			Объект.СуммаДокумента = Объект.РасшифровкаПлатежа.Итог("СуммаПлатежа");
			СуммаДокументаПриИзменении(Элементы.СуммаДокумента);
		КонецЕсли;
		
		Модифицированность = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийФормы

// Процедура - обработчик события ПослеЗаписиНаСервере.
//
&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Если ТекущийОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику") Или
		ТекущийОбъект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам") Тогда
		ЗаполнитьИнформациюПоКредитуЗаймуНаСервере();
	КонецЕсли;
	
	ОбновитьВидимостьИОстаткиВзаиморасчетов();
	ОбновитьВидимостьИОстаткиДС(Объект.БанковскийСчет);
	
	// Зачет долгов
	ОбновитьИнформациюОбАвансеВРасшифровкеПлатежа();
	
	ИмяКлючаОбъекта = СтрЗаменить(ИмяФормы,".","");
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+"_ВариантЗаполненияРасшифровки", Объект.ВариантЗаполненияРасшифровки);
	
	ИсходнаяСуммаРавнаНулю = (Объект.СуммаДокумента = 0);
	// Конец Зачет долгов
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

// Процедура устанавливает параметры выбора элемента Корреспонденция для вида операции "ПрочиеРасчеты".
//
&НаСервере
Процедура УстановитьПараметрыВыбораПоУчетуПрочихРасчетовНаСервереДляЭлементаСчетаКорреспонденции()

	Элемент = Элементы.Корреспонденция;
	
	ПараметрыВыбораЭлемента = Новый Массив;
	ОтборПоТипуСчета = Новый Массив;
	
	Для Каждого Параметр Из Элемент.ПараметрыВыбора Цикл
		Если Параметр.Имя = "Отбор.ТипСчета" Тогда
			ОтборПоТипуСчета.Добавить(Перечисления.ТипыСчетов.Дебиторы);
			ОтборПоТипуСчета.Добавить(Перечисления.ТипыСчетов.Кредиторы);
			
			ПараметрыВыбораЭлемента.Добавить(Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(ОтборПоТипуСчета)));
		Иначе
			ПараметрыВыбораЭлемента.Добавить(Параметр);
		КонецЕсли;
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецПроцедуры

// Процедура устанавливает параметры выбора элемента Корреспонденция для вида операции "ПрочиеРасчеты".
//
&НаСервере
Процедура УстановитьПараметрыВыбораНаРасходыНаСервереДляЭлементаСчетаКорреспонденции()

	Элемент = Элементы.Корреспонденция;
	
	ПараметрыВыбораЭлемента = Новый Массив;
	ОтборПоТипуСчета = Новый Массив;
	
	Для Каждого Параметр Из Элемент.ПараметрыВыбора Цикл
		Если Параметр.Имя = "Отбор.ТипСчета" Тогда
			ОтборПоТипуСчета.Добавить(Перечисления.ТипыСчетов.Расходы);
			ОтборПоТипуСчета.Добавить(Перечисления.ТипыСчетов.ПрочиеРасходы);
			
			ПараметрыВыбораЭлемента.Добавить(Новый ПараметрВыбора("Отбор.ТипСчета", Новый ФиксированныйМассив(ОтборПоТипуСчета)));
		ИначеЕсли Параметр.Имя = "ЗаголовокСчета" Тогда
			ПараметрыВыбораЭлемента.Добавить(Новый ПараметрВыбора("ЗаголовокСчета", "Статья расходов"));
		Иначе
			ПараметрыВыбораЭлемента.Добавить(Параметр);
		КонецЕсли;
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецПроцедуры

// Процедура устанавливает параметры выбора элемента Корреспонденция для всех видов операций кроме "ПрочиеРасчеты".
//
&НаСервере
Процедура УстановитьПараметрыВыбораПоМетаданнымДляЭлементаСчетаКорреспонденции()

	Элемент = Элементы.Корреспонденция;
	
	ПараметрыВыбораЭлемента = Новый Массив;
	ОтборПоТипуСчета = Новый Массив;
	
	ПараметрыВыбораИзМетаданных = Объект.Ссылка.Метаданные().Реквизиты.Корреспонденция.ПараметрыВыбора;
	Для Каждого Параметр Из ПараметрыВыбораИзМетаданных Цикл
		ПараметрыВыбораЭлемента.Добавить(Параметр);
	КонецЦикла;
	
	Элемент.ПараметрыВыбора = Новый ФиксированныйМассив(ПараметрыВыбораЭлемента);
	
КонецПроцедуры

// Процедура заполняет заголовки информационных надписей о договоре кредита (займа) в зависимости от вида операции документа.
//
&НаСервере
Процедура ЗаполнитьИнформациюПоКредитуЗаймуНаСервере()
	
	НастроитьЭлементДоговораЗайма();
	
	Если Объект.ДоговорКредитаЗайма.Пустая() Тогда
		Элементы.НадписьИнформацияПоКредиту.Заголовок = "<Выберите договор кредита (займа)>";
		Элементы.НадписьОстатокДолгаПоКредиту.Заголовок = "";
		
		Элементы.НадписьИнформацияПоКредиту.ЦветТекста = ЦветаСтиля.ЦветРамки;
		Элементы.НадписьОстатокДолгаПоКредиту.ЦветТекста = ЦветаСтиля.ЦветРамки;
		
		Возврат;
	КонецЕсли;
	
	Если Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.ВыдачаЗаймаСотруднику") Тогда
		ЗаполнитьИнформациюПоЗаймуНаСервере();
	ИначеЕсли Объект.ВидОперации = ПредопределенноеЗначение("Перечисление.ВидыОперацийРасходСоСчета.РасчетыПоКредитам") Тогда
		ЗаполнитьИнформациюПоКредитуНаСервере();
	КонецЕсли;
	
КонецПроцедуры

// Процедура заполняет заголовки информационных надписей о договоре кредита.
//
&НаСервере
Процедура ЗаполнитьИнформациюПоКредитуНаСервере();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ГрафикПогашенияКредитовИЗаймовСрезПоследних.Период,
		|	ГрафикПогашенияКредитовИЗаймовСрезПоследних.СуммаОсновногоДолга,
		|	ГрафикПогашенияКредитовИЗаймовСрезПоследних.СуммаПроцентов,
		|	ГрафикПогашенияКредитовИЗаймовСрезПоследних.СуммаКомиссии,
		|	ГрафикПогашенияКредитовИЗаймовСрезПоследних.ДоговорКредитаЗайма.ВалютаРасчетов.Наименование КАК ВалютаРасчетовПредставление
		|ИЗ
		|	РегистрСведений.ГрафикПогашенияКредитовИЗаймов.СрезПоследних(&ДатаСрезаПоследних, ДоговорКредитаЗайма = &ДоговорКредитаЗайма) КАК ГрафикПогашенияКредитовИЗаймовСрезПоследних
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СУММА(РасчетыПоКредитамИЗаймамОстатки.ОсновнойДолгВалОстаток) КАК ОсновнойДолгВалОстаток,
		|	РасчетыПоКредитамИЗаймамОстатки.ДоговорКредитаЗайма.ВалютаРасчетов.Наименование КАК ВалютаРасчетовПредставление,
		|	СУММА(РасчетыПоКредитамИЗаймамОстатки.ПроцентыВалОстаток) КАК ПроцентыВалОстаток,
		|	СУММА(РасчетыПоКредитамИЗаймамОстатки.КомиссияВалОстаток) КАК КомиссияВалОстаток
		|ИЗ
		|	РегистрНакопления.РасчетыПоКредитамИЗаймам.Остатки(, ДоговорКредитаЗайма = &ДоговорКредитаЗайма) КАК РасчетыПоКредитамИЗаймамОстатки
		|
		|СГРУППИРОВАТЬ ПО
		|	РасчетыПоКредитамИЗаймамОстатки.ДоговорКредитаЗайма.ВалютаРасчетов,
		|	РасчетыПоКредитамИЗаймамОстатки.ДоговорКредитаЗайма.ВалютаРасчетов.Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ГрафикПогашенияКредитовИЗаймовСрезПервых.Период,
		|	ГрафикПогашенияКредитовИЗаймовСрезПервых.СуммаОсновногоДолга,
		|	ГрафикПогашенияКредитовИЗаймовСрезПервых.СуммаПроцентов,
		|	ГрафикПогашенияКредитовИЗаймовСрезПервых.СуммаКомиссии,
		|	ГрафикПогашенияКредитовИЗаймовСрезПервых.ДоговорКредитаЗайма.ВалютаРасчетов.Наименование КАК ВалютаРасчетовПредставление
		|ИЗ
		|	РегистрСведений.ГрафикПогашенияКредитовИЗаймов.СрезПервых(&ДатаСрезаПоследних, ДоговорКредитаЗайма = &ДоговорКредитаЗайма) КАК ГрафикПогашенияКредитовИЗаймовСрезПервых";
	
	Запрос.УстановитьПараметр("ДатаСрезаПоследних", ?(Объект.Дата = '00010101', НачалоДня(ТекущаяДата()), НачалоДня(Объект.Дата)));
	Запрос.УстановитьПараметр("ДоговорКредитаЗайма", Объект.ДоговорКредитаЗайма);
	
	МРезультатов = Запрос.ВыполнитьПакет();
	
	Если Объект.ДоговорКредитаЗайма.ВидДоговора = Перечисления.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику Тогда
		Множитель = 1;
	Иначе
		Множитель = -1;
	КонецЕсли;
	
	ВыборкаГрафик = МРезультатов[0].Выбрать();
	ВыборкаГрафикБудущиеМесяцы = МРезультатов[2].Выбрать();
	
	НадписьИнформацияПоКредитуЦветТекста = ЦветаСтиля.ЦветРамки;
	НадписьОстатокДолгаПоКредитуЦветТекста = ЦветаСтиля.ЦветРамки;
	
	Если ВыборкаГрафикБудущиеМесяцы.Следующий() Тогда
		
		Если НачалоМесяца(?(Объект.Дата = '00010101', ТекущаяДата(), Объект.Дата)) = НачалоМесяца(ВыборкаГрафикБудущиеМесяцы.Период) Тогда
			ДатаПлатежа = "" + Формат(ВыборкаГрафикБудущиеМесяцы.Период, "ДФ=dd.MM.yyyy");
		Иначе
			ДатаПлатежа = "" + Формат(ВыборкаГрафикБудущиеМесяцы.Период, "ДФ=dd.MM.yyyy") + " (не в тек. месяце)";
			НадписьИнформацияПоКредитуЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		КонецЕсли;
			
		НадписьИнформацияПоКредиту = "Дата платежа: "+ДатаПлатежа+". Сумма долга: "+Формат(ВыборкаГрафикБудущиеМесяцы.СуммаОсновногоДолга, "ЧДЦ=2; ЧН=0")+". Сумма %: "+
			Формат(ВыборкаГрафикБудущиеМесяцы.СуммаПроцентов, "ЧДЦ=2; ЧН=0")+
			". Комиссия: "+Формат(ВыборкаГрафикБудущиеМесяцы.СуммаКомиссии, "ЧДЦ=2; ЧН=0")+" (" + ВыборкаГрафикБудущиеМесяцы.ВалютаРасчетовПредставление +")";
		
	ИначеЕсли ВыборкаГрафик.Следующий() Тогда
		
		Если НачалоМесяца(?(Объект.Дата = '00010101', ТекущаяДата(), Объект.Дата)) = НачалоМесяца(ВыборкаГрафик.Период) Тогда
			ДатаПлатежа = "" + Формат(ВыборкаГрафик.Период, "ДФ=dd.MM.yyyy");
		Иначе
			ДатаПлатежа = "" + Формат(ВыборкаГрафик.Период, "ДФ=dd.MM.yyyy") + " (не в тек. месяце)";
			НадписьИнформацияПоКредитуЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		КонецЕсли;
			
		НадписьИнформацияПоКредиту = "Дата платежа: "+ДатаПлатежа+". Сумма долга: "+Формат(ВыборкаГрафик.СуммаОсновногоДолга, "ЧДЦ=2; ЧН=0")+". Сумма %: "+
			Формат(ВыборкаГрафик.СуммаПроцентов, "ЧДЦ=2; ЧН=0")+
			". Комиссия: "+Формат(ВыборкаГрафик.СуммаКомиссии, "ЧДЦ=2; ЧН=0")+" (" + ВыборкаГрафик.ВалютаРасчетовПредставление +")";
		
	Иначе
		
		НадписьИнформацияПоКредиту = "Дата платежа: <не определена>";
		
	КонецЕсли;
	
	ВыборкаОстатки = МРезультатов[1].Выбрать();
	Если ВыборкаОстатки.Следующий() Тогда
		
		НадписьОстатокДолгаПоКредиту = "Остаток долга: "+Формат(Множитель * ВыборкаОстатки.ОсновнойДолгВалОстаток, "ЧДЦ=2; ЧН=0")+". Сумма %: "+
			Формат(Множитель * ВыборкаОстатки.ПроцентыВалОстаток, "ЧДЦ=2; ЧН=0")+
			". Комиссия: "+Формат(Множитель * ВыборкаОстатки.КомиссияВалОстаток, "ЧДЦ=2; ЧН=0")+" (" + ВыборкаОстатки.ВалютаРасчетовПредставление +")";
			
		Если Множитель * ВыборкаОстатки.ОсновнойДолгВалОстаток >= 0 И (Множитель * ВыборкаОстатки.ПроцентыВалОстаток < 0 ИЛИ 
			Множитель * ВыборкаОстатки.КомиссияВалОстаток < 0) Тогда
			НадписьОстатокДолгаПоКредитуЦветТекста = ЦветаСтиля.ЦветТекстаФормы;
		КонецЕсли;
		
		Если Множитель * ВыборкаОстатки.ОсновнойДолгВалОстаток < 0 Тогда
			НадписьОстатокДолгаПоКредитуЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
		КонецЕсли;
	Иначе
		
		НадписьОстатокДолгаПоКредиту = "Остаток долга: <не определен> "
		
	КонецЕсли;
	
	Элементы.НадписьИнформацияПоКредиту.Заголовок = НадписьИнформацияПоКредиту;
	Элементы.НадписьОстатокДолгаПоКредиту.Заголовок = НадписьОстатокДолгаПоКредиту;
	
	Элементы.НадписьИнформацияПоКредиту.ЦветТекста = НадписьИнформацияПоКредитуЦветТекста;
	Элементы.НадписьОстатокДолгаПоКредиту.ЦветТекста = НадписьОстатокДолгаПоКредитуЦветТекста;
		
КонецПроцедуры

// Процедура заполняет заголовки информационных надписей о договоре займа сотруднику.
//
&НаСервере
Процедура ЗаполнитьИнформациюПоЗаймуНаСервере()

	НадписьИнформацияПоКредитуЦветТекста = ЦветаСтиля.ЦветРамки;
	НадписьОстатокДолгаПоКредитуЦветТекста = ЦветаСтиля.ЦветРамки;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	РасчетыПоКредитамИЗаймамОбороты.ДоговорКредитаЗайма.ВалютаРасчетов КАК ВалютаРасчетов,
		|	РасчетыПоКредитамИЗаймамОбороты.ОсновнойДолгВалПриход
		|ПОМЕСТИТЬ ВременнаяТаблицаВыданныеРанееСуммы
		|ИЗ
		|	РегистрНакопления.РасчетыПоКредитамИЗаймам.Обороты(
		|			,
		|			,
		|			,
		|			ДоговорКредитаЗайма = &ДоговорКредитаЗайма
		|				И Организация = &Организация) КАК РасчетыПоКредитамИЗаймамОбороты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов,
		|	СУММА(ВременнаяТаблицаВыданныеРанееСуммы.ОсновнойДолгВалПриход) КАК ОсновнойДолгВалПриход,
		|	ДоговорКредитаЗайма.СуммаДокумента
		|ИЗ
		|	ВременнаяТаблицаВыданныеРанееСуммы КАК ВременнаяТаблицаВыданныеРанееСуммы
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ДоговорКредитаИЗайма КАК ДоговорКредитаЗайма
		|		ПО ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов = ДоговорКредитаЗайма.ВалютаРасчетов
		|ГДЕ
		|	ДоговорКредитаЗайма.Ссылка = &ДоговорКредитаЗайма
		|
		|СГРУППИРОВАТЬ ПО
		|	ВременнаяТаблицаВыданныеРанееСуммы.ВалютаРасчетов,
		|	ДоговорКредитаЗайма.СуммаДокумента";
	
	Запрос.УстановитьПараметр("ДоговорКредитаЗайма", Объект.ДоговорКредитаЗайма);
	Запрос.УстановитьПараметр("Организация", Компания);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		НадписьИнформацияПоКредиту = "Сумма займа: "+Выборка.СуммаДокумента+" ("+Выборка.ВалютаРасчетов+")";
		
		Если Выборка.СуммаДокумента < Выборка.ОсновнойДолгВалПриход Тогда
			НадписьОстатокДолгаПоКредиту = "Осталось выдать: "+(Выборка.СуммаДокумента-Выборка.ОсновнойДолгВалПриход)+" ("+Выборка.ВалютаРасчетов+")"+
				". Уже выдано "+Выборка.ОсновнойДолгВалПриход+" ("+Выборка.ВалютаРасчетов+").";
			НадписьОстатокДолгаПоКредитуЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
		ИначеЕсли Выборка.СуммаДокумента = Выборка.ОсновнойДолгВалПриход Тогда
			НадписьОстатокДолгаПоКредиту = "Осталось выдать: 0 ("+Выборка.ВалютаРасчетов+")";
			НадписьОстатокДолгаПоКредитуЦветТекста = ЦветаСтиля.ЦветОсобогоТекста;
		Иначе
			НадписьОстатокДолгаПоКредиту = "Осталось выдать: "+(Выборка.СуммаДокумента-Выборка.ОсновнойДолгВалПриход)+" ("+Выборка.ВалютаРасчетов+")"+
				". Уже выдано "+Выборка.ОсновнойДолгВалПриход+" ("+Выборка.ВалютаРасчетов+").";
		КонецЕсли;
	Иначе
		НадписьИнформацияПоКредиту = "Сумма займа: "+Объект.ДоговорКредитаЗайма.СуммаДокумента+" ("+Объект.ДоговорКредитаЗайма.ВалютаРасчетов+")";
		НадписьОстатокДолгаПоКредиту = "Осталось выдать: "+Объект.ДоговорКредитаЗайма.СуммаДокумента+" ("+Объект.ДоговорКредитаЗайма.ВалютаРасчетов+")";
	КонецЕсли;
	
	Элементы.НадписьИнформацияПоКредиту.Заголовок = НадписьИнформацияПоКредиту;
	Элементы.НадписьОстатокДолгаПоКредиту.Заголовок = НадписьОстатокДолгаПоКредиту;
	
	Элементы.НадписьИнформацияПоКредиту.ЦветТекста = НадписьИнформацияПоКредитуЦветТекста;
	Элементы.НадписьОстатокДолгаПоКредиту.ЦветТекста = НадписьОстатокДолгаПоКредитуЦветТекста;
	
КонецПроцедуры

// Получает договор кредита (займа) по умолчанить в зависимости от вида операции документа.
//
&НаСервереБезКонтекста
Функция ПолучитьДоговорКредитаЗаймаПоУмолчанию(Документ, Контрагент, Организация, ВидОперации)
	
	МенеджерДокумента = Документы.ДоговорКредитаИЗайма;
	
	СписокВидовДоговоров = Новый СписокЗначений;
	СписокВидовДоговоров.Добавить(?(ВидОперации = Перечисления.ВидыОперацийРасходСоСчета.РасчетыПоКредитам, 
		Перечисления.ВидыДоговоровКредитаИЗайма.КредитПолученный,
		Перечисления.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику));
	
	ДоговорКредитаЗаймаПоУмолчанию = МенеджерДокумента.ПолучитьДоговорКредитаИЗаймаПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	
	Возврат ДоговорКредитаЗаймаПоУмолчанию;
	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область Эквайринг

// Функция помещает табличную часть РасшифровкаРасчетов во временное хранилище
// и возвращает адрес.
//
&НаСервере
Функция ПоместитьЭквайринговыеОперацииВХранилище() 
	
	Возврат ПоместитьВоВременноеХранилище(
		Объект.ЭквайринговыеОперации.Выгрузить(),
		УникальныйИдентификатор
	);
	
КонецФункции // ПоместитьЭквайринговыеОперацииВХранилище()

// Функция получает табличную часть РасшифровкаРасчетов из временного хранилища.
//
&НаСервере
Процедура ПолучитьЭквайринговыеОперацииИзХранилища(АдресЭквайринговыеОперацииВХранилище)
	
	ТаблицаЭквайринговыеОперации = ПолучитьИзВременногоХранилища(АдресЭквайринговыеОперацииВХранилище);
	Объект.ЭквайринговыеОперации.Очистить();
	Для каждого СтрокаЭквайринговыеОперации Из ТаблицаЭквайринговыеОперации Цикл
		Строка = Объект.ЭквайринговыеОперации.Добавить();
		ЗаполнитьЗначенияСвойств(Строка, СтрокаЭквайринговыеОперации);
	КонецЦикла;
	
КонецПроцедуры // ПолучитьРасшифровкаПлатежаИзХранилища()

&НаСервереБезКонтекста
Функция ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(ЭквайринговыйТерминал)
	
	ПараметрыЭквайринговогоТерминала = Новый Структура;
	
	ПараметрыЭквайринговогоТерминала.Вставить("Договор", ЭквайринговыйТерминал.Договор);
	Если Не ЭквайринговыйТерминал.СчетУчета.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("СчетУчета", ЭквайринговыйТерминал.СчетУчета);
	Иначе
		ПараметрыЭквайринговогоТерминала.Вставить("СчетУчета", ПредопределенноеЗначение("ПланСчетов.Управленческий.ПереводыВПути"));
	КонецЕсли;
	Если Не ЭквайринговыйТерминал.СчетЗатрат.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("СчетЗатрат", ЭквайринговыйТерминал.СчетЗатрат);
		ПараметрыЭквайринговогоТерминала.Вставить("ТипСчетаЗатрат", ЭквайринговыйТерминал.СчетЗатрат.ТипСчета);
	Иначе
		ПараметрыЭквайринговогоТерминала.Вставить("СчетЗатрат", ПредопределенноеЗначение("ПланСчетов.Управленческий.ПрочиеРасходы"));
		ПараметрыЭквайринговогоТерминала.Вставить("ТипСчетаЗатрат", ПараметрыЭквайринговогоТерминала.СчетЗатрат.ТипСчета);
	КонецЕсли;
	Если Не ЭквайринговыйТерминал.НаправлениеДеятельности.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("НаправлениеДеятельности", ЭквайринговыйТерминал.НаправлениеДеятельности);
	Иначе
		ПараметрыЭквайринговогоТерминала.Вставить("НаправлениеДеятельности", ПредопределенноеЗначение("Справочник.НаправленияДеятельности.Прочее"));
	КонецЕсли;
	Если Не ЭквайринговыйТерминал.Подразделение.Пустая() Тогда
		ПараметрыЭквайринговогоТерминала.Вставить("Подразделение", ЭквайринговыйТерминал.Подразделение);
	Иначе
		Пользователь = Пользователи.ТекущийПользователь();
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновноеПодразделение");
		ПараметрыЭквайринговогоТерминала.Вставить("Подразделение", ?(ЗначениеЗаполнено(ЗначениеНастройки), ЗначениеНастройки, Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение));
	КонецЕсли;
	ПараметрыЭквайринговогоТерминала.Вставить("БанковскийСчетЭквайринг", ЭквайринговыйТерминал.БанковскийСчетЭквайринг);
	
	Возврат ПараметрыЭквайринговогоТерминала;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперации(Команда)
	
	ВыполнитьВозврат = ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти();
	Если ВыполнитьВозврат Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗаполнитьЭквайринговыеОперацииЗавершение", ЭтотОбъект);
	Если Объект.ЭквайринговыеОперации.Количество() > 0 Тогда
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Эквайринговые операции будут полностью перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ЗаполнитьЭквайринговыеОперацииНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти()
	
	ВыполнитьВозврат = Ложь;
	Если Объект.Контрагент.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите вначале контрагента!'");
		Сообщение.Поле = "Контрагент";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
		ВыполнитьВозврат = Истина;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
	   И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите вначале банковский счет!'");
		Сообщение.Поле = "БанковскийСчет";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
		ВыполнитьВозврат = Истина;
	КонецЕсли;

	ВыполнитьВозврат = Ложь;
	Если Объект.ЭквайринговыйТерминал.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru = 'Укажите вначале эквайринговый терминал!'");
		Сообщение.Поле = "ЭквайринговыйТерминал";
		Сообщение.ПутьКДанным = "Объект";
		Сообщение.Сообщить();
		
		ВыполнитьВозврат = Истина;
	КонецЕсли;
	
	Возврат ВыполнитьВозврат;
	
КонецФункции

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперацииЗавершение(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ЗаполнитьЭквайринговыеОперацииНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭквайринговыеОперацииНаСервере(ДатаНачала = '00010101', ДатаОкончания = '00010101')
	
	// Заполним эквайринговые операции
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Организация", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Объект.Организация));
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(новый МоментВремени(Объект.Дата, Объект.Ссылка), ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПродажиПоПлатежнымКартам", Перечисления.ВидыОперацийПоступлениеНаСчет.ПоступлениеОплатыПоКартам);
	Запрос.УстановитьПараметр("Ссылка", Объект.Ссылка);
	Запрос.УстановитьПараметр("РасшифровкаПлатежа", Объект.РасшифровкаПлатежа.Выгрузить());
	Запрос.УстановитьПараметр("ПериодРасчетовДатаНачала", ДатаНачала);
	Запрос.УстановитьПараметр("ПериодРасчетовДатаОкончания", ДатаОкончания);
	Запрос.УстановитьПараметр("Контрагент", Объект.Контрагент);
	Запрос.УстановитьПараметр("ЭквайринговыйТерминал", Объект.ЭквайринговыйТерминал);
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаДС", Объект.ВалютаДенежныхСредств);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасчетыПоЭквайрингуОстатки.Организация,
	|	РасчетыПоЭквайрингуОстатки.ЭквайринговыйТерминал,
	|	РасчетыПоЭквайрингуОстатки.ДатаПлатежа,
	|	РасчетыПоЭквайрингуОстатки.Документ,
	|	РасчетыПоЭквайрингуОстатки.СуммаОстаток,
	|	РасчетыПоЭквайрингуОстатки.СуммаВалОстаток,
	|	РасчетыПоЭквайрингуОстатки.КомиссияОстаток,
	|	РасчетыПоЭквайрингуОстатки.КомиссияВалОстаток,
	|	РасчетыПоЭквайрингуОстатки.Заказ,
	|	РасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга,
	|	РасчетыПоЭквайрингуОстатки.ВидПлатежнойКарты,
	|	РасчетыПоЭквайрингуОстатки.НомерПлатежнойКарты
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента
	|ИЗ
	|	РегистрНакопления.РасчетыПоЭквайрингу.Остатки(
	|			,
	|			ДатаПлатежа >= &ПериодРасчетовДатаНачала
	|				И ВЫБОР
	|					КОГДА &ПериодРасчетовДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА ДатаПлатежа <= &ПериодРасчетовДатаОкончания
	|					ИНАЧЕ ИСТИНА
	|				КОНЕЦ
	|				И Организация = &Организация
	|				И ЭквайринговыйТерминал.Эквайрер = &Контрагент
	|				И ЭквайринговыйТерминал = &ЭквайринговыйТерминал) КАК РасчетыПоЭквайрингуОстатки
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	РасчетыПоЭквайрингу.Организация,
	|	РасчетыПоЭквайрингу.ЭквайринговыйТерминал,
	|	РасчетыПоЭквайрингу.ДатаПлатежа,
	|	РасчетыПоЭквайрингу.Документ,
	|	ВЫБОР
	|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ЕСТЬNULL(РасчетыПоЭквайрингу.Сумма, 0)
	|		ИНАЧЕ ЕСТЬNULL(РасчетыПоЭквайрингу.Сумма, 0)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ЕСТЬNULL(РасчетыПоЭквайрингу.СуммаВал, 0)
	|		ИНАЧЕ ЕСТЬNULL(РасчетыПоЭквайрингу.СуммаВал, 0)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ЕСТЬNULL(РасчетыПоЭквайрингу.Комиссия, 0)
	|		ИНАЧЕ ЕСТЬNULL(РасчетыПоЭквайрингу.Комиссия, 0)
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|			ТОГДА -ЕСТЬNULL(РасчетыПоЭквайрингу.КомиссияВал, 0)
	|		ИНАЧЕ ЕСТЬNULL(РасчетыПоЭквайрингу.КомиссияВал, 0)
	|	КОНЕЦ,
	|	РасчетыПоЭквайрингу.Заказ,
	|	РасчетыПоЭквайрингу.ВидОперацииЭквайринга,
	|	РасчетыПоЭквайрингу.ВидПлатежнойКарты,
	|	РасчетыПоЭквайрингу.НомерПлатежнойКарты
	|ИЗ
	|	РегистрНакопления.РасчетыПоЭквайрингу КАК РасчетыПоЭквайрингу
	|ГДЕ
	|	РасчетыПоЭквайрингу.Регистратор = &Ссылка
	|	И РасчетыПоЭквайрингу.ДатаПлатежа >= &ПериодРасчетовДатаНачала
	|	И ВЫБОР
	|			КОГДА &ПериодРасчетовДатаОкончания <> ДАТАВРЕМЯ(1, 1, 1)
	|				ТОГДА РасчетыПоЭквайрингу.ДатаПлатежа <= &ПериодРасчетовДатаОкончания
	|			ИНАЧЕ ИСТИНА
	|		КОНЕЦ
	|	И РасчетыПоЭквайрингу.Организация = &Организация
	|	И РасчетыПоЭквайрингу.ЭквайринговыйТерминал.Эквайрер = &Контрагент
	|	И РасчетыПоЭквайрингу.ЭквайринговыйТерминал = &ЭквайринговыйТерминал
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Организация,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ЭквайринговыйТерминал,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ДатаПлатежа,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Документ,
	|	СУММА(ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.СуммаОстаток) КАК СуммаОстаток,
	|	СУММА(ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.СуммаВалОстаток) КАК СуммаВалОстаток,
	|	СУММА(ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.КомиссияОстаток) КАК КомиссияОстаток,
	|	СУММА(ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.КомиссияВалОстаток) КАК КомиссияВалОстаток,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Заказ,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ВидОперацииЭквайринга,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ВидПлатежнойКарты,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.НомерПлатежнойКарты
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыПоЭквайрингуОстатки
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента КАК ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента
	|ГДЕ
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Документ <> ЗНАЧЕНИЕ(Документ.ОперацияПоПлатежнымКартам.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Организация,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ЭквайринговыйТерминал,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ДатаПлатежа,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Документ,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.Заказ,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ВидОперацииЭквайринга,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.ВидПлатежнойКарты,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстаткиМинусДвиженияДокумента.НомерПлатежнойКарты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ДатаПлатежа КАК ДатаПлатежа,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.СуммаОстаток,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.СуммаВалОстаток КАК СуммаВалОстаток,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.КомиссияОстаток,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.КомиссияВалОстаток КАК КомиссияВалОстаток,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.Заказ,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга КАК ВидОперацииЭквайринга,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю)
	|			ТОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.СуммаВалОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасчетовВозврата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя)
	|			ТОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.СуммаВалОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасчетов,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ВозвратОплатыПокупателю)
	|			ТОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.КомиссияВалОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасчетовКомиссииВозврата,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ВидОперацииЭквайринга = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭквайринга.ПоступлениеОплатыОтПокупателя)
	|			ТОГДА ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.КомиссияВалОстаток
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасчетовКомиссии,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.ВидПлатежнойКарты КАК ВидПлатежнойКарты,
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки.НомерПлатежнойКарты КАК НомерПлатежнойКарты
	|ПОМЕСТИТЬ ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций
	|ИЗ
	|	ВременнаяТаблицаРасчетыПоЭквайрингуОстатки КАК ВременнаяТаблицаРасчетыПоЭквайрингуОстатки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.ЭквайринговыйТерминал,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.ДатаПлатежа,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.Документ,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаОстаток,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаВалОстаток,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.КомиссияОстаток,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.КомиссияВалОстаток,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.Заказ,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.ВидОперацииЭквайринга,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовВозврата,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетов,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссииВозврата,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссии,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовВозврата + ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссииВозврата + ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссии - ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетов КАК СуммаПлатежаИтогоПоСтроке,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.ВидПлатежнойКарты,
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.НомерПлатежнойКарты
	|ИЗ
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций КАК ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций
	|ГДЕ
	|	(ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетов <> 0
	|			ИЛИ ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовВозврата <> 0
	|			ИЛИ ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссии <> 0
	|			ИЛИ ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссииВозврата <> 0)
	|АВТОУПОРЯДОЧИВАНИЕ
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СУММА(ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовВозврата + ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссииВозврата + ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетовКомиссии - ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций.СуммаРасчетов) КАК СуммаДокумента
	|ИЗ
	|	ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций КАК ВременнаяТаблицаДляЗаполненияЭквайринговыхОпераций";
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	Объект.ЭквайринговыеОперации.Загрузить(МассивРезультатов[3].Выгрузить());
	
	//Если Элементы.Страницы.ТекущаяСтраница <> Элементы.РасчетыПоЭквайрингу Тогда
	//	Элементы.Страницы.ТекущаяСтраница = Элементы.РасчетыПоЭквайрингу;
	//КонецЕсли;
	
	ВыборкаИтог = МассивРезультатов[4].Выбрать();
	ВыборкаИтог.Следующий();
	Объект.СуммаДокумента = ВыборкаИтог.СуммаДокумента;
	СуммаПлатежаЭквайринг = Объект.СуммаДокумента;
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайрингПодборЗавершение(Результат1, ДополнительныеПараметры) Экспорт
	
	АдресЭквайринговыеОперацииВХранилище = ДополнительныеПараметры.АдресЭквайринговыеОперацииВХранилище;
	
	Результат = Результат1;
	Если Результат = КодВозвратаДиалога.OK Тогда
		
		ПолучитьЭквайринговыеОперацииИзХранилища(АдресЭквайринговыеОперацииВХранилище);
		
		Для Каждого ТекущаяСтрока Из Объект.ЭквайринговыеОперации Цикл
			ТекущаяСтрока.СуммаПлатежаИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетовВозврата
									+ТекущаяСтрока.СуммаРасчетовКомиссии+ТекущаяСтрока.СуммаРасчетовКомиссииВозврата-ТекущаяСтрока.СуммаРасчетов;
		КонецЦикла;
		
		УстановитьТекущуюСтраницу();
		
		СуммаПлатежаЭквайринг = Объект.ЭквайринговыеОперации.Итог("СуммаПлатежаИтогоПоСтроке");
		
		Если Объект.ЭквайринговыеОперации.Количество() = 1 Тогда
			Объект.СуммаДокумента = СуммаПлатежаЭквайринг;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры // ЭквайрингПодборЗавершение()

&НаКлиенте
Процедура ЭквайринговыйТерминалПриИзменении(Элемент)
	
	Если ЭквайринговыйТерминал <> Объект.ЭквайринговыйТерминал Тогда
		ПараметрыЭквайринговогоТерминала = ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(Объект.ЭквайринговыйТерминал);
		ТипСчетаЗатратШапка = ПараметрыЭквайринговогоТерминала.ТипСчетаЗатрат;
		НастроитьВидимостьЭлементовВЗависимостиОтТипаСчетаЗатрат();
		Объект.НаправлениеДеятельностиЗатраты = ПараметрыЭквайринговогоТерминала.НаправлениеДеятельности;
		Объект.ПодразделениеЗатраты = ПараметрыЭквайринговогоТерминала.Подразделение;
		Если Объект.БанковскийСчет <> ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг И ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг.Выбран() Тогда
			Объект.БанковскийСчет = ПараметрыЭквайринговогоТерминала.БанковскийСчетЭквайринг;
			БанковскийСчетПриИзменении(Элемент);
		КонецЕсли;
		
		ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыйТерминалНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ЕстьСообщения = Ложь;
	Если Объект.Контрагент.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала выберите банк-эквайрер!'");
		Сообщение.Поле = "Объект.Контрагент";
		Сообщение.Сообщить();
		
		ЕстьСообщения = Истина;
	КонецЕсли;
	
	Если Объект.БанковскийСчет.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Сначала выберите банковский счет!'");
		Сообщение.Поле = "Объект.БанковскийСчет";
		Сообщение.Сообщить();
		
		ЕстьСообщения = Истина;
	КонецЕсли;
	
	Если ЕстьСообщения Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьВидимостьЭлементовВЗависимостиОтТипаСчетаЗатрат()
	
	Если ТипСчетаЗатратШапка = Перечисления.ТипыСчетов.Расходы Тогда
		ПараметрыЭквайринговогоТерминала = ПолучитьПараметрыЭквайринговогоТерминалаПриИзменении(Объект.ЭквайринговыйТерминал);
		
		Объект.НаправлениеДеятельностиЗатраты = ПараметрыЭквайринговогоТерминала.НаправлениеДеятельности;
		
		Объект.ПодразделениеЗатраты = ПараметрыЭквайринговогоТерминала.Подразделение;
		Элементы.ПодразделениеЗатраты.Видимость = Истина;
		Элементы.НаправлениеДеятельностиЗатраты.Видимость = Истина;
	Иначе
		Объект.НаправлениеДеятельностиЗатраты = Справочники.НаправленияДеятельности.Прочее;
		
		Объект.ПодразделениеЗатраты = Неопределено;
		Элементы.ПодразделениеЗатраты.Видимость = Ложь;
		Элементы.НаправлениеДеятельностиЗатраты.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	ПересчитатьСуммуПлатежаЭквайринга(ТекущаяСтрока);
	
	Если ТекущаяСтрока.СуммаРасчетов <> 0 Тогда
		ТекущаяСтрока.СуммаРасчетовВозврата = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПересчитатьСуммуПлатежаЭквайринга(ТекущаяСтрока)
	
	Если ТекущаяСтрока <> Неопределено Тогда
		ТекущаяСтрока.СуммаПлатежаИтогоПоСтроке = ТекущаяСтрока.СуммаРасчетовВозврата
								+ТекущаяСтрока.СуммаРасчетовКомиссии+ТекущаяСтрока.СуммаРасчетовКомиссииВозврата-ТекущаяСтрока.СуммаРасчетов;
		
		СуммаПлатежаЭквайринг = Объект.ЭквайринговыеОперации.Итог("СуммаПлатежаИтогоПоСтроке");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовВозвратаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	ПересчитатьСуммуПлатежаЭквайринга(ТекущаяСтрока);
	
	Если ТекущаяСтрока.СуммаРасчетовВозврата <> 0 Тогда
		ТекущаяСтрока.СуммаРасчетов = 0;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовКомиссииПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	ПересчитатьСуммуПлатежаЭквайринга(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииСуммаРасчетовКомиссииВозвратаПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	ПересчитатьСуммуПлатежаЭквайринга(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ЭквайринговыеОперацииДокументПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ЭквайринговыеОперации.ТекущиеДанные;
	Если ТекущаяСтрока <> Неопределено Тогда
		СтукрутраПараметров = ПолучитьПараметрыЭквайринговойОперации(ТекущаяСтрока.Документ, Объект.ЭквайринговыйТерминал);
		ЗаполнитьЗначенияСвойств(ТекущаяСтрока, СтукрутраПараметров);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьПараметрыЭквайринговойОперации(СсылкаНаДокумент, ЭквайринговыйТерминал)
	
	СтукрутраПараметров = Новый Структура("ДатаПлатежа, НомерПлатежнойКарты, ВидПлатежнойКарты");
	
	Если ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ОперацияПоПлатежнымКартам") Тогда
		СтукрутраПараметров.ДатаПлатежа = СсылкаНаДокумент.Дата;
		СтукрутраПараметров.НомерПлатежнойКарты = СсылкаНаДокумент.НомерПлатежнойКарты;
		СтукрутраПараметров.ВидПлатежнойКарты = СсылкаНаДокумент.ВидПлатежнойКарты;
		Если СсылкаНаДокумент.РасшифровкаПлатежа.Количество() = 1 Тогда
			Если ЗначениеЗаполнено(СсылкаНаДокумент.РасшифровкаПлатежа[0].Заказ) Тогда
				СтукрутраПараметров.Вставить("Заказ", СсылкаНаДокумент.РасшифровкаПлатежа[0].Заказ);
			КонецЕсли;
		КонецЕсли;
	ИначеЕсли ТипЗнч(СсылкаНаДокумент) = Тип("ДокументСсылка.ОтчетОРозничныхПродажах") Тогда
		СтукрутраПараметров.ДатаПлатежа = СсылкаНаДокумент.Дата;
		Если СсылкаНаДокумент.ОплатаПлатежнымиКартами.Количество() = 1 Тогда
			ПерваяСтрокаОплатыКартой = СсылкаНаДокумент.ОплатаПлатежнымиКартами[0];
			Если ПерваяСтрокаОплатыКартой.ЭквайринговыйТерминал = ЭквайринговыйТерминал Тогда
				СтукрутраПараметров.НомерПлатежнойКарты = ПерваяСтрокаОплатыКартой.НомерПлатежнойКарты;
				СтукрутраПараметров.ВидПлатежнойКарты = ПерваяСтрокаОплатыКартой.ВидПлатежнойКарты;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Возврат СтукрутраПараметров;
	
КонецФункции

&НаКлиенте
Процедура ЭквайринговыеОперацииПослеУдаления(Элемент)
	
	СуммаПлатежаЭквайринг = Объект.ЭквайринговыеОперации.Итог("СуммаПлатежаИтогоПоСтроке");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперацииСВыборомПериода(Команда)
	
	ВыполнитьВозврат = ПроверитьРеквизитыПередЗаполнениемТабличнойЧасти();
	Если ВыполнитьВозврат Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ЗаполнитьЭквайринговыеОперацииСВыборомПериодаЗавершение", ЭтотОбъект);
	Если Объект.ЭквайринговыеОперации.Количество() > 0 Тогда
		ПоказатьВопрос(ОписаниеОповещенияОЗавершении, НСтр("ru='Эквайринговые операции будут полностью перезаполнены. Продолжить?'"), РежимДиалогаВопрос.ДаНет,, КодВозвратаДиалога.Да);
	Иначе
		ОткрытьФормуВыбораПериодаРасчета();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьЭквайринговыеОперацииСВыборомПериодаЗавершение(Ответ, Параметры) Экспорт
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОткрытьФормуВыбораПериодаРасчета();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПериодаРасчета()
	
	ПараметрыЗаполнения = Новый Структура("Контрагент, БанковскийСчет, ЭквайринговыйТерминал, Организация, Ссылка, ВалютаДенежныхСредств, РасшифровкаПлатежа", 
		Объект.Контрагент, 
		Объект.БанковскийСчет, 
		Объект.ЭквайринговыйТерминал,
		Объект.Организация,
		Объект.Ссылка,
		Объект.ВалютаДенежныхСредств,
		ПоместитьРасшифровкаПлатежаВХранилище());
	
	ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения("ОткрытьФормуВыбораПериодаРасчетаЗавершение", ЭтотОбъект);
	ОткрытьФорму("Документ.ПоступлениеНаСчет.Форма.ФормаВыбораПериода", ПараметрыЗаполнения, ЭтотОбъект, УникальныйИдентификатор,,, ОписаниеОповещенияОЗавершении, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораПериодаРасчетаЗавершение(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		ЗаполнитьЭквайринговыеОперацииНаСервере(Результат.ПериодРасчетовДатаНачала, Результат.ПериодРасчетовДатаОкончания);
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события Выполнить команды Подбор.
// Открывает форму подбора долгообразующих документов.
//
&НаКлиенте
Процедура ПодборЭквайринговыхОпераций(Команда)
	
	// Эквайринг
	Если (НЕ ЗначениеЗаполнено(Объект.ЭквайринговыйТерминал) ИЛИ НЕ ЗначениеЗаполнено(Объект.Контрагент) 
		ИЛИ (НЕ ЗначениеЗаполнено(Объект.БанковскийСчет)
		И НЕ ЗначениеЗаполнено(Объект.ВалютаДенежныхСредств))) Тогда
		ПоказатьПредупреждение(Неопределено,НСтр("ru = 'Должнен быть выбран банк-эквайрер, эквайринговый терминал и банковский счет!'"));
		Возврат;
	КонецЕсли;
	
	АдресЭквайринговыеОперацииВХранилище = ПоместитьЭквайринговыеОперацииВХранилище();
	
	ПараметрыПодбора = Новый Структура(
		"АдресЭквайринговыеОперацииВХранилище,
		|Компания,
		|Дата,
		|Контрагент,
		|Ссылка,
		|ВидОперации,
		|ВалютаДенежныхСредств,
		|СуммаДокумента,
		|БанковскийСчет,
		|ЭквайринговыйТерминал",
		АдресЭквайринговыеОперацииВХранилище,
		Компания,
		Объект.Дата,
		Объект.Контрагент,
		Объект.Ссылка,
		Объект.ВидОперации,
		Объект.ВалютаДенежныхСредств,
		Объект.СуммаДокумента,
		Объект.БанковскийСчет,
		Объект.ЭквайринговыйТерминал
	);
		
	Результат = Неопределено;

	ОткрытьФорму("ОбщаяФорма.ФормаПодбораДолговПоЭквайрингу", ПараметрыПодбора,,,,, Новый ОписаниеОповещения("ЭквайрингПодборЗавершение", ЭтотОбъект, Новый Структура("АдресЭквайринговыеОперацииВХранилище", АдресЭквайринговыеОперацииВХранилище)));
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаКомиссииДокументаПриИзменении(Элемент)
	
	Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
		
		СтрокаТабличнойЧасти = Объект.РасшифровкаПлатежа[0];
		
		СтрокаТабличнойЧасти.СуммаПлатежа = Объект.СуммаДокумента + Объект.СуммаКомиссииДокумента;
		
		СтрокаТабличнойЧасти.Курс = ?(
			СтрокаТабличнойЧасти.Курс = 0,
			1,
			СтрокаТабличнойЧасти.Курс
		);
		
		СтрокаТабличнойЧасти.Кратность = ?(
			СтрокаТабличнойЧасти.Кратность = 0,
			1,
			СтрокаТабличнойЧасти.Кратность
		);
		
		СтрокаТабличнойЧасти.СуммаРасчетов = УправлениеНебольшойФирмойКлиент.ПересчитатьИзВалютыВВалюту(
			СтрокаТабличнойЧасти.СуммаПлатежа,
			Курс,
			СтрокаТабличнойЧасти.Курс,
			Кратность,
			СтрокаТабличнойЧасти.Кратность
		);
		
		Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СтавкаНДС) Тогда
			СтрокаТабличнойЧасти.СтавкаНДС = СтавкаНДСПоУмолчанию;
		КонецЕсли;
		
		РассчитатьСуммуНДСНаКлиенте(СтрокаТабличнойЧасти);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбменСGoogle

&НаКлиенте
Процедура Подключаемый_ОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	ОбменСGoogleКлиент.Подключаемый_ОбработкаВыбора(
	ЭтаФорма,
	Элемент,
	ВыбранноеЗначение,
	СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_АвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)
	
	ОбменСGoogleКлиент.Подключаемый_АвтоПодбор(
	ЭтаФорма,
	Элемент,
	Текст,
	ДанныеВыбора,
	Параметры,
	Ожидание,
	СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиБиблиотек

// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
&НаКлиенте
Процедура Подключаемый_ВыполнитьНазначаемуюКоманду(Команда)
	Если НЕ ДополнительныеОтчетыИОбработкиКлиент.ВыполнитьНазначаемуюКомандуНаКлиенте(ЭтаФорма, Команда.Имя) Тогда
		РезультатВыполнения = Неопределено;
		ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(Команда.Имя, РезультатВыполнения);
	КонецЕсли;
КонецПроцедуры

&НаСервере
Процедура ДополнительныеОтчетыИОбработкиВыполнитьНазначаемуюКомандуНаСервере(ИмяЭлемента, РезультатВыполнения)
	ДополнительныеОтчетыИОбработки.ВыполнитьНазначаемуюКомандуНаСервере(ЭтаФорма, ИмяЭлемента, РезультатВыполнения);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_РедактироватьСоставСвойств()
	
	УправлениеСвойствамиКлиент.РедактироватьСоставСвойств(ЭтаФорма, Объект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
	
	УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтаФорма);
	
КонецПроцедуры // ОбновитьЭлементыДополнительныхРеквизитов()

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
	УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

#КонецОбласти

#Область Разобрать

&НаСервере
Процедура ПриИзмененииВТаблицеДолгов(ИмяКолонки, ТекущаяСтрокаДолговИдентификатор, УстанавливатьЗачитываемуюСумму = Истина)
	
	ТекущаяСтрокаДолгов = ТаблицаДолгов.НайтиПоИдентификатору(ТекущаяСтрокаДолговИдентификатор);
	
	ТекущаяСтрокаРасшифровки = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ТекущаяСтрокаДолгов.ИдентификаторСтрокиРасшифровкиПлатежа);
	Если ТекущаяСтрокаРасшифровки = Неопределено Тогда
		ТекущаяСтрокаРасшифровки = Объект.РасшифровкаПлатежа.Добавить();
		ЗаполнитьЗначенияСвойств(ТекущаяСтрокаРасшифровки, ТекущаяСтрокаДолгов);
	КонецЕсли;
	
	Если УстанавливатьЗачитываемуюСумму Тогда
		Если СуммаПлатежаАванс = 0 И ИсходнаяСуммаРавнаНулю Тогда
			ТекущаяСтрокаДолгов.Зачесть = ТекущаяСтрокаДолгов.ДолгВалПлатежа;
		Иначе
			ТекущаяСтрокаДолгов.Зачесть = Мин(СуммаПлатежаАванс, ТекущаяСтрокаДолгов.ДолгВалПлатежа);
		КонецЕсли;
	КонецЕсли;
	
	// Алгоритмы работы с суммами и курсом должны быть одинаковы в обоих режимах: "С помощником" и "Вручную".
	Если ИмяКолонки = "Зачесть" Или ИмяКолонки = "Выбран" Тогда
		
		ПересчитатьСуммуРасчетовВСпискеДолгов(ТекущаяСтрокаРасшифровки,
			ТекущаяСтрокаДолгов,
			ИмяКолонки, 
			ТекущаяСтрокаДолгов.ДолгВалДоговора, 
			ТекущаяСтрокаДолгов.ДолгВалПлатежа
		);
		
		ТекущаяСтрокаДолгов.ДолгВалПлатежа = РассчитатьСуммуДолгаВВалютеПлатежа(
			ТекущаяСтрокаДолгов.ДолгВалДоговора,
			ТекущаяСтрокаДолгов.Курс,
			ТекущаяСтрокаДолгов.Кратность,
			ТекущаяСтрокаДолгов.Договор
		);
		
	ИначеЕсли ИмяКолонки = "СуммаРасчетов" Тогда
		
		ПересчитатьСуммуПлатежаВСпискеДолгов(ТекущаяСтрокаРасшифровки, ТекущаяСтрокаДолгов, ИмяКолонки);
		
		ТекущаяСтрокаДолгов.ДолгВалПлатежа = РассчитатьСуммуДолгаВВалютеПлатежа(
			ТекущаяСтрокаДолгов.ДолгВалДоговора,
			ТекущаяСтрокаДолгов.Курс,
			ТекущаяСтрокаДолгов.Кратность,
			ТекущаяСтрокаДолгов.Договор
		);
		
	ИначеЕсли ИмяКолонки = "СтавкаНДС" Тогда
		
		ТекущаяСтрокаРасшифровки.СтавкаНДС = ТекущаяСтрокаДолгов.СтавкаНДС;
		РассчитатьСуммуНДСНаСервере(ТекущаяСтрокаРасшифровки);
		
	ИначеЕсли ИмяКолонки = "Курс" ИЛИ ИмяКолонки = "Кратность" Тогда
		
		Если ТекущаяСтрокаДолгов.СуммаРасчетов <> 0 ИЛИ ТекущаяСтрокаДолгов.Зачесть <> 0 Тогда
			ТекущаяСтрокаРасшифровки.Курс = ТекущаяСтрокаДолгов.Курс;
			ТекущаяСтрокаРасшифровки.Кратность = ТекущаяСтрокаДолгов.Кратность;
			
			РассчитатьСуммуПлатежаНаСервере(ТекущаяСтрокаРасшифровки, ИмяКолонки);
			ТекущаяСтрокаДолгов.Зачесть = ТекущаяСтрокаРасшифровки.СуммаПлатежа;
			
			// На случай, если пользователь обнулил курс или кратность.
			ТекущаяСтрокаДолгов.Курс = ТекущаяСтрокаРасшифровки.Курс;
			ТекущаяСтрокаДолгов.Кратность = ТекущаяСтрокаРасшифровки.Кратность;
			
			ТекущаяСтрокаДолгов.ДолгВалПлатежа = РассчитатьСуммуДолгаВВалютеПлатежа(
				ТекущаяСтрокаДолгов.ДолгВалДоговора,
				ТекущаяСтрокаДолгов.Курс,
				ТекущаяСтрокаДолгов.Кратность,
				ТекущаяСтрокаДолгов.Договор
			);
		КонецЕсли;
		
	ИначеЕсли ИмяКолонки = "СчетНаОплату" Тогда
		
		ТекущаяСтрокаРасшифровки.СчетНаОплату = ТекущаяСтрокаДолгов.СчетНаОплату;
		СтуркутраДанных = ПолучитьДанныеСчетаНаОплатуПриИзменении(ТекущаяСтрокаДолгов.СчетНаОплату);
		ТекущаяСтрокаДолгов.СуммаСчета = СтуркутраДанных.Сумма;
		ТекущаяСтрокаДолгов.ВалютаСчета = СтуркутраДанных.Валюта;
		
	КонецЕсли;
	
	ТекущаяСтрокаДолгов.ИдентификаторСтрокиРасшифровкиПлатежа = ТекущаяСтрокаРасшифровки.ПолучитьИдентификатор();
	ТекущаяСтрокаДолгов.ЗачестьВыбрано = ?(ТекущаяСтрокаДолгов.Выбран, ТекущаяСтрокаДолгов.Зачесть, 0);
	Если ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = ТаблицаДолгов.Итог("ЗачестьВыбрано");
	КонецЕсли;
	ПересчитатьИтогиПриИзмененииАвансаНаСервере(Истина);
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСуммаВСтрокеАванса()
	
	СтрокаАванса = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдентификаторСтрокиАванса);
	ТекущаяСуммаАванса = Объект.СуммаДокумента - ТаблицаДолгов.Итог("ЗачестьВыбрано");
	Если СтрокаАванса = Неопределено И ТекущаяСуммаАванса > 0 Тогда
		СтрокаАванса = ДобавитьСтрокуАвансаНаСервере();
	КонецЕсли;
	Если СтрокаАванса <> Неопределено Тогда
		СтрокаАванса.СуммаПлатежа = ТекущаяСуммаАванса;
		СтрокаАванса.Аванс = СтрокаАванса.СуммаПлатежа;
		
		РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(СтрокаАванса);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьЭлементыДоговоровЗачетаДолгов(ПриСозданииНаСервере = Ложь)
	
	Если ПриСозданииНаСервере Тогда
		Для Каждого ТекущаяСтрокаДоговора Из Объект.ДоговорыАвтоЗачетаДолгов Цикл
			ДобавитьДоговорЗачетаДолговНажатиеНаСервере(ТекущаяСтрокаДоговора);
		КонецЦикла;
	Иначе
		ТЗДоговоров = Объект.РасшифровкаПлатежа.Выгрузить(, "Договор");
		ТЗДоговоров.Свернуть("Договор");
		
		МассивЭлементовДляУдаления = Новый Массив;
		Для Каждого ЭлементДобавленногоДоговора Из Элементы.ГруппаДобавленныхДоговоровСлева.ПодчиненныеЭлементы Цикл
			МассивЭлементовДляУдаления.Добавить(ЭлементДобавленногоДоговора);
		КонецЦикла;
		Для Каждого ЭлементДобавленногоДоговора Из Элементы.ГруппаДобавленныхДоговоровСправа.ПодчиненныеЭлементы Цикл
			МассивЭлементовДляУдаления.Добавить(ЭлементДобавленногоДоговора);
		КонецЦикла;
		
		Для Каждого ЭлементДобавленногоДоговора Из МассивЭлементовДляУдаления Цикл
			Элементы.Удалить(ЭлементДобавленногоДоговора);
		КонецЦикла;
		Элементы.ГруппаДобавленныхДоговоровСправа.Видимость = Ложь;
		
		Если ТЗДоговоров.Количество() = 0 Тогда
			Объект.ДоговорыАвтоЗачетаДолгов.Очистить();
		Иначе
			Объект.ДоговорыАвтоЗачетаДолгов.Очистить();
			
			ЭтоПерваяСтрока = Истина;
			Для Каждого ДоговорЗачетаДолгов Из ТЗДоговоров Цикл
				Если ДоговорЗачетаДолгов.Договор.Пустая() Тогда
					Продолжить;
				КонецЕсли;
				
				НоваяСтрока = Объект.ДоговорыАвтоЗачетаДолгов.Добавить();
				НоваяСтрока.Договор = ДоговорЗачетаДолгов.Договор;
				Если ЭтоПерваяСтрока Тогда
					ЭтоПерваяСтрока = Ложь;
					НоваяСтрока.СуммаПлатежа = Объект.СуммаДокумента;
				КонецЕсли;
				
				ДобавитьДоговорЗачетаДолговНажатиеНаСервере();
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	// На случай, когда в расшифровки платежа только строки с незаполненым договором.
	Если Объект.ДоговорыАвтоЗачетаДолгов.Количество() = 0 Тогда
		НоваяСтрока = Объект.ДоговорыАвтоЗачетаДолгов.Добавить();
		ДоговорПоУмолчанию = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ВидОперации);
		НоваяСтрока.Договор = ДоговорПоУмолчанию;
		НоваяСтрока.СуммаПлатежа = Объект.СуммаДокумента;
		
		ДобавитьДоговорЗачетаДолговНажатиеНаСервере();
	КонецЕсли;
	
	РассчитатьСуммуНДСПоУмолчаниюНаСервере();
	СформироватьЗаголовокГруппыЗачетаДолгов();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьРеквизитыНовойСтрокиТаблицыДолгов(НоваяСтрока, ТекущаяСтрокаРасшифровки)
	
	ЗаполнитьЗначенияСвойств(НоваяСтрока, ТекущаяСтрокаРасшифровки);
	НоваяСтрока.КурсИсходный = НоваяСтрока.Курс;
	НоваяСтрока.Зачесть = ТекущаяСтрокаРасшифровки.СуммаПлатежа;
	НоваяСтрока.ЗачестьВыбрано = ТекущаяСтрокаРасшифровки.СуммаПлатежа;
	НоваяСтрока.Выбран = Истина;
	НоваяСтрока.ИдентификаторСтрокиРасшифровкиПлатежа = ТекущаяСтрокаРасшифровки.ПолучитьИдентификатор();
	Если ЗначениеЗаполнено(ТекущаяСтрокаРасшифровки.Документ) Тогда
		НоваяСтрока.ДатаДокумента = ТекущаяСтрокаРасшифровки.Документ.Дата;
		Попытка
			Если ТипЗнч(ТекущаяСтрокаРасшифровки.Документ) = Тип("ДокументСсылка.ОтчетПереработчика") Тогда
				НоваяСтрока.СуммаДокумента = ТекущаяСтрокаРасшифровки.Документ.Сумма;
			ИначеЕсли ТипЗнч(ТекущаяСтрокаРасшифровки.Документ) = Тип("ДокументСсылка.Взаимозачет") Тогда
				НоваяСтрока.СуммаДокумента = ТекущаяСтрокаРасшифровки.Документ.СуммаУчета;
			Иначе
				НоваяСтрока.СуммаДокумента = ТекущаяСтрокаРасшифровки.Документ.СуммаДокумента;
			КонецЕсли;
			Если ТипЗнч(ТекущаяСтрокаРасшифровки.Документ) = Тип("ДокументСсылка.РасходИзКассы") ИЛИ
				ТипЗнч(ТекущаяСтрокаРасшифровки.Документ) = Тип("ДокументСсылка.РасходСоСчета") Тогда
				НоваяСтрока.ВалютаДокумента = ТекущаяСтрокаРасшифровки.Документ.ВалютаДенежныхСредств.СимвольноеПредставление;
			ИначеЕсли ТипЗнч(ТекущаяСтрокаРасшифровки.Документ) = Тип("ДокументСсылка.Взаимозачет") Тогда
				НоваяСтрока.ВалютаДокумента = Константы.ВалютаУчета.Получить().СимвольноеПредставление;
			Иначе
				НоваяСтрока.ВалютаДокумента = ТекущаяСтрокаРасшифровки.Документ.ВалютаДокумента.СимвольноеПредставление;
			КонецЕсли;
		Исключение
			// Пользователю об этом сообщать не будем.
		КонецПопытки;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяСтрокаРасшифровки.Заказ) Тогда
		НоваяСтрока.ДатаЗаказа = ТекущаяСтрокаРасшифровки.Заказ.Дата;
		НоваяСтрока.ВалютаЗаказа = ТекущаяСтрокаРасшифровки.Заказ.ВалютаДокумента;
		НоваяСтрока.СуммаЗаказа = ТекущаяСтрокаРасшифровки.Заказ.СуммаДокумента;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяСтрокаРасшифровки.Договор) Тогда
		НоваяСтрока.ВалютаДоговора = ТекущаяСтрокаРасшифровки.Договор.ВалютаРасчетов.СимвольноеПредставление;
		НоваяСтрока.ВалютаДоговораСсылка = ТекущаяСтрокаРасшифровки.Договор.ВалютаРасчетов;
	КонецЕсли;
	Если ЗначениеЗаполнено(ТекущаяСтрокаРасшифровки.СчетНаОплату) Тогда
		НоваяСтрока.СуммаСчета = ТекущаяСтрокаРасшифровки.СчетНаОплату.СуммаДокумента;
		НоваяСтрока.ВалютаСчета = ТекущаяСтрокаРасшифровки.СчетНаОплату.ВалютаДокумента;
	КонецЕсли;
	НоваяСтрока.ДатаДокБольшеДатыДокумента = (Объект.Контрагент.ВестиРасчетыПоДокументам И КонецДня(НоваяСтрока.ДатаДокумента) > КонецДня(НоваяСтрока.ДатаЗаказа));
	НоваяСтрока.ДатаЗаказаБольшеДатыДокумента = НЕ Объект.Контрагент.ВестиРасчетыПоДокументам И Объект.Контрагент.ВестиРасчетыПоЗаказам И
		КонецДня(НоваяСтрока.ДатаДокумента) > КонецДня(НоваяСтрока.ДатаЗаказа);
	
КонецПроцедуры

&НаСервере
Процедура РаспределитьРасшифровкуПоТаблицеДолгов()
	
	Если Объект.РасшифровкаПлатежа.Количество() = 0 ИЛИ
		Объект.Контрагент.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	// Обработаем расшифровку платежа.
	ЕстьСтрокаАванса = Ложь;
	ИдентификаторСтрокиАванса = "";
	МассивУдаляемыхСтрок = Новый Массив;
	
	Для Каждого ТекущаяСтрокаРасшифровки Из Объект.РасшифровкаПлатежа Цикл
		Если ТекущаяСтрокаРасшифровки.ПризнакАванса Тогда
			Если Не ЕстьСтрокаАванса Тогда
				
				ЕстьСтрокаАванса = Истина;
				Объект.ДоговорДляЗачетаАвансаСПомощником = ТекущаяСтрокаРасшифровки.Договор;
				ИдентификаторСтрокиАванса = ТекущаяСтрокаРасшифровки.ПолучитьИдентификатор();
				
			КонецЕсли;
			
			Продолжить;
		КонецЕсли;
		
		// Строки, в которых не выбраны данные для идентификации строк в помощнике переходят в аванс.
		Если ТекущаяСтрокаРасшифровки.Договор.Пустая() ИЛИ
			(Объект.Контрагент.ВестиРасчетыПоДокументам И НЕ ЗначениеЗаполнено(ТекущаяСтрокаРасшифровки.Документ)) ИЛИ
			(ТекущаяСтрокаРасшифровки.СуммаРасчетов = 0 И ТекущаяСтрокаРасшифровки.СуммаПлатежа = 0) Тогда
			МассивУдаляемыхСтрок.Добавить(ТекущаяСтрокаРасшифровки);
			Продолжить;
		КонецЕсли;
		
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("Договор", ТекущаяСтрокаРасшифровки.Договор);
		Если Объект.Контрагент.ВестиРасчетыПоДокументам Тогда
			ПараметрыОтбора.Вставить("Документ", ТекущаяСтрокаРасшифровки.Документ);
		КонецЕсли;
		Если Объект.Контрагент.ВестиРасчетыПоЗаказам Тогда
			ПараметрыОтбора.Вставить("Заказ", ?(ЗначениеЗаполнено(ТекущаяСтрокаРасшифровки.Заказ), ТекущаяСтрокаРасшифровки.Заказ, Документы.ЗаказПоставщику.ПустаяСсылка()));
		КонецЕсли;
		
		МассивСтрок = ТаблицаДолгов.НайтиСтроки(ПараметрыОтбора);
		Если МассивСтрок.Количество() > 0 Тогда
			ТекущаяСтрокаДолгов = МассивСтрок[0];
			
			ТекущаяСтрокаДолгов.Зачесть = ТекущаяСтрокаРасшифровки.СуммаПлатежа;
			ТекущаяСтрокаДолгов.Выбран = (ТекущаяСтрокаРасшифровки.СуммаПлатежа > 0);
			ТекущаяСтрокаДолгов.ЗачестьВыбрано = ?(ТекущаяСтрокаДолгов.Выбран, ТекущаяСтрокаРасшифровки.СуммаПлатежа, 0);
			
			ЗаполнитьЗначенияСвойств(
				МассивСтрок[0],
				ТекущаяСтрокаРасшифровки,
				"СтавкаНДС, СуммаРасчетов, Курс, Кратность, СчетНаОплату");
				
			МассивСтрок[0].ИдентификаторСтрокиРасшифровкиПлатежа = ТекущаяСтрокаРасшифровки.ПолучитьИдентификатор();
			
			// Курс в расшифровке может отличаться от курса валюты на дату документа.
			// Нужно обновить информацию о сумме долга в валюте платежа.
			ТекущаяСтрокаДолгов.ДолгВалПлатежа = РассчитатьСуммуДолгаВВалютеПлатежа(
				ТекущаяСтрокаДолгов.ДолгВалДоговора,
				ТекущаяСтрокаДолгов.Курс,
				ТекущаяСтрокаДолгов.Кратность,
				ТекущаяСтрокаДолгов.Договор
			);
			
		Иначе
			НоваяСтрока = ТаблицаДолгов.Добавить();
			ЗаполнитьРеквизитыНовойСтрокиТаблицыДолгов(НоваяСтрока, ТекущаяСтрокаРасшифровки);
		КонецЕсли;
	КонецЦикла;
	
	// Сумму удаляемых строк перенесем в аванс.
	ДельтаСуммыАванса = 0;
	СчУдаляемыхСтрок = МассивУдаляемыхСтрок.Количество();
	Пока СчУдаляемыхСтрок > 0 Цикл
		УдаляемаяСтрока = МассивУдаляемыхСтрок[СчУдаляемыхСтрок - 1];
		ДельтаСуммыАванса = ДельтаСуммыАванса + УдаляемаяСтрока.СуммаПлатежа;
		Объект.РасшифровкаПлатежа.Удалить(Объект.РасшифровкаПлатежа.Индекс(УдаляемаяСтрока));
		СчУдаляемыхСтрок = СчУдаляемыхСтрок - 1;
	КонецЦикла;
	
	// Добавим строку аванса, если это нужно. В этом случае аванс = 0.
	Если НЕ ЕстьСтрокаАванса Тогда
		СтрокаАванса = Объект.РасшифровкаПлатежа.Добавить();
		СтрокаАванса.ПризнакАванса = Истина;
		СтрокаАванса.СуммаПлатежа = ДельтаСуммыАванса;
		//СтрокаАванса.Аванс = ДельтаСуммыАванса;
		СтрокаАванса.Договор = Объект.ДоговорДляЗачетаАвансаСПомощником;
		РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(СтрокаАванса);
		
		ИдентификаторСтрокиАванса = СтрокаАванса.ПолучитьИдентификатор();
	ИначеЕсли ДельтаСуммыАванса > 0 Тогда
		СтрокаАванса = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдентификаторСтрокиАванса);
		СтрокаАванса.СуммаПлатежа = СтрокаАванса.СуммаПлатежа + ДельтаСуммыАванса;
		РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(СтрокаАванса);
	КонецЕсли;
	
	ТаблицаДолгов.Сортировать("ДатаДокумента, Документ, ДатаЗаказа, Заказ, Договор");
	
	// Найдём первую строку, где есть долг и сделаем ее текущей.
	Для Каждого ТекущаяСтрока Из ТаблицаДолгов Цикл
		Если ТекущаяСтрока.СуммаРасчетов > 0 ИЛИ ТекущаяСтрока.Зачесть > 0 Тогда
			Элементы.ТаблицаДолгов.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьСтрокуАвансаНаСервере()
	
	НоваяСтрока = Объект.РасшифровкаПлатежа.Добавить();
	НоваяСтрока.ПризнакАванса = Истина;
	НоваяСтрока.СуммаПлатежа = Объект.СуммаДокумента;
	НоваяСтрока.Аванс = Объект.СуммаДокумента;
	НоваяСтрока.Договор = Объект.ДоговорДляЗачетаАвансаСПомощником;
	РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(НоваяСтрока);
	
	ИдентификаторСтрокиАванса = НоваяСтрока.ПолучитьИдентификатор();
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаКлиенте
Функция ДобавитьСтрокуАвансаНаКлиенте()
	
	НоваяСтрока = Объект.РасшифровкаПлатежа.Добавить();
	НоваяСтрока.ПризнакАванса = Истина;
	НоваяСтрока.СуммаПлатежа = Объект.СуммаДокумента;
	НоваяСтрока.Аванс = Объект.СуммаДокумента;
	Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
		Если Объект.ДоговорыАвтоЗачетаДолгов.Количество() > 0 Тогда
			НоваяСтрока.Договор = Объект.ДоговорыАвтоЗачетаДолгов[0].Договор;
		КонецЕсли;
	Иначе
		НоваяСтрока.Договор = Объект.ДоговорДляЗачетаАвансаСПомощником;
	КонецЕсли;
	РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаКлиенте(НоваяСтрока);
	
	ИдентификаторСтрокиАванса = НоваяСтрока.ПолучитьИдентификатор();
	
	Возврат НоваяСтрока;
	
КонецФункции

&НаСервере
Процедура УдалитьСтрокуСАвансомИСтрокиСНулевымиЗначениями()
	
	Если СуммаПлатежаАванс = 0 И Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		//Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаРаспределениеСПомощником Тогда
		СтрокаАванса = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдентификаторСтрокиАванса);
		Если СтрокаАванса <> Неопределено Тогда
			Объект.РасшифровкаПлатежа.Удалить(СтрокаАванса.НомерСтроки - 1);
		КонецЕсли;
		ИдентификаторСтрокиАванса = "";
	КонецЕсли;
	
	Индекс = Объект.РасшифровкаПлатежа.Количество() - 1;
	Пока Индекс >= 0 Цикл
		ТекущаяСтрока = Объект.РасшифровкаПлатежа[Индекс];
		Если ТекущаяСтрока.СуммаРасчетов = 0 И ТекущаяСтрока.СуммаПлатежа = 0 Тогда
			Объект.РасшифровкаПлатежа.Удалить(Индекс);
		КонецЕсли;
		
		Индекс = Индекс - 1;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьЭлементыРаспределенияДолговНаСервере(ПриСозданииНаСервере = Ложь)
	
	Если Объект.ВидОперации <> Перечисления.ВидыОперацийРасходСоСчета.Поставщику Тогда
		
		Элементы.ВариантЗаполненияРасшифровки.Видимость = Ложь;
		Элементы.ДекорацияРазделитель6.Видимость = Ложь;
		Элементы.КоманднаяПанельПомощника.Видимость = Ложь;
		Элементы.ГруппаФильтры.Видимость = Ложь;
		Элементы.КоманднаяПанельРучногоЗаполнения.Видимость = Истина;
		Элементы.СтраницыРасчетовСКонтрагентомПодвал.ТекущаяСтраница = Элементы.ПодвалВручную;
		
	Иначе
		
		//**********************************************************************************************************
		// Настроим видимость.
		Элементы.ВариантЗаполненияРасшифровки.Видимость = Истина;
		Элементы.ДекорацияРазделитель6.Видимость = Истина;
		Элементы.КоманднаяПанельПомощника.Видимость = Истина;
		Элементы.ГруппаФильтры.Видимость = Ложь;
		
		// При вводе на основании заполним информацию о договре аванса из первой строки расшифровки платежа.
		Если ПриСозданииНаСервере И Объект.Ссылка.Пустая() И Объект.ДоговорДляЗачетаАвансаСПомощником.Пустая() И Параметры.Свойство("Основание") Тогда
			Если Объект.РасшифровкаПлатежа.Количество() = 1 Тогда
				Объект.ДоговорДляЗачетаАвансаСПомощником = Объект.РасшифровкаПлатежа[0].Договор;
			Иначе
				Объект.ДоговорДляЗачетаАвансаСПомощником = ПолучитьДоговорПоУмолчанию(Объект.Ссылка, Объект.Контрагент, Объект.Организация, Объект.ВидОперации);
			КонецЕсли;
		КонецЕсли;
		
		//**********************************************************************************************************
		// Автоматическое заполнение.
		Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
			
			// Удалим строку с авансом, если он = 0, и строки с нулевыми суммами.
			Если Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаЗаполнениеСПомощником Тогда
				УдалитьСтрокуСАвансомИСтрокиСНулевымиЗначениями();
			КонецЕсли;
			
			// Заполнение элементов
			СоздатьЭлементыДоговоровЗачетаДолгов(ПриСозданииНаСервере);
			
			//**********************************************************************************************************
			// Настройка видимости и заголовков
			Элементы.КоманднаяПанельПомощника.Видимость = Ложь;
			Элементы.КоманднаяПанельРучногоЗаполнения.Видимость = Ложь;
			
			//Элементы.СуммаПлатежаАванс.Видимость = Ложь;
			Элементы.СуммаДокумента.КнопкаВыпадающегоСписка = Истина;
			
			//Элементы.СтраницыИтоги.ТекущаяСтраница = Элементы.СтраницаРаспределениеАвтоматическое;
			Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаАвтоматическоеЗаполнение;
			Элементы.СтраницыРасчетовСКонтрагентомПодвал.ТекущаяСтраница = Элементы.ПодвалАвто;
			
		//**********************************************************************************************************
		// Заполнение с помощником.
		ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
			
			// Заполнение элементов
			//Если ПриСозданииНаСервере И Не Объект.Ссылка.Пустая() Тогда // Открываем ранее заполненный документ.
			
			// Если перешли с автоматического режима, то все строки удалим и добавим только одну, с договором по умолчанию.
			Если Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаАвтоматическоеЗаполнение Тогда
				Если Объект.РасшифровкаПлатежа.Количество() = 0 Тогда
					ДобавитьСтрокуАвансаНаСервере();
				КонецЕсли;
			КонецЕсли;
			
			ЗаполнитьДолги(Ложь);
			РаспределитьРасшифровкуПоТаблицеДолгов();
			
			//**********************************************************************************************************
			// Настройка видимости и заголовков
			
			Элементы.КоманднаяПанельПомощника.Видимость = Истина;
			Элементы.КоманднаяПанельРучногоЗаполнения.Видимость = Ложь;
			
			//Элементы.СуммаПлатежаАванс.Видимость = Истина;
			Элементы.СуммаДокумента.КнопкаВыпадающегоСписка = Истина; // Для сулчая, когда хотят зачесть больше, чем изначально планировалось.
			
			Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаЗаполнениеСПомощником;
			Элементы.СтраницыРасчетовСКонтрагентомПодвал.ТекущаяСтраница = Элементы.ПодвалСПомощником;
			
		//**********************************************************************************************************
		// Ручное заполнение.
		ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную Тогда
			
			//Элементы.РасчетыСКонтрагентом.ПутьКДаннымЗаголовка = "Объект.РасшифровкаПлатежа.КоличествоСтрок";
			
			// Удалим строку с авансом, если он = 0, и строки с нулевыми суммами.
			Если Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаЗаполнениеСПомощником Тогда
				УдалитьСтрокуСАвансомИСтрокиСНулевымиЗначениями();
			КонецЕсли;
			
			//**********************************************************************************************************
			// Заполнение элементов
			КоличествоСтрок = Объект.РасшифровкаПлатежа.Количество();
		
			Если КоличествоСтрок = 0 Тогда
				Объект.РасшифровкаПлатежа.Добавить();
				Объект.РасшифровкаПлатежа[0].СуммаПлатежа = Объект.СуммаДокумента;
				РасшифровкаПлатежаСуммаПлатежаПриИзмененииЗавершениеНаСервере(Объект.РасшифровкаПлатежа[0]);
				КоличествоСтрок = 1;
			КонецЕсли;
			
			//**********************************************************************************************************
			// Настройка видимости и заголовков
			Элементы.КоманднаяПанельПомощника.Видимость = Ложь;
			Элементы.КоманднаяПанельРучногоЗаполнения.Видимость = Истина;
			
			//Элементы.СуммаПлатежаАванс.Видимость = Истина;
			Элементы.СуммаДокумента.КнопкаВыпадающегоСписка = Истина;
			
			Элементы.СтраницыРасчетовСКонтрагентом.ТекущаяСтраница = Элементы.СтраницаРучноеЗаполнение;
			Элементы.СтраницыРасчетовСКонтрагентомПодвал.ТекущаяСтраница = Элементы.ПодвалВручную;
			
		КонецЕсли;
		
		//**********************************************************************************************************
		// Общие действия
		СуммаЗачтено = ТаблицаДолгов.Итог("ЗачестьВыбрано");
		ПересчитатьИтогиПриИзмененииАвансаНаСервере();
		СформироватьЗаголовокГруппыЗачетаДолгов();
		УстановитьЗаголовкиКолонокТабличныхЧастей(Объект.ВалютаДенежныхСредств);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьПоискПоДоговоруНаСервере(Знач ИмяКолонкиТЧ)
	
	ОтборПоСуммеЧисло = Ложь;
	Если СтрНайти(ИмяКолонкиТЧ, "ДолгВал") > 0 Тогда
		Если СтрНайти(ИмяКолонкиТЧ, "Строка") > 0 Тогда
			ИмяКолонкиТЧ = СтрЗаменить(ИмяКолонкиТЧ, "Строка", "");
		КонецЕсли;
		
		Попытка
			СтруктураОтбора = Новый Структура(ИмяКолонкиТЧ, Число(СтрокаПоискаПоДоговору));
			Если ТаблицаДолгов.НайтиСтроки(СтруктураОтбора).Количество() = 0 Тогда
				СтруктураОтбора = Новый Структура(ИмяКолонкиТЧ+"Строка", СокрЛП(СтрЗаменить(СтрокаПоискаПоДоговору, Символы.НПП, "")));
			КонецЕсли;
		Исключение
			СтруктураОтбора = Новый Структура(ИмяКолонкиТЧ+"Строка", СокрЛП(СтрЗаменить(СтрокаПоискаПоДоговору, Символы.НПП, "")));
		КонецПопытки;
	Иначе
		СтруктураОтбора = Новый Структура(ИмяКолонкиТЧ, СокрЛП(СтрокаПоискаПоДоговору));
	КонецЕсли;
	Элементы.ТаблицаДолгов.ОтборСтрок = Новый ФиксированнаяСтруктура(СтруктураОтбора);
	Элементы.СнятьОтборПоДоговору.Доступность = Истина;
	Элементы.УстановитьОтборПоДоговору.Доступность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьЗаголовокГруппыЗачетаДолгов()
	
	// Автоматическое заполнение.
	Если Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиАвтоматически Тогда
		
		Если Объект.Контрагент.Пустая() Или НЕ Объект.Контрагент.ВестиРасчетыПоДоговорам Тогда
			Элементы.РасчетыСКонтрагентом.Заголовок = "Зачет долгов: автоматически";
		Иначе
			КоличествоСтрок = Объект.ДоговорыАвтоЗачетаДолгов.Количество();
			Если КоличествоСтрок = 1 И ЗначениеЗаполнено(Объект.ДоговорыАвтоЗачетаДолгов[0].Договор) Тогда
				Элементы.РасчетыСКонтрагентом.Заголовок = "Зачет долгов: автоматически по договору """+Объект.ДоговорыАвтоЗачетаДолгов[0].Договор+"""";
			ИначеЕсли КоличествоСтрок > 1 Тогда
				Если КоличествоСтрок > 4 Тогда
					Элементы.РасчетыСКонтрагентом.Заголовок = НСтр("ru = 'Зачет долгов: автоматически по нескольким договорам ('") + КоличествоСтрок + ")";
				Иначе
					ЗаголовокГруппы = НСтр("ru = 'Зачет долгов: автоматически по %КоличествоСтрок%-м договорам'");
					ЗаголовокГруппы = СтрЗаменить(ЗаголовокГруппы, "%КоличествоСтрок%", КоличествоСтрок);
					Элементы.РасчетыСКонтрагентом.Заголовок = ЗаголовокГруппы;
				КонецЕсли;
			Иначе
				Элементы.РасчетыСКонтрагентом.Заголовок = "Зачет долгов: автоматически (выберите договор)";
			КонецЕсли;
		КонецЕсли;
		
	ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиСПомощником Тогда
		
		ОпределитьТекущуюСтраницуПомощника();
		Элементы.РасчетыСКонтрагентом.Заголовок = НСтр("ru = 'Зачет долгов: с помощником'");
		
	// Ручное заполнение.
	ИначеЕсли Объект.ВариантЗаполненияРасшифровки = ВариантЗаполненияРасшифровкиВручную Тогда
		
		Элементы.РасчетыСКонтрагентом.Заголовок = НСтр("ru = 'Зачет долгов: вручную'");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОпределитьТекущуюСтраницуПомощника()
	
	Если ТаблицаДолгов.Количество() = 0 Тогда
		Элементы.СтраницыПомощник.ТекущаяСтраница = Элементы.СтраницаНетДанныхВПомощнике;
	Иначе
		Элементы.СтраницыПомощник.ТекущаяСтраница = Элементы.СтраницаЕстьДанныеВПомощнике;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьДоговорЗачетаДолговНажатие(Элемент)
	
	ТекущаяСтрока = Объект.ДоговорыАвтоЗачетаДолгов.Добавить();
	ДобавитьДоговорЗачетаДолговНажатиеНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьДоговорЗачетаДолговНажатиеНаСервере(ТекущаяСтрокаТаблицыДоговоровАвтоЗачета = Неопределено)
	
	// Добавление элемента для последней добавленной в таблицу строки.
	Если ТекущаяСтрокаТаблицыДоговоровАвтоЗачета = Неопределено Тогда
		ИндексДоговораУчетаРасчетов = Объект.ДоговорыАвтоЗачетаДолгов.Количество() - 1;
	// Добавление элемента для строки, которая передана в качестве параметра.
	Иначе
		ИндексДоговораУчетаРасчетов = Объект.ДоговорыАвтоЗачетаДолгов.Индекс(ТекущаяСтрокаТаблицыДоговоровАвтоЗачета);
	КонецЕсли;
	
	// Создадим элемент для договора.
	ПолеДоговора = Элементы.Добавить("ДоговорАвтоЗачетаДолгов_" + ИндексДоговораУчетаРасчетов,
		Тип("ПолеФормы"),
		Элементы.ГруппаДобавленныхДоговоровСлева
	);
	
	ПолеДоговора.Вид = ВидПоляФормы.ПолеВвода;
	ПолеДоговора.ПутьКДанным = "Объект.ДоговорыАвтоЗачетаДолгов[" + ИндексДоговораУчетаРасчетов + "].Договор";
	ПолеДоговора.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеДоговора.Заголовок = ?(ИндексДоговораУчетаРасчетов = 0, НСтр("ru = 'По договору'"), НСтр("ru = 'и по договору'"));
	ПолеДоговора.РастягиватьПоГоризонтали = Истина;
	ПолеДоговора.УстановитьДействие("ПриИзменении", "Подключаемый_ДоговорыАвтоЗачетаДолговДоговорПриИзменении");
	ПолеДоговора.УстановитьДействие("НачалоВыбора", "Подключаемый_ДоговорыАвтоЗачетаДолговДоговорНачалоВыбора");
	НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.Владелец", "Объект.Контрагент");
	НовыйМассив = Новый Массив();
	НовыйМассив.Добавить(НоваяСвязь);
	НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
	ПолеДоговора.СвязиПараметровВыбора = НовыеСвязи;
	ПолеДоговора.АвтоОтметкаНезаполненного = Ложь;
	ПолеДоговора.ПодсказкаВвода = "Если не выбран, то игнорируется";
	
	// Создадим элемент для суммы платежа.
	ДобавитьСуммуПлатежаЗачетаДолгов(ИндексДоговораУчетаРасчетов);
	
	// Если договоров > 1, тогда выводим сумму, которая будет зачтена для каждого договора.
	Если ИндексДоговораУчетаРасчетов = 1 Тогда
		Элементы.ГруппаДобавленныхДоговоровСправа.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСуммуПлатежаЗачетаДолгов(ИндексДоговораУчетаРасчетов)

	// Поле суммы.
	ПолеСуммыПлатежа = Элементы.Добавить("СуммаПлатежаАвтоЗачетаДолгов_" + ИндексДоговораУчетаРасчетов,
			Тип("ПолеФормы"),
			Элементы.ГруппаДобавленныхДоговоровСправа
		);
		
	ПолеСуммыПлатежа.Вид = ВидПоляФормы.ПолеВвода;
	ПолеСуммыПлатежа.ПутьКДанным = "Объект.ДоговорыАвтоЗачетаДолгов[" + ИндексДоговораУчетаРасчетов + "].СуммаПлатежа";
	ПолеСуммыПлатежа.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Лево;
	ПолеСуммыПлатежа.Заголовок = НСтр("ru = 'сумма'");
	ПолеСуммыПлатежа.РастягиватьПоГоризонтали = Ложь;
	ПолеСуммыПлатежа.Ширина = 11;
	ПолеСуммыПлатежа.АвтоОтметкаНезаполненного = Ложь;
	ПолеСуммыПлатежа.УстановитьДействие("ПриИзменении", "Подключаемый_ДоговорыАвтоЗачетаДолговСуммаПлатежаПриИзменении");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДоговорыАвтоЗачетаДолговДоговорПриИзменении(Элемент)
	
	СформироватьЗаголовокГруппыЗачетаДолгов();
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДоговорыАвтоЗачетаДолговДоговорНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	НомерСимвола = СтрНайти(Элемент.Имя, "_");
	Если НомерСимвола = 0 Тогда
		ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка);
	Иначе
		Попытка
			Индекс = Число(Сред(Элемент.Имя, НомерСимвола + 1));
			ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка, Объект.ДоговорыАвтоЗачетаДолгов[Индекс].Договор);
		Исключение
			ОбработатьНачалоВыбораДоговораКонтрагента(Элемент, СтандартнаяОбработка);
		КонецПопытки;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ДоговорыАвтоЗачетаДолговСуммаПлатежаПриИзменении(Элемент)
	
	Если ИсходнаяСуммаРавнаНулю Тогда
		Объект.СуммаДокумента = Объект.ДоговорыАвтоЗачетаДолгов.Итог("СуммаПлатежа");
		СуммаДокументаПриИзмененииФрагмент();
	Иначе
		РассчитатьСуммуНДСПоУмолчаниюНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорДляЗачетаАванса2ПриИзменении(Элемент)
	
	СтрокаАванса = Объект.РасшифровкаПлатежа.НайтиПоИдентификатору(ИдентификаторСтрокиАванса);
	Если СтрокаАванса <> Неопределено Тогда
		СтрокаАванса.Договор = Объект.ДоговорДляЗачетаАвансаСПомощником;
		ОбработатьИзменениеДоговораКонтрагента(СтрокаАванса);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоговорДляЗачетаАванса2НачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Объект.Контрагент.Пустая() Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Сначала нужно выбрать поставщика'"));
		
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяТипаДокумента(СсылкаНаДокумент)

	Возврат СсылкаНаДокумент.Метаданные().Имя;

КонецФункции // ПолучитьИмяТипаДокумента()

&НаКлиенте
Процедура РасшифровкаПлатежаПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если Копирование Тогда
		ПересчитатьИтогиПриИзмененииАвансаНаКлиенте();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СуммаДокументаОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.СуммаДокумента = ВыбранноеЗначение;
	СуммаДокументаПриИзменении(Элемент);
	
КонецПроцедуры

&НаСервере
Процедура УстановитьЗаголовкиКолонокТабличныхЧастей(ВалютаСсылка)
	
	// Долги будем всегда выводить в валюте документа, чтобы было легче распределить.
	ПредставлениеВалюты = УправлениеНебольшойФирмойПовтИсп.ПолучитьСимвольноеПредставлениеВалюты(ВалютаСсылка);
	
	Элементы.ТаблицаДолговДолгВалПлатежа.Заголовок = НСтр("ru = 'Текущий долг '")+ПредставлениеВалюты;
	Элементы.ТаблицаДолговЗачесть.Заголовок = НСтр("ru = 'Зачитываем '")+ПредставлениеВалюты;
	
	Элементы.РасшифровкаПлатежаСуммаПлатежа.Заголовок = НСтр("ru = 'Сумма '")+ПредставлениеВалюты;
	
КонецПроцедуры

&НаСервере
Функция РассчитатьСуммуДолгаВВалютеПлатежа(ПараметрДолгВалДоговора, ПараметрКурс, ПараметрКратность, ПараметрДоговор, ПараметрыБанковскогоСчета = Неопределено)
	
	Если ПараметрыБанковскогоСчета = Неопределено Тогда
		ТекущаяВалютаДС = Объект.ВалютаДенежныхСредств;
		ТекущийКурс = Курс;
		ТекущаяКратность = Кратность;
	Иначе
		ТекущаяВалютаДС = ПараметрыБанковскогоСчета.ВалютаДенежныхСредств;
		ТекущийКурс = ПараметрыБанковскогоСчета.ВалютаКурсКратность.Курс;
		ТекущаяКратность = ПараметрыБанковскогоСчета.ВалютаКурсКратность.Кратность;
	КонецЕсли;
	Если ПараметрДоговор.ВалютаРасчетов = ТекущаяВалютаДС Тогда
		Возврат ПараметрДолгВалДоговора;
	Иначе
		Возврат УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
			ПараметрДолгВалДоговора,
			ПараметрКурс,
			ТекущийКурс,
			ПараметрКратность,
			ТекущаяКратность
		)
	КонецЕсли;
	
КонецФункции

&НаСервере
Процедура ОбновитьИнформациюОбАвансеВРасшифровкеПлатежа()
	
	Для каждого ТекущаяСтрока Из Объект.РасшифровкаПлатежа Цикл
		ТекущаяСтрока.Аванс = ?(ТекущаяСтрока.ПризнакАванса , ТекущаяСтрока.СуммаПлатежа, 0);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДолгиВПомощникеЗачетаДолгов(ПараметрыБанковскогоСчета = Неопределено)
	
	Для Каждого ТекущаяСтрокаДолгов Из ТаблицаДолгов Цикл
		ТекущаяСтрокаДолгов.ДолгВалПлатежа = РассчитатьСуммуДолгаВВалютеПлатежа(
			ТекущаяСтрокаДолгов.ДолгВалДоговора,
			ТекущаяСтрокаДолгов.Курс,
			ТекущаяСтрокаДолгов.Кратность,
			ТекущаяСтрокаДолгов.Договор,
			ПараметрыБанковскогоСчета
		);
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
