#Область ОбработчикиСобытийФормы
&НаКлиенте
Процедура СоздатьУведомление(Команда)
	
	ИмяФормыУведомления = ПолучитьИмяФормы(ВидУведомления);
	Данные = ПолучитьИзВременногоХранилища(АдресДанные);
	
	Если ЗначениеЗаполнено(ИмяФормыУведомления) Тогда 
		ПараметрыОткрытия = Новый Структура("Организация, НалоговыйОрган, Данные", Организация, РегистрацияВИФНС, Данные);
		Форма = ПолучитьФорму(ИмяФормыУведомления, ПараметрыОткрытия, ЭтотОбъект.ВладелецФормы, ИмяФормыУведомления);
		Форма.Открыть();
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьИмяФормы(Знач ВидУведомления)
	Возврат Документы.УведомлениеОСпецрежимахНалогообложения.ПолучитьПолноеИмяФормыПоВиду(ВидУведомления);
КонецФункции

&НаКлиенте
Функция ДоступностьКнопкиСозданияУведомления()
	Возврат ЗначениеЗаполнено(Организация);
КонецФункции

&НаСервере
Процедура ОбновитьРегистрациюВИФНС()
	РегистрацияВИФНС = Документы.УведомлениеОСпецрежимахНалогообложения.РегистрацияВФНСОрганизации(Организация);
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	ОбновитьРегистрациюВИФНС();
	Элементы.ФормаСоздатьУведомление.Доступность = ДоступностьКнопкиСозданияУведомления();
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Данные = Неопределено;
	Параметры.Свойство("Данные", Данные);
	АдресДанные = ПоместитьВоВременноеХранилище(Данные, УникальныйИдентификатор);

	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		ОргПоУмолчанию = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации").ОрганизацияПоУмолчанию();
		Организация = ОргПоУмолчанию;
	КонецЕсли;
	
	Если Параметры.Свойство("Тип") Тогда
		ВидУведомления = Параметры.Тип;
		
		УведомленияДляОрганизаций = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВидыУведомленийДляОрганизации();
		УведомленияДляИП = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ВидыУведомленийДляИП();
		
		Если УведомленияДляИП.Найти(ВидУведомления) <> Неопределено И УведомленияДляОрганизаций.Найти(ВидУведомления) = Неопределено Тогда 
			
			МассивОтбора = РегламентированнаяОтчетность.ВсеЮрФизЛица(Ложь);
			МассивПараметровВыбора = Новый Массив;
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(МассивОтбора)));
			Элементы.Организация.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
			
		ИначеЕсли УведомленияДляИП.Найти(ВидУведомления) = Неопределено И УведомленияДляОрганизаций.Найти(ВидУведомления) <> Неопределено Тогда 
			
			МассивОтбора = РегламентированнаяОтчетность.ВсеЮрФизЛица(Истина);
			МассивПараметровВыбора = Новый Массив;
			МассивПараметровВыбора.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", Новый ФиксированныйМассив(МассивОтбора)));
			Элементы.Организация.ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметровВыбора);
			
		КонецЕсли;
		
		Элементы.Декорация1.Заголовок = Элементы.Декорация1.Заголовок + ВидУведомления;
	Иначе 
		Отказ = Истина;
	КонецЕсли;
КонецПроцедуры
#КонецОбласти