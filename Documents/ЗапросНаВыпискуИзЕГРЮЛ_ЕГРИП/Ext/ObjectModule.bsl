#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(СообщениеОснование)
	
	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
        Организация = Модуль.ОрганизацияПоУмолчанию();
	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.РаботаСКонтрагентами") Тогда
		
		МодульРегламентированныеДанныеКлиентСервер = ОбщегоНазначения.ОбщийМодуль("РегламентированныеДанныеКлиентСервер");
	
		ДлинаСтрокиИНН_ОГРН = СтрДлина(СокрЛП(ПараметрЗапроса));
		
		Если ДлинаСтрокиИНН_ОГРН = 13 ИЛИ ДлинаСтрокиИНН_ОГРН = 15 Тогда
			ТекстСообщения = "";
			Если Не МодульРегламентированныеДанныеКлиентСервер.ОГРНСоответствуетТребованиям(ПараметрЗапроса, ?(ДлинаСтрокиИНН_ОГРН = 13, Истина, Ложь), ТекстСообщения) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,, "ПредставлениеЗапроса", "", Отказ);
			КонецЕсли;
		ИначеЕсли ДлинаСтрокиИНН_ОГРН = 10 ИЛИ ДлинаСтрокиИНН_ОГРН = 12 Тогда
			ТекстСообщения = "";
			Если Не МодульРегламентированныеДанныеКлиентСервер.ИННСоответствуетТребованиям(ПараметрЗапроса, ?(ДлинаСтрокиИНН_ОГРН = 10, Истина, Ложь), ТекстСообщения) Тогда
				ТекстСообщения = ТекстСообщения + " " + НСтр("ru = 'Данный ИНН не существует.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстСообщения,, "ПредставлениеЗапроса", "", Отказ);
			КонецЕсли;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
				НСтр("ru = 'Укажите верные параметры поиска ИНН или ОГРН.'"),, "ПредставлениеЗапроса", "", Отказ);
		КонецЕсли;
			
	КонецЕсли;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДлинаСтрокиИНН_ОГРН = СтрДлина(СтрЗаменить(ПараметрЗапроса, " ", ""));
	
	Если ДлинаСтрокиИНН_ОГРН = 13 ИЛИ ДлинаСтрокиИНН_ОГРН = 15 Тогда
		ВидПараметраЗапроса = Перечисления.ВидыПараметровЗапросаНаВыпискуИзЕГРЮЛ_ЕГРИП.ОГРН;
	ИначеЕсли ДлинаСтрокиИНН_ОГРН = 10 ИЛИ ДлинаСтрокиИНН_ОГРН = 12 Тогда
		ВидПараметраЗапроса = Перечисления.ВидыПараметровЗапросаНаВыпискуИзЕГРЮЛ_ЕГРИП.ИНН;
	Иначе
		ВидПараметраЗапроса = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли