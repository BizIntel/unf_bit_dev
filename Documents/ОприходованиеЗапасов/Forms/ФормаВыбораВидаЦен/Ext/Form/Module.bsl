
&НаКлиенте
Перем ВидЦенПриОткрытии;

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

// Процедура - обработчик события ПриСозданииНаСервере формы.
// В процедуре осуществляется
// - инициализация параметров формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидЦен = Параметры.ВидЦен;
	
КонецПроцедуры // ПриСозданииНаСервере()

// Процедура - обработчик события ПриОткрытии формы.
// В процедуре осуществляется
// - инициализация параметров формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ВидЦенПриОткрытии = ВидЦен;
	
КонецПроцедуры // ПриОткрытии()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ДЕЙСТВИЯ КОМАНДНЫХ ПАНЕЛЕЙ ФОРМЫ

// Процедура - обработчик события нажатия кнопки ОК.
//
&НаКлиенте
Процедура КомандаОК(Команда)
	
	Отказ = Ложь;
	Если ПерезаполнитьЦены И НЕ ЗначениеЗаполнено(ВидЦен) Тогда
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = НСтр("ru = 'Не выбран вид цены для заполнения!'");
		Сообщение.Поле = "ВидЦен";
		Сообщение.Сообщить();
		Отказ = Истина;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Если ВидЦенПриОткрытии <> ВидЦен ИЛИ ПерезаполнитьЦены Тогда
			БылиВнесеныИзменения = Истина;
		Иначе
			БылиВнесеныИзменения = Ложь;
		КонецЕсли;
		СтруктураРеквизитовФормы = Новый Структура;
		СтруктураРеквизитовФормы.Вставить("БылиВнесеныИзменения", БылиВнесеныИзменения);
		СтруктураРеквизитовФормы.Вставить("ПерезаполнитьЦены", ПерезаполнитьЦены);
		СтруктураРеквизитовФормы.Вставить("ВидЦен", ВидЦен);
		Закрыть(СтруктураРеквизитовФормы);
	КонецЕсли;
	
КонецПроцедуры // КомандаОК()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ФОРМЫ

// Процедура - обработчик события ПриИзменении поля ввода ВидЦен.
//
&НаКлиенте
Процедура ВидЦеныПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(ВидЦен) Тогда
		Если ВидЦенПриОткрытии <> ВидЦен Тогда
			ПерезаполнитьЦены = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ВидЦеныПриИзменении()
