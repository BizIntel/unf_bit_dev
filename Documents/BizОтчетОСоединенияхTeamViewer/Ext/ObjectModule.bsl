
Процедура ОбработкаПроведения(Отказ, Режим)
	
	// регистр СоединенияTeamViewer
	Движения.СоединенияTeamViewer.Записывать = Истина;
	Движения.BizОбслуживаниеКлиента.Записывать = Истина;
	
	Для Каждого ТекСтрокаСоединения Из Соединения Цикл
		Движение = Движения.СоединенияTeamViewer.Добавить();
		Движение.Контрагент = Контрагент;
		Движение.Исполнитель = ТекСтрокаСоединения.Исполнитель;
		Движение.НачалоСеанса = ТекСтрокаСоединения.НачалоСеанса;
		Движение.ID = ТекСтрокаСоединения.ID;
		
		ТребуетсяВыставитьСчет = ТекСтрокаСоединения.СтатусСоединения <> Перечисления.BizСтатусыСоединенияTeamViewer.НеВыставлять;
		
		Если ТребуетсяВыставитьСчет И СуммаВДокументе  Тогда
			Движение1 = Движения.BizОбслуживаниеКлиента.Добавить();
			Движение1.Период = ТекСтрокаСоединения.ОкончаниеСеанса;
			Движение1.Исполнитель 	= ТекСтрокаСоединения.Исполнитель;
			Движение1.Номенклатура 	= НоменклатураУдаленногоСопровождения;
			Движение1.Контрагент 	= Контрагент;
			Движение1.Статус 		= Перечисления.BizСтатусОбслуживанияКлиента.Завершен;
			Движение1.Количество 	= ТекСтрокаСоединения.ПродолжительностьКОплате;
			Движение1.Цена 			=  Цена;
			Движение1.Сумма 		= ТекСтрокаСоединения.СуммаСоСкидкой;
			
			//{{КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
			// Данный фрагмент построен конструктором.
			// При повторном использовании конструктора, внесенные вручную изменения будут утеряны!!!
			
			Запрос = Новый Запрос;
			Запрос.Текст = 
			"ВЫБРАТЬ ПЕРВЫЕ 1
			|	СвойстваСотрудниковСрезПоследних.КоофицентКЗарплате,
			|	СвойстваСотрудниковСрезПоследних.Сотрудник
			|ИЗ
			|	РегистрСведений.СвойстваСотрудников.СрезПоследних(&ДатаСреза, Сотрудник = &Пользователь) КАК СвойстваСотрудниковСрезПоследних";
			
			Запрос.УстановитьПараметр("ДатаСреза", ТекСтрокаСоединения.ОкончаниеСеанса);
			Запрос.УстановитьПараметр("Пользователь", ТекСтрокаСоединения.Исполнитель);
			
			РезультатЗапроса = Запрос.Выполнить();
			
			Если НЕ РезультатЗапроса.Пустой() Тогда
				
				ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
				ВыборкаДетальныеЗаписи.Следующий();
				
				
				Движение1.СуммаКОплате 	= Окр(ТекСтрокаСоединения.СуммаСоСкидкой*ВыборкаДетальныеЗаписи.КоофицентКЗарплате,2,1);
			КонецЕсли;
			
			Движение1.Содержание = ТекСтрокаСоединения.Комментарий;
			
			//}}КОНСТРУКТОР_ЗАПРОСА_С_ОБРАБОТКОЙ_РЕЗУЛЬТАТА
			
		КонецЕсли;
	КонецЦикла;
	
	ОтчетЗаМесяцГод = НачалоМесяца(Соединения[0].НачалоСеанса);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Сообщение = Новый СообщениеПользователю;
	
	Для каждого СтрТЧСоединения Из Соединения Цикл	
		Если СтрТЧСоединения.СтатусСоединения 
				= Перечисления.BizСтатусыСоединенияTeamViewer.Расчет Тогда
		Сообщение.Текст = "!";
		//Сообщение.Поле = "Соединения["+СтрТЧСоединения.НомерСтроки+"].СтатусСоединения";
		Сообщение.ПутьКДанным = "элементы.Соединения["+СтрТЧСоединения.НомерСтроки+"].СоединенияСтатусСоединения";
		Сообщение.Сообщить();
		Отказ = Истина;
		КонецЕсли;	
	КонецЦикла;	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	СуммаИтог 					 = Соединения.Итог("Сумма");
	ПродолжительностьВсегоИтог	 = Соединения.Итог("ПродолжительностьВсего"); 
	ПродолжительностьКОплатеИтог = Соединения.Итог("ПродолжительностьКОплате");
КонецПроцедуры

Процедура  ПечатьОтчетаОудаленыхСоединениях(ТабДок, ОснованиеОтчета = Неопределено) Экспорт
	
	//ТабДок.АвтоМасштаб = Истина;
	Продолжительность = 0;
	ПродолжительностьБезОплаты = 0;
	ПродолжительностьВСчетИТС = 0;
	
	Сумма = 0;
	СуммаСкидки = 0;

	НомерСтраници = 1;
	МакетСоединенияTV = ЭтотОбъект.ПолучитьМакет("Макет");
	
	ОблШапка = МакетСоединенияTV.ПолучитьОбласть("Шапка");
	
	Если ОснованиеОтчета <> Неопределено Тогда
		ОблЗаголовок = МакетСоединенияTV.ПолучитьОбласть("ЗаголовокПриложения");
		НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ОснованиеОтчета.Номер, Истина, Истина);
		ОблЗаголовок.Параметры.НомерДокумента = НомерДокумента;  
		ОблЗаголовок.Параметры.ДатаДокумента = Формат(ОснованиеОтчета.Дата, "ДЛФ=DD");
	Иначе 	
	    ОблЗаголовок = МакетСоединенияTV.ПолучитьОбласть("ЗаголовокПФ"); 
	КонецЕсли;

	ОблДетальныеЗаписи = МакетСоединенияTV.ПолучитьОбласть("ДетальныеЗаписи");
	ОблНомерСтраници = МакетСоединенияTV.ПолучитьОбласть("НомерСтраници");
	ОблНомерСтраници.Параметры.Номер = НомерСтраници;
	ОблИтоги = МакетСоединенияTV.ПолучитьОбласть("Итоги");
	
	ВалютаВзаиморасчетов = Константы.ВалютаУчета.Получить();
	
	ТабДок.Вывести(ОблНомерСтраници);
	ТабДок.Вывести(ОблЗаголовок);
	ТАбДок.Вывести(ОблШапка);
	
	ПродолжительностьВсегоИТОГО = 0;
	ПродолжительностьКОплатеИТОГО = 0;
	СуммаВсегоИТОГО = 0;
	СуммаКОплатеИТОГО = 0;
	
	Для каждого СтрСоединения Из Соединения Цикл
		
		СуммаВсего = СтрСоединения.ПродолжительностьВсего * Цена;
		
		ОблДетальныеЗаписи.Параметры.СервисИнженер 			  = СтрСоединения.Исполнитель;
		ОблДетальныеЗаписи.Параметры.Компьютер 				  = СтрСоединения.Компьютер;
		ОблДетальныеЗаписи.Параметры.Пуск 					  = СтрСоединения.НачалоСеанса;
		ОблДетальныеЗаписи.Параметры.Окончание 				  = СтрСоединения.ОкончаниеСеанса;
		ОблДетальныеЗаписи.Параметры.ИД 					  = СтрСоединения.ID;
		ОблДетальныеЗаписи.Параметры.ПродолжительностьВсего	  = СтрСоединения.ПродолжительностьВсего;
		ОблДетальныеЗаписи.Параметры.ПродолжительностьКОплате = СтрСоединения.ПродолжительностьКОплате;
		ОблДетальныеЗаписи.Параметры.Расчет 				  = СтрСоединения.СтатусСоединения;
		ОблДетальныеЗаписи.Параметры.СуммаВсего 			  = СуммаВсего;
		ОблДетальныеЗаписи.Параметры.СуммаКОплате 			  = СтрСоединения.Сумма;
		ОблДетальныеЗаписи.Параметры.ВалютаВзаиморасчетов 	  = ВалютаВзаиморасчетов;
		ОблДетальныеЗаписи.Параметры.Комментарий 			  = СтрСоединения.Комментарий;
		
		ПродолжительностьВсегоИТОГО 	= ПродолжительностьВсегоИТОГО 		+ СтрСоединения.ПродолжительностьВсего;
		ПродолжительностьКОплатеИТОГО 	= ПродолжительностьКОплатеИТОГО 	+ СтрСоединения.ПродолжительностьКОплате;
		СуммаВсегоИТОГО		= СуммаВсегоИТОГО 	+ СуммаВсего;
		СуммаКОплатеИТОГО	= СуммаКОплатеИТОГО	+ СтрСоединения.Сумма;
		
		Если НЕ ТабДок.ПроверитьВывод(ОблДетальныеЗаписи) Тогда
			ТабДок.ВывестиГоризонтальныйРазделительСтраниц();
			НомерСтраници = НомерСтраници + 1;
			ОблНомерСтраници.Параметры.Номер = НомерСтраници;
			ТабДок.Вывести(ОблНомерСтраници);
			ТабДок.Вывести(ОблШапка);
		КонецЕсли;

		ТабДок.Вывести(ОблДетальныеЗаписи);
		
	КонецЦикла;
	
	ОблИтоги.Параметры.ПродолжительностьВсегоИТОГ 	= ПродолжительностьВсегоИТОГО;
	ОблИтоги.Параметры.ПродолжительностьКОплатеИТОГ = ПродолжительностьКОплатеИТОГО;
	
	ОблИтоги.Параметры.СуммаВсегоИТОГ 	= СуммаВсегоИТОГО;	 
	ОблИтоги.Параметры.СуммаКОплатеИТОГ = СуммаКОплатеИТОГО;
	
	ОблИтоги.Параметры.ВалютаВзаиморасчетов = ВалютаВзаиморасчетов;
	
	ТабДок.Вывести(ОблИтоги);
	
	облКОплате 			= МакетСоединенияTV.ПолучитьОбласть("КОплате");
	облКОплате.Параметры.СуммаКОплатеИТОГ = СуммаКОплатеИТОГО;
	
	облСкидкаВСчетИТС 	= МакетСоединенияTV.ПолучитьОбласть("СкидкаВСчетИТС");
	облСкидкаВСчетИТС.Параметры.СкидкаВСчетИТС = ?(ВСчетИТССумма = 0, "—", ВСчетИТССумма);
	
	облСкидкаЗаОбъем 	= МакетСоединенияTV.ПолучитьОбласть("СкидкаЗаОбъем");
	облСкидкаЗаОбъем.Параметры.СкидкаЗаОбъем = ?(СкидкаЗаОбъемСумма = 0, "—", СкидкаЗаОбъемСумма);

	облИтого 			= МакетСоединенияTV.ПолучитьОбласть("Итого");
	ИтогоПООтчету 		= СуммаКОплатеИТОГО - ВСчетИТССумма - СкидкаЗаОбъемСумма;
	облИтого.Параметры.Итого = ИтогоПООтчету;
	
	ТабДок.Вывести(облКОплате);
	ТабДок.Вывести(облСкидкаВСчетИТС);
	ТабДок.Вывести(облСкидкаЗаОбъем);
	ТабДок.Вывести(облИтого);

КонецПроцедуры // ()

                                                        