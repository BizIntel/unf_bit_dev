
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВидОтбора = Параметры.ВидОтбора;
	СписокЗначенийОтбора 	   = Параметры.СписокЗначенийОтбора;
	
	МассивТипов = Новый Массив();
	Если ВидОтбора = "ОтборПоНоменклатуре" Тогда
		Заголовок = НСтр("ru = 'Выберите номенклатуру'");
		Элементы.ГруппаНоменклатурыЗначение.ВыборГруппИЭлементов = ГруппыИЭлементы.Элементы;
		МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	ИначеЕсли ВидОтбора = "ОтборПоГруппамНоменклатуры" Тогда
		Заголовок = НСтр("ru = 'Выберите группы номенклатуры'");
		Элементы.ГруппаНоменклатурыЗначение.ВыборГруппИЭлементов = ГруппыИЭлементы.Группы;
		МассивТипов.Добавить(Тип("СправочникСсылка.Номенклатура"));
	Иначе
		Заголовок = НСтр("ru = 'Выберите категории номенклатуры'");
		Элементы.ГруппаНоменклатурыЗначение.ВыборГруппИЭлементов = ГруппыИЭлементы.Элементы;
		МассивТипов.Добавить(Тип("СправочникСсылка.КатегорииНоменклатуры"));
	КонецЕсли;
	
	НовоеОписание = Новый ОписаниеТипов(МассивТипов);
	СписокЗначенийОтбора.ТипЗначения = НовоеОписание;
	
КонецПроцедуры

// Заполняет представления элементов и удаляет пустые значения.
//
&НаСервереБезКонтекста
Процедура ЗаполнитьПредставленияЭлементовСпискаСерверБезКонтекста(СписокЗначенийОтбора)
	
	МассивЭлементовНаУдаление = Новый Массив;
	
	Для каждого ЭлементСписка Из СписокЗначенийОтбора Цикл
	
		Если НЕ ЗначениеЗаполнено(ЭлементСписка.Значение) Тогда
			
			МассивЭлементовНаУдаление.Добавить(ЭлементСписка);
			Продолжить;
			
		КонецЕсли;
		
		ЭлементСписка.Представление = ЭлементСписка.Значение.Наименование;
	
	КонецЦикла;
	
	Для каждого ЭлементМассива Из МассивЭлементовНаУдаление Цикл
	
		СписокЗначенийОтбора.Удалить(ЭлементМассива);
	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОК(Команда)
	
	ЗаполнитьПредставленияЭлементовСпискаСерверБезКонтекста(СписокЗначенийОтбора);
	
	РезультатОтбора = Новый Структура;
	РезультатОтбора.Вставить("ВидОтбора", ВидОтбора);
	РезультатОтбора.Вставить("АдресСпискаЗначенийОтбора", ПоместитьВоВременноеХранилище(СписокЗначенийОтбора));
	
	Закрыть(РезультатОтбора);
	
КонецПроцедуры



