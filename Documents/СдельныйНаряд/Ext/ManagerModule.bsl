#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаСдельныйНаряд, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	("ВЫБРАТЬ
	 |	СдельныйНарядСоставБригады.Сотрудник,
	 |	ВЫБОР
	 |		КОГДА ТаблицаСуммаКТУ.СуммаКТУ = 0
	 |			ТОГДА 0
	 |		ИНАЧЕ СдельныйНарядСоставБригады.КТУ / ТаблицаСуммаКТУ.СуммаКТУ
	 |	КОНЕЦ КАК Коэффициент
	 |ПОМЕСТИТЬ ТаблицаСотрудники
	 |ИЗ
	 |	Документ.СдельныйНаряд.СоставБригады КАК СдельныйНарядСоставБригады
	 |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	 |			СУММА(СдельныйНарядСоставБригады.КТУ) КАК СуммаКТУ,
	 |			СдельныйНарядСоставБригады.Ссылка КАК Ссылка
	 |		ИЗ
	 |			Документ.СдельныйНаряд.СоставБригады КАК СдельныйНарядСоставБригады
	 |		ГДЕ
	 |			СдельныйНарядСоставБригады.Ссылка = &Ссылка
	 |		
	 |		СГРУППИРОВАТЬ ПО
	 |			СдельныйНарядСоставБригады.Ссылка) КАК ТаблицаСуммаКТУ
	 |		ПО СдельныйНарядСоставБригады.Ссылка = ТаблицаСуммаКТУ.Ссылка
	 |ГДЕ
	 |	СдельныйНарядСоставБригады.Ссылка = &Ссылка
	 |	И (НЕ СдельныйНарядСоставБригады.Ссылка.Исполнитель ССЫЛКА Справочник.Сотрудники)
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	СдельныйНаряд.Исполнитель,
	 |	1
	 |ИЗ
	 |	Документ.СдельныйНаряд КАК СдельныйНаряд
	 |ГДЕ
	 |	СдельныйНаряд.Ссылка = &Ссылка
	 |	И СдельныйНаряд.Исполнитель ССЫЛКА Справочник.Сотрудники
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	СдельныйНарядОперации.НомерСтроки КАК НомерСтроки,
	 |	СдельныйНарядОперации.Период КАК Период,
	 |	СдельныйНарядОперации.Ссылка.ВалютаДокумента КАК ВалютаУчета,
	 |	&Организация КАК Организация,
	 |	СдельныйНарядОперации.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	 |	СдельныйНарядОперации.Операция.СчетУчетаЗатрат КАК СчетУчета,
	 |	СдельныйНарядОперации.Номенклатура.СчетУчетаЗатрат КАК КоррСчетУчета,
	 |	СдельныйНарядОперации.Номенклатура КАК КоррНоменклатура,
	 |	СдельныйНарядОперации.Характеристика КАК КоррХарактеристика,
	 |	СдельныйНарядОперации.Партия КАК КоррПартия,
	 |	СдельныйНарядОперации.Спецификация КАК КоррСпецификация,
	 |	СдельныйНарядОперации.ЗаказПокупателя КАК ЗаказПокупателя,
	 |	СдельныйНарядОперации.Стоимость * КурсыВалютРасчетов.Курс * КурсыВалютУчета.Кратность / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК Стоимость,
	 |	СдельныйНарядОперации.Стоимость КАК СтоимостьВал,
	 |	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет) КАК ВидДвиженияУправленческий,
	 |	&НачислениеЗарплаты КАК СодержаниеПроводки,
	 |	СдельныйНарядОперации.Нормочасы КАК Нормочасы,
	 |	СдельныйНарядОперации.Расценка КАК Расценка,
	 |	ИСТИНА КАК ФиксированнаяСтоимость,
	 |	ВЫБОР
	 |		КОГДА &ИспользоватьПодсистемуЗарплата
	 |			ТОГДА СдельныйНарядОперации.Ссылка.Закрыт
	 |		ИНАЧЕ ЛОЖЬ
	 |	КОНЕЦ КАК Закрыт,
	 |	СдельныйНарядОперации.Ссылка.ДатаЗакрытия
	 |ПОМЕСТИТЬ ТаблицаСдельныйНарядОперации
	 |ИЗ
	 |	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	 |				&МоментВремени,
	 |				Валюта В
	 |					(ВЫБРАТЬ
	 |						Константы.ВалютаУчета
	 |					ИЗ
	 |						Константы КАК Константы)) КАК КурсыВалютУчета
	 |		ПО (ИСТИНА)
	 |		ЛЕВОЕ СОЕДИНЕНИЕ Константы КАК Константы
	 |		ПО (ИСТИНА)
	 |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	 |		ПО СдельныйНарядОперации.Ссылка.ВалютаДокумента = КурсыВалютРасчетов.Валюта
	 |ГДЕ
	 |	СдельныйНарядОперации.Ссылка = &Ссылка
	 |	И (СдельныйНарядОперации.Номенклатура.СчетУчетаЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.НезавершенноеПроизводство)
	 |			ИЛИ СдельныйНарядОперации.Номенклатура.СчетУчетаЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.КосвенныеЗатраты))
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	&Организация КАК Организация,
	 |	СдельныйНарядОперации.Период КАК Период,
	 |	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	 |	НАЧАЛОПЕРИОДА(СдельныйНарядОперации.ДатаЗакрытия, МЕСЯЦ) КАК ПериодРегистрации,
	 |	СдельныйНарядОперации.ВалютаУчета КАК Валюта,
	 |	СдельныйНарядОперации.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	 |	ТаблицаСотрудники.Сотрудник КАК Сотрудник,
	 |	СдельныйНарядОперации.Период КАК ДатаНачала,
	 |	СдельныйНарядОперации.Период КАК ДатаОкончания,
	 |	0 КАК ОтработаноДней,
	 |	СдельныйНарядОперации.Нормочасы КАК ОтработаноЧасов,
	 |	СдельныйНарядОперации.Расценка КАК Размер,
	 |	ЗНАЧЕНИЕ(Справочник.ВидыНачисленийИУдержаний.СдельнаяОплата) КАК ВидНачисленияУдержания,
	 |	ТаблицаСотрудники.Сотрудник.СчетРасчетовСПерсоналом КАК СчетУчета,
	 |	СдельныйНарядОперации.НомерСтроки КАК НомерСтроки,
	 |	СдельныйНарядОперации.СчетУчета КАК СчетУчетаЗапасов,
	 |	СдельныйНарядОперации.КоррСчетУчета КАК КоррСчетУчетаЗапасов,
	 |	СдельныйНарядОперации.КоррНоменклатура КАК КоррНоменклатура,
	 |	СдельныйНарядОперации.КоррХарактеристика КАК КоррХарактеристика,
	 |	СдельныйНарядОперации.КоррПартия КАК КоррПартия,
	 |	СдельныйНарядОперации.КоррСпецификация,
	 |	СдельныйНарядОперации.ЗаказПокупателя КАК ЗаказПокупателя,
	 |	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Кредит) КАК ВидДвиженияУправленческий,
	 |	&НачислениеЗарплаты КАК СодержаниеПроводки,
	 |	ВЫРАЗИТЬ(СдельныйНарядОперации.СтоимостьВал * ТаблицаСотрудники.Коэффициент КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	 |	ВЫРАЗИТЬ(СдельныйНарядОперации.Стоимость * ТаблицаСотрудники.Коэффициент КАК ЧИСЛО(15, 2)) КАК Сумма,
	 |	ИСТИНА КАК ФиксированнаяСтоимость,
	 |	ВЫБОР
	 |		КОГДА &ИспользоватьПодсистемуЗарплата
	 |			ТОГДА СдельныйНарядОперации.Закрыт
	 |		ИНАЧЕ ЛОЖЬ
	 |	КОНЕЦ КАК Закрыт,
	 |	СдельныйНарядОперации.ДатаЗакрытия
	 |ПОМЕСТИТЬ ТаблицаОпераций
	 |ИЗ
	 |	ТаблицаСотрудники КАК ТаблицаСотрудники
	 |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСдельныйНарядОперации КАК СдельныйНарядОперации
	 |		ПО (ИСТИНА)
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТаблицаОпераций.Организация КАК Организация,
	 |	ТаблицаОпераций.ДатаЗакрытия КАК Период,
	 |	ТаблицаОпераций.ПериодРегистрации КАК ПериодРегистрации,
	 |	ТаблицаОпераций.Валюта КАК Валюта,
	 |	ТаблицаОпераций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	 |	ТаблицаОпераций.Сотрудник КАК Сотрудник,
	 |	ТаблицаОпераций.Период КАК ДатаНачала,
	 |	ТаблицаОпераций.Период КАК ДатаОкончания,
	 |	ТаблицаОпераций.ОтработаноДней КАК ОтработаноДней,
	 |	ТаблицаОпераций.ОтработаноЧасов КАК ОтработаноЧасов,
	 |	ТаблицаОпераций.Размер КАК Размер,
	 |	ТаблицаОпераций.ВидНачисленияУдержания КАК ВидНачисленияУдержания,
	 |	ТаблицаОпераций.СуммаВал КАК СуммаВал,
	 |	ТаблицаОпераций.Сумма КАК Сумма
	 |ИЗ
	 |	ТаблицаОпераций КАК ТаблицаОпераций
	 |ГДЕ
	 |	ТаблицаОпераций.Закрыт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТаблицаОпераций.Организация КАК Организация,
	 |	ТаблицаОпераций.ДатаЗакрытия КАК Период,
	 |	ТаблицаОпераций.ВидДвижения КАК ВидДвижения,
	 |	ТаблицаОпераций.ПериодРегистрации КАК ПериодРегистрации,
	 |	ТаблицаОпераций.Валюта КАК Валюта,
	 |	ТаблицаОпераций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	 |	ТаблицаОпераций.Сотрудник КАК Сотрудник,
	 |	ТаблицаОпераций.Сумма КАК Сумма,
	 |	ТаблицаОпераций.СуммаВал КАК СуммаВал,
	 |	ТаблицаОпераций.СчетУчета КАК СчетУчета,
	 |	ТаблицаОпераций.ВидДвиженияУправленческий КАК ВидДвиженияУправленческий,
	 |	ТаблицаОпераций.СодержаниеПроводки КАК СодержаниеПроводки
	 |ИЗ
	 |	ТаблицаОпераций КАК ТаблицаОпераций
	 |ГДЕ
	 |	ТаблицаОпераций.Закрыт
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	СдельныйНарядОперации.Период,
	 |	&Организация КАК Организация,
	 |	СдельныйНарядОперации.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	 |	СдельныйНарядОперации.Ссылка.Исполнитель,
	 |	СдельныйНарядОперации.Операция,
	 |	СдельныйНарядОперации.Номенклатура,
	 |	СдельныйНарядОперации.Характеристика,
	 |	СдельныйНарядОперации.ЗаказПокупателя,
	 |	СдельныйНарядОперации.Нормочасы КАК НормоЧасы,
	 |	ВЫБОР
	 |		КОГДА ТИПЗНАЧЕНИЯ(СдельныйНарядОперации.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	 |			ТОГДА СдельныйНарядОперации.КоличествоПлан
	 |		ИНАЧЕ СдельныйНарядОперации.КоличествоПлан * СдельныйНарядОперации.ЕдиницаИзмерения.Коэффициент
	 |	КОНЕЦ КАК КоличествоПлан,
	 |	ВЫБОР
	 |		КОГДА ТИПЗНАЧЕНИЯ(СдельныйНарядОперации.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	 |			ТОГДА СдельныйНарядОперации.КоличествоФакт
	 |		ИНАЧЕ СдельныйНарядОперации.КоличествоФакт * СдельныйНарядОперации.ЕдиницаИзмерения.Коэффициент
	 |	КОНЕЦ КАК КоличествоФакт
	 |ИЗ
	 |	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
	 |ГДЕ
	 |	СдельныйНарядОперации.Ссылка = &Ссылка
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	МИНИМУМ(ТаблицаОпераций.НомерСтроки) КАК НомерСтроки,
	 |	ТаблицаОпераций.ВидДвижения КАК ВидДвижения,
	 |	ТаблицаОпераций.ДатаЗакрытия КАК Период,
	 |	ТаблицаОпераций.Организация КАК Организация,
	 |	ТаблицаОпераций.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	 |	ТаблицаОпераций.СтруктурнаяЕдиница КАК КоррСтруктурнаяЕдиница,
	 |	ТаблицаОпераций.СчетУчетаЗапасов КАК СчетУчета,
	 |	ТаблицаОпераций.КоррСчетУчетаЗапасов КАК КоррСчетУчета,
	 |	ТаблицаОпераций.КоррНоменклатура КАК КоррНоменклатура,
	 |	ТаблицаОпераций.КоррХарактеристика КАК КоррХарактеристика,
	 |	ТаблицаОпераций.КоррПартия КАК КоррПартия,
	 |	ТаблицаОпераций.КоррСпецификация,
	 |	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	 |	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	 |	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
	 |	ЗНАЧЕНИЕ(Справочник.Спецификации.ПустаяСсылка) КАК Спецификация,
	 |	ТаблицаОпераций.ЗаказПокупателя КАК ЗаказПокупателя,
	 |	ТаблицаОпераций.ЗаказПокупателя КАК КоррЗаказПокупателя,
	 |	СУММА(ТаблицаОпераций.Сумма) КАК Сумма,
	 |	ИСТИНА КАК ФиксированнаяСтоимость,
	 |	ЛОЖЬ КАК ЗатратыНаВыпуск,
	 |	ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет) КАК ВидДвиженияУправленческий,
	 |	ТаблицаОпераций.СодержаниеПроводки КАК СодержаниеПроводки
	 |ИЗ
	 |	ТаблицаОпераций КАК ТаблицаОпераций
	 |ГДЕ
	 |	ТаблицаОпераций.Закрыт
	 |
	 |СГРУППИРОВАТЬ ПО
	 |	ТаблицаОпераций.СчетУчетаЗапасов,
	 |	ТаблицаОпераций.КоррСчетУчетаЗапасов,
	 |	ТаблицаОпераций.КоррНоменклатура,
	 |	ТаблицаОпераций.КоррХарактеристика,
	 |	ТаблицаОпераций.КоррПартия,
	 |	ТаблицаОпераций.СодержаниеПроводки,
	 |	ТаблицаОпераций.Организация,
	 |	ТаблицаОпераций.СтруктурнаяЕдиница,
	 |	ТаблицаОпераций.ЗаказПокупателя,
	 |	ТаблицаОпераций.КоррСпецификация,
	 |	ТаблицаОпераций.ДатаЗакрытия,
	 |	ТаблицаОпераций.ВидДвижения,
	 |	ТаблицаОпераций.СтруктурнаяЕдиница,
	 |	ТаблицаОпераций.ЗаказПокупателя
	 |;
	 |
	 |////////////////////////////////////////////////////////////////////////////////
	 |ВЫБРАТЬ
	 |	ТаблицаОпераций.НомерСтроки КАК НомерСтроки,
	 |	ТаблицаОпераций.ДатаЗакрытия КАК Период,
	 |	&Организация КАК Организация,
	 |	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	 |	ТаблицаОпераций.СчетУчетаЗапасов КАК СчетДт,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.СчетУчетаЗапасов.Валютный
	 |			ТОГДА ТаблицаОпераций.Валюта
	 |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	 |	КОНЕЦ КАК ВалютаДт,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.СчетУчетаЗапасов.Валютный
	 |			ТОГДА ТаблицаОпераций.СуммаВал
	 |		ИНАЧЕ 0
	 |	КОНЕЦ КАК СуммаВалДт,
	 |	ТаблицаОпераций.СчетУчета КАК СчетКт,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.СчетУчета.Валютный
	 |			ТОГДА ТаблицаОпераций.Валюта
	 |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	 |	КОНЕЦ КАК ВалютаКт,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.СчетУчета.Валютный
	 |			ТОГДА ТаблицаОпераций.СуммаВал
	 |		ИНАЧЕ 0
	 |	КОНЕЦ КАК СуммаВалКт,
	 |	ТаблицаОпераций.Сумма КАК Сумма,
	 |	&НачислениеЗарплаты КАК Содержание
	 |ИЗ
	 |	ТаблицаОпераций КАК ТаблицаОпераций
	 |ГДЕ
	 |	ТаблицаОпераций.Сумма > 0
	 |	И ТаблицаОпераций.Закрыт
	 |
	 |ОБЪЕДИНИТЬ ВСЕ
	 |
	 |ВЫБРАТЬ
	 |	ТаблицаОпераций.НомерСтроки,
	 |	ТаблицаОпераций.ДатаЗакрытия,
	 |	&Организация,
	 |	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	 |	ТаблицаОпераций.КоррСчетУчетаЗапасов,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.КоррСчетУчетаЗапасов.Валютный
	 |			ТОГДА ТаблицаОпераций.Валюта
	 |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	 |	КОНЕЦ,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.КоррСчетУчетаЗапасов.Валютный
	 |			ТОГДА ТаблицаОпераций.СуммаВал
	 |		ИНАЧЕ 0
	 |	КОНЕЦ,
	 |	ТаблицаОпераций.СчетУчетаЗапасов,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.СчетУчетаЗапасов.Валютный
	 |			ТОГДА ТаблицаОпераций.Валюта
	 |		ИНАЧЕ НЕОПРЕДЕЛЕНО
	 |	КОНЕЦ,
	 |	ВЫБОР
	 |		КОГДА ТаблицаОпераций.СчетУчетаЗапасов.Валютный
	 |			ТОГДА ТаблицаОпераций.СуммаВал
	 |		ИНАЧЕ 0
	 |	КОНЕЦ,
	 |	ТаблицаОпераций.Сумма,
	 |	&РазноскаЗарплаты
	 |ИЗ
	 |	ТаблицаОпераций КАК ТаблицаОпераций
	 |ГДЕ
	 |	ТаблицаОпераций.Сумма > 0
	 |	И ТаблицаОпераций.Закрыт");
	
	Запрос.УстановитьПараметр("Ссылка",					ДокументСсылкаСдельныйНаряд);
	Запрос.УстановитьПараметр("МоментВремени",			Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация",			СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("НачислениеЗарплаты",		НСтр("ru = 'Начисление зарплаты'"));
	Запрос.УстановитьПараметр("РазноскаЗарплаты",		НСтр("ru = 'Отнесение затрат на продукцию'"));
	
	// ФО Использовать подсистему Зарплата.
	Запрос.УстановитьПараметр("ИспользоватьПодсистемуЗарплата", Константы.ФункциональнаяОпцияИспользоватьПодсистемуЗарплата.Получить());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаНачисленияИУдержания", МассивРезультатов[3].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПерсоналом", МассивРезультатов[4].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСдельныеНаряды", МассивРезультатов[5].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыОперации", МассивРезультатов[6].Выгрузить());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОперации.СкопироватьКолонки());
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", МассивРезультатов[7].Выгрузить());
	
	ПустаяСтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ПустаяСсылка();
	ПустойСчетУчета = ПланыСчетов.Управленческий.ПустаяСсылка();
	ПустаяНоменклатура = Справочники.Номенклатура.ПустаяСсылка();
	ПустаяХарактеристика = Справочники.ХарактеристикиНоменклатуры.ПустаяСсылка();
	ПустаяПартия = Справочники.ПартииНоменклатуры.ПустаяСсылка();
	ПустойЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОперации.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасыОперации = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасыОперации[н];
		
		// Оприходуем затраты по зарплате в НЗП.
		СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасыОперации);
		
		СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = ПустаяСтруктурнаяЕдиница;
		СтрокаТаблицыПриход.КоррСчетУчета = ПустойСчетУчета;
		СтрокаТаблицыПриход.КоррНоменклатура = ПустаяНоменклатура;
		СтрокаТаблицыПриход.КоррХарактеристика = ПустаяХарактеристика;
		СтрокаТаблицыПриход.КоррПартия = ПустаяПартия;
		СтрокаТаблицыПриход.КоррЗаказПокупателя = ПустойЗаказПокупателя;
		
		// Спишем их на продукцию.
		СтрокаТаблицыРасход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыРасход, СтрокаТаблицаЗапасыОперации);
		СтрокаТаблицыРасход.ВидДвижения = ВидДвиженияНакопления.Расход;
		СтрокаТаблицыРасход.ВидДвиженияУправленческий = ВидДвиженияБухгалтерии.Кредит;
		СтрокаТаблицыРасход.ФиксированнаяСтоимость = Ложь;
		СтрокаТаблицыРасход.ЗатратыНаВыпуск = Истина;
		
		// Включим в себестоимость продукции.
		СтрокаТаблицыПриход = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицыПриход, СтрокаТаблицаЗапасыОперации);
		
		СтрокаТаблицыПриход.СтруктурнаяЕдиница = СтрокаТаблицаЗапасыОперации.КоррСтруктурнаяЕдиница;
		СтрокаТаблицыПриход.СчетУчета = СтрокаТаблицаЗапасыОперации.КоррСчетУчета;
		СтрокаТаблицыПриход.Номенклатура = СтрокаТаблицаЗапасыОперации.КоррНоменклатура;
		СтрокаТаблицыПриход.Характеристика = СтрокаТаблицаЗапасыОперации.КоррХарактеристика;
		СтрокаТаблицыПриход.Партия = СтрокаТаблицаЗапасыОперации.КоррПартия;
		СтрокаТаблицыПриход.ЗаказПокупателя = СтрокаТаблицаЗапасыОперации.КоррЗаказПокупателя;
		
		СтрокаТаблицыПриход.КоррСтруктурнаяЕдиница = СтрокаТаблицаЗапасыОперации.СтруктурнаяЕдиница;
		СтрокаТаблицыПриход.КоррСчетУчета = СтрокаТаблицаЗапасыОперации.СчетУчета;
		СтрокаТаблицыПриход.КоррНоменклатура = СтрокаТаблицаЗапасыОперации.Номенклатура;
		СтрокаТаблицыПриход.КоррХарактеристика = СтрокаТаблицаЗапасыОперации.Характеристика;
		СтрокаТаблицыПриход.КоррПартия = СтрокаТаблицаЗапасыОперации.Партия;
		СтрокаТаблицыПриход.КоррЗаказПокупателя = СтрокаТаблицаЗапасыОперации.ЗаказПокупателя;
		
		СтрокаТаблицыПриход.ФиксированнаяСтоимость = Ложь;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Удалить("ТаблицаЗапасыОперации");
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#Область ВерсионированиеОбъектов

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ИнтерфейсПечати

// Функция формирует печатную форму документа по указанному макету.
//
// Параметры:
//	ТабличныйДокумент - ТабличныйДокумент в который будет выводится печатная
//				   форма.
//  ИмяМакета    - Строка, имя макета печатной формы.
//
Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_СдельныйНаряд";
	
	ПервыйДокумент = Истина;
	
	Для каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если НЕ ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос();
		
		Запрос.УстановитьПараметр("Организация", 		УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(ТекущийДокумент.Организация));
		Запрос.УстановитьПараметр("ТекущийДокумент", 	ТекущийДокумент);
		
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	СдельныйНаряд.Ссылка КАК Ссылка,
		|	СдельныйНаряд.ВерсияДанных,
		|	СдельныйНаряд.ПометкаУдаления,
		|	СдельныйНаряд.Номер,
		|	СдельныйНаряд.Дата КАК ДатаДокумента,
		|	СдельныйНаряд.Проведен,
		|	СдельныйНаряд.Организация,
		|	СдельныйНаряд.СтруктурнаяЕдиница,
		|	СдельныйНаряд.Исполнитель,
		|	СдельныйНаряд.Комментарий,
		|	СдельныйНаряд.ВалютаДокумента,
		|	СдельныйНаряд.СуммаДокумента,
		|	СдельныйНаряд.Автор,
		|	СдельныйНаряд.Закрыт,
		|	СдельныйНаряд.ДатаЗакрытия
		|ИЗ
		|	Документ.СдельныйНаряд КАК СдельныйНаряд
		|ГДЕ
		|	СдельныйНаряд.Ссылка = &ТекущийДокумент
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	СдельныйНарядОперации.Ссылка,
		|	СдельныйНарядОперации.НомерСтроки,
		|	СдельныйНарядОперации.Период КАК День,
		|	СдельныйНарядОперации.ЗаказПокупателя,
		|	СдельныйНарядОперации.Номенклатура,
		|	СдельныйНарядОперации.Номенклатура.Код КАК Код,
		|	СдельныйНарядОперации.Номенклатура.Артикул КАК Артикул,
		|	СдельныйНарядОперации.Характеристика,
		|	СдельныйНарядОперации.Операция,
		|	СдельныйНарядОперации.ЕдиницаИзмерения,
		|	СдельныйНарядОперации.КоличествоПлан КАК КоличествоПлан,
		|	СдельныйНарядОперации.КоличествоФакт КАК КоличествоФакт,
		|	СдельныйНарядОперации.НормаВремени,
		|	СдельныйНарядОперации.Расценка,
		|	СдельныйНарядОперации.Нормочасы КАК Нормочасы,
		|	СдельныйНарядОперации.Стоимость КАК Стоимость,
		|	СдельныйНарядОперации.Партия,
		|	СдельныйНарядОперации.Спецификация
		|ИЗ
		|	Документ.СдельныйНаряд.Операции КАК СдельныйНарядОперации
		|ГДЕ
		|	СдельныйНарядОперации.Ссылка = &ТекущийДокумент
		|
		|УПОРЯДОЧИТЬ ПО
		|	СдельныйНарядОперации.Период
		|ИТОГИ
		|	СУММА(КоличествоПлан),
		|	СУММА(КоличествоФакт),
		|	СУММА(Нормочасы),
		|	СУММА(Стоимость)
		|ПО
		|	День";
		
		РезультатЗапроса	= Запрос.ВыполнитьПакет();
		Шапка 				= РезультатЗапроса[0].Выбрать();
		Шапка.Следующий();
		
		ВыборкаДни			=РезультатЗапроса[1].Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
		
		Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.СдельныйНаряд.ПФ_MXL_СдельныйНаряд");
		
		ОблШапка		= Макет.ПолучитьОбласть("Шапка");
		ОблШапкаТаблицы	= Макет.ПолучитьОбласть("ШапкаТаблицы");
		ОблДень			= Макет.ПолучитьОбласть("День");
		ОблДетали		= Макет.ПолучитьОбласть("Детали");
		ОблПодвал		= Макет.ПолучитьОбласть("Подвал");
		
		ОблШапка.Параметры.Заполнить(Шапка);
		ОблШапка.Параметры.ЗаголовокДокумента = "Сдельный наряд № " + Шапка.Номер + " от " + Формат(Шапка.ДатаДокумента, "ДЛФ=DD");
		ТабличныйДокумент.Вывести(ОблШапка);
		
		ОблШапкаТаблицы.Параметры.Стоимость = "Стоимость
		|(" + Шапка.ВалютаДокумента + ")";
		ТабличныйДокумент.Вывести(ОблШапкаТаблицы);
		
		НПП = 0;
		
		Пока ВыборкаДни.Следующий() Цикл
			
			ОблДень.Параметры.Заполнить(ВыборкаДни);
			ТабличныйДокумент.Вывести(ОблДень);
			
			ВыборкаОпераций = ВыборкаДни.Выбрать();
			Пока ВыборкаОпераций.Следующий() Цикл
				
				НПП = НПП + 1;
				
				ОблДетали.Параметры.Заполнить(ВыборкаОпераций);
				ОблДетали.Параметры.НПП = НПП;
				
				ТабличныйДокумент.Вывести(ОблДетали);
				
			КонецЦикла;
			
		КонецЦикла;
		
		ОблПодвал.Параметры.Стоимость = Шапка.СуммаДокумента;
		ТабличныйДокумент.Вывести(ОблПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, Шапка.Ссылка);
		
	КонецЦикла;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СдельныйНаряд") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "СдельныйНаряд", "Сдельный наряд", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СдельныйНаряд";
	//Длинное название печатной формы не помещается на форме документа
	КомандаПечати.Представление = НСтр("ru = 'Сдельный наряд'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;

КонецПроцедуры

#КонецОбласти

#КонецЕсли