
#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	ИнтеграцияЕГАИСВызовСервера.ПриПолученииФормыДокумента(
		"ПередачаВТорговыйЗалЕГАИС",
		ВидФормы,
		Параметры,
		ВыбраннаяФорма,
		ДополнительнаяИнформация,
		СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Добавляет на форму списка условное оформление состояния фиксации.
//
//  Параметры:
//   Форма - УправляемаяФорма – форма документа.
//   ОформляемоеПоле - Строка – имя поля для оформления.
//   ПутьКДанным - Строка - путь к реквизиту СтатусОбработки.
//
Процедура УстановитьУсловноеОформлениеСтатусаОбработки(Форма, ОформляемоеПоле = "Список", ПутьКДанным = "Список.СтатусОбработки") Экспорт
	
	УсловноеОформлениеКД = Форма.УсловноеОформление;
	
	// Представление статуса Передается
	ПредставлениеЭлемента = НСтр("ru = 'Документ передается в ЕГАИС'");
	
	ЭлементУсловногоОформления = УсловноеОформлениеКД.Элементы.Добавить();
	ЭлементУсловногоОформления.Представление = ПредставлениеЭлемента;
	ЭлементУсловногоОформления.Использование = Истина;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЕГАИССтатусОбработкиПередается);
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ОформляемоеПоле);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		ПутьКДанным,
		Перечисления.СтатусыОбработкиПередачиВТорговыйЗалЕГАИС.ПередаетсяВЕГАИС,
		ВидСравненияКомпоновкиДанных.Равно,
		ПредставлениеЭлемента,
		Истина);
	
	// Представление статуса ОшибкаПередачи
	ПредставлениеЭлемента = НСтр("ru = 'Получена ошибка передачи в ЕГАИС'");
	
	ЭлементУсловногоОформления = УсловноеОформлениеКД.Элементы.Добавить();
	ЭлементУсловногоОформления.Представление = ПредставлениеЭлемента;
	ЭлементУсловногоОформления.Использование = Истина;
	ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЕГАИССтатусОбработкиОшибкаПередачи);
	
	ПолеЭлемента = ЭлементУсловногоОформления.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(ОформляемоеПоле);
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
		ЭлементУсловногоОформления.Отбор,
		ПутьКДанным,
		Перечисления.СтатусыОбработкиПередачиВТорговыйЗалЕГАИС.ОшибкаПередачиВЕГАИС,
		ВидСравненияКомпоновкиДанных.Равно,
		ПредставлениеЭлемента,
		Истина);
	
КонецПроцедуры

// Возвращает данные документа в виде структуры перед выгрузкой в УТМ.
//
// Параметры:
//  ДокументСсылка - ДокументСсылка.ПередачаВТорговыйЗал - выгружаемая передача.
//
// Возвращаемое значение:
//   Структура - данные передачи.
//
Функция ИнициализироватьДанныеДокументаДляВыгрузки(ДокументСсылка) Экспорт
	
	ДанныеДокумента = СтркуктураДанныхПередачиВТорговыйЗал();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПередачаВТорговыйЗалЕГАИС.Номер КАК Номер,
	|	ПередачаВТорговыйЗалЕГАИС.Дата КАК Дата,
	|	ПередачаВТорговыйЗалЕГАИС.Идентификатор КАК Идентификатор,
	|	ПередачаВТорговыйЗалЕГАИС.Комментарий КАК Комментарий
	|ИЗ
	|	Документ.ПередачаВТорговыйЗалЕГАИС КАК ПередачаВТорговыйЗалЕГАИС
	|ГДЕ
	|	ПередачаВТорговыйЗалЕГАИС.Ссылка = &Ссылка";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ЗаполнитьЗначенияСвойств(ДанныеДокумента, Выборка);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ПередачаВТорговыйЗалЕГАИСТовары.НомерСтроки КАК НомерСтроки,
	|	ПередачаВТорговыйЗалЕГАИСТовары.АлкогольнаяПродукция.Код КАК КодАлкогольнойПродукции,
	|	ПередачаВТорговыйЗалЕГАИСТовары.Количество КАК Количество,
	|	ПередачаВТорговыйЗалЕГАИСТовары.СправкаБ.РегистрационныйНомер КАК НомерСправкиБ
	|ИЗ
	|	Документ.ПередачаВТорговыйЗалЕГАИС.Товары КАК ПередачаВТорговыйЗалЕГАИСТовары
	|ГДЕ
	|	ПередачаВТорговыйЗалЕГАИСТовары.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СтрокаДокумента = СтркуктураДанныхСтрокиПередачиВТорговыйЗал();
		ЗаполнитьЗначенияСвойств(СтрокаДокумента, Выборка);
		СтрокаДокумента.ИдентификаторСтроки = Формат(Выборка.НомерСтроки, "ЧГ=0");
		
		ДанныеДокумента.ТаблицаТоваров.Добавить(СтрокаДокумента);
	КонецЦикла;
	
	Возврат ДанныеДокумента;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает структуру, необходимую для выгрузки передачи в торговый зал в УТМ.
//
Функция СтркуктураДанныхПередачиВТорговыйЗал()
	
	Результат = Новый Структура;
	Результат.Вставить("Идентификатор" , Неопределено); // Идентификатор документа (клиентский).
	Результат.Вставить("Номер"         , "");           // Номер документа.
	Результат.Вставить("Дата"          , '00010101');   // Дата составления.
	Результат.Вставить("Комментарий"   , Неопределено); // Произвольный комментарий к документу.
	Результат.Вставить("ТаблицаТоваров", Новый Массив); // Массив передаваемой продукции.
	
	Возврат Результат;
	
КонецФункции

// Возвращает структуру, необходимую для выгрузки строки передачи в торговый зал в УТМ.
//
Функция СтркуктураДанныхСтрокиПередачиВТорговыйЗал()
	
	Результат = Новый Структура;
	Результат.Вставить("ИдентификаторСтроки"    , ""); // Идентификатор позиции внутри документа.
	Результат.Вставить("КодАлкогольнойПродукции", ""); // Код передаваемой алкогольной продукции.
	Результат.Вставить("Количество"             , 0);  // Количество единиц передаваемого товара.
	Результат.Вставить("НомерСправкиБ"          , ""); // Номер справки Б, по которой товар поступил на склад.
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#КонецЕсли