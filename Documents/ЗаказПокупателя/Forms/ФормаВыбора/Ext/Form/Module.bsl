
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("СчетЗатрат") Тогда
		
		Если ЗначениеЗаполнено(Параметры.СчетЗатрат) Тогда
			
			Если Параметры.СчетЗатрат.ТипСчета <> Перечисления.ТипыСчетов.Расходы
				И Параметры.СчетЗатрат.ТипСчета <> Перечисления.ТипыСчетов.Доходы
				И Параметры.СчетЗатрат.ТипСчета <> Перечисления.ТипыСчетов.НезавершенноеПроизводство
				И Параметры.СчетЗатрат.ТипСчета <> Перечисления.ТипыСчетов.КосвенныеЗатраты Тогда
				
				ТекстСообщения = НСтр("ru = 'Для данного типа счета заказ покупателя не указывается!'");
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(, ТекстСообщения, , , , Отказ);
				
			КонецЕсли;
			
		Иначе
			
			ТекстСообщения = НСтр("ru = 'Не выбран счет!'");
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(, ТекстСообщения, , , , Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВыбиратьЗаказНаряд")
		И Параметры.ВыбиратьЗаказНаряд Тогда
		Параметры.Отбор.Вставить("ВидОперации", Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд);
		АвтоЗаголовок = Ложь;
		Заголовок = НСтр("ru = 'Заказ-наряды'");
		ЭтотОбъект.ЭтоЗаказНаряд = Истина;
	КонецЕсли;
	
	Если Параметры.Свойство("ВидыОпераций") Тогда
		Если Параметры.ВидыОпераций[0] = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru = 'Заказ-наряды'");
			ЭтотОбъект.ЭтоЗаказНаряд = Истина;
		КонецЕсли;
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(
		Список,
		"ВидОперации",
		Параметры.ВидыОпераций,
		Истина,
		ВидСравненияКомпоновкиДанных.ВСписке);
	КонецЕсли;
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеОтмененногоЗаказа(
		Список.КомпоновщикНастроек.Настройки.УсловноеОформление
	);
	
	УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	
	// МобильноеПриложение
	Если МобильноеПриложениеВызовСервера.НужноОграничитьФункциональность() Тогда
		Элементы.ФормаОбщаяКомандаСтруктураПодчиненности.Видимость = Ложь;
		Элементы.ГруппаГлобальныеКомандыЗаказПокупателя.Видимость = Ложь;
	КонецЕсли;
	// Конец МобильноеПриложение
	
	// Установим формат для текущей даты: ДФ=Ч:мм
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(Список);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если (ЭтотОбъект.ЭтоЗаказНаряд И ИмяСобытия = "Запись_СостоянияЗаказНарядов")
		Или (Не ЭтотОбъект.ЭтоЗаказНаряд И ИмяСобытия = "Запись_СостоянияЗаказовПокупателей") Тогда
		
		УстановитьУсловноеОформлениеПоЦветамСостоянийСервер();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьУсловноеОформлениеПоЦветамСостоянийСервер()
	
	СостоянияЗаказов.УстановитьУсловноеОформлениеПоЦветамСостояний(
		Список.КомпоновщикНастроек.Настройки.УсловноеОформление,
		?(ЭтотОбъект.ЭтоЗаказНаряд, Метаданные.Справочники.СостоянияЗаказНарядов.ПолноеИмя(),
			Метаданные.Справочники.СостоянияЗаказовПокупателей.ПолноеИмя())
	);
	
КонецПроцедуры

#КонецОбласти
