#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Процедура получает ставку НДС по умолчанию.
//
Функция ПолучитьСтавкуНДСПоУмолчанию(НалогообложениеНДС)
	
	Если НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = Организация.СтавкаНДСПоУмолчанию;
	ИначеЕсли НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.НеОблагаетсяНДС Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСБезНДС();
	Иначе
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДСНоль();
	КонецЕсли;
	
	Возврат СтавкаНДСПоУмолчанию;
	
КонецФункции // ПолучитьСтавкуНДСПоУмолчанию()

#КонецОбласти

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности.Получить() Тогда
		
		Для каждого СтрокаВнеоборотныеАктивы из ВнеоборотныеАктивы Цикл
			
			Если СтрокаВнеоборотныеАктивы.СчетЗатрат.ТипСчета = Перечисления.ТипыСчетов.Расходы Тогда
				
				СтрокаВнеоборотныеАктивы.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ОсновноеНаправление;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из РасчетыСПокупателями Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.Контрагент)
		И НЕ СтрокаТЧ.Контрагент.ВестиРасчетыПоДоговорам
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Договор) Тогда
			СтрокаТЧ.Договор = СтрокаТЧ.Контрагент.ДоговорПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из РасчетыСПоставщиками Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.Контрагент)
		И НЕ СтрокаТЧ.Контрагент.ВестиРасчетыПоДоговорам
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Договор) Тогда
			СтрокаТЧ.Договор = СтрокаТЧ.Контрагент.ДоговорПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из ЗапасыПереданные Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.Контрагент)
		И НЕ СтрокаТЧ.Контрагент.ВестиРасчетыПоДоговорам
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Договор) Тогда
			СтрокаТЧ.Договор = СтрокаТЧ.Контрагент.ДоговорПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из ЗапасыПринятые Цикл
		Если ЗначениеЗаполнено(СтрокаТЧ.Контрагент)
		И НЕ СтрокаТЧ.Контрагент.ВестиРасчетыПоДоговорам
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Договор) Тогда
			СтрокаТЧ.Договор = СтрокаТЧ.Контрагент.ДоговорПоУмолчанию;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи.
// Осуществляется формирование документов при автозаполнении.
//
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ Автоформирование Тогда
		Возврат;
	КонецЕсли;
	
	БылиВнесеныИзменения = Ложь;
	
	// Формирование документов для табличной части РасчетыСПокупателями.
	Для каждого Строка Из РасчетыСПокупателями Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Документ)
			  И Строка.Контрагент.ВестиРасчетыПоДокументам Тогда
			Если Строка.ПризнакАванса Тогда
				НовыйДокумент = Документы.ПоступлениеВКассу.СоздатьДокумент();
				НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийПоступлениеВКассу.ОтПокупателя;
				НовыйДокумент.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаОтПокупателей;
				НовыйДокумент.Касса = Организация.КассаПоУмолчанию;
				НовыйДокумент.ВалютаДенежныхСредств = Строка.Договор.ВалютаРасчетов;
				НовыйДокумент.СуммаДокумента = Строка.СуммаВал;
				НовыйДокумент.НалогообложениеНДС = УправлениеНебольшойФирмойСервер.НалогообложениеНДС(
					НовыйДокумент.Организация, , Дата);
				НоваяСтрока = НовыйДокумент.РасшифровкаПлатежа.Добавить();
				НоваяСтрока.Договор = Строка.Договор;
				ДоговорВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
					Дата,
					Новый Структура("Валюта", НоваяСтрока.Договор.ВалютаРасчетов)
				);
				ДокументВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
					Дата,
					Новый Структура("Валюта", НовыйДокумент.ВалютаДенежныхСредств)
				);
				НоваяСтрока.Курс = ?(
					ДоговорВалютаКурсКратность.Курс = 0,
					1,
					ДоговорВалютаКурсКратность.Курс
				);
				НоваяСтрока.Кратность = ?(
					ДоговорВалютаКурсКратность.Кратность = 0,
					1,
					ДоговорВалютаКурсКратность.Кратность
				);
				НоваяСтрока.ПризнакАванса = Истина;
				НоваяСтрока.СуммаПлатежа = НовыйДокумент.СуммаДокумента;
				НоваяСтрока.СуммаРасчетов = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
					НоваяСтрока.СуммаПлатежа,
					ДокументВалютаКурсКратность.Курс,
					НоваяСтрока.Курс,
					ДокументВалютаКурсКратность.Кратность,
					НоваяСтрока.Кратность
				);
				НоваяСтрока.СтавкаНДС = ПолучитьСтавкуНДСПоУмолчанию(НовыйДокумент.НалогообложениеНДС);
				НоваяСтрока.СуммаНДС = НоваяСтрока.СуммаПлатежа - (НоваяСтрока.СуммаПлатежа) / ((НоваяСтрока.СтавкаНДС.Ставка + 100) / 100);
			Иначе
				НовыйДокумент = Документы.РасходнаяНакладная.СоздатьДокумент();
				НовыйДокумент.Подразделение = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
				НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийРасходнаяНакладная.ПродажаПокупателю;
				НовыйДокумент.НалогообложениеНДС = УправлениеНебольшойФирмойСервер.НалогообложениеНДС(
					НовыйДокумент.Организация, , Дата);
				Если ЗначениеЗаполнено(Строка.Договор) Тогда
					НовыйДокумент.Договор = Строка.Договор;
				Иначе
					НовыйДокумент.Договор = Строка.Контрагент.ДоговорПоУмолчанию;
				КонецЕсли;
				НовыйДокумент.ВалютаДокумента = НовыйДокумент.Договор.ВалютаРасчетов;
				ДокументВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
					Дата,
					Новый Структура("Валюта", НовыйДокумент.ВалютаДокумента)
				);
				НовыйДокумент.Курс = ?(
					ДокументВалютаКурсКратность.Курс = 0,
					1,
					ДокументВалютаКурсКратность.Курс
				);
				НовыйДокумент.Кратность = ?(
					ДокументВалютаКурсКратность.Кратность = 0,
					1,
					ДокументВалютаКурсКратность.Кратность
				);
				НовыйДокумент.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
			КонецЕсли;
			НовыйДокумент.Дата = Дата;
			НовыйДокумент.Организация = Организация;
			НовыйДокумент.Контрагент = Строка.Контрагент;
			
			СтрокаКомментарий = Нстр("ru='Сформирован автоматически документом Ввод начальных остатков № %Номер% от %Дата%'");
			СтрокаКомментарий = СтрЗаменить(СтрокаКомментарий, "%Номер%", Строка(Номер));
			СтрокаКомментарий = СтрЗаменить(СтрокаКомментарий, "%Дата%", Строка(Дата));
			НовыйДокумент.Комментарий = СтрокаКомментарий;
			
			НовыйДокумент.Записать();
			
			Строка.Документ = НовыйДокумент.Ссылка;
			БылиВнесеныИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Формирование документов для табличной части РасчетыСПоставщиками.
	Для каждого Строка Из РасчетыСПоставщиками Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Документ)
			  И Строка.Контрагент.ВестиРасчетыПоДокументам Тогда
			Если Строка.ПризнакАванса Тогда
				НовыйДокумент = Документы.РасходИзКассы.СоздатьДокумент();
				НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.Поставщику;
				НовыйДокумент.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
				НовыйДокумент.Касса = Организация.КассаПоУмолчанию;
				НовыйДокумент.ВалютаДенежныхСредств = Строка.Договор.ВалютаРасчетов;
				НовыйДокумент.СуммаДокумента = Строка.СуммаВал;
				НовыйДокумент.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
				НоваяСтрока = НовыйДокумент.РасшифровкаПлатежа.Добавить();
				НоваяСтрока.Договор = Строка.Договор;
				ДоговорВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
					Дата,
					Новый Структура("Валюта", НоваяСтрока.Договор.ВалютаРасчетов)
				);
				ДокументВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
					Дата,
					Новый Структура("Валюта", НовыйДокумент.ВалютаДенежныхСредств)
				);
				НоваяСтрока.Курс = ?(
					ДоговорВалютаКурсКратность.Курс = 0,
					1,
					ДоговорВалютаКурсКратность.Курс
				);
				НоваяСтрока.Кратность = ?(
					ДоговорВалютаКурсКратность.Кратность = 0,
					1,
					ДоговорВалютаКурсКратность.Кратность
				);
				НоваяСтрока.СуммаПлатежа = НовыйДокумент.СуммаДокумента;
				НоваяСтрока.ПризнакАванса = Истина;
				НоваяСтрока.СуммаРасчетов = УправлениеНебольшойФирмойСервер.ПересчитатьИзВалютыВВалюту(
					НоваяСтрока.СуммаПлатежа,
					ДокументВалютаКурсКратность.Курс,
					НоваяСтрока.Курс,
					ДокументВалютаКурсКратность.Кратность,
					НоваяСтрока.Кратность
				);
				НоваяСтрока.СтавкаНДС = ПолучитьСтавкуНДСПоУмолчанию(НовыйДокумент.НалогообложениеНДС);
				НоваяСтрока.СуммаНДС = НоваяСтрока.СуммаПлатежа - (НоваяСтрока.СуммаПлатежа) / ((НоваяСтрока.СтавкаНДС.Ставка + 100) / 100);
			Иначе
				НовыйДокумент = Документы.ПриходнаяНакладная.СоздатьДокумент();
				НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийПриходнаяНакладная.ПоступлениеОтПоставщика;
				НовыйДокумент.НалогообложениеНДС = Перечисления.ТипыНалогообложенияНДС.ОблагаетсяНДС;
				Если ЗначениеЗаполнено(Строка.Договор) Тогда
					НовыйДокумент.Договор = Строка.Договор;
				Иначе
					НовыйДокумент.Договор = Строка.Контрагент.ДоговорПоУмолчанию;
				КонецЕсли;
				НовыйДокумент.ВалютаДокумента = НовыйДокумент.Договор.ВалютаРасчетов;
				ДокументВалютаКурсКратность = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(
					Дата,
					Новый Структура("Валюта", НовыйДокумент.ВалютаДокумента)
				);
				НовыйДокумент.Курс = ?(
					ДокументВалютаКурсКратность.Курс = 0,
					1,
					ДокументВалютаКурсКратность.Курс
				);
				НовыйДокумент.Кратность = ?(
					ДокументВалютаКурсКратность.Кратность = 0,
					1,
					ДокументВалютаКурсКратность.Кратность
				);
				НовыйДокумент.СтруктурнаяЕдиница = Справочники.СтруктурныеЕдиницы.ОсновнойСклад;
			КонецЕсли;
			НовыйДокумент.Дата = Дата;
			НовыйДокумент.Организация = Организация;
			НовыйДокумент.Контрагент = Строка.Контрагент;
			
			СтрокаКомментарий = Нстр("ru='Сформирован автоматически документом Ввод начальных остатков № %Номер% от %Дата%'");
			СтрокаКомментарий = СтрЗаменить(СтрокаКомментарий, "%Номер%", Строка(Номер));
			СтрокаКомментарий = СтрЗаменить(СтрокаКомментарий, "%Дата%", Строка(Дата));
			НовыйДокумент.Комментарий = СтрокаКомментарий;
			
			НовыйДокумент.Записать();
			
			Строка.Документ = НовыйДокумент.Ссылка;
			БылиВнесеныИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
	
	// Формирование документов для табличной части РасчетыСПодотчетниками.
	Для каждого Строка Из РасчетыСПодотчетниками Цикл
		Если НЕ ЗначениеЗаполнено(Строка.Документ) Тогда
			Если Строка.Перерасход Тогда
				НовыйДокумент = Документы.АвансовыйОтчет.СоздатьДокумент();
				НовыйДокумент.Сотрудник = Строка.Сотрудник;
				НовыйДокумент.ВалютаДокумента = Строка.Валюта;
			Иначе
				НовыйДокумент = Документы.РасходИзКассы.СоздатьДокумент();
				НовыйДокумент.ВидОперации = Перечисления.ВидыОперацийРасходИзКассы.Подотчетнику;
				НовыйДокумент.Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
				НовыйДокумент.Касса = Организация.КассаПоУмолчанию;
				НовыйДокумент.Подотчетник = Строка.Сотрудник;
				НовыйДокумент.ВалютаДенежныхСредств = Строка.Валюта;
				НовыйДокумент.СуммаДокумента = Строка.СуммаВал;
			КонецЕсли;
			НовыйДокумент.Дата = Дата;
			НовыйДокумент.Организация = Организация;
			
			СтрокаКомментарий = Нстр("ru='Сформирован автоматически документом Ввод начальных остатков № %Номер% от %Дата%'");
			СтрокаКомментарий = СтрЗаменить(СтрокаКомментарий, "%Номер%", Строка(Номер));
			СтрокаКомментарий = СтрЗаменить(СтрокаКомментарий, "%Дата%", Строка(Дата));
			НовыйДокумент.Комментарий = СтрокаКомментарий;
			
			НовыйДокумент.Записать();
			
			Строка.Документ = НовыйДокумент.Ссылка;
			БылиВнесеныИзменения = Истина;
		КонецЕсли;
	КонецЦикла;
		
	Если БылиВнесеныИзменения Тогда
		Записать();
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если Автоформирование Тогда
		УправлениеНебольшойФирмойСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "РасчетыСПодотчетниками.Документ");
	КонецЕсли;
	
	Для каждого СтрокаТЧ Из ПрочиеРазделы Цикл
		Если СтрокаТЧ.Счет.Валютный
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Валюта) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Валюта"" для валютного счета в строке %НомерСтроки% списка ""Прочие разделы"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"ПрочиеРазделы",
				СтрокаТЧ.НомерСтроки,
				"Валюта",
				Отказ
			);
		КонецЕсли;
		Если СтрокаТЧ.Счет.Валютный
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.СуммаВал) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Сумма (вал.)"" для валютного счета в строке %НомерСтроки% списка ""Прочие разделы"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"ПрочиеРазделы",
				СтрокаТЧ.НомерСтроки,
				"СуммаВал",
				Отказ
			);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из РасчетыСПокупателями Цикл
		Если СтрокаТЧ.Контрагент.ВестиРасчетыПоДокументам
		И НЕ Автоформирование
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Документ) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Документ расчетов"" в строке %НомерСтроки% списка ""Расчеты с покупателями"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Оплаты",
				СтрокаТЧ.НомерСтроки,
				"Документ",
				Отказ
			);
		КонецЕсли;
	КонецЦикла;
	
	Для каждого СтрокаТЧ Из РасчетыСПоставщиками Цикл
		Если СтрокаТЧ.Контрагент.ВестиРасчетыПоДокументам
		И НЕ Автоформирование
		И НЕ ЗначениеЗаполнено(СтрокаТЧ.Документ) Тогда
			ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Документ расчетов"" в строке %НомерСтроки% списка ""Расчеты с покупателями"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Оплаты",
				СтрокаТЧ.НомерСтроки,
				"Документ",
				Отказ
			);
		КонецЕсли;
	КонецЦикла;
	
	// Серийные номера
	СкладыЗапасов = Запасы.Выгрузить(,"СтруктурнаяЕдиница");
	СкладыЗапасов.Свернуть("СтруктурнаяЕдиница","");
	Для каждого СкладЗапасов Из СкладыЗапасов Цикл
		ОтборСклада = Новый Структура("СтруктурнаяЕдиница", СкладЗапасов.СтруктурнаяЕдиница);
		СтрокиПоСкладу = Запасы.НайтиСтроки(ОтборСклада);
		Для каждого строкаЗапасов Из СтрокиПоСкладу Цикл
			РаботаССерийнымиНомерами.ПроверкаЗаполненияСерийныхНомеров(Отказ, СтрокиПоСкладу, СерийныеНомера, СкладЗапасов.СтруктурнаяЕдиница, ЭтотОбъект);
		КонецЦикла;
	КонецЦикла; 
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаЗаполнения.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("ДокументСсылка.ИнвентаризацияЗапасов")] = "ЗаполнитьПоИнвентаризацииЗапасов";
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ВводНачальныхОстатков.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗапасыНаСкладах") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьЗапасыНаСкладах(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗапасы") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьЗапасы(ДополнительныеСвойства, Движения, Отказ);
		
		// Серийные номера
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаСерийныеНомераОстатки") Тогда
			УправлениеНебольшойФирмойСервер.ОтразитьСерийныеНомераОстатки(ДополнительныеСвойства, Движения, Отказ);	
		КонецЕсли;
		Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаСерийныеНомераГарантии") Тогда
			УправлениеНебольшойФирмойСервер.ОтразитьСерийныеНомераГарантии(ДополнительныеСвойства, Движения, Отказ);	
		КонецЕсли;
		// Серийные номера
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗапасыПереданные") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьЗапасыПереданные(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗапасыПринятые") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьЗапасыПринятые(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаЗапасыВРазрезеГТД") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьЗапасыВРазрезеГТД(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаДенежныеСредства") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьДенежныеСредства(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПокупателями") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьРасчетыСПокупателями(ДополнительныеСвойства, Движения, Отказ);
		УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходыКассовыйМетод(ДополнительныеСвойства, Движения, Отказ);
		УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходыНераспределенные(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПоставщиками") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьРасчетыСПоставщиками(ДополнительныеСвойства, Движения, Отказ);
		УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходыКассовыйМетод(ДополнительныеСвойства, Движения, Отказ);
		УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходыНераспределенные(ДополнительныеСвойства, Движения, Отказ);
		УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходыОтложенные(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыПоНалогам") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьРасчетыПоНалогам(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПерсоналом") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьРасчетыСПерсоналом(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыСПодотчетниками") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьРасчетыСПодотчетниками(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаСостоянияВнеоборотныхАктивов") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьСостоянияВнеоборотныхАктивов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаПараметрыВнеоборотныхАктивов") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьПараметрыВнеоборотныхАктивов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаВнеоборотныеАктивы") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьВнеоборотныеАктивы(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаВыработкаВнеоборотныхАктивов") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьВыработкаВнеоборотныхАктивов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаОплатаСчетовИЗаказов") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьОплатаСчетовИЗаказов(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
 
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаУправленческий") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	
	// Эквайринг
	Если ДополнительныеСвойства.ТаблицыДляДвижений.Свойство("ТаблицаРасчетыПоЭквайрингу") Тогда
		УправлениеНебольшойФирмойСервер.ОтразитьРасчетыПоЭквайрингу(ДополнительныеСвойства, Движения, Отказ);
	КонецЕсли;
	// Конец Эквайринг
		
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.ВводНачальныхОстатков.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	// Контроль
	Документы.ВводНачальныхОстатков.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры

#КонецОбласти

# Область ПроцедурыЗаполненияДокумента

// Обработчик заполнения на основании документа на основании ИнвентаризацияЗапасов.
//
// Параметры:
//	ДокументСсылкаИнвентаризацияЗапасов - ДокументСсылка.ИнвентаризацияЗапасов.
//	
Процедура ЗаполнитьПоИнвентаризацииЗапасов(ДокументСсылкаИнвентаризацияЗапасов) Экспорт
	
	// Заполнение шапки документа.
	ДокументОснование = ДокументСсылкаИнвентаризацияЗапасов.Ссылка;
	Организация = ДокументСсылкаИнвентаризацияЗапасов.Организация;
	РазделУчета = "Запасы";
	
	// Заполнение табличной части документа.
	Для каждого СтрокаТабличнойЧасти Из ДокументСсылкаИнвентаризацияЗапасов.Запасы Цикл
		
		Если СтрокаТабличнойЧасти.Количество > 0 Тогда
			НоваяСтрока = Запасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТабличнойЧасти);
			НоваяСтрока.СтруктурнаяЕдиница = ДокументСсылкаИнвентаризацияЗапасов.СтруктурнаяЕдиница;
			НоваяСтрока.Ячейка = ДокументСсылкаИнвентаризацияЗапасов.Ячейка;
		КонецЕсли;
		
	КонецЦикла;
		
КонецПроцедуры // ЗаполнитьПоИнвентаризацииЗапасов()

#КонецОбласти

#КонецЕсли
