&НаКлиенте
Перем мТекущееСотрудник;

//////////////////////////////////////////////////////////////////////////////////
//// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ (НА СЕРВЕРЕ БЕЗ КОНТЕКСТА)

&НаСервереБезКонтекста
Функция КатегорииЗастрахованныхЛицОрганизации(Организация, ОтчетныйПериод)
	Возврат УчетСтраховыхВзносов.КатегорииЗастрахованныхЛицОрганизации(Организация, ?(ОтчетныйПериод = 0, '20110101', Дата(ОтчетныйПериод, 1, 1)));	
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборСведенийОЗаработке(ЭлементСведенияОЗаработке, Сотрудник)
	СтруктураОтбора = Новый ФиксированнаяСтруктура("Сотрудник", Сотрудник);
	ЭлементСведенияОЗаработке.ОтборСтрок = СтруктураОтбора;		
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция НомерМесяцаПоСтроковомуПредставлению(МесяцСтрока)
	Для НомерМесяца = 1 По 12 Цикл
		Если МесяцСтрока = Формат(Дата(2011, НомерМесяца, 1), "ДФ=ММММ") Тогда
			Возврат НомерМесяца;
		КонецЕсли;	
	КонецЦикла;		
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция МесяцСтрокой(НомерМесяца)	
	Возврат Формат(Дата(2011, НомерМесяца, 1), "ДФ=ММММ");
КонецФункции	

&НаСервере
Процедура ЗаполнитьСписокМесяцев()	
	Для Каждого СтрокаДоходов Из Объект.СведенияОЗаработке Цикл
		СтрокаДоходов.МесяцСтрокой = МесяцСтрокой(СтрокаДоходов.Месяц);
	КонецЦикла;
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////
//// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ НА СЕРВЕРЕ


&НаСервере
Процедура ОбработкаПодбораНаСервере(МассивСотрудников)
	
	Для Каждого Сотрудник Из МассивСотрудников Цикл
		СтруктураПоиска = Новый Структура("Сотрудник", Сотрудник);
		Если Объект.Сотрудники.НайтиСтроки(СтруктураПоиска).Количество() > 0 Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(МассивСотрудников, Сотрудник);
		КонецЕсли;
	КонецЦикла;
	
	ДанныеСотрудника = Документы.ПачкаДокументовСЗВ_6_3.ПолучитьДанныеФизическихЛиц(Объект.Организация, Объект.ОтчетныйПериод, 
																					Объект.КатегорияЗастрахованныхЛиц, 
																					Объект.ТипДоговора, МассивСотрудников);
																					
	Для Каждого ДанныеФизЛица Из ДанныеСотрудника.СведенияОФизЛицах Цикл 																				
		
		НоваяСтрокаСотрудники = Объект.Сотрудники.Добавить();
		
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСотрудники, ДанныеФизЛица);
		
	КонецЦикла;	
	
	Если ДанныеСотрудника.СведенияОФизЛицах.Количество() > 0 Тогда
		Элементы.Сотрудники.ТекущаяСтрока = НоваяСтрокаСотрудники.ПолучитьИдентификатор();
	КонецЕсли;	
	
	Для Каждого СтрокаСведенийОзаработке Из ДанныеСотрудника.СведенияОЗаработке Цикл
		НоваяСтрокаСведений = Объект.СведенияОЗаработке.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрокаСведений, СтрокаСведенийОзаработке);
	КонецЦикла;
	
	ЗаполнитьСписокМесяцев();
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДанныеСотрудникаНаСервере(Сотрудник, ТекущаяСтрока)
	СтрокаТаблицыСотрудники = Объект.Сотрудники.НайтиПоИдентификатору(ТекущаяСтрока);
	
	СтруктураПоиска = Новый Структура("Сотрудник", СтрокаТаблицыСотрудники.Сотрудник);
	Если Объект.Сотрудники.НайтиСтроки(СтруктураПоиска).Количество() = 1 Тогда	
		ДанныеСотрудника = Документы.ПачкаДокументовСЗВ_6_3.ПолучитьДанныеФизическихЛиц(Объект.Организация, Объект.ОтчетныйПериод, 
																						Объект.КатегорияЗастрахованныхЛиц, 
																						Объект.ТипДоговора, Сотрудник);
																						
		Если ДанныеСотрудника.СведенияОФизЛицах.Количество() > 0 Тогда 																				
			ЗаполнитьЗначенияСвойств(СтрокаТаблицыСотрудники, ДанныеСотрудника.СведенияОФизЛицах[0]);
			
			Для Каждого СтрокаСведенийОзаработке Из ДанныеСотрудника.СведенияОЗаработке Цикл
				НоваяСтрокаСведений = Объект.СведенияОЗаработке.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрокаСведений, СтрокаСведенийОзаработке);
			КонецЦикла;
		КонецЕсли;	
		УстановитьОтборСведенийОЗаработке(Элементы.СведенияОЗаработке, Сотрудник);
	КонецЕсли;	
	
	ЗаполнитьСписокМесяцев();
КонецПроцедуры	

//////////////////////////////////////////////////////////////////////////////////
//// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ (НА КЛИЕНТЕ)

&НаКлиенте
Процедура ОтобразитьДанныеСотрудника()
	ДанныеТекущейСтроки = Элементы.Сотрудники.ТекущиеДанные;
	
	Если ДанныеТекущейСтроки <> Неопределено Тогда 
		ДанныеСотрудникаТекст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(Нстр("ru = 'Данные сотрудника: %1'"), Строка(ДанныеТекущейСтроки.Сотрудник));
	Иначе
		ДанныеСотрудникаТекст = Нстр("ru = 'Данные сотрудника:'");
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура УдалитьСтрокиТаблицыСведенияОЗаработке(Сотрудник)
	УдаляемыеСтрокиТаблицы = Объект.СведенияОЗаработке.НайтиСтроки(Новый Структура("Сотрудник", Сотрудник));
	
	Для Каждого УдаляемаяСтрока Из УдаляемыеСтрокиТаблицы Цикл
		Объект.СведенияОЗаработке.Удалить(Объект.СведенияОЗаработке.Индекс(УдаляемаяСтрока));
	КонецЦикла;	
КонецПроцедуры	
	
//////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.ОтчетныйПериод) Тогда 
		Объект.ОтчетныйПериод = 2011;
	КонецЕсли;	
	
	
	СписокКатегорийЗастрахованныхЛиц = КатегорииЗастрахованныхЛицОрганизации(Объект.Организация, Объект.ОтчетныйПериод);
	Если Не ЗначениеЗаполнено(Объект.КатегорияЗастрахованныхЛиц) Тогда 
		Объект.КатегорияЗастрахованныхЛиц = СписокКатегорийЗастрахованныхЛиц[0].Значение;
	КонецЕсли;	
	
	Для Сч = 1 По 12 Цикл 
		Элементы.СведенияОЗаработкеМесяц.СписокВыбора.Добавить(Формат(Дата(2011, Сч, 1), "ДФ=ММММ"));
	КонецЦикла;
	
	ОтчетныйПериодСтрока = Формат(Объект.ОтчетныйПериод, "ЧГ=") + НСтр("ru=' г.'");
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ЗаполнитьСписокМесяцев();
КонецПроцедуры

&НаКлиенте
 Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	ОчиститьСообщения();
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	ЗаполнитьСписокМесяцев();
КонецПроцедуры

/////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ КОМАНД


&НаКлиенте
Процедура ПосмотретьПечатнуюФормуСЗВ63(Команда)
	
	Если Модифицированность И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Ссылка);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ПачкаДокументовСЗВ_6_3", "ФормаСЗВ_6_3", МассивОбъектов, ЭтаФорма, Новый Структура());
	
КонецПроцедуры

&НаКлиенте
Процедура ПосмотретьПечатнуюФормуАДВ64(Команда)
	
	Если Модифицированность И Не Записать() Тогда
		Возврат;
	КонецЕсли;
	
	МассивОбъектов = Новый Массив;
	МассивОбъектов.Добавить(Объект.Ссылка);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати("Документ.ПачкаДокументовСЗВ_6_3", "ФормаАДВ_6_4", МассивОбъектов, ЭтаФорма, Новый Структура());
	
КонецПроцедуры
//////////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ "СОТРУДНИКИ"

&НаКлиенте
Процедура СотрудникПриАктивизацииСтроки(Элемент)
	ТекущиеДанныеСтроки =  Элементы.Сотрудники.ТекущиеДанные;
	
	Если ТекущиеДанныеСтроки <> Неопределено Тогда
		мТекущееСотрудник = ТекущиеДанныеСтроки.Сотрудник;
		УстановитьОтборСведенийОЗаработке(Элементы.СведенияОЗаработке, ТекущиеДанныеСтроки.Сотрудник);
		
	КонецЕсли;	
	ОтобразитьДанныеСотрудника();	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Копирование Тогда
		Отказ = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиСотрудникПриИзменении(Элемент)
	ТекущиеДанныеСтроки = Элементы.Сотрудники.ТекущиеДанные;	
	Если ТекущиеДанныеСтроки <> Неопределено Тогда
		Если мТекущееСотрудник <> ТекущиеДанныеСтроки.Сотрудник Тогда
			УдалитьСтрокиТаблицыСведенияОЗаработке(мТекущееСотрудник);
			мТекущееСотрудник = ТекущиеДанныеСтроки.Сотрудник;
			ЗаполнитьДанныеСотрудникаНаСервере(ТекущиеДанныеСтроки.Сотрудник, Элементы.Сотрудники.ТекущаяСтрока);
		КонецЕсли;	
	КонецЕсли;	
	ОтобразитьДанныеСотрудника();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	ОбработкаПодбораНаСервере(ВыбранноеЗначение);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередУдалением(Элемент, Отказ)
	Для Каждого Идентификатор Из Элемент.ВыделенныеСтроки Цикл
		СтрокаСотрудник = Объект.Сотрудники.НайтиПоИдентификатору(Идентификатор);
		Если СтрокаСотрудник <> Неопределено Тогда 
			УдалитьСтрокиТаблицыСведенияОЗаработке(СтрокаСотрудник.Сотрудник);
		КонецЕсли;
	КонецЦикла;	
КонецПроцедуры

//////////////////////////////////////////////////////////////////////////////////////
//// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ "СВЕДЕНИЯ О ЗАРАБОТКЕ"

&НаКлиенте
Процедура СведенияОЗаработкеПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	Если Элементы.Сотрудники.ТекущиеДанные = Неопределено Тогда
		Отказ = Истина;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОЗаработкеПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработке.ТекущиеДанные;
	Если Не ЗначениеЗаполнено(ДанныеТекущейСтроки.Сотрудник) Тогда
		ДанныеТекущейСтроки.Сотрудник = Элементы.Сотрудники.ТекущиеДанные.Сотрудник;
	КонецЕсли;		
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	ТекущиеДанные = Элементы.Сотрудники.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И Не ОтменаРедактирования Тогда
		СтруктураПоиска = Новый Структура("Сотрудник", ТекущиеДанные.Сотрудник);
		Если Объект.Сотрудники.НайтиСтроки(СтруктураПоиска).Количество() > 1 Тогда
			ПоказатьПредупреждение(,НСтр("ru = 'В пачке сотрудники не могут повторяться!'"));
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура СведенияОЗаработкеМесяцПриИзменении(Элемент)
	ДанныеТекущейСтроки = Элементы.СведенияОЗаработке.ТекущиеДанные;
	ДанныеТекущейСтроки.Месяц = НомерМесяцаПоСтроковомуПредставлению(ДанныеТекущейСтроки.МесяцСтрокой);
КонецПроцедуры

&НаКлиенте
Процедура СотрудникиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Не ТолькоПросмотр И Поле.Имя = "СотрудникиСотрудник" Тогда
		ТекущиеДанныеСтроки = Элементы.Сотрудники.ТекущиеДанные;
		Если ТекущиеДанныеСтроки <> Неопределено Тогда
			ПоказатьЗначение(,ТекущиеДанныеСтроки.Сотрудник);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры




