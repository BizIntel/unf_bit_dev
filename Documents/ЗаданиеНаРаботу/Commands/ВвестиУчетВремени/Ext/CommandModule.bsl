// Возвращает массив строк понедельно.
//
&НаСервере
Функция ПолучитьМассивСтрок(ДокументСсылка)
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗаданиеНаРаботуРаботы.НомерСтроки КАК НомерСтроки,
	|	НАЧАЛОПЕРИОДА(ЗаданиеНаРаботуРаботы.ДатаНачала, НЕДЕЛЯ) КАК НачалоНедели
	|ИЗ
	|	Документ.ЗаданиеНаРаботу.Работы КАК ЗаданиеНаРаботуРаботы
	|ГДЕ
	|	ЗаданиеНаРаботуРаботы.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачалоНедели
	|ИТОГИ ПО
	|	НачалоНедели";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	МассивМассивовСтрок = Новый Массив;
	
	ВыборкаНачалоНедели = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам, "НачалоНедели");
	Пока ВыборкаНачалоНедели.Следующий() Цикл
		МассивСтрок = Новый Массив;
		Выборка = ВыборкаНачалоНедели.Выбрать();
		Пока Выборка.Следующий() Цикл
			МассивСтрок.Добавить(Выборка.НомерСтроки);
		КонецЦикла;
		МассивМассивовСтрок.Добавить(МассивСтрок);
	КонецЦикла;
	
	Возврат МассивМассивовСтрок;
	
КонецФункции // ПолучитьМассивСтрок()

// Процедура обработки команды.
//
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	МассивСтрок = ПолучитьМассивСтрок(ПараметрКоманды);
	
	Для каждого ЭлементМассива Из МассивСтрок Цикл
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Основание", ПараметрКоманды);
		СтруктураПараметров.Вставить("МассивСтрок", ЭлементМассива);
		Параметры = Новый Структура("Основание", СтруктураПараметров);
		ОткрытьФорму("Документ.УчетВремени.ФормаОбъекта", Параметры, ,Параметры);
	КонецЦикла;
	
	Если МассивСтрок.Количество() = 0 Тогда
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("Основание", ПараметрКоманды);
		СтруктураПараметров.Вставить("МассивСтрок", Неопределено);
		Параметры = Новый Структура("Основание", СтруктураПараметров);
		ОткрытьФорму("Документ.УчетВремени.ФормаОбъекта", Параметры, ,Параметры);
	КонецЕсли;
	
КонецПроцедуры // ОбработкаКоманды()
