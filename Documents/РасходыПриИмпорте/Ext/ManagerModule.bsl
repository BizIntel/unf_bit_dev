#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция РаспределитьПропорционально(Знач ИсхСумма, МассивКоэф, Знач Точность = 2) Экспорт
	
	Если МассивКоэф.Количество() = 0 Или ИсхСумма = 0 Или ИсхСумма = Null Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	ИндексМакс = 0;
	МаксЗнач   = 0;
	РаспрСумма = 0;
	СуммаКоэф  = 0;
	
	Для К = 0 По МассивКоэф.Количество() - 1 Цикл
		
		МодульЧисла = ?(МассивКоэф[К] > 0, МассивКоэф[К], - МассивКоэф[К]);
		
		Если МаксЗнач < МодульЧисла Тогда
			
			МаксЗнач = МодульЧисла;
			ИндексМакс = К;
			
		КонецЕсли;
		
		СуммаКоэф = СуммаКоэф + МассивКоэф[К];
		
	КонецЦикла;
	
	Если СуммаКоэф = 0 Тогда
		
		Возврат Неопределено;
		
	КонецЕсли;
	
	МассивСумм = Новый Массив(МассивКоэф.Количество());
	Для К = 0 По МассивКоэф.Количество() - 1 Цикл
		
		МассивСумм[К] = Окр(ИсхСумма * МассивКоэф[К] / СуммаКоэф, Точность, 1);
		РаспрСумма = РаспрСумма + МассивСумм[К];
		
	КонецЦикла;
	
	// Погрешности округления отнесем на коэффициент с максимальным весом
	Если Не РаспрСумма = ИсхСумма Тогда
		
		МассивСумм[ИндексМакс] = МассивСумм[ИндексМакс] + ИсхСумма - РаспрСумма;
		
	КонецЕсли;
	
	Возврат МассивСумм;
	
КонецФункции

Процедура РаспределитьСуммуТаможенногоСбораПоКолонкамТаблицыЗапасов(РаспределяемаяСумма, Таблица, ИмяКолонки, ИмяКолонкиБазы) Экспорт

	Если РаспределяемаяСумма <> 0 Тогда
		
		МассивСтарыхСумм = Таблица.ВыгрузитьКолонку(?(ПустаяСтрока(ИмяКолонкиБазы), ИмяКолонки, ИмяКолонкиБазы));
		МассивНовыхСумм = РаспределитьПропорционально(РаспределяемаяСумма, МассивСтарыхСумм);
		
		Если МассивНовыхСумм <> Неопределено Тогда
			
			Таблица.ЗагрузитьКолонку(МассивНовыхСумм, ИмяКолонки);
			
		КонецЕсли;
		
	Иначе
		
		Таблица.ЗаполнитьЗначения(0, ИмяКолонки);
		
	КонецЕсли;

КонецПроцедуры

Процедура СформироватьТаблицаЗапасы(ДокументСсылкаРасходыПриИмпорте, СтруктураДополнительныеСвойства);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
	|	,ТаблицаЗапасы.Период КАК Период
	|	,ТаблицаЗапасы.Организация КАК Организация
	|	,ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница
	|	,ТаблицаЗапасы.СчетУчета КАК СчетУчета
	|	,ТаблицаЗапасы.Номенклатура КАК Номенклатура
	|	,ТаблицаЗапасы.Характеристика КАК Характеристика
	|	,ТаблицаЗапасы.Партия КАК Партия
	|	,ТаблицаЗапасы.ЗаказПокупателя КАК ЗаказПокупателя
	|	,ИСТИНА КАК ФиксированнаяСтоимость
	|	,&ТаможеннаяПошлина КАК СодержаниеПроводки
	|	,0 КАК Количество
	|	,СУММА(ТаблицаЗапасы.СуммаПошлины) КАК Сумма
	|Поместить СводнаяТаблицаЗапасы
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период
	|	,ТаблицаЗапасы.Организация
	|	,ТаблицаЗапасы.СтруктурнаяЕдиница
	|	,ТаблицаЗапасы.СчетУчета
	|	,ТаблицаЗапасы.Номенклатура
	|	,ТаблицаЗапасы.Характеристика
	|	,ТаблицаЗапасы.Партия
	|	,ТаблицаЗапасы.ЗаказПокупателя
	|
	|Объединить все
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки)
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	,ТаблицаЗапасы.Период
	|	,ТаблицаЗапасы.Организация
	|	,ТаблицаЗапасы.СтруктурнаяЕдиница
	|	,ТаблицаЗапасы.СчетУчета
	|	,ТаблицаЗапасы.Номенклатура
	|	,ТаблицаЗапасы.Характеристика
	|	,ТаблицаЗапасы.Партия
	|	,ТаблицаЗапасы.ЗаказПокупателя
	|	,ИСТИНА
	|	,&ТаможенныйСбор
	|	,0
	|	,СУММА(ТаблицаЗапасы.СуммаСбора)
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период
	|	,ТаблицаЗапасы.Организация
	|	,ТаблицаЗапасы.СтруктурнаяЕдиница
	|	,ТаблицаЗапасы.СчетУчета
	|	,ТаблицаЗапасы.Номенклатура
	|	,ТаблицаЗапасы.Характеристика
	|	,ТаблицаЗапасы.Партия
	|	,ТаблицаЗапасы.ЗаказПокупателя
	|
	|Объединить все
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки)
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	,ТаблицаЗапасы.Период
	|	,ТаблицаЗапасы.Организация
	|	,ТаблицаЗапасы.СтруктурнаяЕдиница
	|	,ТаблицаЗапасы.СчетУчета
	|	,ТаблицаЗапасы.Номенклатура
	|	,ТаблицаЗапасы.Характеристика
	|	,ТаблицаЗапасы.Партия
	|	,ТаблицаЗапасы.ЗаказПокупателя
	|	,ИСТИНА
	|	,&ТаможенныйНДС
	|	,0
	|	,СУММА(ТаблицаЗапасы.СуммаНДС)
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период
	|	,ТаблицаЗапасы.Организация
	|	,ТаблицаЗапасы.СтруктурнаяЕдиница
	|	,ТаблицаЗапасы.СчетУчета
	|	,ТаблицаЗапасы.Номенклатура
	|	,ТаблицаЗапасы.Характеристика
	|	,ТаблицаЗапасы.Партия
	|	,ТаблицаЗапасы.ЗаказПокупателя;
	|	
	|Выбрать
	|	СводнаяТаблицаЗапасы.НомерСтроки
	|	,СводнаяТаблицаЗапасы.ВидДвижения
	|	,СводнаяТаблицаЗапасы.Период
	|	,СводнаяТаблицаЗапасы.Организация
	|	,СводнаяТаблицаЗапасы.СтруктурнаяЕдиница
	|	,СводнаяТаблицаЗапасы.СчетУчета
	|	,СводнаяТаблицаЗапасы.Номенклатура
	|	,СводнаяТаблицаЗапасы.Характеристика
	|	,СводнаяТаблицаЗапасы.Партия
	|	,СводнаяТаблицаЗапасы.ЗаказПокупателя
	|	,СводнаяТаблицаЗапасы.ФиксированнаяСтоимость
	|	,СводнаяТаблицаЗапасы.СодержаниеПроводки
	|	,СводнаяТаблицаЗапасы.Количество
	|	,СводнаяТаблицаЗапасы.Сумма
	|Из СводнаяТаблицаЗапасы КАК СводнаяТаблицаЗапасы
	|Где СводнаяТаблицаЗапасы.Сумма > 0
	|Упорядочить по СводнаяТаблицаЗапасы.НомерСтроки";
	
	Запрос.УстановитьПараметр("ТаможеннаяПошлина", НСтр("ru = 'Таможенная пошлина'"));
	Запрос.УстановитьПараметр("ТаможенныйСбор", НСтр("ru = 'Таможенный сбор'"));
	Запрос.УстановитьПараметр("ТаможенныйНДС", НСтр("ru = 'НДС, уплаченный таможенным органам'"));
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПрочимиКонтрагентами(ДокументСсылкаРасходыПриИмпорте, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаРасходыПриИмпорте);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ТаможенныйСбор", НСтр("ru = 'Таможенный сбор'"));
	Запрос.УстановитьПараметр("ТаможеннаяПошлина", НСтр("ru = 'Таможенная пошлина'"));
	Запрос.УстановитьПараметр("ТаможенныйШтраф", НСтр("ru = 'Таможенный штраф'"));
	Запрос.УстановитьПараметр("ТаможенныйНДС", НСтр("ru = 'НДС, уплаченный таможенным органам'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокументаСборы.Период КАК Дата,
	|	ТаблицаДокументаСборы.Организация КАК Организация,
	|	ТаблицаДокументаСборы.Контрагент КАК Контрагент,
	|	ТаблицаДокументаСборы.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокументаСборы.СчетУчетаРасчетовСПоставщиком КАК СчетУчета,
	|	ТаблицаДокументаСборы.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаСборы.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаСборы.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокументаСборы.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокументаСборы.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокументаСборы.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокументаСборы.ВалютаРасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетов,
	|	СУММА(ТаблицаДокументаСборы.СуммаСбора) КАК Сумма,
	|	СУММА(ТаблицаДокументаСборы.СуммаСбораВал) КАК СуммаВал,
	|	СУММА(ТаблицаДокументаСборы.СуммаСбора) КАК СуммаДляОстатка,
	|	СУММА(ТаблицаДокументаСборы.СуммаСбораВал) КАК СуммаВалДляОстатка,
	|	&ТаможенныйСбор КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПрочимиКонтрагентами
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокументаСборы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументаСборы.Период,
	|	ТаблицаДокументаСборы.Организация,
	|	ТаблицаДокументаСборы.Контрагент,
	|	ТаблицаДокументаСборы.ВестиРасчетыПоДокументам,
	|	ТаблицаДокументаСборы.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаСборы.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокументаСборы.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокументаСборы.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокументаСборы.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокументаСборы.ВалютаРасчетов,
	|	ТаблицаДокументаСборы.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаСборы.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокументаПошлина.Период,
	|	ТаблицаДокументаПошлина.Организация,
	|	ТаблицаДокументаПошлина.Контрагент,
	|	ТаблицаДокументаПошлина.ВестиРасчетыПоДокументам,
	|	ТаблицаДокументаПошлина.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокументаПошлина.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаПошлина.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаПошлина.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокументаПошлина.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокументаПошлина.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокументаПошлина.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокументаПошлина.ВалютаРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг),
	|	СУММА(ТаблицаДокументаПошлина.СуммаПошлины),
	|	СУММА(ТаблицаДокументаПошлина.СуммаПошлиныВал),
	|	СУММА(ТаблицаДокументаПошлина.СуммаПошлины),
	|	СУММА(ТаблицаДокументаПошлина.СуммаПошлиныВал),
	|	&ТаможеннаяПошлина
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокументаПошлина
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокументаПошлина.Период,
	|	ТаблицаДокументаПошлина.Организация,
	|	ТаблицаДокументаПошлина.Контрагент,
	|	ТаблицаДокументаПошлина.ВестиРасчетыПоДокументам,
	|	ТаблицаДокументаПошлина.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаПошлина.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокументаПошлина.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокументаПошлина.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокументаПошлина.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокументаПошлина.ВалютаРасчетов,
	|	ТаблицаДокументаПошлина.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокументаПошлина.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг),
	|	СУММА(ТаблицаДокумента.СуммаШтраф),
	|	СУММА(ТаблицаДокумента.СуммаШтрафВал),
	|	СУММА(ТаблицаДокумента.СуммаШтраф),
	|	СУММА(ТаблицаДокумента.СуммаШтрафВал),
	|	&ТаможенныйШтраф
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.СуммаШтраф > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг),
	|	СУММА(ТаблицаДокумента.СуммаНДС),
	|	СУММА(ТаблицаДокумента.СуммаНДСВал),
	|	СУММА(ТаблицаДокумента.СуммаНДС),
	|	СУММА(ТаблицаДокумента.СуммаНДСВал),
	|	&ТаможенныйНДС
	|ИЗ
	|	ВременнаяТаблицаНДС КАК ТаблицаДокумента
	|ГДЕ
	|	ТаблицаДокумента.СуммаНДС > 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ЗаказПоставщику ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПрочимиКонтрагентами.СчетУчета КАК СчетУчета
	|	,ВременнаяТаблицаРасчетыСПрочимиКонтрагентами.Организация КАК Организация
	|	,ВременнаяТаблицаРасчетыСПрочимиКонтрагентами.Контрагент КАК Контрагент
	|	,ВременнаяТаблицаРасчетыСПрочимиКонтрагентами.Договор КАК Договор
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПрочимиКонтрагентами КАК ВременнаяТаблицаРасчетыСПрочимиКонтрагентами";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПрочимиКонтрагентами");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПрочимиКонтрагентами(Запрос.МенеджерВременныхТаблиц, Ложь, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПрочимиКонтрагентами", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	ТаблицаДокумента.Дата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК СтруктурнаяЕдиница,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельности,
	|	НЕОПРЕДЕЛЕНО КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ КАК СчетУчета,
	|	ТаблицаДокумента.Валюта КАК Аналитика,
	|	&КурсоваяРазница КАК СодержаниеПроводки,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ КАК СуммаДоходов,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаРасходов,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ КАК Сумма
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПрочимиКонтрагентами КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПрочимиКонтрагентами КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru = 'Курсовая разница'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаТаможенныеСборы.НомерСтроки КАК НомерСтроки,
	|	ТаблицаТаможенныеСборы.Период КАК Период,
	|	ТаблицаТаможенныеСборы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаТаможенныеСборы.СчетУчета КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныеСборы.СчетУчета.Валютный
	|			ТОГДА ТаблицаТаможенныеСборы.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныеСборы.СчетУчета.Валютный
	|			ТОГДА ТаблицаТаможенныеСборы.СуммаСбораВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ТаблицаТаможенныеСборы.СчетУчетаРасчетовСПоставщиком КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныеСборы.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаТаможенныеСборы.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныеСборы.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаТаможенныеСборы.СуммаСбораВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаТаможенныеСборы.СуммаСбора КАК Сумма,
	|	&ТаможенныйСбор КАК Содержание
	|ПОМЕСТИТЬ ВременнаяТаблицаУправленческогоРегистра
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаТаможенныеСборы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	1,
	|	ТаблицаТаможеннаяПошлина.НомерСтроки,
	|	ТаблицаТаможеннаяПошлина.Период,
	|	ТаблицаТаможеннаяПошлина.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаТаможеннаяПошлина.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможеннаяПошлина.СчетУчета.Валютный
	|			ТОГДА ТаблицаТаможеннаяПошлина.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможеннаяПошлина.СчетУчета.Валютный
	|			ТОГДА ТаблицаТаможеннаяПошлина.СуммаПошлиныВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаТаможеннаяПошлина.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможеннаяПошлина.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаТаможеннаяПошлина.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможеннаяПошлина.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаТаможеннаяПошлина.СуммаПошлиныВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаТаможеннаяПошлина.СуммаПошлины,
	|	&ТаможеннаяПошлина
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаТаможеннаяПошлина
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаТаможенныйШтраф.НомерСтроки,
	|	ТаблицаТаможенныйШтраф.Период,
	|	ТаблицаТаможенныйШтраф.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаТаможенныйШтраф.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныйШтраф.СчетУчета.Валютный
	|			ТОГДА ТаблицаТаможенныйШтраф.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныйШтраф.СчетУчета.Валютный
	|			ТОГДА ТаблицаТаможенныйШтраф.СуммаШтрафВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаТаможенныйШтраф.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныйШтраф.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаТаможенныйШтраф.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаТаможенныйШтраф.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаТаможенныйШтраф.СуммаШтрафВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаТаможенныйШтраф.СуммаШтраф,
	|	&ТаможенныйШтраф
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаТаможенныйШтраф
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ВременнаяТаблицаНДС.НомерСтроки,
	|	ВременнаяТаблицаНДС.Период,
	|	ВременнаяТаблицаНДС.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВременнаяТаблицаНДС.СчетУчета,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНДС.СчетУчета.Валютный
	|			ТОГДА ВременнаяТаблицаНДС.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНДС.СчетУчета.Валютный
	|			ТОГДА ВременнаяТаблицаНДС.СуммаНДСВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВременнаяТаблицаНДС.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНДС.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ВременнаяТаблицаНДС.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаНДС.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ВременнаяТаблицаНДС.СуммаНДСВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВременнаяТаблицаНДС.СуммаНДС,
	|	&ТаможенныйНДС
	|ИЗ
	|	ВременнаяТаблицаНДС КАК ВременнаяТаблицаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	1,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета КАК СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный КАК СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчета КАК СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный КАК СчетУчетаВалютный,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПрочимиКонтрагентами КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПрочимиКонтрагентами КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаУправленческий
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаУправленческогоРегистра.Порядок КАК Порядок,
	|	ВременнаяТаблицаУправленческогоРегистра.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаУправленческогоРегистра.Период КАК Период,
	|	ВременнаяТаблицаУправленческогоРегистра.Организация КАК Организация,
	|	ВременнаяТаблицаУправленческогоРегистра.СценарийПланирования КАК СценарийПланирования,
	|	ВременнаяТаблицаУправленческогоРегистра.СчетДт КАК СчетДт,
	|	ВременнаяТаблицаУправленческогоРегистра.ВалютаДт КАК ВалютаДт,
	|	ВременнаяТаблицаУправленческогоРегистра.СуммаВалДт КАК СуммаВалДт,
	|	ВременнаяТаблицаУправленческогоРегистра.СчетКт КАК СчетКт,
	|	ВременнаяТаблицаУправленческогоРегистра.ВалютаКт КАК ВалютаКт,
	|	ВременнаяТаблицаУправленческогоРегистра.СуммаВалКт КАК СуммаВалКт,
	|	ВременнаяТаблицаУправленческогоРегистра.Сумма КАК Сумма,
	|	ВременнаяТаблицаУправленческогоРегистра.Содержание КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаУправленческогоРегистра КАК ВременнаяТаблицаУправленческогоРегистра
	|ГДЕ
	|	ВременнаяТаблицаУправленческогоРегистра.Сумма > 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ТаможенныйСбор", НСтр("ru = 'Таможенный сбор'"));
	Запрос.УстановитьПараметр("ТаможеннаяПошлина", НСтр("ru = 'Таможенная пошлина'"));
	Запрос.УстановитьПараметр("ТаможенныйШтраф", НСтр("ru = 'Таможенный штраф'"));
	Запрос.УстановитьПараметр("ТаможенныйНДС", НСтр("ru = 'НДС, уплаченный таможенным органам'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru = 'Курсовая разница'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	
	РезультатЗапроса = Запрос.Выполнить();
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	РасходыПриИмпортеЗапасы.НомерСтроки КАК НомерСтроки,
	|	РасходыПриИмпортеЗапасы.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент КАК Контрагент,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	РасходыПриИмпортеЗапасы.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	РасходыПриИмпортеЗапасы.Ссылка.Договор КАК Договор,
	|	РасходыПриИмпортеЗапасы.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	РасходыПриИмпортеЗапасы.Ссылка.ЗаказПоставщику КАК ЗаказПоставщику,
	|	РасходыПриИмпортеЗапасы.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РасходыПриИмпортеЗапасы.Номенклатура.СчетУчетаЗапасов КАК СчетУчета,
	|	РасходыПриИмпортеЗапасы.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА РасходыПриИмпортеЗапасы.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА РасходыПриИмпортеЗапасы.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	РасходыПриИмпортеЗапасы.ЗаказПокупателя КАК ЗаказПокупателя,
	|	РасходыПриИмпортеЗапасы.Количество КАК Количество,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасы.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасы.СуммаСбора * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасы.СуммаСбора * РасходыПриИмпортеЗапасы.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * РасходыПриИмпортеЗапасы.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаСбора,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасы.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасы.СуммаСбора * КурсыРегВалюты.Курс * РасходыПриИмпортеЗапасы.Ссылка.Кратность / (РасходыПриИмпортеЗапасы.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасы.СуммаСбора
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаСбораВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасы.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасы.СуммаПошлины * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасы.СуммаПошлины * РасходыПриИмпортеЗапасы.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * РасходыПриИмпортеЗапасы.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаПошлины,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасы.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасы.СуммаПошлины * КурсыРегВалюты.Курс * РасходыПриИмпортеЗапасы.Ссылка.Кратность / (РасходыПриИмпортеЗапасы.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасы.СуммаПошлины
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаПошлиныВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасы.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасы.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасы.СуммаНДС * РасходыПриИмпортеЗапасы.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * РасходыПриИмпортеЗапасы.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасы.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасы.СуммаНДС * КурсыРегВалюты.Курс * РасходыПриИмпортеЗапасы.Ссылка.Кратность / (РасходыПриИмпортеЗапасы.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасы.СуммаНДС
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	Документ.РасходыПриИмпорте.Запасы КАК РасходыПриИмпортеЗапасы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|ГДЕ
	|	РасходыПриИмпортеЗапасы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	1 КАК НомерСтроки,
	|	РасходыПриИмпортеРасходы.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	РасходыПриИмпортеРасходы.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	РасходыПриИмпортеРасходы.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	РасходыПриИмпортеРасходы.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	РасходыПриИмпортеРасходы.Контрагент КАК Контрагент,
	|	РасходыПриИмпортеРасходы.Договор КАК Договор,
	|	РасходыПриИмпортеРасходы.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	РасходыПриИмпортеРасходы.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	РасходыПриИмпортеРасходы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РасходыПриИмпортеРасходы.Дата КАК Период,
	|	РасходыПриИмпортеРасходы.Ссылка КАК Документ,
	|	РасходыПриИмпортеРасходы.СчетУчетаЗатрат КАК СчетУчета,
	|	РасходыПриИмпортеРасходы.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&Организация КАК Организация,
	|	РасходыПриИмпортеРасходы.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеРасходы.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеРасходы.ТаможенныйШтраф * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеРасходы.ТаможенныйШтраф * РасходыПриИмпортеРасходы.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * РасходыПриИмпортеРасходы.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаШтраф,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеРасходы.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеРасходы.ТаможенныйШтраф * КурсыРегВалюты.Курс * РасходыПриИмпортеРасходы.Кратность / (РасходыПриИмпортеРасходы.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеРасходы.ТаможенныйШтраф
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаШтрафВал
	|ПОМЕСТИТЬ ВременнаяТаблицаРасходы
	|ИЗ
	|	Документ.РасходыПриИмпорте КАК РасходыПриИмпортеРасходы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|ГДЕ
	|	РасходыПриИмпортеРасходы.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РасходыПриИмпортеЗапасыНДС.НомерСтроки КАК НомерСтроки,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Контрагент КАК Контрагент,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Договор КАК Договор,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.Дата КАК Период,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка КАК Документ,
	|	РасходыПриИмпортеЗапасыНДС.Номенклатура.СчетУчетаЗапасов КАК СчетУчета,
	|	РасходыПриИмпортеЗапасыНДС.Номенклатура.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	&Организация КАК Организация,
	|	РасходыПриИмпортеЗапасыНДС.Ссылка.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасыНДС.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасыНДС.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасыНДС.СуммаНДС * РасходыПриИмпортеЗапасыНДС.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * РасходыПриИмпортеЗапасыНДС.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА РасходыПриИмпортеЗапасыНДС.Ссылка.ВалютаДокумента = КонстантаНациональнаяВалюта.Значение
	|				ТОГДА РасходыПриИмпортеЗапасыНДС.СуммаНДС * КурсыРегВалюты.Курс * РасходыПриИмпортеЗапасыНДС.Ссылка.Кратность / (РасходыПриИмпортеЗапасыНДС.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ РасходыПриИмпортеЗапасыНДС.СуммаНДС
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал
	|ПОМЕСТИТЬ ВременнаяТаблицаНДС
	|ИЗ
	|	Документ.РасходыПриИмпорте.Запасы КАК РасходыПриИмпортеЗапасыНДС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаНациональнаяВалюта.Значение
	|					ИЗ
	|						Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта)) КАК КурсыРегВалюты
	|		ПО (ИСТИНА),
	|	Константа.НациональнаяВалюта КАК КонстантаНациональнаяВалюта
	|ГДЕ
	|	РасходыПриИмпортеЗапасыНДС.Ссылка = &Ссылка И РасходыПриИмпортеЗапасыНДС.СуммаНДС <> 0";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаДополнительныеРасходы);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.Дляпроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	
	Запрос.ВыполнитьПакет();
	
	СформироватьТаблицаЗапасы(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПрочимиКонтрагентами(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаУправленческий(ДокументСсылкаДополнительныеРасходы, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаРасходыПриИмпорте, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ УправлениеНебольшойФирмойСервер.ВыполнитьКонтрольОстатков() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы "ДвиженияРасчетыСПоставщикамиИзменение", "ДвиженияЗаказыПоставщикамИзменение",
	// содержат записи, необходимо выполнить контроль реализации товаров.
	
	Если СтруктураВременныеТаблицы.ДвиженияЗапасыИзменение Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчетаПредставление,
		|	ДвиженияЗапасыИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ЗапасыОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.СуммаОстаток, 0) КАК СуммаОстатокЗапасы
		|ИЗ
		|	ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(&МоментКонтроля, ) КАК ЗапасыОстатки
		|		ПО ДвиженияЗапасыИзменение.Организация = ЗапасыОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.СчетУчета = ЗапасыОстатки.СчетУчета
		|			И ДвиженияЗапасыИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = ЗапасыОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = ЗапасыОстатки.Партия
		|			И ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗапасыОстатки.ЗаказПокупателя
		|			И (ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			Тогда
			
			ДокументОбъектРасходыПриИмпорте = ДокументСсылкаРасходыПриИмпорте.ПолучитьОбъект();
			
		КонецЕсли;
		
		// Отрицательный остаток учета запасов.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасы(ДокументОбъектРасходыПриИмпорте, ВыборкаИзРезультатаЗапроса, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
