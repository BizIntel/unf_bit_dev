///////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		Если ЗначениеЗаполнено(Параметры.Организация) Тогда
			Объект.Организация = Параметры.Организация;
		Иначе
			Объект.Организация = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
			Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
				Объект.Организация =УправлениеНебольшойФирмойСервер.ПолучитьПредопределеннуюОрганизацию();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.СобытиеКалендаря) Тогда
		СобытиеКалендаря = Параметры.СобытиеКалендаря;
	КонецЕсли;
	
	Если Параметры.ОшибкиЗаполнения Тогда
		ПроверкаДанныхКлиентСервер.ВывестиСообщенияОбОшибкахЗаполнения("Объект", Параметры.ПереченьОшибок);
	КонецЕсли;
	
	ЗаполнитьСвязанныеССобытиеРеквизиты();
	НастроитьФорму();
	УстановитьВидимостьРабочихДней(Ложь);
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СОБЫТИЯ ЭЛЕМЕНТОВ ФОРМЫ


&НаКлиенте
Процедура Перезаполнить(Команда)
	спсОтвет = Новый СписокЗначений;
	спсОтвет.Добавить(КодВозвратаДиалога.Да,НСтр("ru='Перезаполнить'"));
	спсОтвет.Добавить(КодВозвратаДиалога.Отмена,НСтр("ru='Отмена'"));
	
	Если Объект.ПоказателиЕНВД.Количество() > 0 Тогда
		оп = Новый ОписаниеОповещения("ОповещениеОчистки", ЭтотОбъект);
		ПоказатьВопрос(оп,НСтр("ru='Табличная часть будет очищена'"),спсОтвет);
		Возврат;
	КонецЕсли;
	
	ПерезаполнитьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОповещениеОчистки(Ответ,Параметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		ПерезаполнитьНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗадатьРабочиеДни(Команда)
	УстановитьВидимостьРабочихДней();
КонецПроцедуры


&НаКлиенте
Процедура ПоказателиЕНВДВидДеятельностиЕНВДПриИзменении(Элемент)
	
	Если Элементы.ПоказателиЕНВД.ТекущиеДанные.ВидДеятельностиЕНВД.Пустая() Тогда
		Возврат;
	КонецЕсли;
	
	РабочиеДни = ПолучитьДанныеРабочихДней(
		Объект.Дата,
		Элементы.ПоказателиЕНВД.ТекущиеДанные.ВидДеятельностиЕНВД);
	
	Элементы.ПоказателиЕНВД.ТекущиеДанные.ВыработкаДней1 = РабочиеДни[0];
	Элементы.ПоказателиЕНВД.ТекущиеДанные.ВыработкаДней2 = РабочиеДни[1];
	Элементы.ПоказателиЕНВД.ТекущиеДанные.ВыработкаДней3 = РабочиеДни[2];
	
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ


&НаСервере
Процедура ЗаполнитьСвязанныеССобытиеРеквизиты()
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЗначениеЗаполнено(СобытиеКалендаря) Тогда
			ДатаДокумента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СобытиеКалендаря, "ДатаДокументаОбработкиСобытия");
		Иначе
			//предыдущий квартал
			ДатаДокумента = НачалоКвартала(ТекущаяДата())-1;
		КонецЕсли;
		
		Док = РегламентированнаяОтчетностьЕНВД.ПолучитьДокументПоказателейЕНВД(Объект.Организация,ДатаДокумента);
	Иначе
		Док = Объект.Ссылка;
	КонецЕсли;
	
	докОбъект = Док.ПолучитьОбъект();
	Если докОбъект.ПоказателиЕНВД.Количество() = 0 Тогда
		докОбъект.ЗаполнитьДанныеДокумента();
		Модифицированность = Истина;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(докОбъект,"Объект");
	
	Заголовок = НСтр("ru='Показатели ЕНВД за '")
		+ СтроковыеФункцииКлиентСервер.ПреобразоватьЧислоВРимскуюНотацию(Цел(Месяц(Объект.Дата)/3), Ложь)
		+ НСтр("ru=' квартал '")
		+ Формат(Объект.Дата,"ДФ='yyyy ""г.""'");
	
КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьНаСервере()
	
	мОбъект = РеквизитФормыВЗначение("Объект");
	мОбъект.ЗаполнитьДанныеДокумента();
	мОбъект.Записать();
	ЗначениеВРеквизитФормы(мОбъект, "Объект");
	Модифицированность = Истина;
	
КонецПроцедуры

&НаСервере
Процедура НастроитьФорму()
	
	Элементы.ПоказателиЕНВДЗаголовок1.Заголовок = 
		Формат(НачалоКвартала(Объект.Дата),"ДФ=MMMM") 
		+ " ("
		+ Объект.ДнейВсего1 
		+ " "
		+ ПодобратьСловоПодЧисло(Объект.ДнейВсего1, НСтр("ru='день'"),НСтр("ru='дня'"),НСтр("ru='дней'"))
		+ ")";
	
	Элементы.ПоказателиЕНВДЗаголовок2.Заголовок = 
		Формат(ДобавитьМесяц(НачалоКвартала(Объект.Дата),1),"ДФ=MMMM") 
		+ " ("
		+ Объект.ДнейВсего2 
		+ " "
		+ ПодобратьСловоПодЧисло(Объект.ДнейВсего2, НСтр("ru='день'"),НСтр("ru='дня'"),НСтр("ru='дней'"))
		+ ")";
	
	Элементы.ПоказателиЕНВДЗаголовок3.Заголовок = 
		Формат(КонецКвартала(Объект.Дата),"ДФ=MMMM") 
		+ " ("
		+ Объект.ДнейВсего3 
		+ " "
		+ ПодобратьСловоПодЧисло(Объект.ДнейВсего3, НСтр("ru='день'"),НСтр("ru='дня'"),НСтр("ru='дней'"))
		+ ")";
	
	
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьРабочихДней(СохранитьДанные=Истина)
	
	Если СохранитьДанные Тогда
		Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка = НЕ Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка;
		ХранилищеОбщихНастроек.Сохранить("ПоказателиЕНВД","НастройкаДней",Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка);
	Иначе
		Если ХранилищеОбщихНастроек.Загрузить("ПоказателиЕНВД","НастройкаДней") = Истина Тогда
			Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка = Истина;
		Иначе
			Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Элементы.ПоказателиЕНВДВыработкаДней1.Видимость = Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка;
	Элементы.ПоказателиЕНВДВыработкаДней2.Видимость = Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка;
	Элементы.ПоказателиЕНВДВыработкаДней3.Видимость = Элементы.ПоказателиЕНВДУменьшитьНаНерабочие.Пометка;
	
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеРабочихДней(Дата, ВидДеятельности)
	
	МассивРезультат = Новый Массив(3);
	
	Реквизиты = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ВидДеятельности, "ДатаНачала, ДатаОкончания, Актуально");
	
	Если НЕ ЗначениеЗаполнено(Реквизиты.ДатаНачала) Тогда
		Реквизиты.ДатаНачала = НачалоКвартала(Дата);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Реквизиты.ДатаОкончания) ИЛИ 
		Реквизиты.Актуально Тогда
		
		Реквизиты.ДатаОкончания = КонецКвартала(Дата);
	КонецЕсли;
	
	НачДата = НачалоДня(Макс(НачалоКвартала(Дата),Реквизиты.ДатаНачала));
	КонДата = НачалоДня(Мин(КонецМесяца(НачалоКвартала(Дата)), Реквизиты.ДатаОкончания));
	Дней = (КонДата-НачДата)/(24*60*60);
	МассивРезультат[0] = ?(Дней>=0,Дней+1,0);
	
	НачДата = НачалоДня(Макс(ДобавитьМесяц(НачалоКвартала(Дата),1),Реквизиты.ДатаНачала));
	КонДата = НачалоДня(Мин(КонецМесяца(ДобавитьМесяц(НачалоКвартала(Дата),1)), Реквизиты.ДатаОкончания));
	Дней = (КонДата-НачДата)/(24*60*60);
	МассивРезультат[1] = ?(Дней>=0,Дней+1,0);
	
	НачДата = НачалоДня(Макс(ДобавитьМесяц(НачалоКвартала(Дата),2),Реквизиты.ДатаНачала));
	КонДата = НачалоДня(Мин(КонецМесяца(ДобавитьМесяц(НачалоКвартала(Дата),2)), Реквизиты.ДатаОкончания));
	Дней = (КонДата-НачДата)/(24*60*60);
	МассивРезультат[2] = ?(Дней>=0,Дней+1,0);
	
	Возврат МассивРезультат;
	
КонецФункции


&НаСервереБезКонтекста
Функция ПодобратьСловоПодЧисло(Число, СловоДляОдин, СловоДляДва, СловоДляПять)
	
	Если Число > 4 И Число < 21 Тогда
		
		Возврат СловоДляПять;
		
	ИначеЕсли (Число % 10) = 1 Тогда
		
		Возврат СловоДляОдин;
		
	ИначеЕсли (Число % 10) <= 5 Тогда
		
		Возврат СловоДляДва;
		
	Иначе
		
		Возврат СловоДляПять;
		
	КонецЕсли;
	
КонецФункции
