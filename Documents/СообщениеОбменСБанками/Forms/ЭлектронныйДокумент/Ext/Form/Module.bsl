
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Не Параметры.СообщениеОбмена.Пустая() Тогда
		ОбъектСправочника = Параметры.СообщениеОбмена.ПолучитьОбъект();
		ЗначениеВРеквизитФормы(ОбъектСправочника, "Объект");
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		Объект.НастройкаОбмена, "ПрограммаБанка, АутентификацияПоСертификату, Представление");
	
	ПрограммаБанка = РеквизитыНастройкиОбмена.ПрограммаБанка;
	АутентификацияПоСертификату = РеквизитыНастройкиОбмена.АутентификацияПоСертификату;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		ЭДОтклонен = ЭДОтклонен(Объект.Статус);
		ОбновитьСтатус();
		Заголовок = Объект.Представление;
		ЗаполнитьТаблицуЭП();
		
		Если НЕ Отказ Тогда
			ВыполнитьПросмотрЭДИзБДСервер(Отказ);
		КонецЕсли;
	КонецЕсли;
	
	МассивОтпечатков = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.МассивОтпечатковСертификатов();
	Если Не ЗначениеЗаполнено(МассивОтпечатков) Тогда
		МассивОтпечатков = Новый Массив;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.МассивОтпечатков) Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьМассив(МассивОтпечатков, Параметры.МассивОтпечатков);
	КонецЕсли;
	
	СсылкаНаМассивОтпечатков = ПоместитьВоВременноеХранилище(МассивОтпечатков, УникальныйИдентификатор);
		
	ТребуетсяПодпись = ОбменСБанкамиСлужебныйВызовСервера.ПодписыватьВидЭД(Объект.НастройкаОбмена, Объект.ВидЭД);
	
	ИзменитьВидимостьДоступность();
	
	Если Параметры.ТолькоЧтение = Истина Тогда
		Элементы.ГруппаОсновныхКоманд.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ Отказ И ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Если ЭтоАдресВременногоХранилища(АдресФайлаВХранилище) Тогда
			#Если ВебКлиент Тогда
				ПутьКФайлуПросмотра = АдресФайлаВХранилище;
			#Иначе
				ПутьКФайлуПросмотра = ПолучитьИмяВременногоФайла(РасширениеФайла);
				ДДФайла = ПолучитьИзВременногоХранилища(АдресФайлаВХранилище);
				ДДФайла.Записать(ПутьКФайлуПросмотра);
			#КонецЕсли
			Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
			НачатьЗапускПриложения(Оповещение, ПутьКФайлуПросмотра);
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	Иначе
		Отказ = Истина;
	КонецЕсли;
	
	Если Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ДополнительныеДанные") Тогда
		СохранитьЭДНаДиск(Неопределено);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОбновитьСостояниеОбменСБанками" Тогда
		ВыполнитьОбработкуОповещенияНаСервере();
		ОбновитьОтображениеДанных();
	ИначеЕсли ИмяСобытия = "ПроведенаПроверкаЭП" Тогда
		Для Каждого СообщениеОбмена Из Параметр Цикл
			Если СообщениеОбмена = Объект.Ссылка Тогда
				ОбновитьОтображениеДанных();
				ЗаполнитьТаблицуЭП();
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	ИДПараметра = "ЭлектронноеВзаимодействие." + УникальныйИдентификатор;
	ПараметрыФормы = ПараметрыПриложения[ИДПараметра];
	Если ПараметрыФормы <> Неопределено Тогда
		ПараметрыПриложения.Удалить(ИДПараметра);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейФормы

&НаКлиенте
Процедура ТекстДокументИБНажатие(Элемент, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, ДокументУчета);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейТаблицыЭП

&НаКлиенте
Процедура ЭПВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если СтрНайти(Поле.Имя, "ЭПКомуВыданСертификат") > 0 Тогда
		Если Элемент.ТекущиеДанные <> Неопределено И НЕ Элемент.ТекущиеДанные.ОтсутствуетВСписке
				И СертификатыВФорматеX508() Тогда
			ПоказатьСертификат(Элемент.ТекущиеДанные.Отпечаток);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Отклонить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОтклонитьЭДПродолжить", ЭтотОбъект);
	ОбменСБанкамиСлужебныйКлиент.ОбработатьОтклонениеЭД(Объект.Ссылка, ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЭДНаДиск(Команда)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		СохранитьБанковскиеДокументыСбербанка(Объект.Ссылка);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = Объект.Ссылка;
	ПрисоединенныйФайл = Неопределено;
	ДанныеФайла = ДанныеФайла(СообщениеОбмена, УникальныйИдентификатор);
	
	Если Не ЗначениеЗаполнено(ДанныеФайла) Тогда
		Возврат;
	КонецЕсли;
	
	ПрисоединенныеФайлыКлиент.СохранитьВместеСЭП(ДанныеФайла.ПрисоединенныйФайл, ДанныеФайла, УникальныйИдентификатор);
	
	Если Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ВыпискаБанка") Тогда
		СсылкаНаХранилище = "";
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьДанныеВыписки(Объект.Ссылка, СсылкаНаХранилище);
		Если ЗначениеЗаполнено(СсылкаНаХранилище) Тогда
			ПолучитьФайл(СсылкаНаХранилище, ДанныеФайла.Наименование + ".txt");
		КонецЕсли;
	КонецЕсли;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
			И Объект.ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение") Тогда
		ДанныеФайла = ДанныеСлужебногоЭДБанка(СообщениеОбмена, УникальныйИдентификатор);
		Если НЕ ДанныеФайла = Неопределено Тогда
			ПолучитьФайл(ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ДанныеФайла.ИмяФайла);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКЖурналуСобытийЭД(Команда)
	
	ПараметрыФормы = Новый Структура;
	
	Отбор = Новый Структура;
	Отбор.Вставить("Сообщение", Объект.Ссылка);
	
	ПараметрыФормы.Вставить("Отбор", Отбор);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.ЖурналСобытийОбменСБанками.Форма.ФормаСписка", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСертификат(Команда)
	
	Если Элементы.ЭП.ТекущиеДанные <> Неопределено Тогда
		ПоказатьСертификат(Элементы.ЭП.ТекущиеДанные.Отпечаток);
	Иначе
		ОчиститьСообщения();
		ТекстОшибки = НСтр("ru = 'Выберите сертификат в списке установленных подписей.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьПодписи(Команда)
	
	ОчиститьСообщения();
	Оповещение = Новый ОписаниеОповещения("ОбновитьДанныеФормыПослеПроверкиПодписей", ЭтотОбъект);

	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		МассивСообщенийОбмена = Новый Массив;
		МассивСообщенийОбмена.Добавить(Объект.Ссылка);
		ОбменСБанкамиСлужебныйКлиент.ОпределитьСтатусыПодписейСбербанк(
			Оповещение, Объект.НастройкаОбмена, МассивСообщенийОбмена);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		МассивПроверки = Новый Массив;
		МассивПроверки.Добавить(Объект.Ссылка);
		ПараметрыПроверки = Новый Структура;
		ПараметрыПроверки.Вставить("НастройкаОбмена", Объект.НастройкаОбмена);
		ПараметрыПроверки.Вставить("МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку", МассивПроверки);
		ПараметрыПроверки.Вставить("ТекущийИндексПроверкиПодписейЧерезДополнительнуюОбработку", 0);
		ПараметрыПроверки.Вставить("ОповеститьОПроверкеЭП");
		ОбменСБанкамиСлужебныйКлиент.НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(
			Неопределено, ПараметрыПроверки);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписиСообщенияОбменаЧерезВК(
			Оповещение, Объект.НастройкаОбмена, Объект.Ссылка);
	Иначе
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписи(Оповещение, Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УтвердитьЭД(Команда)
	
	ОчиститьСообщения();
	ДокументУчета = ОбменСБанкамиСлужебныйВызовСервера.ДокументУчета(Объект.Ссылка);
	ОбменСБанкамиСлужебныйКлиент.УтвердитьЭД(ДокументУчета, Объект.Ссылка);
	ОбновитьОтображениеДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЭД(Команда)
	
	ОчиститьСообщения();
	ДокументУчета = ОбменСБанкамиСлужебныйВызовСервера.ДокументУчета(Объект.Ссылка);
	ОбменСБанкамиКлиент.СформироватьПодписатьОтправитьЭД(ДокументУчета, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьПовторно(Команда)
	
	ОчиститьСообщения();
	ДокументУчета = ОбменСБанкамиСлужебныйВызовСервера.ДокументУчета(Объект.Ссылка);
	ОбменСБанкамиКлиент.ОтправитьПовторноЭД(ДокументУчета, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодписатьЭД(Команда)
	
	ОчиститьСообщения();
	ДокументУчета = ОбменСБанкамиСлужебныйВызовСервера.ДокументУчета(Объект.Ссылка);
	ОбменСБанкамиКлиент.СформироватьПодписатьОтправитьЭД(ДокументУчета, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатеж(Команда)
	
	РеквизитыНастройкиОбмена = Новый Структура("ПрограммаБанка, ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(Объект.НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПодтвердитьПлатежныйДокументПослеУстановкиСоединенияЧерезВК", ЭтотОбъект, РеквизитыНастройкиОбмена);
		ОбменСБанкамиСлужебныйКлиент.УстановитьСоединениеЧерезВК(Оповещение, Объект.НастройкаОбмена);
	Иначе
		ОбработчикПослеПодключения = Новый ОписаниеОповещения("ПодтвердитьПлатежЧерезДополнительнуюОбработку", ЭтотОбъект);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(
			ОбработчикПослеПодключения, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗапроситьСостояниеЭД(Команда)
	
	Перем ДанныеАвторизации;
	
	ОчиститьСообщения();
	
	ВидЭДЗапросОСостоянии = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросОСостоянииЭД");
	
	Если НЕ ЕстьПоддержкаВидаЭД(Объект.НастройкаОбмена, ВидЭДЗапросОСостоянии) Тогда
		ТекстСообщения = НСтр("ru = 'Банк не поддерживает данный функционал'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТребуетсяПодписьЗапроса = ОбменСБанкамиСлужебныйВызовСервера.ПодписыватьВидЭД(
		Объект.НастройкаОбмена, ВидЭДЗапросОСостоянии);
	
	Если ТребуетсяПодписьЗапроса Тогда
		Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
			Оповещение = Новый ОписаниеОповещения("СформироватьЗапросСостоянияЭДПослеПолученияОтпечатков", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина);
		Иначе // Обмен через ВК
			ЗапросСостоянияЭД = Неопределено;
			МассивСертификатов = Неопределено;
			СформироватьЗапросСостоянияЭД(
				Объект.НастройкаОбмена, Объект.Ссылка, Истина, ЗапросСостоянияЭД, Неопределено, МассивСертификатов);
			Если Не ЗначениеЗаполнено(ЗапросСостоянияЭД) Тогда
				Возврат;
			КонецЕсли;
			СоответствиеСертификатов = Новый Соответствие;
			Для Каждого СертификатСсылка Из МассивСертификатов Цикл
				СоответствиеСертификатов.Вставить(СертификатСсылка, Новый Структура("ПарольПолучен", Ложь));
			КонецЦикла;
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ЗапросСостоянияЭД", ЗапросСостоянияЭД);
			
			Если ОбменСБанкамиСлужебныйКлиент.ЕстьСертификатССохраненнымПаролем(СоответствиеСертификатов) Тогда
				Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
					ДанныеАутентификации = Новый Структура;
					ДанныеАутентификации.Вставить("ВыбранныйСертификат", КлючЗначение.Ключ);
					ДанныеАутентификации.Вставить("ПарольПользователя", КлючЗначение.Значение.ПарольПользователя);
					ПодписатьЗапросСостоянияПослеВводаПароляЧерезВК(ДанныеАутентификации, ДополнительныеПараметры);
				КонецЦикла;
			Иначе
				Оповещение = Новый ОписаниеОповещения(
					"ПодписатьЗапросСостоянияПослеВводаПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
				ВидОперации = НСтр("ru = 'Подписание электронного документа'");
				ОбменСБанкамиСлужебныйКлиент.ПолучитьПарольКСертификату(
					Оповещение, СоответствиеСертификатов, ВидОперации, ЗапросСостоянияЭД);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ЗапросСостоянияЭД = Неопределено;
		СформироватьЗапросСостоянияЭД(Объект.НастройкаОбмена, Объект.Ссылка, Ложь, ЗапросСостоянияЭД);
		Если ЗначениеЗаполнено(ЗапросСостоянияЭД) Тогда
			Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
				ОтправитьЗапросОСостоянииЭДВБанк(ЗапросСостоянияЭД);
			Иначе // обмен через ВК
				ДополнительныеПараметры = Новый Структура;
				ДополнительныеПараметры.Вставить("ЗапросСостоянияЭД", ЗапросСостоянияЭД);
				ОтправитьЗапросСостоянияВБанкПослеПодписанияЧерезВК(Истина, ДополнительныеПараметры);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтозватьЭД(Команда)
	
	ОчиститьСообщения();
	
	ВидЭДЗапросНаОтзыв = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросНаОтзывЭД");
	
	Если НЕ ЕстьПоддержкаВидаЭД(Объект.НастройкаОбмена, ВидЭДЗапросНаОтзыв) Тогда
		ТекстСообщения = НСтр("ru = 'Банк не поддерживает данный функционал'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если МожноОтозватьЭД(Объект.Ссылка) Тогда
		ОО = Новый ОписаниеОповещения("НачатьОтзывЭД", ЭтотОбъект);
		ЗаголовокОкна = НСтр("ru = 'Укажите причину отзыва электронного документа'");
		ПоказатьВводСтроки(ОО, , ЗаголовокОкна, , Истина);
	КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокЭД(Команда)
	
	ОбменСБанкамиКлиент.ОткрытьСписокЭД(ДокументУчета);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИдентификаторСообщенияОбмена(Знач СообщениеОбмена)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеОбмена, "Идентификатор");
	
КонецФункции

&НаКлиенте
Процедура СформироватьЗапросСостоянияЭДПослеПолученияОтпечатков(ОтпечаткиСертификатов, ПараметрыОбработки) Экспорт
	
	МассивОтпечатковСертификатов = Новый Массив;
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	Иначе
		Возврат;
	КонецЕсли;

	ТаблицаСертификатов = Неопределено;
	ЗапросСостоянияЭД = Неопределено;
	МассивСертификатов = Неопределено;
	СформироватьЗапросСостоянияЭД(
		Объект.НастройкаОбмена, Объект.Ссылка, Истина, ЗапросСостоянияЭД, МассивОтпечатковСертификатов, МассивСертификатов);
	
	Если ЗапросСостоянияЭД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Операция = НСтр("ru = 'Подписание электронного документа'");
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция", Операция);
	ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("БезПодтверждения", Истина);
	НаборДанных = Новый Массив;
	Данные = Новый Структура;
	ПараметрыДляПолученияДД = Новый Структура("СообщениеОбмена, ОписаниеДанных", ЗапросСостоянияЭД, ОписаниеДанных);
	СсылкаНаДД = Новый ОписаниеОповещения(
		"ПолучитьДвоичныеДанныеДляСообщенияОбмена", ОбменСБанкамиСлужебныйКлиент, ПараметрыДляПолученияДД);
	Данные.Вставить("Данные", СсылкаНаДД);
	ПредставлениеИПрисоединенныйФайл = ПредставлениеИПрисоединенныйФайл(ЗапросСостоянияЭД);
	
	Данные.Вставить("Объект", ПредставлениеИПрисоединенныйФайл.ПрисоединенныйФайл);
	Представление = ПредставлениеИПрисоединенныйФайл.Представление;
	ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
		"ПриОткрытииЭлектронногоДокумента", ОбменСБанкамиСлужебныйКлиент, ЗапросСостоянияЭД);
	ДанныеДляПредставления = Новый Структура("Представление, Значение", Представление, ОбработчикОткрытияЭД);
	Данные.Вставить("Представление", ДанныеДляПредставления);
	НаборДанных.Добавить(Данные);
	ОписаниеДанных.Вставить("НаборДанных", НаборДанных);
	ПараметрыОбработчика = Новый Структура("ЗапросСостоянияЭД", ЗапросСостоянияЭД);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПроверитьПодписиЗапросаСостоянияЭДПослеПодписания", ЭтотОбъект, ПараметрыОбработчика);
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеОповещения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПредставлениеИПрисоединенныйФайл(СообщениеОбмена)
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить(
		"Представление", ОбменСБанкамиСлужебныйВызовСервера.ПредставлениеЭлектронногоДокумента(СообщениеОбмена));
	СтруктураВозврата.Вставить(
		"ПрисоединенныйФайл", ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеОбмена));
	Возврат СтруктураВозврата;
	
КонецФункции

&НаКлиенте
Процедура ПроверитьПодписиЗапросаСостоянияЭДПослеПодписания(Результат, ПараметрыОбработки) Экспорт
	
	Если Результат.Успех Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОтправитьЗапросСостоянияЭДПослеПодписания", ЭтотОбъект, ПараметрыОбработки);
		ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписи(ОписаниеОповещения, ПараметрыОбработки.ЗапросСостоянияЭД);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСостоянияЭДПослеПодписания(Результат, ПараметрыОбработки) Экспорт

	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	ОтправитьЗапросОСостоянииЭДВБанк(ПараметрыОбработки.ЗапросСостоянияЭД);

КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросОСостоянииЭДВБанк(ЗапросСостоянияЭД)
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("ЗапросСостоянияЭД", ЗапросСостоянияЭД);
	
	Если АутентификацияПоСертификату Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПолученияОтпечатковЗапроситьСостояниеЭД", ЭтотОбъект, ПараметрыОбработки);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина);
	Иначе
		ДанныеАвторизации = Неопределено;
		Если ОбменСБанкамиСлужебныйКлиент.ПолученыДанныеАвторизации(Объект.НастройкаОбмена, ДанныеАвторизации) Тогда
			ОтправитьЗапросСтатусаЭДВБанк(ДанныеАвторизации, ПараметрыОбработки)
		Иначе
			Оповещение = Новый ОписаниеОповещения("ОтправитьЗапросСтатусаЭДВБанк", ЭтотОбъект, ПараметрыОбработки);
			ОбменСБанкамиСлужебныйКлиент.ПолучитьДанныеАутентификации(Объект.НастройкаОбмена, Оповещение);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДанныеФормыПослеПроверкиПодписей(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
	КонецЕсли;
	
	ЗаполнитьТаблицуЭП();
	ОбновитьОтображениеДанных();

КонецПроцедуры

&НаСервере
Функция СертификатыВФорматеX508()
	
	Если НЕ ЭП.Количество() ИЛИ (НЕ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
									И НЕ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК) Тогда
		Возврат Истина;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЭлектронныеПодписи.Сертификат
	|ИЗ
	|	Справочник.СообщениеОбменСБанкамиПрисоединенныеФайлы.ЭлектронныеПодписи КАК ЭлектронныеПодписи
	|ГДЕ
	|	ЭлектронныеПодписи.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(Объект.Ссылка));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Выборка.Следующий();
	
	ДвоичныеДанныеСертификата = Выборка.Сертификат.Получить();
	
	Попытка
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
		Возврат Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаСервереБезКонтекста
Функция ЕстьПоддержкаВидаЭД(НастройкаОбмена, ВидЭД)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанкамиИсходящиеДокументы
	|ГДЕ
	|	НастройкиОбменСБанкамиИсходящиеДокументы.ИсходящийДокумент = &ВидЭД
	|	И НастройкиОбменСБанкамиИсходящиеДокументы.Ссылка = &НастройкаОбмена";
	
	Запрос.УстановитьПараметр("ВидЭД", ВидЭД);
	Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();
	
КонецФункции

&НаСервереБезКонтекста
Функция УникальныйИДВнешний(СообщениеОбмена)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеОбмена, "ВнешнийИдентификатор");
	
КонецФункции

&НаСервереБезКонтекста
Процедура УстановитьСтатусДоставлен(СообщениеОбмена)
	
	СтруктураРеквизитов = Новый Структура("Статус", Перечисления.СтатусыОбменСБанками.Доставлен);
	ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(СообщениеОбмена, СтруктураРеквизитов);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция МассивФайловСбербанка(СообщениеОбмена)
	
	МассивВозврата = Новый Массив;
	
	СтруктураДайджест = Новый Структура;
	ПрисоединенныйФайл = ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеОбмена);
	
	Если Не ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
		Возврат МассивВозврата;
	КонецЕсли;

	ОсноваИмениФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(Строка(ПрисоединенныйФайл));
	
	СтрокаBase64 = ОбменСБанкамиСлужебныйВызовСервера.ПодписанныеДанныеBase64(СообщениеОбмена);
	ИмяФайлаДанных = "doc_" + ОсноваИмениФайла + ".txt";
	ДвоичныеДанные = Base64Значение(СтрокаBase64);
	СсылкаНаДайджест = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	СтруктураДайджест = Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаДайджест, ИмяФайлаДанных);
	МассивВозврата.Добавить(СтруктураДайджест);
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЭлектронныеПодписи.НомерСтроки,
	|	ЭлектронныеПодписи.Подпись,
	|	ЭлектронныеПодписи.Сертификат
	|ИЗ
	|	Справочник.СообщениеОбменСБанкамиПрисоединенныеФайлы.ЭлектронныеПодписи КАК ЭлектронныеПодписи
	|ГДЕ
	|	ЭлектронныеПодписи.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", ПрисоединенныйФайл);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ДвоичныеДанныеЭП = Выборка.Подпись.Получить();
		ИмяФайлаДанных = "sign_" + ОсноваИмениФайла+ "_" + Выборка.НомерСтроки + ".txt";
		СсылкаНаПодпись = ПоместитьВоВременноеХранилище(ДвоичныеДанныеЭП);
		МассивВозврата.Добавить(Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаПодпись, ИмяФайлаДанных));
				
		ДвоичныеДанныеСертификата = Выборка.Сертификат.Получить();
		ИмяФайлаДанных = "cert_" + ОсноваИмениФайла + "_" + Выборка.НомерСтроки + ".cer";
		СсылкаНаСертификат = ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата);
		МассивВозврата.Добавить(Новый Структура("СсылкаНаФайл, ИмяФайла", СсылкаНаСертификат, ИмяФайлаДанных));
	КонецЦикла;

	ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ПрисоединенныйФайл);
	ИмяФайлаДанных = ОсноваИмениФайла + ".xml";
	СтруктураФайла = Новый Структура("СсылкаНаФайл, ИмяФайла", ДанныеФайла.СсылкаНаДвоичныеДанныеФайла, ИмяФайлаДанных);
	МассивВозврата.Добавить(СтруктураФайла);
	
	Возврат МассивВозврата;
	
КонецФункции

&НаСервере
Функция ЭтоСлужебныйДокумент(ВидЭД)
	
	Возврат ВидЭД <> Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение
		И ВидЭД <> Перечисления.ВидыЭДОбменСБанками.ПлатежноеТребование
		И ВидЭД <> Перечисления.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников
		И ВидЭД <> Перечисления.ВидыЭДОбменСБанками.СписокНаОткрытиеСчетовПоЗарплатномуПроекту
		И ВидЭД <> Перечисления.ВидыЭДОбменСБанками.СписокУволенныхСотрудников;
	
КонецФункции

&НаСервере
Процедура ИзменитьВидимостьДоступность()
	
	МассивОтпечатков = ПолучитьИзВременногоХранилища(СсылкаНаМассивОтпечатков);

	ЕстьВозможностьПодписания = (ЗначениеЗаполнено(МассивОтпечатков)
									ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
									ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК
									ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн)
								И ДоступныДляПодписиСертификаты(МассивОтпечатков)
								И (Объект.Статус = Перечисления.СтатусыОбменСБанками.Сформирован
									ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Утвержден
									ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.ЧастичноПодписан);
	
	Если ЗначениеЗаполнено(Объект.СообщениеРодитель) Тогда
		СсылкаНаСообщениеОбмена = Объект.СообщениеРодитель;
	Иначе
		СсылкаНаСообщениеОбмена = Объект.Ссылка;
	КонецЕсли;
	МожноОтклонитьЭтотЭД = ОбменСБанкамиСлужебныйВызовСервера.МожноОтклонитьЭтотЭД(СсылкаНаСообщениеОбмена);
	Элементы.ОтозватьЭД.Видимость = МожноОтозватьЭД(СсылкаНаСообщениеОбмена);
	
	Если НЕ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ВыпискаБанка И НЕ Элементы.Найти("Разобрать") = Неопределено Тогда
		Элементы.Разобрать.Видимость = Ложь;
	КонецЕсли;
	
	ЭДОтклонен = ЭДОтклонен(Объект.Статус);
	
	ДокументПодписывается = ОбменСБанкамиСлужебныйВызовСервера.ПодписыватьВидЭД(Объект.НастройкаОбмена, Объект.ВидЭД);

	Если НЕ ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ЗначениеФункциональнойОпции("ИспользоватьЭлектронныеПодписиЭД")
		ИЛИ НЕ ДокументПодписывается Тогда
		Элементы.КомандаУтвердитьЭД.Заголовок = НСтр("ru = 'Утвердить и отправить'");
	КонецЕсли;
	
	КомандаПодписиОтправки = Элементы.КомандаОтправитьЭД;
	
	ПодготовленКОтправке = Объект.Статус = Перечисления.СтатусыОбменСБанками.ПодготовленКОтправке
							ИЛИ (НЕ ДокументПодписывается И Объект.Статус = Перечисления.СтатусыОбменСБанками.Утвержден);
	
	Если ПодготовленКОтправке Тогда
		КомандаПодписиОтправки.Заголовок = НСтр("ru = 'Отправить'");
	КонецЕсли;
	
	Если Объект.Направление = Перечисления.НаправленияЭД.Входящий Тогда
		
		// КомандаУтвердитьЭД видимость и доступность:
		Элементы.КомандаУтвердитьЭД.Видимость = Объект.ВидЭД <> Перечисления.ВидыЭДОбменСБанками.ВыпискаБанка
			И Объект.ВидЭД <> Перечисления.ВидыЭДОбменСБанками.Квитанция
			И Объект.ВидЭД <> Перечисления.ВидыЭДОбменСБанками.ИзвещениеОСостоянииЭД И НЕ ЕстьВозможностьПодписания;
		
	ИначеЕсли Объект.Направление = Перечисления.НаправленияЭД.Исходящий Тогда
		
		Если Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеТребование
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокНаОткрытиеСчетовПоЗарплатномуПроекту
			ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокУволенныхСотрудников Тогда
			КоличествоНеустановленныхПодписей =  ПолучитьКоличествоНеустановленныхПодписей();
			ЕстьОшибкаПередачи = (Объект.Статус = Перечисления.СтатусыОбменСБанками.ОшибкаПередачи);
			Элементы.КомандаОтправитьЭД.Видимость = (КоличествоНеустановленныхПодписей = 1 И ЕстьВозможностьПодписания
													И НЕ ЭДОтклонен) ИЛИ ПодготовленКОтправке;
			Элементы.КомандаПодписать.Видимость = КоличествоНеустановленныхПодписей > 1 И ЕстьВозможностьПодписания;
			Элементы.ОтправитьПовторно.Видимость = ЕстьОшибкаПередачи;
		Иначе
			КомандаПодписиОтправки.Видимость = ЕстьВозможностьПодписания И ТребуетсяПодпись;
			КомандаПодписиОтправки.Доступность = НЕ ЭДОтклонен И ОтраженВУчете
				И (Объект.Статус = Перечисления.СтатусыОбменСБанками.Сформирован
					ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Утвержден);
		КонецЕсли;
		
		Элементы.КомандаУтвердитьЭД.Видимость = Объект.Статус = Перечисления.СтатусыОбменСБанками.Сформирован
			И НЕ ЭтоСлужебныйДокумент(Объект.ВидЭД)
			И (Не ТребуетсяПодпись ИЛИ (НЕ ЕстьВозможностьПодписания И КоличествоНеустановленныхПодписей > 0));
		
	КонецЕсли;
	
	Элементы.КомандаОтклонить.Доступность = НЕ ЭДОтклонен И МожноОтклонитьЭтотЭД;
	
	Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.СтраницаСтатусов;
	
	Если ЭДОтклонен Тогда
		Элементы.ГруппаСтраницыПодвала.ТекущаяСтраница = Элементы.СтраницаОтклонение;
		Если Объект.Статус = Перечисления.СтатусыОбменСБанками.ОшибкаПередачи Тогда
			Элементы.ГруппаСтраницыПодвала.Видимость = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ РазрешеноОтклонитьЭД() Тогда
		Элементы.КомандаОтклонить.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
	Элементы.ЗапроситьСостояниеЭД.Видимость = (ПрограммаБанка = Перечисления.ПрограммыБанка.АсинхронныйОбмен
												ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК)
											И (Объект.Статус = Перечисления.СтатусыОбменСБанками.Принят
												ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Отправлен
												ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Доставлен
												ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Приостановлен);
	
	Элементы.КомандаПодтвердитьПлатеж.Видимость = Объект.Статус = Перечисления.СтатусыОбменСБанками.НеПодтвержден;
	
	Элементы.ФормаПерейтиКЖурналуСобытий.Видимость = ЭлектронноеВзаимодействиеПереопределяемый.ЕстьПравоОткрытияЖурналаРегистрации();
	
	Элементы.ОткрытьСертификат.Видимость = СертификатыВФорматеX508();
	
КонецПроцедуры

Функция ЭДОтклонен(Статус)
	
	ЭДОтклонен = Статус = Перечисления.СтатусыОбменСБанками.Отклонен
		ИЛИ Статус = Перечисления.СтатусыОбменСБанками.ОтклоненБанком
		ИЛИ Статус = Перечисления.СтатусыОбменСБанками.ОшибкаПередачи
		ИЛИ Статус = Перечисления.СтатусыОбменСБанками.Аннулирован;
	Возврат ЭДОтклонен
	
КонецФункции

&НаСервере
Функция РазрешеноОтклонитьЭД()
	
	Если Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение
		ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеТребование
		ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников
		ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокНаОткрытиеСчетовПоЗарплатномуПроекту
		ИЛИ Объект.ВидЭД = Перечисления.ВидыЭДОбменСБанками.СписокУволенныхСотрудников Тогда
		Если Объект.Статус = Перечисления.СтатусыОбменСБанками.Подписан
			ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Сформирован
			ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.Утвержден
			ИЛИ Объект.Статус = Перечисления.СтатусыОбменСБанками.ЧастичноПодписан Тогда
				Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	Возврат Истина;
	
КонецФункции

&НаСервере
Процедура ОбновитьСтатус()
	
	ДокументУчета = ОбменСБанкамиСлужебныйВызовСервера.ОбъектПривязки(Объект.Ссылка);
	
	ТекстСтатусЭД = " " +  Объект.Статус + ", " + Формат(Объект.ДатаИзмененияСтатуса, "ДЛФ=");
	ТекстСостояния = ОбменСБанкамиКлиентСервер.ПолучитьТекстСостоянияЭД(ДокументУчета);
	ТекстДокументИБ = Строка(ДокументУчета);
	
	ЗапросПоОтражению = Новый Запрос;
	ЗапросПоОтражению.УстановитьПараметр("СсылкаНаОбъект", ДокументУчета);
	
	ЗапросПоОтражению.Текст =
	"ВЫБРАТЬ
	|	СостоянияЭД.СсылкаНаОбъект,
	|	СостоянияЭД.СообщениеОбмена
	|ИЗ
	|	РегистрСведений.СостоянияОбменСБанками КАК СостоянияЭД
	|ГДЕ
	|	СостоянияЭД.СсылкаНаОбъект = &СсылкаНаОбъект";
	
	Выборка = ЗапросПоОтражению.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		ВерсияЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Выборка.СообщениеОбмена, "Дата");
		ОтраженВУчете = (Объект.Ссылка = Выборка.СообщениеОбмена);
	КонецЕсли;
	
	ТаблицаСтатусов = КартаСтатусовЭД(Объект.Ссылка);
	Если ЗначениеЗаполнено(ТаблицаСтатусов) Тогда
		ЗначениеВРеквизитФормы(ТаблицаСтатусов, "СтатусыЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьТаблицуЭП()
	
	НастройкаОбмена = Объект.НастройкаОбмена;

	ТаблицаЭП = РеквизитФормыВЗначение("ЭП");
	ТаблицаЭП.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.КомуВыданСертификат,
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.ДатаПроверкиПодписи,
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.ПодписьВерна,
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.ДатаПодписи,
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.Отпечаток,
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.НомерСтроки
	|ИЗ
	|	Справочник.СообщениеОбменСБанкамиПрисоединенныеФайлы.ЭлектронныеПодписи КАК СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи
	|ГДЕ
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.Ссылка.ВладелецФайла = &ВладелецФайла";
	
	Запрос.УстановитьПараметр("ВладелецФайла", Объект.Ссылка);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НоваяСтрока = ТаблицаЭП.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		ЗаполнитьСтатусПодписи(НоваяСтрока, Выборка);
	КонецЦикла;
	
	ЗначениеВРеквизитФормы(ТаблицаЭП, "ЭП");
	ШаблонЗаголовка = НСтр("ru = 'Подписи %1и статусы'");
	
	Элементы.СтраницаПодписиИСтатусы.Заголовок = СтрШаблон(
		ШаблонЗаголовка, ?(ТаблицаЭП.Количество() = 0, "", "(" + ТаблицаЭП.Количество() + ") "));
		
	Если ТаблицаЭП.НайтиСтроки(Новый Структура("ПодписьВерна", Ложь)).Количество() = 0 Тогда
		Элементы.СтраницаПодписиИСтатусы.Картинка = Новый Картинка;
	Иначе
		Элементы.СтраницаПодписиИСтатусы.Картинка = БиблиотекаКартинок.Предупреждение;
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы,
		"КомандаПроверитьПодписи",
		"Видимость",
		ЭП.Количество());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСтатусПодписи(НоваяСтрока, ТекСтрока)
	
	Если ЗначениеЗаполнено(ТекСтрока.ДатаПроверкиПодписи) Тогда
		НоваяСтрока.Представление = ?(ТекСтрока.ПодписьВерна, НСтр("ru = 'Верна'"), НСтр("ru = 'Неверна'"))
			+" (" + ТекСтрока.ДатаПроверкиПодписи + ")";
	Иначе
		НоваяСтрока.Представление = НСтр("ru = 'Не проверена'");
	КонецЕсли
	
КонецПроцедуры

&НаСервере
Функция ДоступныДляПодписиСертификаты(МассивОтпечатков)
	
	УстановитьПривилегированныйРежим(Истина);

	ЗапросПоСертификатам = Новый Запрос;
	ЗапросПоСертификатам.Текст =
	"ВЫБРАТЬ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК НастройкиОбменСБанкамиСертификатыПодписейОрганизации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписываемыеВидыЭД КАК ПодписываемыеВидыЭД
	|		ПО НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП = ПодписываемыеВидыЭД.СертификатЭП
	|ГДЕ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.Ссылка = &НастройкаОбмена
	|	И ПодписываемыеВидыЭД.ВидЭД = &ВидЭД
	|	И НЕ НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.Отозван
	|	И &ПроверкаПользователя
	|	И НЕ НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.ПометкаУдаления
	|	И ПодписываемыеВидыЭД.Использовать";
	
	ЗапросПоСертификатам.УстановитьПараметр("ВидЭД", Объект.ВидЭД);
	ЗапросПоСертификатам.УстановитьПараметр("НастройкаОбмена", Объект.НастройкаОбмена);

	Если Пользователи.ЭтоПолноправныйПользователь( , , Ложь) Тогда
		ЗапросПоСертификатам.Текст = СтрЗаменить(ЗапросПоСертификатам.Текст, "&ПроверкаПользователя", "ИСТИНА");
	Иначе
		ЗапросПоСертификатам.УстановитьПараметр("ТекущийПользователь", Пользователи.АвторизованныйПользователь());
		ЗапросПоСертификатам.УстановитьПараметр("ПользовательНеУказан", Пользователи.СсылкаНеуказанногоПользователя());
		ЗапросПоСертификатам.Текст = СтрЗаменить(ЗапросПоСертификатам.Текст, "&ПроверкаПользователя",
			"НастройкиОбменСБанкамиСертификатыПодписейОрганизации.СертификатЭП.Пользователь В (&ТекущийПользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), &ПользовательНеУказан)");
	КонецЕсли;
	
	ИспользуютсяЭП = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ЗначениеФункциональнойОпции("ИспользоватьЭлектронныеПодписиЭД")
		ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезДопОбработку
		ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК
		ИЛИ ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн;
	
	ВозвращаемыйПараметр = ИспользуютсяЭП И НЕ ЗапросПоСертификатам.Выполнить().Пустой() И ТребуетсяПодпись;
		
	Возврат ВозвращаемыйПараметр;
	
КонецФункции

&НаСервере
Процедура ВыполнитьПросмотрЭДИзБДСервер(Отказ = Ложь)
	
	ДанныеЭД = Документы.СообщениеОбменСБанками.ФайлДанныхЭД(Объект.Ссылка);
	Если ДанныеЭД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если ТипЗнч(ДанныеЭД) = Тип("ТабличныйДокумент") Тогда
		ТабличныйДокументФормы = ДанныеЭД;
		ИсходныйТабличныйДокумент = ДанныеЭД;
	Иначе
		Если ТипЗнч(ДанныеЭД) = Тип("Строка") Тогда
			АдресФайлаВХранилище = ДанныеЭД;
		Иначе
			Отказ = Истина;
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОбработкуОповещенияНаСервере()
	
	ВыполнитьПросмотрЭДИзБДСервер();
	ЗначениеВРеквизитФормы(Объект.Ссылка.ПолучитьОбъект(), "Объект");
	ЗаполнитьТаблицуЭП();
	ОбновитьСтатус();
	ИзменитьВидимостьДоступность();
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтатус(КартаСтатусовЭД, Статус, Значение = Ложь)
	
	НовСтрока = КартаСтатусовЭД.Добавить();
	НовСтрока.Статус = Статус;
	НовСтрока.Пройден = Значение;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьКартуСтатусовЭД(КартаСтатусовЭД, СообщениеОбмена)
	
	ПараметрыЭД = Новый Структура("ВидЭД, Направление, Организация, Банк, НастройкаОбмена, Статус");
	
	ЗаполнитьЗначенияСвойств(ПараметрыЭД, СообщениеОбмена);
	
	Настройки = Новый Структура("Направление, ВидЭД, ИспользоватьПодпись, ИспользуетсяНесколькоПодписей, ПрограммаБанка,
								|Статус");
	
	ИспользуетсяЭП = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеПодписиЭД");

	ПрограммаБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ПараметрыЭД.НастройкаОбмена, "ПрограммаБанка");
	
	ЗаполнитьЗначенияСвойств(Настройки, ПараметрыЭД);
		
	Настройки.ИспользоватьПодпись = ОбменСБанкамиСлужебныйВызовСервера.ПодписыватьВидЭД(
		Объект.НастройкаОбмена, Объект.ВидЭД);
	Настройки.ПрограммаБанка = ПрограммаБанка;
	
	Настройки.ИспользуетсяНесколькоПодписей = ИспользуетсяНесколькоПодписей(ПараметрыЭД.НастройкаОбмена);
		
	УстановитьСтатусы(КартаСтатусовЭД, Настройки);
	
КонецПроцедуры

&НаСервере
Функция ИспользуетсяНесколькоПодписей(НастройкаОбмена)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(НастройкиОбменСБанкамиСертификатыПодписейОрганизации.НомерСтроки) КАК НомерСтроки
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК НастройкиОбменСБанкамиСертификатыПодписейОрганизации
	|ГДЕ
	|	НастройкиОбменСБанкамиСертификатыПодписейОрганизации.Ссылка = &Ссылка";
	Запрос.УстановитьПараметр("Ссылка", НастройкаОбмена);
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат Выборка.НомерСтроки > 1;
	
КонецФункции

&НаСервере
Процедура УстановитьСтатусы(КартаСтатусовЭД, НастройкиСтатусов)
	
	МассивСтатусов = ОбменСБанкамиСлужебный.МассивСтатусовЭД(НастройкиСтатусов);
	Для Каждого Элемент Из МассивСтатусов Цикл
		ДобавитьСтатус(КартаСтатусовЭД, Элемент);
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция КартаСтатусовЭД(СообщениеОбмена)
	
	КартаСтатусовЭД = Новый ТаблицаЗначений;
	КартаСтатусовЭД.Колонки.Добавить("Статус");
	КартаСтатусовЭД.Колонки.Добавить("Пройден");
	
	Если СообщениеОбмена.Статус = Перечисления.СтатусыОбменСБанками.Отклонен Тогда
		
		Если СообщениеОбмена.Направление = Перечисления.НаправленияЭД.Входящий Тогда
		
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыОбменСБанками.Получен;
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыОбменСБанками.Отклонен;
		Иначе
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = Перечисления.СтатусыОбменСБанками.Сформирован;
			
			Стр = КартаСтатусовЭД.Добавить();
			Стр.Статус = СообщениеОбмена.Статус;
		КонецЕсли;
		
		КартаСтатусовЭД.ЗаполнитьЗначения(Истина, "Пройден");
		
	Иначе
		
		ЗаполнитьКартуСтатусовЭД(КартаСтатусовЭД, СообщениеОбмена);
		ПризнакПройден = Истина;
		Для Каждого ТекСтрока Из КартаСтатусовЭД Цикл 
			ТекСтрока.Пройден = ПризнакПройден;
			Если ТекСтрока.Статус = Перечисления.СтатусыОбменСБанками.Утвержден
				И СообщениеОбмена.Статус = Перечисления.СтатусыОбменСБанками.Отклонен Тогда
				ТекСтрока.Статус = Перечисления.СтатусыОбменСБанками.Отклонен;
				Прервать;
			КонецЕсли;
			
			Если ТекСтрока.Статус = Перечисления.СтатусыОбменСБанками.Отправлен
				И СообщениеОбмена.Статус = Перечисления.СтатусыОбменСБанками.ОтклоненБанком Тогда
				ТекСтрока.Статус = Перечисления.СтатусыОбменСБанками.ОтклоненБанком;
				Прервать;
			КонецЕсли;
			
			Если СообщениеОбмена.Статус = ТекСтрока.Статус Тогда
				Прервать;
			КонецЕсли;
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат КартаСтатусовЭД;
	
КонецФункции

&НаКлиенте
Процедура ПоказатьСертификат(Отпечаток)
	
	АдресДанныхСертификата = АдресДанныхСертификата(Объект.Ссылка, Отпечаток, УникальныйИдентификатор);
	
	СтруктураСертификата = СтруктураСертификата(АдресДанныхСертификата);
	Если СтруктураСертификата <> Неопределено Тогда
		ПараметрыФормы = Новый Структура(
			"СтруктураСертификата, Отпечаток, АдресСертификата", СтруктураСертификата, Отпечаток, АдресДанныхСертификата);
		ОткрытьФорму("ОбщаяФорма.Сертификат", ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СтруктураСертификата(Знач АдресДанныхСертификата)
	
	ДвоичныеДанныеСертификата = ПолучитьИзВременногоХранилища(АдресДанныхСертификата);

	ВыбранныйСертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
	Если ВыбранныйСертификат = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Сертификат не найден'"));
		Возврат Неопределено;
	КонецЕсли;
	
	Возврат ЭлектроннаяПодписьКлиентСервер.ЗаполнитьСтруктуруСертификата(ВыбранныйСертификат);
	
КонецФункции


&НаСервереБезКонтекста
Функция АдресДанныхСертификата(СообщениеОбмена, Отпечаток, УникальныйИдентификатор)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.Сертификат
	|ИЗ
	|	Справочник.СообщениеОбменСБанкамиПрисоединенныеФайлы.ЭлектронныеПодписи КАК СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи
	|ГДЕ
	|	СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.Отпечаток = &Отпечаток
	|	И СообщениеОбменСБанкамиПрисоединенныеФайлыЭлектронныеПодписи.Ссылка.ВладелецФайла = &ВладелецФайла";
	
	Запрос.УстановитьПараметр("Отпечаток", Отпечаток);
	Запрос.УстановитьПараметр("ВладелецФайла", СообщениеОбмена);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	
	ДвоичныеДанныеСертификата = Выборка.Сертификат.Получить();
	
	Возврат ПоместитьВоВременноеХранилище(ДвоичныеДанныеСертификата, УникальныйИдентификатор);
	
КонецФункции

&НаСервере
Функция ПолучитьКоличествоНеустановленныхПодписей()
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос();
	Запрос.Текст =
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ЭлектронныеПодписи.Отпечаток
	|ПОМЕСТИТЬ УстановленныеПодписи
	|ИЗ
	|	Справочник.СообщениеОбменСБанкамиПрисоединенныеФайлы.ЭлектронныеПодписи КАК ЭлектронныеПодписи
	|ГДЕ
	|	ЭлектронныеПодписи.Ссылка.ВладелецФайла = &СообщениеОбмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	НастройкиОбменСБанками.ИспользоватьЭП
	|ПОМЕСТИТЬ ПодписываемыйВидЭД
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.ИсходящиеДокументы КАК НастройкиОбменСБанками
	|ГДЕ
	|	НастройкиОбменСБанками.ИсходящийДокумент = &ВидЭД
	|	И НастройкиОбменСБанками.Ссылка = &НастройкаОбмена
	|	И НастройкиОбменСБанками.ИспользоватьЭП
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ СертификатыПодписейОрганизации.СертификатЭП) КАК КоличествоНеобходимыхПодписей
	|ИЗ
	|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК СертификатыПодписейОрганизации
	|ГДЕ
	|	НЕ СертификатыПодписейОрганизации.СертификатЭП.Отпечаток В
	|				(ВЫБРАТЬ
	|					УстановленныеПодписи.Отпечаток
	|				ИЗ
	|					УстановленныеПодписи КАК УстановленныеПодписи)
	|	И СертификатыПодписейОрганизации.Ссылка = &НастройкаОбмена
	|	И ИСТИНА В
	|			(ВЫБРАТЬ
	|				ПодписываемыйВидЭД.ИспользоватьЭП
	|			ИЗ
	|				ПодписываемыйВидЭД КАК ПодписываемыйВидЭД)";
	Запрос.УстановитьПараметр("СообщениеОбмена", Объект.Ссылка);
	Запрос.УстановитьПараметр("ВидЭД", Объект.ВидЭД);
	Запрос.УстановитьПараметр("НастройкаОбмена", Объект.НастройкаОбмена);
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	РезультатЗапроса.Следующий();
	
	Возврат РезультатЗапроса.КоличествоНеобходимыхПодписей;
	
КонецФункции

&НаКлиенте
Процедура СохранитьБанковскиеДокументыСбербанка(СообщениеОбмена)
	
	КоличествоВыгруженныхДокументов = 0;
	
	МассивФайловСбербанка = МассивФайловСбербанка(СообщениеОбмена);
	
	Для Каждого Элемент Из МассивФайловСбербанка Цикл
		ПолучитьФайл(Элемент.СсылкаНаФайл, Элемент.ИмяФайла);
		КоличествоВыгруженныхДокументов = КоличествоВыгруженныхДокументов + 1;
	КонецЦикла;
	
	ТекстОповещения = НСтр ("ru = 'Выгружено файлов: (" + КоличествоВыгруженныхДокументов + ")'");
	ПоказатьОповещениеПользователя(ТекстОповещения);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ДанныеСлужебногоЭДБанка(Знач СообщениеОбмена, Знач УникальныйИдентификатор)

	ДанныеФайла = Неопределено;
	СлужебноеСообщениеБанка = ОбменСБанкамиСлужебный.СлужебноеСообщениеБанка(СообщениеОбмена);
	Если ЗначениеЗаполнено(СлужебноеСообщениеБанка) Тогда
		ДанныеФайла = ДанныеФайла(СлужебноеСообщениеБанка, УникальныйИдентификатор);
	КонецЕсли;
	Возврат ДанныеФайла;
	
КонецФункции

&НаСервереБезКонтекста
Функция ДанныеФайла(Знач СообщениеОбмена, Знач ИдентификаторФормы)
	
	ПрисоединенныйФайл = ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеОбмена);
	
	Если ЗначениеЗаполнено(ПрисоединенныйФайл) Тогда
		ДанныеФайла = ПрисоединенныеФайлы.ПолучитьДанныеФайла(ПрисоединенныйФайл, ИдентификаторФормы, Истина);
		Если ЗначениеЗаполнено(ДанныеФайла) Тогда
			ДанныеФайла.Вставить("ПрисоединенныйФайл", ПрисоединенныйФайл);
		КонецЕсли;
		Возврат ДанныеФайла;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ПодтвердитьПлатежЗавершитьЧерезДополнительнуюОбработку(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Сессия = Неопределено;
	ВнешнийПодключаемыйМодуль = Неопределено;
	СертификатXML = Неопределено;
	Если Результат <> Неопределено
		И ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("Сессия", Сессия)
		И ДополнительныеПараметры.Свойство("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль)
		И ДополнительныеПараметры.Свойство("СертификатXML", СертификатXML) Тогда
		Пароль = Результат;
		ПараметрыЗапроса = Новый Структура("Способ, Пароль, Сессия");
		ПараметрыЗапроса.Способ = "SMS";
		ПараметрыЗапроса.Пароль = Пароль;
		ПараметрыЗапроса.Сессия = Сессия;
			
		Попытка
			Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 5, ПараметрыЗапроса);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка подтверждения платежного поручения.
									|Код ошибки: ДО-%1
									|%2'");
			ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Подтверждение платежного поручения'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, Объект.Ссылка);
			Возврат;
		КонецПопытки;
		
		Если Результат.Количество() = 0 Тогда
			ПоказатьОповещениеПользователя(НСтр("ru = 'Нет данных для подтверждения'"));
		КонецЕсли;
		
		Если Не ПустаяСтрока(Результат[0].ТекстОшибки) Тогда
			ТекстСообщения = НСтр("ru = 'Ошибка при подтверждении платежного поручения:'") + " " + Результат[0].ТекстОшибки;
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		УстановитьСтатусДоставлен(Объект.Ссылка);
		
		Оповестить("ОбновитьСостояниеОбменСБанками");
		
		ПоказатьОповещениеПользователя(НСтр("ru = 'Документ подтвержден'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ДополнительныеПараметры = Неопределено) Экспорт
	
	ОчиститьСообщения();
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		ПарольУстановленРанее = Ложь;
		МассивСертификатов = Новый Массив;
		ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(Объект.НастройкаОбмена);
		
		ДанныеСертификата = Неопределено;
		Для Каждого Элемент Из ДоступныеСертификаты Цикл
			ПарольУстановленРанее = ОбменСБанкамиСлужебныйКлиент.УстановленПарольСертификатаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, Элемент.Значение.ДанныеСертификата);
			Если ПарольУстановленРанее Тогда
				ВыбранныйСертификат = Элемент.Ключ;
				ДанныеСертификата = Элемент.Значение;
				ДанныеСертификата.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
				СертификатXML = Элемент.Значение.ДанныеСертификата;
				Прервать;
			КонецЕсли;
			МассивСертификатов.Добавить(Элемент.Ключ);
		КонецЦикла;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ПарольУстановленРанее", ПарольУстановленРанее);
		ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
		ДополнительныеПараметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		
		Если ПарольУстановленРанее Тогда
			ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(
				ДанныеСертификата, ДополнительныеПараметры);
		Иначе
			ВидОперации = НСтр("ru = 'Аутентификация на ресурсе банка'");
			Если ОбменСБанкамиСлужебныйКлиент.ЕстьСертификатССохраненнымПаролем(ДоступныеСертификаты) Тогда
				ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
			Иначе
				ДополнительныеПараметры.Вставить("СоотвСертификатовИИхСтруктур", ДоступныеСертификаты);
				Оповещение = Новый ОписаниеОповещения(
					"ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект,
					ДополнительныеПараметры);
				ОбменСБанкамиСлужебныйКлиент.ПолучитьПарольКСертификату(Оповещение, ДоступныеСертификаты, ВидОперации);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтклонитьЭДПродолжить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = Истина Тогда
		ИзменитьВидимостьДоступность();
		Оповестить("ОбновитьСостояниеОбменСБанками");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(Результат, ДополнительныеПараметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		ДоступныеСертификаты = ДополнительныеПараметры.СоотвСертификатовИИхСтруктур;
		ПарольУстановленРанее = ДополнительныеПараметры.ПарольУстановленРанее;
		ВнешнийПодключаемыйМодуль = ДополнительныеПараметры.ВнешнийПодключаемыйМодуль;
		
		ПарольПользователя = Неопределено;
		Результат.Свойство("ПарольПользователя", ПарольПользователя);
		Если ДоступныеСертификаты.Количество() > 0 Тогда
			Для Каждого КлючИЗначение Из ДоступныеСертификаты Цикл
				Если КлючИЗначение.Ключ = ВыбранныйСертификат Тогда
					ПараметрыСертификата = КлючИЗначение.Значение;
					СертификатXML = ПараметрыСертификата.ДанныеСертификата;
					Прервать;
				КонецЕсли;
			КонецЦикла;
			ДанныеСертификата = ОбменСБанкамиСлужебныйКлиент.ДанныеСертификатаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, СертификатXML);

			Если ДанныеСертификата <> Неопределено Тогда
				ПараметрыВыполнения = Новый Структура;
				ПараметрыВыполнения.Вставить("ИмяПроцедуры", "ВыполнитьПодтверждениеПлатежаЧерезДополнительнуюОбработку");
				ПараметрыВыполнения.Вставить("Модуль", ЭтотОбъект);
				ПараметрыВыполнения.Вставить("СертификатXML", СертификатXML);
				ПараметрыВыполнения.Вставить("ПарольУстановленРанее", ПарольУстановленРанее);
				ПараметрыВыполнения.Вставить("ПарольПользователя", ПарольПользователя);
				ПараметрыВыполнения.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
				ПараметрыВыполнения.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
				
				ТребуетсяУстановкаPINКода = ОбменСБанкамиСлужебныйКлиент.НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
					ВнешнийПодключаемыйМодуль, ДанныеСертификата.ИдентификаторХранилища);
					
				Если ТребуетсяУстановкаPINКода = Ложь Тогда
					ПродолжитьПодтверждениеПлатежаЧерезДополнительнуюОбработку(ПараметрыВыполнения);
				ИначеЕсли ТребуетсяУстановкаPINКода = Истина Тогда
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
						"ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, ПараметрыВыполнения);
					ОбменСБанкамиСлужебныйКлиент.НачатьУстановкуPINКодаХранилища(
						Объект.НастройкаОбмена, ДанныеСертификата.ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, ПараметрыВыполнения) Экспорт
	
	ВнешнийПодключаемыйМодуль = ПараметрыВыполнения.ВнешнийПодключаемыйМодуль;
	
	Если PINКод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановленPIN = ОбменСБанкамиСлужебныйКлиент.УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ПараметрыВыполнения.ИдентификаторХранилища, PINКод);
		
	Если НЕ УстановленPIN Тогда
		Возврат;
	КонецЕсли;
	
	ПродолжитьПодтверждениеПлатежаЧерезДополнительнуюОбработку(ПараметрыВыполнения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПродолжитьПодтверждениеПлатежаЧерезДополнительнуюОбработку(ПараметрыВыполнения)
	
	ПарольУстановленРанее = ПараметрыВыполнения.ПарольУстановленРанее;
	ВнешнийПодключаемыйМодуль = ПараметрыВыполнения.ВнешнийПодключаемыйМодуль;
	СертификатXML = ПараметрыВыполнения.СертификатXML;
	ПарольПользователя = ПараметрыВыполнения.ПарольПользователя;

	Если НЕ ПарольУстановленРанее Тогда
		ПарольУстановлен = ОбменСБанкамиСлужебныйКлиент.УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, СертификатXML, ПарольПользователя);
		Если НЕ ПарольУстановлен Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ВыполнитьПодтверждениеПлатежаЧерезДополнительнуюОбработку", ЭтотОбъект, ПараметрыВыполнения);
	ОбменСБанкамиСлужебныйКлиент.УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ВнешнийПодключаемыйМодуль, СертификатXML, Объект.НастройкаОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСтатусаЭДВБанк(ДанныеАутентификации, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДанныеАутентификации) Тогда
		Возврат;
	КонецЕсли;
	ПараметрыПолучения = Новый Структура;
	ПараметрыПолучения.Вставить("ДанныеАвторизации", ДанныеАутентификации);
	ПараметрыПолучения.Вставить("НастройкаОбмена", Объект.НастройкаОбмена);
	ПараметрыПолучения.Вставить("СообщениеОбмена", ДополнительныеПараметры.ЗапросСостоянияЭД);
	РеквизитыНастройкиОбмена = Новый Структура("АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		Объект.НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ДанныеАутентификации.Вставить("Пароль", ДанныеАутентификации.ПарольПользователя);
	Обработчик = Новый ОписаниеОповещения("ОтправитьЗапросСтатусаЭДВБанкПослеПолученияМаркера", ЭтотОбъект, ПараметрыПолучения);
	ОбменСБанкамиСлужебныйКлиент.ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
		РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
		Объект.НастройкаОбмена );
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСтатусаЭДВБанкПослеПолученияМаркера(Маркер, ДополнительныеПараметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		Возврат;
	КонецЕсли;

	ДополнительныеПараметры.Вставить("ИдентификаторСессииБанка", Маркер);
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ДополнительныеПараметры);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьПодтверждениеПлатежаЧерезДополнительнуюОбработку(АутентификацияВыполнена, ДополнительныеПараметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = ДополнительныеПараметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = ДополнительныеПараметры.СертификатXML;
	
	ВнешниеИдентификаторы = Новый Массив;
	ВнешниеИдентификаторы.Добавить(УникальныйИДВнешний(Объект.Ссылка));
	ПараметрыЗапроса = Новый Структура("ИдентификаторыДокументов", ВнешниеИдентификаторы);
	
	Попытка
		Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 4, ПараметрыЗапроса);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка инициализации сессии подтверждения.
							|Код ошибки: ДО-%1
							|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Инициализация сессии подтверждения'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, Объект.Ссылка);
		Возврат;
	КонецПопытки;
	
	Сессия = Результат.Сессия;
	
	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьКодПодтвержденияПоSMS(СертификатXML, Сессия);
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки кода подтверждения платежного поручения.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка кода подтверждения платежного поручения'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, Объект.Ссылка);
		Возврат;
	КонецПопытки;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторСессии", Сессия.Идентификатор);
	ПараметрыФормы.Вставить("СообщениеОбмена", Объект.Ссылка);
	
	ДопДанные = Новый Структура("Сессия, ВнешнийПодключаемыйМодуль, СертификатXML",
		Сессия, ВнешнийПодключаемыйМодуль, СертификатXML);
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодтвердитьПлатежЗавершитьЧерезДополнительнуюОбработку", ЭтотОбъект, ДопДанные);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ПодтверждениеПлатежныхПорученийПоSMS",
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтпечатковЗапроситьСостояниеЭД(Отпечатки, ПараметрыОбработки) Экспорт
	
	МассивОтпечатковСертификатов = Новый Массив;
	
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
	
	Соответствие = ОбменСБанкамиСлужебныйВызовСервера.СоответствиеДоступныхСертификатовИПараметров(
		МассивОтпечатковСертификатов, Объект.НастройкаОбмена);
	
	Сертификат = Неопределено;
	МассивСертификатов = Новый Массив;
	ПарольПолучен = Ложь;
	Для Каждого КлючЗначение Из Соответствие Цикл
		МассивСертификатов.Добавить(КлючЗначение.Ключ);
		Если КлючЗначение.Значение.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен = Истина Тогда
			МассивСертификатов.Очистить();
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если МассивСертификатов.Количество() Тогда
			
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		
		ПараметрыЗапросаМаркера = Новый Структура("НастройкаОбмена", Объект.НастройкаОбмена);
		ОбработчикПолученияМаркера = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ОбменСБанкамиСлужебныйКлиент, ПараметрыЗапросаМаркера);
		
		ОписаниеДанных.Вставить("Данные", ОбработчикПолученияМаркера);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПослеРасшифровкиМаркераБанкаОтправитьЗапросСостоянияЭД", ЭтотОбъект, ПараметрыОбработки);
					
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
			
	Иначе
		ТекстСообщения = НСтр("ru = 'Нет доступных сертификатов.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;

КонецПроцедуры

&НаСервереБезКонтекста
Функция МожноОтозватьЭД(СообщениеОбмена)
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СообщениеОбменСБанками.Ссылка
	|ИЗ
	|	Документ.СообщениеОбменСБанками КАК СообщениеОбменСБанками
	|ГДЕ
	|	СообщениеОбменСБанками.НастройкаОбмена.ПрограммаБанка В (ЗНАЧЕНИЕ(Перечисление.ПрограммыБанка.АсинхронныйОбмен), ЗНАЧЕНИЕ(Перечисление.ПрограммыБанка.ОбменЧерезВК))
	|	И СообщениеОбменСБанками.Ссылка = &Ссылка
	|	И СообщениеОбменСБанками.Состояние В (ЗНАЧЕНИЕ(Перечисление.СостоянияОбменСБанками.ОжидаетсяИсполнение), ЗНАЧЕНИЕ(Перечисление.СостоянияОбменСБанками.ТребуетсяПодтверждение))
	|	И СообщениеОбменСБанками.НастройкаОбмена.ИспользуетсяКриптография";
	
	Запрос.УстановитьПараметр("Ссылка", СообщениеОбмена);
	Результат = Запрос.Выполнить();
	Возврат НЕ Результат.Пустой();
	
КонецФункции

&НаКлиенте
Процедура НачатьОтзывЭД(Строка, Параметры) Экспорт
	
	Если Строка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Отзыв = СформироватьЗапросНаОтзывЭД(Объект.НастройкаОбмена, Объект.Ссылка, Строка);
	
	МассивСсылок = Новый Массив;
	МассивСсылок.Добавить(ОбменСБанкамиСлужебныйВызовСервера.ДокументУчета(Объект.Ссылка));
	ОбменСБанкамиСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "УтвердитьПодписатьОтправить", Отзыв);

КонецПроцедуры

&НаКлиенте
Процедура ПослеРасшифровкиМаркераБанкаОтправитьЗапросСостоянияЭД(Результат, ПараметрыОбработки) Экспорт
	
	Если НЕ Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ИдентификаторСессииБанка = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
		Результат.РасшифрованныеДанные);
	ПараметрыОбработки.Вставить("СообщениеОбмена", ПараметрыОбработки.ЗапросСостоянияЭД);
	ПараметрыОбработки.Вставить("НастройкаОбмена", Объект.НастройкаОбмена);
		
	ОтправитьЗапросСтатусаЭДВБанкПослеПолученияМаркера(ИдентификаторСессииБанка, ПараметрыОбработки);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция СформироватьЗапросНаОтзывЭД(Знач НастройкаОбмена, Знач СообщениеОбменаПлатежноеПоручение, Знач Причина)
	
	ТекстОшибки = "";
	ВерсияПрограммы = ОбменСБанкамиСлужебныйПовтИсп.ВерсияПрограммыКлиентаДляБанка();
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "Организация, ИдентификаторОрганизации, Банк, ВерсияФормата");
		
	Если ЗначениеЗаполнено(РеквизитыНастройкиОбмена.ВерсияФормата) Тогда
		ВерсияФормата = РеквизитыНастройкиОбмена.ВерсияФормата;
	Иначе
		ВерсияФормата = ОбменСБанкамиКлиентСервер.АктуальнаяВерсияФорматаАсинхронногоОбмена();
	КонецЕсли;
	ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(ВерсияФормата);
	Фабрика = ОбменСБанкамиСлужебный.ФабрикаAsyncXDTO(ВерсияФормата);
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	
	ОтправительНаименование = ЭлектронноеВзаимодействиеСлужебный.СокращенноеНаименованиеОрганизации(
		РеквизитыНастройкиОбмена.Организация);
	РеквизитыОрганизации = ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(
		РеквизитыНастройкиОбмена.Организация);
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыНастройкиОбмена.Банк, "Код, Наименование");
	
	Попытка
	
		ВремФайл = ПолучитьИмяВременногоФайла("xml");
			
		УникальныйИдЭД = Новый УникальныйИдентификатор;
			
		ЭД = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "CancelationRequest", ПространствоИмен);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "id", Строка(УникальныйИдЭД), Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "formatVersion", ВерсияФормата, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "userAgent", ВерсияПрограммы, , ТекстОшибки);
			
		Отправитель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "CustomerPartyType", ПространствоИмен);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "id", ИдентификаторОрганизации, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "name", ОтправительНаименование, , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "inn", РеквизитыОрганизации.ИНН, , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "kpp", РеквизитыОрганизации.КПП, , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "Sender", Отправитель, Истина, ТекстОшибки);
			
		Получатель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "BankPartyType", ПространствоИмен);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Получатель, "bic", РеквизитыБанка.Код, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Получатель, "name", РеквизитыБанка.Наименование, , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "Recipient", Получатель, Истина, ТекстОшибки);
		ИДЭД = Строка(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеОбменаПлатежноеПоручение, "Идентификатор"));
		
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "ExtID", ИДЭД, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЭД, "Reason", Причина, , ТекстОшибки);
		ЭД.Проверить();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
			Возврат Неопределено;
		КонецЕсли;
		
		ОбменСБанкамиСлужебный.ВыгрузитьЭДВФайл(Фабрика, ЭД, ВремФайл, Ложь);
		
	Исключение
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Операция = НСтр("ru = 'Формирование ЭД'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, КраткоеПредставлениеОшибки, 1, СообщениеОбменаПлатежноеПоручение);
		Возврат Неопределено;
	КонецПопытки;

	ДвоичныеДанные = Новый ДвоичныеДанные(ВремФайл);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Если ОбменСБанкамиСлужебный.ФайлИзменен(Фабрика, ЭД, ВремФайл, Ложь) Тогда
		Возврат Неопределено;
	КонецЕсли;

	ЭлектронноеВзаимодействиеСлужебный.УдалитьВременныеФайлы(ВремФайл);
	
	НазваниеЭД = НСтр("ru = 'Запрос на отзыв электронного документа'");

	ТекущаяДатаСеанса = ТекущаяДатаСеанса();
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Представление", НазваниеЭД);
	Реквизиты.Вставить("АдресФайлаВоВременномХранилище", АдресФайла);
	Реквизиты.Вставить("Идентификатор", Строка(УникальныйИдЭД));
	Реквизиты.Вставить("Направление", Перечисления.НаправленияЭД.Исходящий);
	Реквизиты.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ЗапросНаОтзывЭД);
	Реквизиты.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Сформирован);
	Реквизиты.Вставить("Организация", РеквизитыНастройкиОбмена.Организация);
	Реквизиты.Вставить("Банк", РеквизитыНастройкиОбмена.Банк);
	Реквизиты.Вставить("СообщениеРодитель", СообщениеОбменаПлатежноеПоручение);
	Реквизиты.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	СообщениеЗапросНаОтзыв = Неопределено;
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(Реквизиты, СообщениеЗапросНаОтзыв);
	
	Возврат СообщениеЗапросНаОтзыв;
	
КонецФункции

&НаСервереБезКонтекста
Процедура СформироватьЗапросСостоянияЭД(Знач НастройкаОбмена, Знач СообщениеОбменаПлатежныйДокумент, Знач ТребуетсяПодпись, ЗапросСостоянияЭД, Знач МассивОтпечатковСертификатов = Неопределено, МассивСертификатов = Неопределено)
	
	Если ТребуетсяПодпись Тогда
		// Определение сертификатов подписи
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	Сертификаты.СертификатЭП КАК Сертификат,
		|	Сертификаты.СертификатЭП.Отпечаток КАК Отпечаток
		|ИЗ
		|	Справочник.НастройкиОбменСБанками.СертификатыПодписейОрганизации КАК Сертификаты
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПодписываемыеВидыЭД КАК ВидыЭД
		|		ПО (ВидыЭД.СертификатЭП = Сертификаты.СертификатЭП)
		|ГДЕ
		|	НЕ Сертификаты.СертификатЭП.ПометкаУдаления
		|	И НЕ Сертификаты.СертификатЭП.Отозван
		|	И ВидыЭД.ВидЭД = &ВидЭД
		|	И &ПроверкаПользователя
		|	И ВидыЭД.Использовать
		|	И Сертификаты.Ссылка = &НастройкаОбмена";
		
		Запрос.УстановитьПараметр("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ЗапросОСостоянииЭД);
		Запрос.УстановитьПараметр("НастройкаОбмена", НастройкаОбмена);
		
		Если Пользователи.ЭтоПолноправныйПользователь( , , Ложь) Тогда
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаПользователя", "ИСТИНА");
		Иначе
			Запрос.УстановитьПараметр("ПользовательНеУказан", Пользователи.СсылкаНеуказанногоПользователя());
			Запрос.УстановитьПараметр("Пользователь",  Пользователи.АвторизованныйПользователь());
			Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ПроверкаПользователя",
				"Сертификаты.СертификатЭП.Пользователь В (&Пользователь, ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка), &ПользовательНеУказан)");
		КонецЕсли;
			
		ТаблицаСертификатов = Запрос.Выполнить().Выгрузить();
		
		Если ТаблицаСертификатов.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат подписи для документа Запрос состояния электронного документа.
										|Проверьте настройки обмена через сервис 1С:ДиректБанк.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ПрограммаБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НастройкаОбмена, "ПрограммаБанка");
		
		МассивСертификатов = Новый Массив;
		Если ПрограммаБанка = Перечисления.ПрограммыБанка.ОбменЧерезВК Тогда 
			Для Каждого Строка Из ТаблицаСертификатов Цикл
				МассивСертификатов.Добавить(Строка.Сертификат);
			КонецЦикла;
		ИначеЕсли МассивОтпечатковСертификатов <> Неопределено Тогда
			Для Каждого Строка Из ТаблицаСертификатов Цикл
				Если МассивОтпечатковСертификатов.Найти(Строка.Отпечаток) <> Неопределено Тогда
					МассивСертификатов.Добавить(Строка.Сертификат);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
		Если МассивСертификатов.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'На компьютере не установлен ни один сертификат, указанный в настройке обмена.
										|Установите сертификаты или обратитесь к администратору.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	// Формирование электронного документа
	ТекстОшибки = "";
	ВерсияПрограммы = ОбменСБанкамиСлужебныйПовтИсп.ВерсияПрограммыКлиентаДляБанка();
	РеквизитыНастройкиОбмена = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		НастройкаОбмена, "Организация, ИдентификаторОрганизации, Банк, Недействительна, ВерсияФормата");
	Если РеквизитыНастройкиОбмена.Недействительна Тогда
		ШаблонСообщения = НСтр("ru = 'Настройка обмена с банком %1 недействительна'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, НастройкаОбмена);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(РеквизитыНастройкиОбмена.ВерсияФормата) Тогда
		ВерсияФормата = РеквизитыНастройкиОбмена.ВерсияФормата;
	Иначе
		ВерсияФормата = ОбменСБанкамиКлиентСервер.АктуальнаяВерсияФорматаАсинхронногоОбмена();
	КонецЕсли;
	
	ПространствоИмен = ОбменСБанкамиСлужебный.ПространствоИменАсинхронногоОбмена(ВерсияФормата);

	
	Фабрика = ОбменСБанкамиСлужебный.ФабрикаAsyncXDTO(ВерсияФормата);
	
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	
	ОтправительНаименование = ЭлектронноеВзаимодействиеСлужебный.СокращенноеНаименованиеОрганизации(
		РеквизитыНастройкиОбмена.Организация);
	РеквизитыОрганизации = ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьДанныеЮрФизЛица(
		РеквизитыНастройкиОбмена.Организация);
	РеквизитыБанка = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(РеквизитыНастройкиОбмена.Банк, "Код, Наименование");
	
	ИдентификаторПлатежа = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СообщениеОбменаПлатежныйДокумент, "Идентификатор");
	Попытка
			
		ВремФайл = ПолучитьИмяВременногоФайла("xml");
			
		УникальныйИдЭД = Новый УникальныйИдентификатор;

		ЗапросОСостоянииЭД = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "StatusRequest", ПространствоИмен);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросОСостоянииЭД, "id", Строка(УникальныйИдЭД), Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросОСостоянииЭД, "ExtID", ИдентификаторПлатежа, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросОСостоянииЭД, "formatVersion", ВерсияФормата, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(
			ЗапросОСостоянииЭД, "creationDate", ТекущаяДатаСеанса(), Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросОСостоянииЭД, "userAgent", ВерсияПрограммы, , ТекстОшибки);
		Отправитель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "CustomerPartyType", ПространствоИмен);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(
			Отправитель, "id", РеквизитыНастройкиОбмена.ИдентификаторОрганизации, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "name", ОтправительНаименование, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "inn", РеквизитыОрганизации.ИНН, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Отправитель, "kpp", РеквизитыОрганизации.КПП, , ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросОСостоянииЭД, "Sender", Отправитель, Истина, ТекстОшибки);
		
		Получатель = ОбменСБанкамиСлужебный.ОбъектТипаCML(Фабрика, "BankPartyType", ПространствоИмен);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Получатель, "bic", РеквизитыБанка.Код, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(Получатель, "name", РеквизитыБанка.Наименование, Истина, ТекстОшибки);
		ОбменСБанкамиСлужебный.ЗаполнитьСвойствоXDTO(ЗапросОСостоянииЭД, "Recipient", Получатель, Истина, ТекстОшибки);
		ЗапросОСостоянииЭД.Проверить();
		
		Если ЗначениеЗаполнено(ТекстОшибки) Тогда
			ТекстСообщения = ТекстОшибки;
			Операция = НСтр("ru = 'Формирование электронного документа'");
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ТекстОшибки, ТекстСообщения, 1, СообщениеОбменаПлатежныйДокумент);
			Возврат;
		КонецЕсли;
		
		ОбменСБанкамиСлужебный.ВыгрузитьЭДВФайл(Фабрика, ЗапросОСостоянииЭД, ВремФайл, Ложь);
		
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		Операция = НСтр("ru = 'Формирование электронного документа'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, СообщениеОбменаПлатежныйДокумент);
		Возврат;
	КонецПопытки;
	
	ДвоичныеДанные = Новый ДвоичныеДанные(ВремФайл);
	АдресФайла = ПоместитьВоВременноеХранилище(ДвоичныеДанные);
	
	Если ОбменСБанкамиСлужебный.ФайлИзменен(Фабрика, ЗапросОСостоянииЭД, ВремФайл, Ложь) Тогда
		Возврат;
	КонецЕсли;

	ЭлектронноеВзаимодействиеСлужебный.УдалитьВременныеФайлы(ВремФайл);
	
	НазваниеЭД = НСтр("ru = 'Запрос состояния электронного документа'");
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("Представление", НазваниеЭД);
	Реквизиты.Вставить("Расширение", "xml");
	Реквизиты.Вставить("АдресФайлаВоВременномХранилище", АдресФайла);
	Реквизиты.Вставить("Идентификатор", Строка(УникальныйИдЭД));
	Реквизиты.Вставить("Направление", Перечисления.НаправленияЭД.Исходящий);
	Реквизиты.Вставить("ВидЭД", Перечисления.ВидыЭДОбменСБанками.ЗапросОСостоянииЭД);
	Реквизиты.Вставить("Статус", Перечисления.СтатусыОбменСБанками.Сформирован);
	Реквизиты.Вставить("Организация", РеквизитыНастройкиОбмена.Организация);
	Реквизиты.Вставить("Банк", РеквизитыНастройкиОбмена.Банк);
	Реквизиты.Вставить("СообщениеРодитель", СообщениеОбменаПлатежныйДокумент);
	Реквизиты.Вставить("НастройкаОбмена", НастройкаОбмена);
	Реквизиты.Вставить("СсылкаНаОбъект", НастройкаОбмена);
	
	ОбменСБанкамиСлужебный.СохранитьСообщениеОбмена(Реквизиты, ЗапросСостоянияЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура КомментарийПриИзменении(Элемент)
	
	КомментарийПриИзмененииНаСервере();
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Процедура КомментарийПриИзмененииНаСервере()
	
	ОбъектДокумента = Объект.Ссылка.ПолучитьОбъект();
	ОбъектДокумента.Комментарий = Объект.Комментарий;
	ОбъектДокумента.Записать();
	Элементы.СтраницаКомментарий.Картинка = ОбщегоНазначенияКлиентСервер.КартинкаКомментария(Объект.Комментарий);
	
КонецПроцедуры

#Область ОбменЧерезВК

&НаКлиенте
Процедура ПодписатьЗапросСостоянияПослеВводаПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьЗапросСостоянияВБанкПослеПодписанияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПодписатьЭДПоСертификатуЧерезВК(Оповещение, ДополнительныеПараметры.ЗапросСостоянияЭД,
		Объект.НастройкаОбмена, Результат.ВыбранныйСертификат, Результат.ПарольПользователя);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьЗапросСостоянияВБанкПослеПодписанияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		Возврат;
	КонецЕсли;
	
	ДанныеОтправки = Новый Соответствие;
	МассивСообщенийОбмена = Новый Массив;
	МассивСообщенийОбмена.Добавить(ДополнительныеПараметры.ЗапросСостоянияЭД);
	ДанныеОтправки.Вставить(Объект.НастройкаОбмена, МассивСообщенийОбмена);
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьСостояниеЭДПослеОтправкиЗапросаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбменСБанкамиСлужебныйКлиент.ОтправитьДокументыЧерезВК(Оповещение, ДанныеОтправки, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкуПодтвержденияПлатежногоПорученияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При подтверждении платежного поручения произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Подтверждение платежного поручения.'");
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователю", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкуОтправкиКодаПодтвержденияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При отправке кода подтверждения через SMS произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Отправка кода подтверждения платежного поручения.'");
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователю", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьОшибкуНачалаПодтвержденияПлатежногоПоручения(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При начале подтверждения платежного поручения произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Начало подтверждения платежного поручения.'");
	Оповещение = Новый ОписаниеОповещения("СообщитьОшибкуПользователю", ЭтотОбъект);
	
	ОбменСБанкамиСлужебныйКлиент.ПолучитьИнформациюОбОшибкеВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежноеПоручениеПослеВводаПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПодтвержденияПлатежногоПорученияЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодтвержденияПлатежногоПорученияЧерезВК", ЭтотОбъект);
		
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодтвердитьПлатежноеПоручение(
		Оповещение, ДополнительныеПараметры.Сессия, Результат);
		
КонецПроцедуры

&НаКлиенте
Процедура ПодтвердитьПлатежныйДокументПослеУстановкиСоединенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Или Не Результат Тогда
		Возврат;
	КонецЕсли;
	
	// После установки соединения внешняя компонента точно должна быть в кэше.
	ПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ПодключеннаяВнешняяКомпонентаБанка(
		ДополнительныеПараметры.ИмяВнешнегоМодуля);
		
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ПослеНачалаПодтвержденияПлатежногоПоручения", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуНачалаПодтвержденияПлатежногоПоручения", ЭтотОбъект);
	
	ИдентификаторСообщенияОбмена = ИдентификаторСообщенияОбмена(Объект.Ссылка);
	
	ПодключаемыйМодуль.НачатьВызовНачатьПодтверждениеПлатежногоПоручения(Оповещение, ИдентификаторСообщенияОбмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьСостояниеЭДПослеОтправкиЗапросаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ИтогКолОтправленных = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СообщениеОбмена", ДополнительныеПараметры.ЗапросСостоянияЭД);
	ПараметрыФормы.Вставить("НастройкаОбмена", Объект.НастройкаОбмена);
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеНачалаПодтвержденияПлатежногоПоручения(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ПараметрыПодтвержденияПлатежа = ОбменСБанкамиСлужебныйКлиент.ДеСериализованныеДанные(РезультатВызова);
	ДополнительныеПараметры.Вставить("ИдентификаторСессии", ПараметрыПодтвержденияПлатежа.Сессия.Идентификатор);
	СессияСтрокой = ОбменСБанкамиСлужебныйКлиент.СериализованныеДанные(ПараметрыПодтвержденияПлатежа.Сессия);
	ДополнительныеПараметры.Вставить("Сессия", СессияСтрокой);
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиКодаПодтвержденияПоSMSЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуОтправкиКодаПодтвержденияЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовОтправитьКодПодтвержденияПоSMS(Оповещение, СессияСтрокой);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеОтправкиКодаПодтвержденияПоSMSЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторСессии", ДополнительныеПараметры.ИдентификаторСессии);
	ПараметрыФормы.Вставить("СообщениеОбмена", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"ПодтвердитьПлатежноеПоручениеПослеВводаПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ПодтверждениеПлатежныхПорученийПоSMS",
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор, , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПодтвержденияПлатежногоПорученияЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СтатусПринят = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Принят");
	СтруктураРеквизитов = Новый Структура("Статус", СтатусПринят);
	ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(Объект.Ссылка, СтруктураРеквизитов);
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ПоказатьОповещениеПользователя(НСтр("ru = 'Документ подтвержден.'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СообщитьОшибкуПользователю(ТекстСообщения, ДополнительныеПараметры) Экспорт
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры

#КонецОбласти

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();
	
	Элемент = УсловноеОформление.Элементы.Добавить();
	
	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ЭПСтатус.Имя);
	
	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("ЭП.ПодписьВерна");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", Новый Цвет(255, 0, 0));
	
	
КонецПроцедуры

#КонецОбласти
