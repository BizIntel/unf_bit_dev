
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("КассоваяСмена") Тогда
		
		КассоваяСмена = Параметры.КассоваяСмена;
		
		Запрос = Новый Запрос();
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КассоваяСмена.ФДОЗакрытииСмены КАК ФДОЗакрытииСмены
		|ИЗ
		|	Документ.КассоваяСмена КАК КассоваяСмена
		|ГДЕ
		|	КассоваяСмена.Ссылка = &КассоваяСмена";
		Запрос.УстановитьПараметр("КассоваяСмена", КассоваяСмена);
		
		Результат = Запрос.Выполнить().Выбрать();
		
		Результат.Следующий();
		
		XMLСтрока = Результат.ФДОЗакрытииСмены.Получить();
		Если XMLСтрока = Неопределено или не ТипЗнч(XMLСтрока) = Тип("Строка") или XMLСтрока = "" Тогда
			Отказ = Истина;
			ТекстПредупреждения = НСтр("ru = 'Нет фискальных данных'");
		Иначе
			ЗаполнитьДеревоЗначенийПоXMLСтроке(ДеревоВсехДанных, XMLСтрока);
		КонецЕсли;
		
	Иначе
		Отказ = Истина;
		ТекстПредупреждения = НСтр("ru = 'Форма фискальных данных пердназначена для открытия из элемента Кассовой смены'");
	КонецЕсли;
	
	Если Отказ Тогда
		Сообщить(ТекстПредупреждения);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьДеревоЗначенийПоXMLСтроке(ДеревоВсехДанных, XMLСтрока)
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(XMLСтрока);
	
	ЗаполнитьЭлементыИзЧтенияXML(ЧтениеXML, ДеревоВсехДанных);
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьЭлементыИзЧтенияXML(ЧтениеXML, ТекущийЭлементДерева)
	
	Пока ЧтениеXML.Прочитать() Цикл
		
		Если ЧтениеXML.ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
			
			НовыйЭлемент = ТекущийЭлементДерева.ПолучитьЭлементы().Добавить();
			НовыйЭлемент.Реквизит = ЧтениеXML.Имя;
			НовыйЭлемент.Значение = ЧтениеXML.Значение;
			
			ЗаполнитьЭлементыИзЧтенияXML(ЧтениеXML, НовыйЭлемент);
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.Текст Тогда
			
			ТекущийЭлементДерева.Значение = ЧтениеXML.Значение;
			
		ИначеЕсли ЧтениеXML.ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
			
			Прервать;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
