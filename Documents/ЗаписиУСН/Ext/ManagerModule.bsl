#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылка, ДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	&Период,
	|	&Организация,
	|	ЗаписиУСНЗаписиКУДиР.ДоходВсего,
	|	ЗаписиУСНЗаписиКУДиР.ДоходБаза,
	|	ЗаписиУСНЗаписиКУДиР.РасходВсего,
	|	ЗаписиУСНЗаписиКУДиР.РасходБаза,
	|	ЗаписиУСНЗаписиКУДиР.Содержание,
	|	ЗаписиУСНЗаписиКУДиР.ПервичныйДокумент,
	|	ЗаписиУСНЗаписиКУДиР.ДатаПервичногоДокумента,
	|	ЗаписиУСНЗаписиКУДиР.НомерПервичногоДокумента,
	|	ЗаписиУСНЗаписиКУДиР.Ссылка.ВидЗаписей КАК ВидЗаписи,
	|	ЗаписиУСНЗаписиКУДиР.НомерСтроки Как НомерСтрокиДокумента
	|ИЗ
	|	Документ.ЗаписиУСН.ЗаписиКУДиР КАК ЗаписиУСНЗаписиКУДиР
	|ГДЕ
	|	ЗаписиУСНЗаписиКУДиР.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЗаписиУСНГрафикПоОС.ПериодПризнания КАК Период,
	|	&Организация,
	|	ЗаписиУСНГрафикПоОС.ОсновноеСредство,
	|	ЗаписиУСНГрафикПоОС.Сумма
	|ИЗ
	|	Документ.ЗаписиУСН.ГрафикПоОС КАК ЗаписиУСНГрафикПоОС
	|ГДЕ
	|	ЗаписиУСНГрафикПоОС.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("Период", ДокументСсылка.Дата);
	Запрос.УстановитьПараметр("Организация", ДокументСсылка.Организация);
	
	
	Результат = Запрос.ВыполнитьПакет();
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаКнигаУчетаДоходовИРасходов",Результат[0].Выгрузить());
	
	ДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаГрафикПризнанияРасходовНаОСиНМА",Результат[1].Выгрузить());
	
КонецПроцедуры

#КонецЕсли
