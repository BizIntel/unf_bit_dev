
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ПоказыватьПоВидамЗаписей = Настройки.Получить("ПоказыватьПоВидамЗаписей");
	Если ПоказыватьПоВидамЗаписей = Неопределено Тогда
		ПоказыватьПоВидамЗаписей = Ложь;
	КонецЕсли;
	ПостроитьДинамическийСписокОтВидовЗаписей();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМ

&НаКлиенте
Процедура ПоВидамЗаписей(Команда)
	
	ПоказыватьПоВидамЗаписей = Не ПоказыватьПоВидамЗаписей;
	
	ПостроитьДинамическийСписокОтВидовЗаписей();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// Процедура устанавливает режим группировки по видам записей
//
&НаСервере
Процедура ПостроитьДинамическийСписокОтВидовЗаписей()
	
	НайденноеПолеГруппировки = Неопределено;
	НайденноеПолеСортировки = Неопределено;
	НайденноеПолеСортировкиПериода = Неопределено;
	
	ПолеПоиска = Новый ПолеКомпоновкиДанных("ВидЗаписей");
	ПолеПоискаПериода = Новый ПолеКомпоновкиДанных("Дата");
	ПолеПоискаПериода1 = Новый ПолеКомпоновкиДанных("Date"); 
	
	Для Каждого ЭлементГруппировки Из Список.Группировка.Элементы Цикл
		
		Если ЭлементГруппировки.Поле =  ПолеПоиска Тогда
			НайденноеПолеГруппировки = ЭлементГруппировки;
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Для Каждого ЭлементСортировки Из Список.Порядок.Элементы Цикл
		
		Если ЭлементСортировки.Поле =  ПолеПоиска Тогда
			НайденноеПолеСортировки = ЭлементСортировки;
		ИначеЕсли ЭлементСортировки.Поле = ПолеПоискаПериода ИЛИ ЭлементСортировки.Поле =  ПолеПоискаПериода1 Тогда
			НайденноеПолеСортировкиПериода = ЭлементСортировки;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НайденноеПолеГруппировки = Неопределено Тогда
		НайденноеПолеГруппировки = Список.Группировка.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		НайденноеПолеГруппировки.Поле = ПолеПоиска;
	КонецЕсли;
	Если НайденноеПолеСортировкиПериода = Неопределено Тогда
		НайденноеПолеСортировкиПериода = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		НайденноеПолеСортировкиПериода.Поле = ПолеПоискаПериода;
		НайденноеПолеСортировкиПериода.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	КонецЕсли;
	Если НайденноеПолеСортировки = Неопределено Тогда
		НайденноеПолеСортировки = Список.Порядок.Элементы.Добавить(Тип("ЭлементПорядкаКомпоновкиДанных"));
		НайденноеПолеСортировки.Поле = ПолеПоиска;
		НайденноеПолеСортировки.ТипУпорядочивания = НаправлениеСортировкиКомпоновкиДанных.Возр;
	КонецЕсли;

	
	// передвигаем сортировку поля по видам записей наверх
	//
	Пока Список.Порядок.Элементы.Индекс(НайденноеПолеСортировки) <> 0 Цикл
		Список.Порядок.Элементы.Сдвинуть(НайденноеПолеСортировки,-1);
	КонецЦикла;
	
	НайденноеПолеСортировкиПериода.Использование = Истина;
	
	Если ПоказыватьПоВидамЗаписей Тогда
		НайденноеПолеГруппировки.Использование = Истина;
		НайденноеПолеСортировки.Использование = Истина;
		Элементы.ВидЗаписей.Видимость = Ложь;
	Иначе
		НайденноеПолеГруппировки.Использование = Ложь;
		НайденноеПолеСортировки.Использование = Ложь;
		Элементы.ВидЗаписей.Видимость = Истина;
	КонецЕсли;
	
	Элементы.ФормаПоВидамЗаписей.Пометка = ПоказыватьПоВидамЗаписей;
	
КонецПроцедуры

