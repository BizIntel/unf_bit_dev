////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ


&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Объект.Дата = КонецКвартала(ТекущаяДатаСеанса());
		Объект.ВидЗаписей = Перечисления.ВидыЗаписейФормированийКУДиР.РучныеЗаписи;
		ДокументыЗаПериод = Параметры.ДокументыЗаПериод;
	КонецЕсли;
	
	Если Объект.ВидЗаписей = Перечисления.ВидыЗаписейФормированийКУДиР.РучныеЗаписи Тогда
		// прочитаем ручные данные первой строки
		Если Объект.ЗаписиКУДиР.Количество() > 0 Тогда
			РабочаяСтрока = Объект.ЗаписиКУДиР[0];
			ДоходыВсего = РабочаяСтрока.ДоходВсего;
			Доходы = РабочаяСтрока.ДоходБаза;
			РасходыВсего = РабочаяСтрока.РасходВсего;
			Расходы = РабочаяСтрока.РасходБаза;
			ДатаПервичногоДокумента = РабочаяСтрока.ДатаПервичногоДокумента;
			НомерПервичногоДокумента = РабочаяСтрока.НомерПервичногоДокумента;
			Содержание = РабочаяСтрока.Содержание;
		КонецЕсли;
		
		Элементы.ГруппаПредупреждения.Видимость = Ложь;
	Иначе
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	Если Параметры.НомерСтроки > 0 Тогда
		
		Элементы.Строки.ТекущаяСтрока =Объект.ЗаписиКУДиР.Получить(Параметры.НомерСтроки-1).ПолучитьИдентификатор();
	КонецЕсли;
	
	УстановитьВидимостьИДоступностьЭлементов();
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)
	Если Объект.ВидЗаписей = Перечисления.ВидыЗаписейФормированийКУДиР.РучныеЗаписи Тогда
		ПроверяемыеРеквизиты.Добавить("НомерПервичногоДокумента");
		ПроверяемыеРеквизиты.Добавить("ДатаПервичногоДокумента");
		ПроверяемыеРеквизиты.Добавить("Содержание");
	КонецЕсли;
КонецПроцедуры



&НаКлиенте
Процедура ПриОткрытии(Отказ)
#Если ВебКлиент Тогда
	Элементы.Дата.Формат= "ДФ=dd.MM.yyyy";
	Элементы.Дата.ФорматРедактирования= "ДФ=dd.MM.yyyy";
#КонецЕсли
КонецПроцедуры


&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	Если ТекущийОбъект.ВидЗаписей = Перечисления.ВидыЗаписейФормированийКУДиР.РучныеЗаписи Тогда
		// сохраняем ручные данные первой строки
		ТекущийОбъект.Дата = КонецКвартала(ДатаПервичногоДокумента);
		ТекущийОбъект.ЗаписиКУДиР.Очистить();
		РабочаяСтрока = ТекущийОбъект.ЗаписиКУДиР.Добавить();
		РабочаяСтрока.ДоходВсего = ДоходыВсего;
		РабочаяСтрока.ДоходБаза = Доходы;
		РабочаяСтрока.РасходВсего = РасходыВсего;
		РабочаяСтрока.РасходБаза = Расходы;
		РабочаяСтрока.ДатаПервичногоДокумента = ДатаПервичногоДокумента;
		РабочаяСтрока.НомерПервичногоДокумента = НомерПервичногоДокумента;
		РабочаяСтрока.Содержание = Содержание;
		
	КонецЕсли;
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМ

&НаКлиенте
Процедура ДатаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Объект.Дата = КонецКвартала(ДобавитьМесяц(Объект.Дата, Направление*3));
	
КонецПроцедуры

&НаКлиенте
Процедура ВключитьФормуДляРедактирования(Команда)
	
	
	ТолькоПросмотр = Ложь;
	Элементы.ГруппаПредупреждения.Видимость = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокиДоходБазаПриИзменении(Элемент)
	
	Элементы.Строки.ТекущиеДанные.ДоходВсего = Элементы.Строки.ТекущиеДанные.ДоходБаза;
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокиРасходБазаПриИзменении(Элемент)
	
	Элементы.Строки.ТекущиеДанные.РасходВсего = Элементы.Строки.ТекущиеДанные.РасходБаза;
	
КонецПроцедуры

&НаКлиенте
Процедура Декорация2Нажатие(Элемент)
	
	ПоказатьПредупреждение(
			,
			НСтр("ru='Данные записи сгенерированы автоматически. 
					|Производить ручное изменение данного документа не рекомендуется. 
					|При повторном автоматическом формировании ручные корректировки будут утеряны.
					|Для включения возможности исправлений, нажмите  «Включить режим редактирования»'"));
	
КонецПроцедуры

&НаКлиенте
Процедура СтрокиВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если Поле.Имя = "СтрокиПервичныйДокумент" И ЭтаФорма.ТолькоПросмотр Тогда
		
		Если Элементы.Строки.ТекущиеДанные <> Неопределено Тогда
			Если Элементы.Строки.ТекущиеДанные.ПервичныйДокумент <> Неопределено Тогда
				СтандартнаяОбработка = Ложь;
				ПоказатьЗначение(,Элементы.Строки.ТекущиеДанные.ПервичныйДокумент);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтрокиДатаПервичногоДокументаПриИзменении(Элемент)
	
	Если Элементы.Строки.ТекущиеДанные.ДатаПервичногоДокумента >= НачалоКвартала(Объект.Дата)
		И Элементы.Строки.ТекущиеДанные.ДатаПервичногоДокумента <= КонецКвартала(Объект.Дата) Тогда
		Возврат;
	КонецЕсли;
	
	ПредставлениеПериода = ПредставлениеПериода(
		НачалоКвартала(Объект.Дата),
		КонецКвартала(Объект.Дата));
	
	ТекстПредупреждения = НСтр("ru='Дата должны быть в интервале '")+ПредставлениеПериода;
	
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		ТекстПредупреждения,
		,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			"Объект.ЗаписиКУДиР[%1].ДатаПервичногоДокумента",
			Элементы.Строки.ТекущиеДанные.НомерСтроки-1));
	
	Элементы.Строки.ТекущиеДанные.ДатаПервичногоДокумента = Неопределено;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоходыПриИзменении(Элемент)
	ДоходыВсего = Доходы;
КонецПроцедуры

&НаКлиенте
Процедура РасходыПриИзменении(Элемент)
	РасходыВсего = Расходы; 
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБЩИЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьВидимостьИДоступностьЭлементов() 
	
	Если Объект.ВидЗаписей = Перечисления.ВидыЗаписейФормированийКУДиР.РасходыНаОСиНМА Тогда
		Элементы.ГруппаГрафикиОСиНМА.Видимость = Истина;
		Элементы.ГруппаТаблицы.ОтображениеСтраниц = ОтображениеСтраницФормы.ЗакладкиСверху;
	Иначе
		Элементы.ГруппаГрафикиОСиНМА.Видимость = Ложь;
		Элементы.ГруппаТаблицы.ОтображениеСтраниц = ОтображениеСтраницФормы.Нет;
	КонецЕсли;
	
	Если Объект.ВидЗаписей = Перечисления.ВидыЗаписейФормированийКУДиР.РучныеЗаписи Тогда
		Элементы.ГруппаРежимы.ТекущаяСтраница = Элементы.ГруппаРучная;
		Элементы.СтрокиПервичныйДокумент.Видимость = Ложь;
	Иначе
		Элементы.ГруппаРежимы.ТекущаяСтраница = Элементы.ГруппаАвтозаписи;
		Элементы.СтрокиПервичныйДокумент.Видимость = Истина;
	КонецЕсли;
	
	
КонецПроцедуры




