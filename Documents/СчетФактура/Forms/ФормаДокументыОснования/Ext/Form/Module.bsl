
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.ДокументыОснованияДокументОснование.ОграничениеТипа = Параметры.ДопустимыеТипы;
	
	АдресДокументыОснованияВХранилище = Параметры.АдресДокументыОснованияВХранилище;
	
	ДокументыОснования.Загрузить(ПолучитьИзВременногоХранилища(АдресДокументыОснованияВХранилище));
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Отказ = Ложь;
	
	ПроверитьЗаполнениеРеквизитовФормы(Отказ);
	
	Если НЕ Отказ Тогда
		ЗаписатьДокументыОснованияВХранилище();
		Закрыть(КодВозвратаДиалога.OK);
	КонецЕсли;

КонецПроцедуры

// Процедура помещает результаты подбора в хранилище.
//
&НаСервере
Процедура ЗаписатьДокументыОснованияВХранилище()
	
	ДокументыОснованияВХранилище = ДокументыОснования.Выгрузить(, "ДокументОснование");
	ПоместитьВоВременноеХранилище(ДокументыОснованияВХранилище, АдресДокументыОснованияВХранилище);
	
КонецПроцедуры // ЗаписатьПодборВХранилище()

// Процедура проверяет правильность заполнения реквизитов формы.
//
&НаКлиенте
Процедура ПроверитьЗаполнениеРеквизитовФормы(Отказ)
	
	// Проверка заполненности реквизитов.
	НомерСтроки = 0;
		
	Для каждого СтрокаДокументыОснования Из ДокументыОснования Цикл
		НомерСтроки = НомерСтроки + 1;
		Если НЕ ЗначениеЗаполнено(СтрокаДокументыОснования.ДокументОснование) Тогда
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = НСтр("ru = 'Не заполнена колонка ""Документ основание"" в строке '")
				+ Строка(НомерСтроки)
				+ НСтр("ru = ' списка ""Документы основания"".'");
			Сообщение.Поле = "Документ";
			Сообщение.Сообщить();
			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры // ПроверитьЗаполнениеРеквизитовФормы()

&НаСервере
Функция ДокументыОснованияДокументОснованиеОбработкаВыбораНаСервере(ВыбранноеЗначение)
	
	Если ДокументыОснования.Количество() = 0 Тогда
		Возврат Ложь;
	КонецЕсли;
	
	ВалютаДляОтбора = Неопределено;
	КонтрагентДляОтбора = Неопределено;
	ДоговорДляОтбора = Неопределено;
	ВидОперацииДляОтбора = Неопределено;
	
	Для каждого ТекСтрока Из ДокументыОснования Цикл
		Если ТекСтрока = ДокументыОснования.НайтиПоИдентификатору(Элементы.ДокументыОснования.ТекущаяСтрока) Тогда
			Продолжить;
		КонецЕсли;
		Если (ЗначениеЗаполнено(ТекСтрока.ДокументОснование))
			И (ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.РасходнаяНакладная")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомиссионера")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.АктВыполненныхРабот")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.ОтчетОПереработке")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.ЗаказПокупателя")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.СчетНаОплату")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.ОтчетКомитенту")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации"))
			И ЗначениеЗаполнено(ТекСтрока.ДокументОснование.ВалютаДокумента) Тогда
			КонтрагентДляОтбора = ТекСтрока.ДокументОснование.Контрагент;
			ДоговорДляОтбора = ТекСтрока.ДокументОснование.Договор;
			ВалютаДляОтбора = ТекСтрока.ДокументОснование.ВалютаДокумента;
			Если ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.КорректировкаРеализации") Тогда
				ВидОперацииДляОтбора = ТекСтрока.ДокументОснование.ВидОперации;
			КонецЕсли;
		КонецЕсли;
		Если (ЗначениеЗаполнено(ТекСтрока.ДокументОснование))
			И (ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеВКассу")
			ИЛИ ТипЗнч(ТекСтрока.ДокументОснование) = Тип("ДокументСсылка.ПоступлениеНаСчет")) Тогда
			КонтрагентДляОтбора = ТекСтрока.ДокументОснование.Контрагент;
			ВалютаДляОтбора = ТекСтрока.ДокументОснование.ВалютаДенежныхСредств;
		КонецЕсли;
	КонецЦикла;
	
	Если НЕ ЗначениеЗаполнено(ВалютаДляОтбора)
		И НЕ ЗначениеЗаполнено(КонтрагентДляОтбора)
		И НЕ ЗначениеЗаполнено(ДоговорДляОтбора) Тогда
		Возврат Ложь;
	Конецесли;
	
	Если (ЗначениеЗаполнено(ВыбранноеЗначение))
		И (ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.РасходнаяНакладная")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОтчетКомиссионера")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.АктВыполненныхРабот")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОтчетОПереработке")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ЗаказПокупателя")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.СчетНаОплату")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ОтчетКомитенту")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.КорректировкаРеализации")) Тогда
		Возврат ВалютаДляОтбора <> ВыбранноеЗначение.ВалютаДокумента
			ИЛИ КонтрагентДляОтбора <> ВыбранноеЗначение.Контрагент
			ИЛИ ДоговорДляОтбора <> ВыбранноеЗначение.Договор
			ИЛИ (ВидОперацииДляОтбора <> Неопределено И ВидОперацииДляОтбора <> ВыбранноеЗначение.ВидОперации);
	КонецЕсли;
	
	Если (ЗначениеЗаполнено(ВыбранноеЗначение))
		И (ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ПоступлениеВКассу")
		ИЛИ ТипЗнч(ВыбранноеЗначение) = Тип("ДокументСсылка.ПоступлениеНаСчет")) Тогда
		Возврат ВалютаДляОтбора <> ВыбранноеЗначение.ВалютаДенежныхСредств
			ИЛИ КонтрагентДляОтбора <> ВыбранноеЗначение.Контрагент;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

&НаКлиенте
Процедура ДокументыОснованияДокументОснованиеОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	
	Отказ = ДокументыОснованияДокументОснованиеОбработкаВыбораНаСервере(ВыбранноеЗначение);
	
	Если Отказ Тогда
		ТекстСообщения = НСтр("ru='Контрагент, договор, валюта или операция выбираемого документа отличается от выбранных ранее документов.'");
		ОчиститьСообщения();
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
	
КонецПроцедуры
