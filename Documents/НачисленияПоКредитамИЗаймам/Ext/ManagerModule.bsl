#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаНачисленияПоКредитамИЗаймам, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("НачисленыПроцентыПоКредиту", НСтр("ru='Начислены проценты по кредиту (полученному)'"));
	Запрос.УстановитьПараметр("НачисленаКомиссияПоКредиту", НСтр("ru='Начислена комиссия по кредиту (полученному))'"));
	Запрос.УстановитьПараметр("НачисленыПроцентыПоЗайму", НСтр("ru='Начислены проценты по займу (выданному)'"));
	Запрос.УстановитьПараметр("НачисленаКомиссияПоЗайму", НСтр("ru='Начислена комиссия по займу (выданному))'"));
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачисленияПоКредитамИЗаймам);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|		ИНАЧЕ ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|	КОНЕЦ КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоКредитам)
	|			ТОГДА НачисленияПоКредитамИЗаймамНачисления.Контрагент
	|		ИНАЧЕ НачисленияПоКредитамИЗаймамНачисления.Сотрудник
	|	КОНЕЦ КАК Контрагент,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.КредитПолученный)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыДоговоровКредитаИЗайма.ДоговорЗаймаСотруднику)
	|	КОНЕЦ КАК ВидДоговора,
	|	НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма КАК ДоговорКредитаЗайма,
	|	НачисленияПоКредитамИЗаймамНачисления.Дата КАК Период,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА НачисленияПоКредитамИЗаймамНачисления.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК ПроцентыВал,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА НачисленияПоКредитамИЗаймамНачисления.Сумма
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КомиссияВал,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|			ТОГДА ВЫРАЗИТЬ(НачисленияПоКредитамИЗаймамНачисления.Сумма * КурсыВалютУчета.Кратность * КурсыВалютРасчетов.Курс / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Проценты,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|			ТОГДА ВЫРАЗИТЬ(НачисленияПоКредитамИЗаймамНачисления.Сумма * КурсыВалютУчета.Кратность * КурсыВалютРасчетов.Курс / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК Комиссия,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоКредитам)
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|						ТОГДА &НачисленаКомиссияПоКредиту
	|					ИНАЧЕ &НачисленыПроцентыПоКредиту
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Комиссия)
	|					ТОГДА &НачисленаКомиссияПоЗайму
	|				ИНАЧЕ &НачисленыПроцентыПоЗайму
	|			КОНЕЦ
	|	КОНЕЦ КАК СодержаниеПроводки
	|ИЗ
	|	Документ.НачисленияПоКредитамИЗаймам.Начисления КАК НачисленияПоКредитамИЗаймамНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачисленияПоКредитамИЗаймамНачисления.ВалютаРасчетов = КурсыВалютРасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|ГДЕ
	|	НачисленияПоКредитамИЗаймамНачисления.Ссылка = &Ссылка";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыПоКредитамИЗаймам", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРасчетыСПокупателями()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаНачисленияПоКредитамИЗаймам, СтруктураДополнительныеСвойства) Экспорт

	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	НачисленияПоКредитамИЗаймамНачисления.Ссылка,
	|	НачисленияПоКредитамИЗаймамНачисления.НомерСтроки,
	|	НачисленияПоКредитамИЗаймамНачисления.ТипСуммы,
	|	НачисленияПоКредитамИЗаймамНачисления.Дата,
	|	НачисленияПоКредитамИЗаймамНачисления.Сотрудник,
	|	НачисленияПоКредитамИЗаймамНачисления.Контрагент,
	|	НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма,
	|	НачисленияПоКредитамИЗаймамНачисления.ВалютаРасчетов,
	|	НачисленияПоКредитамИЗаймамНачисления.Сумма КАК СуммаВал,
	|	ВЫРАЗИТЬ(НачисленияПоКредитамИЗаймамНачисления.Сумма * КурсыВалютУчета.Кратность * КурсыВалютРасчетов.Курс / (КурсыВалютУчета.Курс * КурсыВалютРасчетов.Кратность) КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.Ссылка.ВидОперации = &НачисленияПоКредитам
	|			ТОГДА НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетЗатрат
	|		ИНАЧЕ ВЫБОР
	|				КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|					ТОГДА НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|				ИНАЧЕ НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|			КОНЕЦ
	|	КОНЕЦ КАК СчетЗатрат,
	|	НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетЗатрат КАК СчетУчетаДоходовИРасходов,
	|	НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.Заказ КАК ЗаказПокупателя,
	|	ВЫБОР
	|		КОГДА НачисленияПоКредитамИЗаймамНачисления.Ссылка.ВидОперации = &НачисленияПоКредитам
	|			ТОГДА ВЫБОР
	|					КОГДА НачисленияПоКредитамИЗаймамНачисления.ТипСуммы = ЗНАЧЕНИЕ(Перечисление.ТипыСуммГрафикаКредитовИЗаймов.Проценты)
	|						ТОГДА НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетУчетаПроцентов
	|					ИНАЧЕ НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетУчетаКомиссии
	|				КОНЕЦ
	|		ИНАЧЕ НачисленияПоКредитамИЗаймамНачисления.ДоговорКредитаЗайма.СчетЗатрат
	|	КОНЕЦ КАК Корреспонденция
	|ПОМЕСТИТЬ Начисления
	|ИЗ
	|	Документ.НачисленияПоКредитамИЗаймам.Начисления КАК НачисленияПоКредитамИЗаймамНачисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыВалютРасчетов
	|		ПО НачисленияПоКредитамИЗаймамНачисления.ВалютаРасчетов = КурсыВалютРасчетов.Валюта
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта = &ВалютаУчета) КАК КурсыВалютУчета
	|		ПО (КурсыВалютУчета.Валюта = &ВалютаУчета)
	|ГДЕ
	|	НачисленияПоКредитамИЗаймамНачисления.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|				ИЛИ Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПроцентыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|		ИНАЧЕ Начисления.ДоговорКредитаЗайма.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|				ИЛИ Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПроцентыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		ИНАЧЕ Начисления.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	Начисления.СчетУчетаДоходовИРасходов КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА Начисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоЗаймамСотрудникам)
	|			ТОГДА Начисления.Сотрудник
	|		ИНАЧЕ Начисления.Контрагент
	|	КОНЕЦ КАК Аналитика,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|				ИЛИ Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПроцентыПоКредитам)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Начисления.ЗаказПокупателя
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	0 КАК СуммаДоходов,
	|	Начисления.Сумма КАК СуммаРасходов,
	|	Начисления.Сумма КАК Сумма,
	|	&СодержаниеПроводкиПрочихДоходовИРасходов КАК СодержаниеПроводки
	|ИЗ
	|	Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка.ВидОперации = &НачисленияПоКредитам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|				ИЛИ Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПроцентыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|		ИНАЧЕ Начисления.ДоговорКредитаЗайма.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|				ИЛИ Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПроцентыПоКредитам)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|		ИНАЧЕ Начисления.СтруктурнаяЕдиница
	|	КОНЕЦ КАК СтруктурнаяЕдиница,
	|	Начисления.СчетУчетаДоходовИРасходов КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА Начисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоЗаймамСотрудникам)
	|			ТОГДА Начисления.Сотрудник
	|		ИНАЧЕ Начисления.Контрагент
	|	КОНЕЦ КАК Аналитика,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|				ИЛИ Начисления.СчетЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПроцентыПоКредитам)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ Начисления.ЗаказПокупателя
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	Начисления.Сумма КАК СуммаДоходов,
	|	0 КАК СуммаРасходов,
	|	Начисления.Сумма КАК Сумма,
	|	&СодержаниеПроводкиПрочихДоходовИРасходов КАК СодержаниеПроводки
	|ИЗ
	|	Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Ссылка.ВидОперации <> &НачисленияПоКредитам
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки КАК НомерСтроки,
	|	Начисления.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	Начисления.СчетЗатрат КАК СчетДт,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.Валютный
	|			ТОГДА Начисления.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.Валютный
	|			ТОГДА Начисления.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	Начисления.Корреспонденция КАК СчетКт,
	|	ВЫБОР
	|		КОГДА Начисления.Корреспонденция.Валютный
	|			ТОГДА Начисления.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА Начисления.Корреспонденция.Валютный
	|			ТОГДА Начисления.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	Начисления.Сумма КАК Сумма,
	|	&СодержаниеПроводкиПрочихДоходовИРасходов КАК Содержание
	|ИЗ
	|	Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сумма > 0
	|	И Начисления.Ссылка.ВидОперации = &НачисленияПоКредитам
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Начисления.НомерСтроки,
	|	Начисления.Ссылка.Дата,
	|	&Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	Начисления.СчетЗатрат,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.Валютный
	|			ТОГДА Начисления.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Начисления.СчетЗатрат.Валютный
	|			ТОГДА Начисления.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	Начисления.Корреспонденция,
	|	ВЫБОР
	|		КОГДА Начисления.Корреспонденция.Валютный
	|			ТОГДА Начисления.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Начисления.Корреспонденция.Валютный
	|			ТОГДА Начисления.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	Начисления.Сумма,
	|	&СодержаниеПроводкиПрочихДоходовИРасходов
	|ИЗ
	|	Начисления КАК Начисления
	|ГДЕ
	|	Начисления.Сумма > 0
	|	И Начисления.Ссылка.ВидОперации <> &НачисленияПоКредитам");
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаНачисленияПоКредитамИЗаймам);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.Дляпроведения.Организация);
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("НачисленияПоКредитам", Перечисления.ВидыОперацийНачисленияПоКредитамИЗаймам.НачисленияПоКредитам);
	
	Запрос.УстановитьПараметр("СодержаниеПроводкиПрочихДоходовИРасходов", НСтр("ru = 'Расчеты по кредитам и займам'"));
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", МассивРезультатов[1].Выгрузить());
	
	Если СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", МассивРезультатов[2].Выгрузить());
	Иначе
		
		Выборка = МассивРезультатов[2].Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаДоходыИРасходы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаУправленческий", МассивРезультатов[3].Выгрузить());
	
	// РасчетыПоКредитамИЗаймам
	СформироватьТаблицаРасчетыПоКредитамИЗаймам(ДокументСсылкаНачисленияПоКредитамИЗаймам, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
