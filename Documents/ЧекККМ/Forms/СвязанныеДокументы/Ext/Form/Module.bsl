#Область ОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере формы.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АдресВХранилищеСвязанныхДокументов = Параметры.АдресВХранилищеСвязанныхДокументов;
	СвязанныеДокументы.Загрузить(ПолучитьИзВременногоХранилища(АдресВХранилищеСвязанныхДокументов));
	
	Если СвязанныеДокументы.Количество() = 0 Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии формы.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если СвязанныеДокументы.Количество() = 1 Тогда
		ТекДокумент = СвязанныеДокументы[0].СвязанныйДокумент;
		Если ЗначениеЗаполнено(ТекДокумент) Тогда
			ОткрытьДокумент(ТекДокумент);
		КонецЕсли;
		
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды ОК формы.
//
&НаКлиенте
Процедура ОК(Команда)
	
	Если Элементы.СвязанныеДокументы.ТекущиеДанные <> Неопределено Тогда
		ТекДокумент = Элементы.СвязанныеДокументы.ТекущиеДанные.СвязанныйДокумент;
		Если ЗначениеЗаполнено(ТекДокумент) Тогда
			ОткрытьДокумент(ТекДокумент);
		КонецЕсли;
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыИФункцииОбщегоНазначения

// Процедура - обработчик события Выбор ТЧ СвязанныеДокументы формы.
//
&НаКлиенте
Процедура СвязанныеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ТекДокумент = СвязанныеДокументы[ВыбраннаяСтрока].СвязанныйДокумент;
	Если ЗначениеЗаполнено(ТекДокумент) Тогда
		ОткрытьДокумент(ТекДокумент);
	КонецЕсли;
	
	Закрыть();
	
КонецПроцедуры

// Процедура анализирует тип значения документы и открывает его форму.
//
&НаКлиенте
Процедура ОткрытьДокумент(ТекДокумент)
	
	Если ТипЗнч(ТекДокумент) = Тип("ДокументСсылка.РасходИзКассы") Тогда
		ОткрытьФорму("Документ.РасходИзКассы.ФормаОбъекта", Новый Структура("Ключ", ТекДокумент));
	ИначеЕсли ТипЗнч(ТекДокумент) = Тип("ДокументСсылка.ПриходнаяНакладная") Тогда
		ОткрытьФорму("Документ.ПриходнаяНакладная.ФормаОбъекта", Новый Структура("Ключ", ТекДокумент));
	ИначеЕсли ТипЗнч(ТекДокумент) = Тип("ДокументСсылка.ЧекККМВозврат") Тогда
		ОткрытьФорму("Документ.ЧекККМВозврат.ФормаОбъекта", Новый Структура("Ключ", ТекДокумент));
	ИначеЕсли ТипЗнч(ТекДокумент) = Тип("ДокументСсылка.ЧекККМ") Тогда
		ОткрытьФорму("Документ.ЧекККМ.ФормаОбъекта", Новый Структура("Ключ", ТекДокумент));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
