#Область ПроцедурыИФункцииОбщегоНазначения

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет документ чек ККМ по кассе ККМ.
//
// Параметры
//  ДанныеЗаполнения - Структура со значениями отбора
//
&НаСервере
Процедура ЗаполнитьДокументПоКассеККМ(КассаККМ, ЗначенияЗаполнения = Неопределено)
	
	СостояниеКассовойСмены = РозничныеПродажиСервер.ПолучитьСостояниеКассовойСмены(КассаККМ);
	ЗаполнитьЗначенияСвойств(Объект, СостояниеКассовойСмены);
	
	Если ЭквайринговыйТерминал.Пустая() ИЛИ КассаККМ <> ЭквайринговыйТерминал.Касса Тогда
		Если ЗначенияЗаполнения = Неопределено Тогда
			Объект.ЭквайринговыйТерминал = Справочники.ЭквайринговыеТерминалы.ПолучитьЭквайринговыйТерминалПоУмолчанию(Объект.КассаККМ);
			ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
		Иначе
			Объект.ЭквайринговыйТерминал = ЗначенияЗаполнения.ЭквайринговыйТерминал;
			ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьДокументПоОтбору()

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийФормы

// Процедура - обработчик события ПриСозданииНаСервере.
//
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Объект.КассаККМ = Параметры.ЗначенияЗаполнения.КассаККМ;
	Если Объект.КассаККМ <> Неопределено Тогда
		ЗаполнитьДокументПоКассеККМ(Объект.КассаККМ, Параметры.ЗначенияЗаполнения);
	КонецЕсли;
	
	РабочееМесто = МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента();
	Если НЕ ЗначениеЗаполнено(РабочееМесто) Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Не удалось определить рабочее место для работы с подключаемым оборудованием!";
		Сообщение.Сообщить();
	КонецЕсли;
		
	НастройкаРМК = РабочееМестоКассираВызовСервера.ПолучитьНастройкуРМК(РабочееМесто);
	Если Объект.КассаККМ.Пустая() Тогда
		Если Не ЗначениеЗаполнено(НастройкаРМК) Тогда
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = "Не удалось получить настройки РМК для текущего рабочего места!";
			Сообщение.Сообщить();
		Иначе
			НеПоказыватьПриОткрытииФормуВыбораКассы = НастройкаРМК.НеПоказыватьПриОткрытииФормуВыбораКассы;
			СверятьИтогиНаЭТПриЗакрытииСмены = НастройкаРМК.СверятьИтогиНаЭТПриЗакрытииСмены;
		КонецЕсли;
	Иначе
		Если Параметры.ЗначенияЗаполнения.КоличествоЭквайринговыхТерминалов < 2 Тогда
			НеПоказыватьПриОткрытииФормуВыбораКассы = Истина;
			СверятьИтогиНаЭТПриЗакрытииСмены = Ложь;
		Иначе
			НеПоказыватьПриОткрытииФормуВыбораКассы = НастройкаРМК.НеПоказыватьПриОткрытииФормуВыбораКассы;
			СверятьИтогиНаЭТПриЗакрытииСмены = НастройкаРМК.СверятьИтогиНаЭТПриЗакрытииСмены;
		КонецЕсли;
	КонецЕсли;
	
	КассаККМ = Объект.КассаККМ;
	ЭквайринговыйТерминал = Объект.ЭквайринговыйТерминал;
	
КонецПроцедуры

// Процедура - обработчик события ПриОткрытии.
//
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НеПоказыватьПриОткрытииФормуВыбораКассы И Не Объект.КассаККМ.Пустая() Тогда
		ОткрытьРабочееМестоКассира(Команды.ОткрытьРабочееМестоКассира);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ПриЗагрузкеДанныхИзНастроекНаСервере.
//
&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ТекКассаККМ = Настройки.Получить("КассаККМ");
	Если ЗначениеЗаполнено(ТекКассаККМ) Тогда
		КассаККМ = ТекКассаККМ;
		Объект.КассаККМ = КассаККМ;
	КонецЕсли;
	
	ТекЭквайринговыйТерминал = Настройки.Получить("ЭквайринговыйТерминал");
	Если ЗначениеЗаполнено(ТекЭквайринговыйТерминал) Тогда
		ЭквайринговыйТерминал = ТекЭквайринговыйТерминал;
		Объект.ЭквайринговыйТерминал = ЭквайринговыйТерминал;
	КонецЕсли;
	
	ЗаполнитьДокументПоКассеККМ(Объект.КассаККМ);
	
КонецПроцедуры

// Процедура - обработчик события ПриСохраненииДанныхВНастройкахНаСервере.
//
&НаСервере
Процедура ПриСохраненииДанныхВНастройкахНаСервере(Настройки)
	
	Если Не ЗакрываемФормуПослеОткрытияРМК Тогда
		Настройки.Очистить();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик команды ОткрытьРабочееМестоКассира формы.
//
&НаКлиенте
Процедура ОткрытьРабочееМестоКассира(Команда)
	
	Если Объект.КассаККМ.Пустая() Тогда
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = "Выберите рабочее место кассира";
		Сообщение.Поле = "КассаККМ";
		Сообщение.Сообщить();
		Возврат;
	КонецЕсли;
	
	ЗакрываемФормуПослеОткрытияРМК = Истина;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Организация", Объект.Организация);
	ЗначенияЗаполнения.Вставить("КассаККМ", Объект.КассаККМ);
	ЗначенияЗаполнения.Вставить("СтруктурнаяЕдиница", Объект.СтруктурнаяЕдиница);
	ЗначенияЗаполнения.Вставить("ЭквайринговыйТерминал", Объект.ЭквайринговыйТерминал);
	
	РабочееМестоКассираВызовСервера.ОбновитьНастройкиРМК(
	НастройкаРМК,
	НеПоказыватьПриОткрытииФормуВыбораКассы,
	СверятьИтогиНаЭТПриЗакрытииСмены);
	
	ОткрытьФорму(
	"Документ.ЧекККМ.Форма.ФормаДокумента_РМК",
	Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));
	Закрыть();
	
КонецПроцедуры

#КонецОбласти

#Область ПроцедурыОбработчикиСобытийЭлементовФормы

// Процедура - обработчик события ПриИзменении элемента КассаККМ формы.
//
&НаКлиенте
Процедура КассаККМПриИзменении(Элемент)
	
	Объект.КассаККМ = КассаККМ;
	
	ЗаполнитьДокументПоКассеККМ(Объект.КассаККМ);
	
КонецПроцедуры

// Процедура - обработчик события ПриИзменении элемента ЭквайринговыйТерминал формы.
//
&НаКлиенте
Процедура ЭквайринговыйТерминалПриИзменении(Элемент)
	
	Объект.ЭквайринговыйТерминал = ЭквайринговыйТерминал;
	
КонецПроцедуры

#КонецОбласти

