#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.Операция.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	УправлениеНебольшойФирмойСервер.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УчетВалютныхОпераций = Константы.ФункциональнаяУчетВалютныхОпераций.Получить();
	
	Если УчетВалютныхОпераций Тогда
		Для каждого СтрокаТЧ Из Проводки Цикл
			Если СтрокаТЧ.СчетДт.Валютный
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.ВалютаДт) Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Валюта Дт"" для валютного счета в строке %НомерСтроки% списка ""Проводки"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"Проводки",
					СтрокаТЧ.НомерСтроки,
					"ВалютаДт",
					Отказ
				);
			КонецЕсли;
			Если СтрокаТЧ.СчетДт.Валютный
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.СуммаВалДт) Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Сумма (вал.) Дт"" для валютного счета в строке %НомерСтроки% списка ""Проводки"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"Проводки",
					СтрокаТЧ.НомерСтроки,
					"СуммаВалДт",
					Отказ
				);
			КонецЕсли;
			Если СтрокаТЧ.СчетКт.Валютный
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.ВалютаКт) Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Валюта Кт"" для валютного счета в строке %НомерСтроки% списка ""Проводки"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"Проводки",
					СтрокаТЧ.НомерСтроки,
					"ВалютаКт",
					Отказ
				);
			КонецЕсли;
			Если СтрокаТЧ.СчетКт.Валютный
			И НЕ ЗначениеЗаполнено(СтрокаТЧ.СуммаВалКт) Тогда
				ТекстСообщения = НСтр("ru = 'Не заполнена колонка ""Сумма (вал.) Кт"" для валютного счета в строке %НомерСтроки% списка ""Проводки"".'");
				ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", Строка(СтрокаТЧ.НомерСтроки));
				УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
					ЭтотОбъект,
					ТекстСообщения,
					"Проводки",
					СтрокаТЧ.НомерСтроки,
					"СуммаВалКт",
					Отказ
				);
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если НЕ Константы.ФункциональнаяУчетВалютныхОпераций.Получить() Тогда
		Для каждого СтрокаТабличнойЧасти Из Проводки Цикл
			Если СтрокаТабличнойЧасти.СчетДт.Валютный Тогда
				СтрокаТабличнойЧасти.ВалютаДт = Константы.НациональнаяВалюта.Получить();
				СтрокаТабличнойЧасти.СуммаВалДт = СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
			Если СтрокаТабличнойЧасти.СчетКт.Валютный Тогда
				СтрокаТабличнойЧасти.ВалютаКт = Константы.НациональнаяВалюта.Получить();
				СтрокаТабличнойЧасти.СуммаВалКт = СтрокаТабличнойЧасти.Сумма;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СуммаДокумента = Проводки.Итог("Сумма");
	
КонецПроцедуры // ПередЗаписью()

#КонецОбласти

#КонецЕсли