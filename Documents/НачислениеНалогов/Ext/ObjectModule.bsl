#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям.Получить() Тогда
		
		Для каждого СтрокаНалоги Из Налоги Цикл
			
			Если СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.НезавершенноеПроизводство
			 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.КосвенныеЗатраты
			 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Доходы
			 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Расходы Тогда
				СтрокаНалоги.Подразделение = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности.Получить() Тогда
		
		Для каждого СтрокаНалоги Из Налоги Цикл
			Если СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Доходы
			 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Расходы Тогда
				СтрокаНалоги.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ОсновноеНаправление;
			КонецЕсли;
		КонецЦикла;
			
	КонецЕсли;
	
	СуммаДокумента = Налоги.Итог("Сумма");
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого СтрокаНалоги Из Налоги Цикл
		
		Если Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям.Получить()
		   И (СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.НезавершенноеПроизводство
		 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.КосвенныеЗатраты
		 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Доходы
		 ИЛИ СтрокаНалоги.Корреспонденция.ТипСчета = Перечисления.ТипыСчетов.Расходы)
		 И НЕ ЗначениеЗаполнено(СтрокаНалоги.Подразделение) Тогда
			ТекстСообщения = НСтр(
				"ru = 'Для счета затрат ""%Корреспонденция%"" указанного в строке %НомерСтроки% списка ""Налоги"", должен быть заполнен реквизит ""Подразделение"".'"
			);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Корреспонденция%", СокрЛП(Строка(СтрокаНалоги.Корреспонденция))); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%",Строка(СтрокаНалоги.НомерСтроки));
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Налоги",
				СтрокаНалоги.НомерСтроки,
				"Подразделение",
				Отказ
			);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения. Формирует движения
// документа по регистрам накопления и бухгалтерскому регистру.
//
// 1. Удаление существующих движений документа.
// 2. Формирование структуры шапки документа с полями, используемыми алгоритмах
// проведения документа.
// 3. Проверка заполнения значений шапки и табличных частей документа.
// 4. Формирование временной таблицы по документу, необходимой для формирования
// движений.
// 5. Формирование движений документа по регистрам накопления.
// 6. Формирование движений документа по регистру бухгалтерии.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.НачислениеНалогов.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	УправлениеНебольшойФирмойСервер.ОтразитьРасчетыПоНалогам(ДополнительныеСвойства, Движения, Отказ);
	УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходы(ДополнительныеСвойства, Движения, Отказ);
	УправлениеНебольшойФирмойСервер.ОтразитьЗапасы(ДополнительныеСвойства, Движения, Отказ);
	УправлениеНебольшойФирмойСервер.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

#КонецОбласти

#КонецЕсли