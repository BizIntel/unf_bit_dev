#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Выполняет контроль противоречий.
//
Процедура ВыполнитьПредварительныйКонтроль(Отказ)
	
	ТаблицаСотрудники 			= Сотрудники.Выгрузить(,"НомерСтроки, Сотрудник, Период, КлючСвязи");
	ТаблицаНачисленияУдержания 	= НачисленияУдержания.Выгрузить(,"НомерСтроки, ВидНачисленияУдержания, Валюта, КлючСвязи");
	
	//Добавим колонки и заполним по ключу связи. 
	//Связь должна присутствовать обязательно и соответствовать только одному сотруднику.
	ТаблицаНачисленияУдержания.Колонки.Добавить("Сотрудник", Новый ОписаниеТипов("СправочникСсылка.Сотрудники"));
	ТаблицаНачисленияУдержания.Колонки.Добавить("Период", Новый ОписаниеТипов("Дата"));
	
	Для каждого СтрокаНачисленияУдержания Из ТаблицаНачисленияУдержания Цикл
		
		МассивСтрокСотрудников = ТаблицаСотрудники.НайтиСтроки(Новый Структура("КлючСвязи", СтрокаНачисленияУдержания.КлючСвязи));
		
		Если МассивСтрокСотрудников.Количество() = 1 Тогда
			
			СтрокаНачисленияУдержания.Сотрудник	= МассивСтрокСотрудников[0].Сотрудник;
			СтрокаНачисленияУдержания.Период	= МассивСтрокСотрудников[0].Период;
			
		Иначе
			
			//Ошибочная связь, не должна встречаться, но проверку оставим
			ТекстСообщения = НСтр("ru = 'Не верное условие связи в строке №%Номер% табл. части ""Начислений и удержаний"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", СтрокаНачисленияУдержания.НомерСтроки); 
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"НачисленияУдержания",
				 СтрокаНачисленияУдержания.НомерСтроки,
				"НомерСтроки",
				Отказ);
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ 
	|	ПриемНаРаботуСотрудники.НомерСтроки,
	|	ПриемНаРаботуСотрудники.Сотрудник,
	|	ПриемНаРаботуСотрудники.Период,
	|	ПриемНаРаботуСотрудники.КлючСвязи
	|ПОМЕСТИТЬ ТаблицаСотрудники
	|ИЗ
	|	&ТаблицаСотрудники КАК ПриемНаРаботуСотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаНачисленияУдержания.НомерСтроки,
	|	ТаблицаНачисленияУдержания.ВидНачисленияУдержания,
	|	ТаблицаНачисленияУдержания.Валюта,
	|	ТаблицаНачисленияУдержания.Сотрудник,
	|	ТаблицаНачисленияУдержания.Период
	|ПОМЕСТИТЬ ТаблицаНачисленияУдержания
	|ИЗ
	|	&ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаСотрудники.НомерСтроки КАК НомерСтроки,
	|	Сотрудники.Регистратор
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|		ПО (Сотрудники.Организация = &Организация)
	|			И ТаблицаСотрудники.Сотрудник = Сотрудники.Сотрудник
	|			И ТаблицаСотрудники.Период = Сотрудники.Период
	|			И (Сотрудники.Регистратор <> &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ТаблицаНачисленияУдержания.НомерСтроки КАК НомерСтроки,
	|	ПлановыеНачисленияИУдержания.Регистратор
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияИУдержания КАК ПлановыеНачисленияИУдержания
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаНачисленияУдержания КАК ТаблицаНачисленияУдержания
	|		ПО (ПлановыеНачисленияИУдержания.Организация = &Организация)
	|			И ПлановыеНачисленияИУдержания.Сотрудник = ТаблицаНачисленияУдержания.Сотрудник
	|			И ПлановыеНачисленияИУдержания.ВидНачисленияУдержания = ТаблицаНачисленияУдержания.ВидНачисленияУдержания
	|			И ПлановыеНачисленияИУдержания.Валюта = ТаблицаНачисленияУдержания.Валюта
	|			И ПлановыеНачисленияИУдержания.Период = ТаблицаНачисленияУдержания.Период
	|			И (ПлановыеНачисленияИУдержания.Регистратор <> &Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	МАКСИМУМ(ТаблицаСотрудникиДублиСтрок.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаСотрудникиДублиСтрок.Сотрудник
	|ИЗ
	|	ТаблицаСотрудники КАК ТаблицаСотрудники
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСотрудники КАК ТаблицаСотрудникиДублиСтрок
	|		ПО ТаблицаСотрудники.НомерСтроки <> ТаблицаСотрудникиДублиСтрок.НомерСтроки
	|			И ТаблицаСотрудники.Сотрудник = ТаблицаСотрудникиДублиСтрок.Сотрудник
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаСотрудникиДублиСтрок.Сотрудник
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки");
	
	
	Запрос.УстановитьПараметр("Ссылка", 					Ссылка);
	Запрос.Параметры.Вставить("Организация", 				УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	Запрос.Параметры.Вставить("ТаблицаСотрудники", 			ТаблицаСотрудники);
	Запрос.Параметры.Вставить("ТаблицаНачисленияУдержания", ТаблицаНачисленияУдержания);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Регистр "Сотрудники".
	Если НЕ МассивРезультатов[2].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%Номер% табл. части ""Сотрудники"" период действия приказа противоречит кадровому приказу ""%КадровыйПриказ%"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КадровыйПриказ%", ВыборкаИзРезультатаЗапроса.Регистратор);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Период",
				Отказ);
		КонецЦикла;
	КонецЕсли;

	// Регистр "Плановые начисления и удержания".
	Если НЕ МассивРезультатов[3].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%Номер% табл. части ""Начисления и удержания"" период действия приказа противоречит кадровому приказу ""%КадровыйПриказ%"".'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%КадровыйПриказ%", ВыборкаИзРезультатаЗапроса.Регистратор);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"НачисленияУдержания",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"ВидНачисленияУдержания",
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
	// Дубли строк.
	Если НЕ МассивРезультатов[4].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%Номер% табл. части ""Сотрудники"" сотрудник указывается повторно.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Сотрудник",
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет контроль противоречий.
//
Процедура ВыполнитьКонтроль(ДополнительныеСвойства, Отказ)
	
	Если Отказ Тогда
		Возврат;	
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ВложенныйЗапрос.Сотрудник,
	|	ВложенныйЗапрос.НомерСтроки,
	|	Сотрудники.СтруктурнаяЕдиница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСотрудники.Сотрудник КАК Сотрудник,
	|		ТаблицаСотрудники.НомерСтроки КАК НомерСтроки,
	|		МАКСИМУМ(Сотрудники.Период) КАК Период
	|	ИЗ
	|		РегистрСведений.Сотрудники КАК Сотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСотрудники КАК ТаблицаСотрудники
	|			ПО Сотрудники.Сотрудник = ТаблицаСотрудники.Сотрудник
	|				И (Сотрудники.Организация = &Организация)
	|				И Сотрудники.Период <= ТаблицаСотрудники.Период
	|				И (Сотрудники.Регистратор <> &Ссылка)
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаСотрудники.Сотрудник,
	|		ТаблицаСотрудники.НомерСтроки) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|		ПО ВложенныйЗапрос.Сотрудник = Сотрудники.Сотрудник
	|			И ВложенныйЗапрос.Период = Сотрудники.Период
	|			И (Сотрудники.Организация = &Организация)
	|ГДЕ
	|	Сотрудники.СтруктурнаяЕдиница <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВложенныйЗапрос.Сотрудник,
	|	ВложенныйЗапрос.Сотрудник.Физлицо КАК Физлицо,
	|	ВложенныйЗапрос.НомерСтроки,
	|	Сотрудники.СтруктурнаяЕдиница,
	|	Сотрудники.Сотрудник КАК ПринятыйСотрудник
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаСотрудники.Сотрудник КАК Сотрудник,
	|		ТаблицаСотрудники.НомерСтроки КАК НомерСтроки,
	|		МАКСИМУМ(Сотрудники.Период) КАК ДатаПриема,
	|		Сотрудники.Сотрудник КАК ОсновнойСотрудник
	|	ИЗ
	|		РегистрСведений.Сотрудники КАК Сотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСотрудники КАК ТаблицаСотрудники
	|			ПО (Сотрудники.Организация = &Организация)
	|				И (Сотрудники.Регистратор <> &Ссылка)
	|				И Сотрудники.Период <= ТаблицаСотрудники.Период
	|				И ТаблицаСотрудники.Сотрудник.Физлицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|				И Сотрудники.Сотрудник.Физлицо = ТаблицаСотрудники.Сотрудник.Физлицо
	|				И (ТаблицаСотрудники.Сотрудник.ТипЗанятости = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятости.ОсновноеМестоРаботы))
	|				И (Сотрудники.Сотрудник.ТипЗанятости = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятости.ОсновноеМестоРаботы))
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаСотрудники.Сотрудник,
	|		ТаблицаСотрудники.НомерСтроки,
	|		Сотрудники.Сотрудник) КАК ВложенныйЗапрос
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|		ПО (Сотрудники.Организация = &Организация)
	|			И ВложенныйЗапрос.ОсновнойСотрудник = Сотрудники.Сотрудник
	|			И ВложенныйЗапрос.ДатаПриема = Сотрудники.Период
	|ГДЕ
	|	Сотрудники.СтруктурнаяЕдиница <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВложенныйЗапрос.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудники.НомерСтроки,
	|	ТаблицаСотрудникиДубль.НомерСтроки КАК НомерСтрокиДубль,
	|	ТаблицаСотрудникиДубль.Сотрудник,
	|	ТаблицаСотрудники.Сотрудник.Физлицо КАК Физлицо
	|ИЗ
	|	
	|		ТаблицаСотрудники КАК ТаблицаСотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаСотрудники КАК ТаблицаСотрудникиДубль
	|			ПО ТаблицаСотрудники.Сотрудник.Физлицо = ТаблицаСотрудникиДубль.Сотрудник.Физлицо
	|				И ТаблицаСотрудники.Сотрудник.Физлицо <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка)
	|				И (ТаблицаСотрудники.Сотрудник.ТипЗанятости = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятости.ОсновноеМестоРаботы))
	|				И (ТаблицаСотрудникиДубль.Сотрудник.ТипЗанятости = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятости.ОсновноеМестоРаботы))
	|				И ТаблицаСотрудникиДубль.НомерСтроки > ТаблицаСотрудники.НомерСтроки
	|	
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаСотрудники.НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаСотрудники.НомерСтроки,
	|	Сотрудники.Сотрудник
	|ИЗ
	|	
	|		ТаблицаСотрудники КАК ТаблицаСотрудники
	|			ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|			ПО ТаблицаСотрудники.Сотрудник = Сотрудники.Сотрудник
	|				И (Сотрудники.Регистратор <> &Ссылка)
	|				И (Сотрудники.Регистратор ССЫЛКА Документ.ПриемНаРаботу)
	|
	|СГРУППИРОВАТЬ ПО
	|		Сотрудники.Сотрудник,
	|		ТаблицаСотрудники.НомерСтроки
	|		
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаСотрудники.НомерСтроки");
	
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", ДополнительныеСвойства.ДляПроведения.Организация);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	// Сотрудник уже принят на работу на дату приема.
	Если НЕ МассивРезультатов[0].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%Номер% табл. части ""Сотрудники"" сотрудник %Сотрудник% уже работает в подразделении %СтруктурнаяЕдиница%.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", ВыборкаИзРезультатаЗапроса.Сотрудник); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтруктурнаяЕдиница%", ВыборкаИзРезультатаЗапроса.СтруктурнаяЕдиница);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Сотрудник",
				Отказ);
		КонецЦикла;
	КонецЕсли;

	// Для Физлица Сотрудника, принимаемого на основное место работы, уже принят на основное место работы другой Сотрудник.
	Если НЕ МассивРезультатов[1].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%Номер% табл. части ""Сотрудники"" для физлица %Физлицо% уже принят на основное место работы сотрудник %Сотрудник% в подразделение %СтруктурнаяЕдиница%.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", ВыборкаИзРезультатаЗапроса.ПринятыйСотрудник); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%СтруктурнаяЕдиница%", ВыборкаИзРезультатаЗапроса.СтруктурнаяЕдиница); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Физлицо%", ВыборкаИзРезультатаЗапроса.Физлицо);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Сотрудник",
				Отказ);
		КонецЦикла;
	КонецЕсли;

	// Для Физлица Сотрудника, принимаемого на основное место работы, в этом документе уже указан другой Сотрудник с основным местом работы.
	Если НЕ МассивРезультатов[2].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%НомерДубль% табл. части ""Сотрудники"" для физлица %Физлицо% повторно принимается на основное место работы сотрудник %Сотрудник%. Физлицо уже указано в строке №%Номер%.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерДубль%", ВыборкаИзРезультатаЗапроса.НомерСтрокиДубль); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", ВыборкаИзРезультатаЗапроса.Сотрудник); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Физлицо%", ВыборкаИзРезультатаЗапроса.Физлицо);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтрокиДубль,
				"Сотрудник",
				Отказ);
		КонецЦикла;
	КонецЕсли;

	// Сотрудник принимается повторно. 
	Если НЕ МассивРезультатов[3].Пустой() Тогда
		ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
		Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
			ТекстСообщения = НСтр(
				"ru = 'В строке №%Номер% табл. части ""Сотрудники"": сотрудник %Сотрудник% уже работал в компании. Для повторного приема на работу необходимо создать нового сотрудника.'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", ВыборкаИзРезультатаЗапроса.НомерСтроки); 
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Сотрудник%", ВыборкаИзРезультатаЗапроса.Сотрудник);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				ВыборкаИзРезультатаЗапроса.НомерСтроки,
				"Сотрудник",
				Отказ);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Выполняет контроль штатного расписания.
//
Процедура ВыполнитьКонтрольШтатногоРасписания(ДополнительныеСвойства, Отказ) 
	
	Если Отказ ИЛИ НЕ Константы.ФункциональнаяОпцияВестиШтатноеРасписание.Получить() Тогда
		Возврат;	
	КонецЕсли; 
	
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	ВЫБОР
	                      |		КОГДА ЕСТЬNULL(ИтогШтатноеРасписание.КоличествоСтавокПоШР, 0) - ЕСТЬNULL(ИтогЗанятоСтавок.ЗанимаемыхСтавок, 0) < 0
	                      |			ТОГДА ИСТИНА
	                      |		ИНАЧЕ ЛОЖЬ
	                      |	КОНЕЦ КАК ПротиворечиеШР,
	                      |	ВЫБОР
	                      |		КОГДА ИтогШтатноеРасписание.НомерСтроки ЕСТЬ NULL 
	                      |			ТОГДА ИтогЗанятоСтавок.НомерСтроки
	                      |		ИНАЧЕ ИтогШтатноеРасписание.НомерСтроки
	                      |	КОНЕЦ КАК НомерСтроки
	                      |ИЗ
	                      |	(ВЫБРАТЬ
	                      |		МаксимальныеПериодыШтатногоРасписания.НомерСтроки КАК НомерСтроки,
	                      |		ШтатноеРасписание.КоличествоСтавок КАК КоличествоСтавокПоШР,
	                      |		ШтатноеРасписание.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	                      |		ШтатноеРасписание.Должность КАК Должность,
	                      |		ШтатноеРасписание.Организация КАК Организация
	                      |	ИЗ
	                      |		(ВЫБРАТЬ
	                      |			ШтатноеРасписание.Организация КАК Организация,
	                      |			ШтатноеРасписание.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	                      |			ШтатноеРасписание.Должность КАК Должность,
	                      |			МАКСИМУМ(ШтатноеРасписание.Период) КАК Период,
	                      |			ПриемНаРаботуСотрудники.НомерСтроки КАК НомерСтроки
	                      |		ИЗ
	                      |			Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	                      |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписание КАК ШтатноеРасписание
	                      |				ПО ПриемНаРаботуСотрудники.СтруктурнаяЕдиница = ШтатноеРасписание.СтруктурнаяЕдиница
	                      |					И ПриемНаРаботуСотрудники.Должность = ШтатноеРасписание.Должность
	                      |					И ПриемНаРаботуСотрудники.Период >= ШтатноеРасписание.Период
	                      |					И (ШтатноеРасписание.Организация = &Организация)
	                      |		ГДЕ
	                      |			ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			ШтатноеРасписание.Должность,
	                      |			ШтатноеРасписание.СтруктурнаяЕдиница,
	                      |			ШтатноеРасписание.Организация,
	                      |			ПриемНаРаботуСотрудники.НомерСтроки) КАК МаксимальныеПериодыШтатногоРасписания
	                      |			ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтатноеРасписание КАК ШтатноеРасписание
	                      |			ПО МаксимальныеПериодыШтатногоРасписания.Период = ШтатноеРасписание.Период
	                      |				И МаксимальныеПериодыШтатногоРасписания.Организация = ШтатноеРасписание.Организация
	                      |				И МаксимальныеПериодыШтатногоРасписания.СтруктурнаяЕдиница = ШтатноеРасписание.СтруктурнаяЕдиница
	                      |				И МаксимальныеПериодыШтатногоРасписания.Должность = ШтатноеРасписание.Должность) КАК ИтогШтатноеРасписание
	                      |		ПОЛНОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	                      |			Сотрудники.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	                      |			Сотрудники.Должность КАК Должность,
	                      |			СУММА(Сотрудники.ЗанимаемыхСтавок) КАК ЗанимаемыхСтавок,
	                      |			Сотрудники.Организация КАК Организация,
	                      |			МаксимальныеПериодыСотрудников.НомерСтроки КАК НомерСтроки
	                      |		ИЗ
	                      |			(ВЫБРАТЬ
	                      |				Сотрудники.Организация КАК Организация,
	                      |				МАКСИМУМ(Сотрудники.Период) КАК Период,
	                      |				ПриемНаРаботуСотрудники.НомерСтроки КАК НомерСтроки,
	                      |				Сотрудники.Сотрудник КАК Сотрудник,
	                      |				ПриемНаРаботуСотрудники.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	                      |				ПриемНаРаботуСотрудники.Должность КАК Должность
	                      |			ИЗ
	                      |				Документ.ПриемНаРаботу.Сотрудники КАК ПриемНаРаботуСотрудники
	                      |					ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	                      |					ПО ПриемНаРаботуСотрудники.Период >= Сотрудники.Период
	                      |						И (Сотрудники.Организация = &Организация)
	                      |			ГДЕ
	                      |				ПриемНаРаботуСотрудники.Ссылка = &Ссылка
	                      |			
	                      |			СГРУППИРОВАТЬ ПО
	                      |				Сотрудники.Организация,
	                      |				ПриемНаРаботуСотрудники.НомерСтроки,
	                      |				Сотрудники.Сотрудник,
	                      |				ПриемНаРаботуСотрудники.СтруктурнаяЕдиница,
	                      |				ПриемНаРаботуСотрудники.Должность) КАК МаксимальныеПериодыСотрудников
	                      |				ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	                      |				ПО МаксимальныеПериодыСотрудников.Сотрудник = Сотрудники.Сотрудник
	                      |					И МаксимальныеПериодыСотрудников.СтруктурнаяЕдиница = Сотрудники.СтруктурнаяЕдиница
	                      |					И МаксимальныеПериодыСотрудников.Должность = Сотрудники.Должность
	                      |					И МаксимальныеПериодыСотрудников.Период = Сотрудники.Период
	                      |					И (Сотрудники.Организация = &Организация)
	                      |		ГДЕ
	                      |			Сотрудники.СтруктурнаяЕдиница <> ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)
	                      |		
	                      |		СГРУППИРОВАТЬ ПО
	                      |			Сотрудники.СтруктурнаяЕдиница,
	                      |			Сотрудники.Должность,
	                      |			Сотрудники.Организация,
	                      |			МаксимальныеПериодыСотрудников.НомерСтроки) КАК ИтогЗанятоСтавок
	                      |		ПО ИтогШтатноеРасписание.НомерСтроки = ИтогЗанятоСтавок.НомерСтроки
	                      |
	                      |УПОРЯДОЧИТЬ ПО
	                      |	НомерСтроки");
						  
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Организация", УправлениеНебольшойФирмойСервер.ПолучитьОрганизацию(Организация));
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		Если Выборка.ПротиворечиеШР Тогда
			ТекстСообщения = НСтр("ru = 'Строка №%Номер% табл. части ""Сотрудники"": в штатном расписании не предусмотрены ставки для приема сотрудника!'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Номер%", Выборка.НомерСтроки);
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
				ЭтотОбъект,
				ТекстСообщения,
				"Сотрудники",
				Выборка.НомерСтроки,
				"ЗанимаемыхСтавок",
				);
		КонецЕсли; 
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

// В обработчике события ОбработкаПроверкиЗаполнения документа выполняется
// копирование и обнуление проверяемых реквизитов для исключения стандартной
// проверки заполнения платформой и последующей проверки средствами встроенного языка.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	// Предварительный контроль
	ВыполнитьПредварительныйКонтроль(Отказ);	
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры // ОбработкаЗаполнения()

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа.
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа.
	Документы.ПриемНаРаботу.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей.
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	УправлениеНебольшойФирмойСервер.ОтразитьСотрудники(ДополнительныеСвойства, Движения, Отказ);
	УправлениеНебольшойФирмойСервер.ОтразитьПлановыеНачисленияИУдержания(ДополнительныеСвойства, Движения, Отказ);
	
	// Запись наборов записей.
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Отражение текущих кадровых данных
	РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.ОбновитьТекущиеКадровыеДанныеСотрудников();
	
	// Контроль
	ВыполнитьКонтроль(ДополнительныеСвойства, Отказ);
	ВыполнитьКонтрольШтатногоРасписания(ДополнительныеСвойства, Отказ);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры // ОбработкаПроведения()

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для удаления проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Отражение текущих кадровых данных
	РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.ОбновитьТекущиеКадровыеДанныеСотрудников();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли