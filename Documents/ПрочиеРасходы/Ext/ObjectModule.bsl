#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью объекта.
//
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	СуммаДокумента = Расходы.Итог("Сумма");
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности.Получить() Тогда
		
		Для каждого СтрокаРасходы из Расходы Цикл
			
			Если СтрокаРасходы.СчетЗатрат.ТипСчета = Перечисления.ТипыСчетов.Расходы Тогда
				
				СтрокаРасходы.НаправлениеДеятельности = Справочники.НаправленияДеятельности.ОсновноеНаправление;
				
			КонецЕсли;	
			
		КонецЦикла;	
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ОбработкаЗаполнения объекта.
//
Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения,, "АналитикаПрочихДоходовИРасходов");
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ПрочиеРасходы.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета
	УправлениеНебольшойФирмойСервер.ОтразитьЗапасы(ДополнительныеСвойства, Движения, Отказ);
	УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходы(ДополнительныеСвойства, Движения, Отказ);
	УправлениеНебольшойФирмойСервер.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
	// Прочие расчеты
	УправлениеНебольшойФирмойСервер.ОтразитьРасчетыСПрочимиКонтрагентами(ДополнительныеСвойства, Движения, Отказ);

	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);

КонецПроцедуры

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта.
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если УчетПрочихРасчетов Тогда
		Если Корреспонденция.ТипСчета <> Перечисления.ТипыСчетов.Дебиторы 
			И Корреспонденция.ТипСчета <> Перечисления.ТипыСчетов.Кредиторы Тогда
			
			УправлениеНебольшойФирмойСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Контрагент");
			УправлениеНебольшойФирмойСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Договор");	
			
		КонецЕсли;
		
		Для Каждого ТекущаяСтрокаРасходы Из Расходы Цикл
			Если ТекущаяСтрокаРасходы.СчетЗатрат.ТипСчета = Перечисления.ТипыСчетов.Дебиторы 
				ИЛИ ТекущаяСтрокаРасходы.СчетЗатрат.ТипСчета = Перечисления.ТипыСчетов.Кредиторы Тогда
				
				Если ТекущаяСтрокаРасходы.Контрагент.Пустая() Тогда
					ТекстСообщения = НСтр("ru = 'Укажите контрагента в строке %НомерСтроки% списка ""Расходы"".'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", ТекущаяСтрокаРасходы.НомерСтроки);
					УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
						ЭтотОбъект,
						ТекстСообщения,
						"Расходы",
						ТекущаяСтрокаРасходы.НомерСтроки,
						"Контрагент",
						Отказ
					);
				ИначеЕсли ТекущаяСтрокаРасходы.Контрагент.ВестиРасчетыПоДоговорам И ТекущаяСтрокаРасходы.Договор.Пустая() Тогда
					ТекстСообщения = НСтр("ru = 'Укажите договор в строке %НомерСтроки% списка ""Расходы"".'");
					ТекстСообщения = СтрЗаменить(ТекстСообщения, "%НомерСтроки%", ТекущаяСтрокаРасходы.НомерСтроки);
					УправлениеНебольшойФирмойСервер.СообщитьОбОшибке(
						ЭтотОбъект,
						ТекстСообщения,
						"Расходы",
						ТекущаяСтрокаРасходы.НомерСтроки,
						"Договор",
						Отказ
					);
					
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	Иначе
		УправлениеНебольшойФирмойСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Контрагент");
		УправлениеНебольшойФирмойСервер.УдалитьПроверяемыйРеквизит(ПроверяемыеРеквизиты, "Договор");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли