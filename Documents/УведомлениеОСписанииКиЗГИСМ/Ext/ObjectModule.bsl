#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ИнтеграцияГИСМПереопределяемый.ОбработкаЗаполненияУведомленияОСписанииКиЗ(ЭтотОбъект, ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка);
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтоНовый() Тогда
		ДополнительныеСвойства.Вставить("ЗаписьНового", Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	МассивНепроверяемыхРеквизитов.Добавить("НомераКиЗ.НомерКиЗ");
	МассивНепроверяемыхРеквизитов.Добавить("НомераКиЗ.RFIDTID");
	МассивНепроверяемыхРеквизитов.Добавить("НомераКиЗ.RFIDEPC");
	
	Если Не ИнтеграцияГИСМ.ИспользоватьВозможностиВерсии("2.41") Тогда
		МассивНепроверяемыхРеквизитов.Добавить("НомераКиЗ.ПричинаСписания");
	КонецЕсли;
	
	МассивНепроверяемыхРеквизитов.Добавить("НомераКиЗ.ЗаявкаНаВыпускКиЗ");
	
	Если ЭтоНовый() И ЗначениеЗаполнено(Основание) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = "
		|ВЫБРАТЬ
		|	Состояния.ТекущееУведомление
		|ИЗ
		|	РегистрСведений.СтатусыИнформированияГИСМ КАК Состояния
		|ГДЕ
		|	Состояния.Документ = &Основание
		|	И НЕ Состояния.Статус В (
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.ОтклоненоГИСМ),
		|		ЗНАЧЕНИЕ(Перечисление.СтатусыИнформированияГИСМ.Отсутствует))
		|	И НЕ Состояния.ТекущееУведомление = ЗНАЧЕНИЕ(Документ.УведомлениеОСписанииКиЗГИСМ.ПустаяСсылка)";
		
		Запрос.УстановитьПараметр("Основание", Основание);
		
		Результат = Запрос.Выполнить();
		
		Если Не Результат.Пустой() Тогда
			Выборка = Результат.Выбрать();
			Выборка.Следующий();
			
			ТекстОшибки = НСтр("ru='Для документа %1 уже существует текущая %2.'");
				ТекстОшибки =  СтрШаблон(ТекстОшибки, Основание, Выборка.ТекущееУведомление);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					,
					,
					Отказ);
			
		КонецЕсли;
	КонецЕсли;
	
	Для каждого СтрокаНомераКиЗ Из НомераКиЗ Цикл
		
		Если СтрокаНомераКиЗ.Индивидуализирован Тогда
			
			Если НЕ ЗначениеЗаполнено(СтрокаНомераКиЗ.НомерКиЗ)
				И НЕ ЗначениеЗаполнено(СтрокаНомераКиЗ.RFIDTID)
				И НЕ ЗначениеЗаполнено(СтрокаНомераКиЗ.RFIDEPC) Тогда
				
				НомерСтроки = СтрокаНомераКиЗ.НомерСтроки;
					
				ТекстОшибки = НСтр("ru='В строке %НомерСтроки% списка ""Товары"" не указан ни ""Номер КиЗ"", ни ""RFID TID"", ни ""RFID EPC""'");
				ТекстОшибки =  СтрЗаменить(ТекстОшибки, "%НомерСтроки%", НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НомераКиЗ", НомерСтроки, "НомерКиЗ"),
					,
					Отказ);
				
			КонецЕсли;
			
		Иначе
			
			Если НЕ ЗначениеЗаполнено(СтрокаНомераКиЗ.НомерКиЗ) Тогда
				
				НомерСтроки = СтрокаНомераКиЗ.НомерСтроки;
				
				ТекстОшибки = НСтр("ru='В строке %НомерСтроки% списка ""Товары"" не указан ""Номер КиЗ""'");
				ТекстОшибки =  СтрЗаменить(ТекстОшибки, "%НомерСтроки%", НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НомераКиЗ", НомерСтроки, "НомерКиЗ"),
					,
					Отказ);
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаНомераКиЗ.ПричинаСписания) Тогда
				
				НомерСтроки = СтрокаНомераКиЗ.НомерСтроки;
				
				ТекстОшибки = НСтр("ru='В строке %НомерСтроки% списка ""Товары"" не указана ""Причина списания""'");
				ТекстОшибки =  СтрЗаменить(ТекстОшибки, "%НомерСтроки%", НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НомераКиЗ", НомерСтроки, "ПричинаСписания"),
					,
					Отказ);
				
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(СтрокаНомераКиЗ.ЗаявкаНаВыпускКиЗ) Тогда
				
				НомерСтроки = СтрокаНомераКиЗ.НомерСтроки;
				
				ТекстОшибки = НСтр("ru='В строке %НомерСтроки% списка ""Товары"" не указана ""Заявка на выпуск КиЗ""'");
				ТекстОшибки =  СтрЗаменить(ТекстОшибки, "%НомерСтроки%", НомерСтроки);
				
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
					ТекстОшибки,
					ЭтотОбъект,
					ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("НомераКиЗ", НомерСтроки, "ЗаявкаНаВыпускКиЗ"),
					,
					Отказ);
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли