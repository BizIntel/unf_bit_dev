#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция ПолучитьПоследнийКорректирующийДокумент(ДокументСсылка, СсылкаНаТекущийДокумент) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка"      , ДокументСсылка);
	Запрос.УстановитьПараметр("ЭтотДокумент", СсылкаНаТекущийДокумент);
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КорректировкаПоступления.Ссылка
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	КорректировкаПоступления.ИсправляемыйДокументПоступления = &Ссылка
	|	И КорректировкаПоступления.ВидОперации <> ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение)
	|	И КорректировкаПоступления.Ссылка <> &ЭтотДокумент
	|	И КорректировкаПоступления.Проведен
	|
	|УПОРЯДОЧИТЬ ПО
	|	КорректировкаПоступления.Дата УБЫВ";
	
	Результат = Запрос.Выполнить().Выбрать();
	Если Результат.Следующий() Тогда
		Возврат Результат.Ссылка;
	Иначе
		Возврат ДокументСсылка;
	КонецЕсли;
		
КонецФункции

Функция СформироватьПараметрыИсправленияКорректировочногоДокумента(ВидОперации, Дата, ДокументПоступления) Экспорт
	
	СтруктураПараметров = Новый Структура("");
	
	Если НЕ ЗначениеЗаполнено(ДокументПоступления) Тогда
		
		Если НЕ ЗначениеЗаполнено(ВидОперации) 
			ИЛИ	ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
			СтруктураПараметров.Вставить("НомерИсправления", Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправления",  Неопределено);
		КонецЕсли;
		
		Возврат СтруктураПараметров;
		
	КонецЕсли;  	
		
	Если НЕ ЗначениеЗаполнено(ВидОперации) 
		ИЛИ	ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.СогласованноеИзменение Тогда
		
		Если ТипЗнч(ДокументПоступления) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда 
			
			РеквизитыДокументаПоступления 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПоступления, 
				"НомерВходящегоДокумента, ДатаВходящегоДокумента, НомерИсправления, ДатаИсправления");
				
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 			РеквизитыДокументаПоступления.НомерВходящегоДокумента);
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 				РеквизитыДокументаПоступления.ДатаВходящегоДокумента);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	РеквизитыДокументаПоступления.НомерИсправления);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	РеквизитыДокументаПоступления.ДатаИсправления);
   			СтруктураПараметров.Вставить("НомерИсправления", 					Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправления", 					Неопределено);

		Иначе
			
			МетаданныеДокументаПоступления = ДокументПоступления.Метаданные();

			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("НомерВходящегоДокумента", МетаданныеДокументаПоступления) Тогда 
				РеквизитыДокументаПоступления 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПоступления, 
					"НомерВходящегоДокумента, ДатаВходящегоДокумента");
				
				СтруктураПараметров.Вставить("НомерИсходногоДокумента", 		РеквизитыДокументаПоступления.НомерВходящегоДокумента);
				СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 			РеквизитыДокументаПоступления.ДатаВходящегоДокумента);
			Иначе
				СтруктураПараметров.Вставить("НомерИсходногоДокумента", 		Неопределено);
				СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 			Неопределено);
			КонецЕсли;
			
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("НомерИсправления", 					Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправления", 					Неопределено);
			
		КонецЕсли;
		
	ИначеЕсли ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки Тогда
		
		Если ТипЗнч(ДокументПоступления) = Тип("ДокументСсылка.КорректировкаПоступления") Тогда 

			РеквизитыДокументаПоступления 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПоступления, 
				"НомерВходящегоДокумента, ДатаВходящегоДокумента, НомерИсправления, ДатаИсправления,
				|НомерИсходногоДокумента, ДатаИсходногоДокумента, НомерИсправленияИсходногоДокумента, ДатаИсправленияИсходногоДокумента");
				
			СтруктураПараметров.Вставить("НомерВходящегоДокумента", 			РеквизитыДокументаПоступления.НомерВходящегоДокумента);
			СтруктураПараметров.Вставить("ДатаВходящегоДокумента", 				РеквизитыДокументаПоступления.ДатаВходящегоДокумента);
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 			РеквизитыДокументаПоступления.НомерИсходногоДокумента);
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 				РеквизитыДокументаПоступления.ДатаИсходногоДокумента);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	РеквизитыДокументаПоступления.НомерИсправленияИсходногоДокумента);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	РеквизитыДокументаПоступления.ДатаИсправленияИсходногоДокумента);
   			
		Иначе
			
			МетаданныеДокументаПоступления = ДокументПоступления.Метаданные();
			
			Если ОбщегоНазначения.ЕстьРеквизитОбъекта("НомерВходящегоДокумента", МетаданныеДокументаПоступления) Тогда 
				РеквизитыДокументаПоступления 	= ОбщегоНазначения.ЗначенияРеквизитовОбъекта(ДокументПоступления, 
					"НомерВходящегоДокумента, ДатаВходящегоДокумента");
				СтруктураПараметров.Вставить("НомерВходящегоДокумента", 	 	РеквизитыДокументаПоступления.НомерВходящегоДокумента);
				СтруктураПараметров.Вставить("ДатаВходящегоДокумента", 			РеквизитыДокументаПоступления.ДатаВходящегоДокумента);
			КонецЕсли;
				
			СтруктураПараметров.Вставить("НомерИсходногоДокумента", 			Неопределено);
			СтруктураПараметров.Вставить("ДатаИсходногоДокумента", 				Неопределено);
			СтруктураПараметров.Вставить("НомерИсправленияИсходногоДокумента", 	Неопределено);
			СтруктураПараметров.Вставить("ДатаИсправленияИсходногоДокумента", 	Неопределено);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат СтруктураПараметров;
	
КонецФункции

// Функция находит и возвращает документ, являющийся отправной точкой исправлений (либо ПН либо корректировку ПН)
// либо первоначальный документ ПН при передаче истины в параметр Исходный
Функция ПолучитьИсправляемыйДокументПоступления(ДокПоступления, Исходный = Ложь) Экспорт
	
	Если ЗначениеЗаполнено(ДокПоступления) 
		И ТипЗнч(ДокПоступления) = Тип("ДокументСсылка.КорректировкаПоступления")
		И (ДокПоступления.ВидОперации = Перечисления.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки ИЛИ Исходный) 
		И ДокПоступления.ДокументОснование <> ДокПоступления Тогда
		
		Возврат ПолучитьИсправляемыйДокументПоступления(ДокПоступления.ДокументОснование, Исходный);
		
	Иначе
		Возврат ДокПоступления;
	КонецЕсли;	
	
КонецФункции

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасы(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.ВидДвижения КАК ВидДвижения,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаЗапасы.КоррОрганизация КАК КоррОрганизация,
	|	ЕСТЬNULL(ТаблицаЗапасы.СтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.Контрагенты.ПустаяСсылка)) КАК СтруктурнаяЕдиница,
	|	ЕСТЬNULL(ТаблицаЗапасы.КоррСтруктурнаяЕдиница, ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка)) КАК КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета КАК КоррСчетУчета,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.КоррНоменклатура КАК КоррНоменклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.КоррХарактеристика КАК КоррХарактеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.КоррПартия КАК КоррПартия,
	|	ТаблицаЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ЗНАЧЕНИЕ(Справочник.Сотрудники.ПустаяСсылка) КАК Ответственный,
	|	ЛОЖЬ КАК Возврат,
	|	НЕОПРЕДЕЛЕНО КАК ДокументПродажи,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК ЗаказПродажи,
	|	ЗНАЧЕНИЕ(Справочник.СтруктурныеЕдиницы.ПустаяСсылка) КАК Подразделение,
	|	НЕОПРЕДЕЛЕНО КАК ИсточникОбеспечения,
	|	ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка) КАК КоррЗаказПокупателя,
	|	ТаблицаЗапасы.ФиксированнаяСтоимость КАК ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СодержаниеПроводки КАК СодержаниеПроводки,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаЗапасы.ЭтоДопРасходы
	|				ТОГДА 0
	|			ИНАЧЕ ТаблицаЗапасы.Количество
	|		КОНЕЦ) КАК Количество,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаЗапасы.ЭтоДопРасходы
	|				ТОГДА ТаблицаЗапасы.СуммаРасходов
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ТаблицаЗапасы.ВключатьРасходыВСебестоимость
	|						ТОГДА ТаблицаЗапасы.Сумма + ТаблицаЗапасы.СуммаРасходов
	|					ИНАЧЕ ТаблицаЗапасы.Сумма
	|				КОНЕЦ
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(0) КАК Себестоимость
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы,
	|	Константа.ФункциональнаяОпцияРезервированиеЗапасов КАК ФункциональнаяОпцияРезервированиеЗапасов
	|ГДЕ
	|	НЕ ТаблицаЗапасы.ПеремещениеВРозницуСуммовойУчет
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.КоррОрганизация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.КоррСтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.КоррСчетУчета,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.КоррНоменклатура,
	|	ТаблицаЗапасы.КоррХарактеристика,
	|	ТаблицаЗапасы.КоррПартия,
	|	ТаблицаЗапасы.СтавкаНДС,
	|	ТаблицаЗапасы.ФиксированнаяСтоимость,
	|	ТаблицаЗапасы.СодержаниеПроводки,
	|	ТаблицаЗапасы.ВидДвижения,
	|	ВЫБОР
	|		КОГДА ТаблицаЗапасы.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И НЕ ФункциональнаяОпцияРезервированиеЗапасов.Значение
	|			ТОГДА ТаблицаЗапасы.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|	КОНЕЦ";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());
	
	Если ТипЗнч(СтруктураДополнительныеСвойства.ДляПроведения.ДокументОснование) <> Тип("ДокументСсылка.ДополнительныеРасходы") Тогда
		СформироватьТаблицаЗапасыПоступление(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
		СформироватьТаблицаЗапасыРасходы(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	КонецЕсли;
	
КонецПроцедуры // СформироватьТаблицаЗапасы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыПоступление(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	ТаблицаЗапасы = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.СкопироватьКолонки();
	ТаблицаРазмещениеЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.СкопироватьКолонки();
	
	КоличествоРазмещений = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.Итог("Количество");
	
	Для н = 0 По СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() - 1 Цикл
		
		СтрокаТаблицаЗапасы = ТаблицаЗапасы.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаТаблицаЗапасы, СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы[н]);
		
		СтруктураДляПоиска = Новый Структура;
		СтруктураДляПоиска.Вставить("ИсточникОбеспечения",	СтрокаТаблицаЗапасы.ИсточникОбеспечения);
		СтруктураДляПоиска.Вставить("Номенклатура",			СтрокаТаблицаЗапасы.Номенклатура);
		СтруктураДляПоиска.Вставить("Характеристика",		СтрокаТаблицаЗапасы.Характеристика);
		
		СтрокаТаблицаЗапасы.ЗаказПокупателя = Документы.ЗаказПокупателя.ПустаяСсылка();
		
		Если КоличествоРазмещений = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		МассивРазмещенныхЗаказов = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов.НайтиСтроки(СтруктураДляПоиска);
		
		СтрокаТаблицаЗапасыКоличество = СтрокаТаблицаЗапасы.Количество;
		
		Если МассивРазмещенныхЗаказов.Количество() > 0 Тогда
			
			Для каждого СтрокаМассива Из МассивРазмещенныхЗаказов Цикл
				
				Если СтрокаТаблицаЗапасыКоличество > 0 И СтрокаМассива.Количество >= СтрокаТаблицаЗапасыКоличество Тогда
					
					// Размещение
					НоваяСтрокаТаблицаРазмещениеЗаказов = ТаблицаРазмещениеЗаказов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, СтрокаМассива);
					
					НоваяСтрокаТаблицаРазмещениеЗаказов.Количество = СтрокаТаблицаЗапасыКоличество;
					
					// Запасы
					СтрокаТаблицаЗапасы.ЗаказПокупателя = СтрокаМассива.ЗаказПокупателя;
					СтрокаТаблицаЗапасыКоличество = 0;
					
				ИначеЕсли СтрокаТаблицаЗапасыКоличество > 0 И СтрокаМассива.Количество < СтрокаТаблицаЗапасыКоличество Тогда
					
					// Размещение
					НоваяСтрокаТаблицаРазмещениеЗаказов = ТаблицаРазмещениеЗаказов.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаРазмещениеЗаказов, СтрокаМассива);
					
					// Запасы
					СуммаКСписанию = Окр(СтрокаТаблицаЗапасы.Сумма * СтрокаМассива.Количество / СтрокаТаблицаЗапасыКоличество, 2, 1);
					
					НоваяСтрокаТаблицаЗапасы = ТаблицаЗапасы.Добавить();
					ЗаполнитьЗначенияСвойств(НоваяСтрокаТаблицаЗапасы, СтрокаТаблицаЗапасы);
					НоваяСтрокаТаблицаЗапасы.ЗаказПокупателя = СтрокаМассива.ЗаказПокупателя;
					НоваяСтрокаТаблицаЗапасы.Количество = СтрокаМассива.Количество;
					НоваяСтрокаТаблицаЗапасы.Сумма = СуммаКСписанию;
					
					СтрокаТаблицаЗапасыКоличество = СтрокаТаблицаЗапасыКоличество - СтрокаМассива.Количество;
					
					СтрокаТаблицаЗапасы.Количество = СтрокаТаблицаЗапасыКоличество;
					СтрокаТаблицаЗапасы.Сумма = СтрокаТаблицаЗапасы.Сумма - СуммаКСписанию;
					
				КонецЕсли;
				
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЦикла;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы = ТаблицаЗапасы;
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаРазмещениеЗаказов = ТаблицаРазмещениеЗаказов;
	ТаблицаРазмещениеЗаказов = Неопределено;
	
КонецПроцедуры // СформироватьТаблицаЗапасыПоступление()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыРасходы(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.ВидДвижения КАК ВидДвижения,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета КАК СчетУчета,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.Заказ КАК ЗаказПокупателя,
	|	0 КАК Количество,
	|	СУММА(ТаблицаЗапасы.Сумма) КАК Сумма,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	&ПрочиеРасходы КАК СодержаниеПроводки
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаЗапасы
	|ГДЕ
	|	(НЕ ТаблицаЗапасы.ВключатьРасходыВСебестоимость)
	|	И (ТаблицаЗапасы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.НезавершенноеПроизводство)
	|			ИЛИ ТаблицаЗапасы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.КосвенныеЗатраты))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.СчетУчета,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.Партия,
	|	ТаблицаЗапасы.Заказ,
	|	ТаблицаЗапасы.ВидДвижения";
	
	Запрос.УстановитьПараметр("ПрочиеРасходы", НСтр("ru = 'Отражение затрат'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Количество() = 0 Тогда
		СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасы", РезультатЗапроса.Выгрузить());		
	Иначе
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			
			НоваяСтрока = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаЗапасы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // СформироватьТаблицаЗапасыРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходы(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	МИНИМУМ(ТаблицаДоходыИРасходы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаДоходыИРасходы.Период КАК Период,
	|	ТаблицаДоходыИРасходы.Организация КАК Организация,
	|	ТаблицаДоходыИРасходы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|		ИНАЧЕ ТаблицаДоходыИРасходы.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаДоходыИРасходы.Заказ
	|	КОНЕЦ КАК ЗаказПокупателя,
	|	ТаблицаДоходыИРасходы.СчетУчета КАК СчетУчета,
	|	ТаблицаДоходыИРасходы.Номенклатура КАК Аналитика,
	|	ВЫРАЗИТЬ(&ПрочиеРасходы КАК СТРОКА(100)) КАК СодержаниеПроводки,
	|	0 КАК СуммаДоходов,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК СуммаРасходов,
	|	СУММА(ТаблицаДоходыИРасходы.Сумма) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаДоходыИРасходы
	|ГДЕ
	|	НЕ ТаблицаДоходыИРасходы.ЭтоДопРасходы
	|	И НЕ ТаблицаДоходыИРасходы.ВключатьРасходыВСебестоимость
	|	И (ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|			ИЛИ ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы))
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДоходыИРасходы.Период,
	|	ТаблицаДоходыИРасходы.Организация,
	|	ТаблицаДоходыИРасходы.СтруктурнаяЕдиница,
	|	ТаблицаДоходыИРасходы.СчетУчета,
	|	ТаблицаДоходыИРасходы.Номенклатура,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее)
	|		ИНАЧЕ ТаблицаДоходыИРасходы.НаправлениеДеятельности
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДоходыИРасходы.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.ПрочиеРасходы)
	|			ТОГДА НЕОПРЕДЕЛЕНО
	|		ИНАЧЕ ТаблицаДоходыИРасходы.Заказ
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	1,
	|	ТаблицаДокумента.Дата,
	|	ТаблицаДокумента.Организация,
	|	НЕОПРЕДЕЛЕНО,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее),
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ТаблицаДокумента.Валюта,
	|	&КурсоваяРазница,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА 0
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаДокумента.СуммаКурсовойРазницы
	|	КОНЕЦ
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
	
	Запрос.УстановитьПараметр("ПрочиеРасходы", НСтр("ru = 'Отражение затрат'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходы", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗакупки(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЗакупки.Период КАК Период,
	|	ТаблицаЗакупки.Организация КАК Организация,
	|	ТаблицаЗакупки.Номенклатура КАК Номенклатура,
	|	ТаблицаЗакупки.Характеристика КАК Характеристика,
	|	ТаблицаЗакупки.Партия КАК Партия,
	|	ТаблицаЗакупки.Заказ КАК ЗаказПоставщику,
	|	ТаблицаЗакупки.Документ КАК Документ,
	|	ТаблицаЗакупки.СтавкаНДС КАК СтавкаНДС,
	|	СУММА(ТаблицаЗакупки.Количество) КАК Количество,
	|	СУММА(ТаблицаЗакупки.СуммаНДСЗакупкиПродажи) КАК СуммаНДС,
	|	СУММА(ТаблицаЗакупки.Сумма) КАК Сумма
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗакупки
	|ГДЕ
	|	(ТаблицаЗакупки.Количество <> 0
	|			ИЛИ ТаблицаЗакупки.Сумма <> 0)
	|	И НЕ ТаблицаЗакупки.ЭтоДопРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗакупки.Период,
	|	ТаблицаЗакупки.Организация,
	|	ТаблицаЗакупки.Номенклатура,
	|	ТаблицаЗакупки.Характеристика,
	|	ТаблицаЗакупки.Партия,
	|	ТаблицаЗакупки.Заказ,
	|	ТаблицаЗакупки.Документ,
	|	ТаблицаЗакупки.СтавкаНДС
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ТаблицаЗакупки.Период,
	|	ТаблицаЗакупки.Организация,
	|	ТаблицаЗакупки.Номенклатура,
	|	ТаблицаЗакупки.Характеристика,
	|	ТаблицаЗакупки.Партия,
	|	ТаблицаЗакупки.ЗаказПоставщику,
	|	ТаблицаЗакупки.Документ,
	|	ТаблицаЗакупки.СтавкаНДС,
	|	СУММА(ТаблицаЗакупки.Количество),
	|	СУММА(ТаблицаЗакупки.СуммаНДСЗакупкиПродажи),
	|	СУММА(ТаблицаЗакупки.Сумма)
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаЗакупки
	|ГДЕ
	|	(ТаблицаЗакупки.Количество <> 0
	|			ИЛИ ТаблицаЗакупки.Сумма <> 0)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗакупки.Период,
	|	ТаблицаЗакупки.Организация,
	|	ТаблицаЗакупки.Номенклатура,
	|	ТаблицаЗакупки.Характеристика,
	|	ТаблицаЗакупки.Партия,
	|	ТаблицаЗакупки.ЗаказПоставщику,
	|	ТаблицаЗакупки.Документ,
	|	ТаблицаЗакупки.СтавкаНДС";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗакупки", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗакупки()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыНаСкладах(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасы.НомерСтроки) КАК НомерСтроки,
	|	ТаблицаЗапасы.ВидДвижения КАК ВидДвижения,
	|	ТаблицаЗапасы.Период КАК Период,
	|	ТаблицаЗапасы.Организация КАК Организация,
	|	ТаблицаЗапасы.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасы.Характеристика КАК Характеристика,
	|	ТаблицаЗапасы.Партия КАК Партия,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.Ячейка КАК Ячейка,
	|	СУММА(ТаблицаЗапасы.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасы
	|ГДЕ
	|	НЕ ТаблицаЗапасы.ПеремещениеВРозницуСуммовойУчет
	|	И НЕ ТаблицаЗапасы.ОрдерныйСклад
	|	И ТаблицаЗапасы.Количество <> 0
	|	И НЕ ТаблицаЗапасы.ЭтоДопРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасы.Организация,
	|	ТаблицаЗапасы.Ячейка,
	|	ТаблицаЗапасы.Номенклатура,
	|	ТаблицаЗапасы.Характеристика,
	|	ТаблицаЗапасы.СтруктурнаяЕдиница,
	|	ТаблицаЗапасы.Период,
	|	ТаблицаЗапасы.ВидДвижения,
	|	ТаблицаЗапасы.Партия";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыНаСкладах", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыНаСкладах()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗапасыКПоступлениюНаСклады(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыКПоступлениюНаСклады.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Период КАК Период,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Организация КАК Организация,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Номенклатура КАК Номенклатура,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Характеристика КАК Характеристика,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Партия КАК Партия,
	|	ТаблицаЗапасыКПоступлениюНаСклады.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	СУММА(ТаблицаЗапасыКПоступлениюНаСклады.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗапасыКПоступлениюНаСклады
	|ГДЕ
	|	НЕ ТаблицаЗапасыКПоступлениюНаСклады.ПеремещениеВРозницуСуммовойУчет
	|	И ТаблицаЗапасыКПоступлениюНаСклады.ОрдерныйСклад
	|	И НЕ ТаблицаЗапасыКПоступлениюНаСклады.ЭтоДопРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыКПоступлениюНаСклады.Период,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Организация,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Номенклатура,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Характеристика,
	|	ТаблицаЗапасыКПоступлениюНаСклады.Партия,
	|	ТаблицаЗапасыКПоступлениюНаСклады.СтруктурнаяЕдиница";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыКПоступлениюНаСклады", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗапасыКПоступлениюНаСклады()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаЗаказыПоставщикам(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗаказыПоставщикам.НомерСтроки) КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаЗаказыПоставщикам.Период КАК Период,
	|	ТаблицаЗаказыПоставщикам.Организация КАК Организация,
	|	ТаблицаЗаказыПоставщикам.Номенклатура КАК Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика КАК Характеристика,
	|	ТаблицаЗаказыПоставщикам.Заказ КАК ЗаказПоставщику,
	|	СУММА(ТаблицаЗаказыПоставщикам.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаЗаказыПоставщикам
	|ГДЕ
	|	ТаблицаЗаказыПоставщикам.Заказ <> НЕОПРЕДЕЛЕНО
	|	И НЕ ТаблицаЗаказыПоставщикам.ЭтоДопРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.Заказ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗаказыПоставщикам.НомерСтроки),
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику,
	|	СУММА(ТаблицаЗаказыПоставщикам.Количество)
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаЗаказыПоставщикам
	|ГДЕ
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗаказыПоставщикам.Период,
	|	ТаблицаЗаказыПоставщикам.Организация,
	|	ТаблицаЗаказыПоставщикам.Номенклатура,
	|	ТаблицаЗаказыПоставщикам.Характеристика,
	|	ТаблицаЗаказыПоставщикам.ЗаказПоставщику";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗаказыПоставщикам", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаЗаказыПоставщикам()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРазмещениеЗаказов(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	// Размещение запасов и расходов.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаРазмещение.Период КАК Период,
	|	ТаблицаРазмещение.Организация КАК Организация,
	|	ТаблицаРазмещение.Номенклатура КАК Номенклатура,
	|	ТаблицаРазмещение.Характеристика КАК Характеристика,
	|	ТаблицаРазмещение.Заказ КАК Заказ,
	|	СУММА(ТаблицаРазмещение.Количество) КАК Количество
	|ПОМЕСТИТЬ ВременнаяТаблицаРазмещение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаРазмещениеЗапасы.Период КАК Период,
	|		ТаблицаРазмещениеЗапасы.Организация КАК Организация,
	|		ТаблицаРазмещениеЗапасы.Номенклатура КАК Номенклатура,
	|		ТаблицаРазмещениеЗапасы.Характеристика КАК Характеристика,
	|		ТаблицаРазмещениеЗапасы.Заказ КАК Заказ,
	|		ТаблицаРазмещениеЗапасы.Количество КАК Количество
	|	ИЗ
	|		ВременнаяТаблицаЗапасы КАК ТаблицаРазмещениеЗапасы
	|	ГДЕ
	|		НЕ ТаблицаРазмещениеЗапасы.Заказ В (ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка), ЗНАЧЕНИЕ(Документ.ЗаказНаПроизводство.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|		И НЕ ТаблицаРазмещениеЗапасы.ЭтоДопРасходы
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаРазмещениеРасходы.Период,
	|		ТаблицаРазмещениеРасходы.Организация,
	|		ТаблицаРазмещениеРасходы.Номенклатура,
	|		ТаблицаРазмещениеРасходы.Характеристика,
	|		ТаблицаРазмещениеРасходы.ЗаказПоставщику,
	|		ТаблицаРазмещениеРасходы.Количество
	|	ИЗ
	|		ВременнаяТаблицаРасходы КАК ТаблицаРазмещениеРасходы
	|	ГДЕ
	|		НЕ ТаблицаРазмещениеРасходы.ЗаказПоставщику В (ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка), НЕОПРЕДЕЛЕНО)
	|		И НЕ ТаблицаРазмещениеРасходы.ЭтоДопРасходы) КАК ТаблицаРазмещение
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаРазмещение.Период,
	|	ТаблицаРазмещение.Организация,
	|	ТаблицаРазмещение.Номенклатура,
	|	ТаблицаРазмещение.Характеристика,
	|	ТаблицаРазмещение.Заказ";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых размещений заказов.
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТаблицаРазмещениеЗаказов.Организация КАК Организация,
	|	ТаблицаРазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|	ТаблицаРазмещениеЗаказов.Характеристика КАК Характеристика,
	|	ТаблицаРазмещениеЗаказов.Заказ КАК ИсточникОбеспечения
	|ИЗ
	|	ВременнаяТаблицаРазмещение КАК ТаблицаРазмещениеЗаказов";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РазмещениеЗаказов");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	// Получение остатков.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаРазмещениеЗаказов.Период КАК Период,
	|	ТаблицаРазмещениеЗаказов.Организация КАК Организация,
	|	РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|	ТаблицаРазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|	ТаблицаРазмещениеЗаказов.Характеристика КАК Характеристика,
	|	ТаблицаРазмещениеЗаказов.Заказ КАК ИсточникОбеспечения,
	|	ВЫБОР
	|		КОГДА ТаблицаРазмещениеЗаказов.Количество > ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|			ТОГДА ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|		КОГДА ТаблицаРазмещениеЗаказов.Количество <= ЕСТЬNULL(РазмещениеЗаказовОстатки.Количество, 0)
	|			ТОГДА ТаблицаРазмещениеЗаказов.Количество
	|	КОНЕЦ КАК Количество
	|ИЗ
	|	ВременнаяТаблицаРазмещение КАК ТаблицаРазмещениеЗаказов
	|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
	|			РазмещениеЗаказовОстатки.Организация КАК Организация,
	|			РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|			РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|			РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|			РазмещениеЗаказовОстатки.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|			СУММА(РазмещениеЗаказовОстатки.КоличествоОстаток) КАК Количество
	|		ИЗ
	|			(ВЫБРАТЬ
	|				РазмещениеЗаказовОстатки.Организация КАК Организация,
	|				РазмещениеЗаказовОстатки.Номенклатура КАК Номенклатура,
	|				РазмещениеЗаказовОстатки.Характеристика КАК Характеристика,
	|				РазмещениеЗаказовОстатки.ЗаказПокупателя КАК ЗаказПокупателя,
	|				РазмещениеЗаказовОстатки.ИсточникОбеспечения КАК ИсточникОбеспечения,
	|				РазмещениеЗаказовОстатки.КоличествоОстаток КАК КоличествоОстаток
	|			ИЗ
	|				РегистрНакопления.РазмещениеЗаказов.Остатки(
	|						&МоментКонтроля,
	|						(Организация, Номенклатура, Характеристика, ИсточникОбеспечения) В
	|							(ВЫБРАТЬ
	|								ТаблицаРазмещениеЗаказов.Организация КАК Организация,
	|								ТаблицаРазмещениеЗаказов.Номенклатура КАК Номенклатура,
	|								ТаблицаРазмещениеЗаказов.Характеристика КАК Характеристика,
	|								ТаблицаРазмещениеЗаказов.Заказ КАК ИсточникОбеспечения
	|							ИЗ
	|								ВременнаяТаблицаРазмещение КАК ТаблицаРазмещениеЗаказов)) КАК РазмещениеЗаказовОстатки
	|			
	|			ОБЪЕДИНИТЬ ВСЕ
	|			
	|			ВЫБРАТЬ
	|				ДвиженияДокументаРазмещениеЗаказов.Организация,
	|				ДвиженияДокументаРазмещениеЗаказов.Номенклатура,
	|				ДвиженияДокументаРазмещениеЗаказов.Характеристика,
	|				ДвиженияДокументаРазмещениеЗаказов.ЗаказПокупателя,
	|				ДвиженияДокументаРазмещениеЗаказов.ИсточникОбеспечения,
	|				ВЫБОР
	|					КОГДА ДвиженияДокументаРазмещениеЗаказов.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход)
	|						ТОГДА ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|					ИНАЧЕ -ЕСТЬNULL(ДвиженияДокументаРазмещениеЗаказов.Количество, 0)
	|				КОНЕЦ
	|			ИЗ
	|				РегистрНакопления.РазмещениеЗаказов КАК ДвиженияДокументаРазмещениеЗаказов
	|			ГДЕ
	|				ДвиженияДокументаРазмещениеЗаказов.Регистратор = &Ссылка
	|				И ДвиженияДокументаРазмещениеЗаказов.Период <= &ПериодКонтроля) КАК РазмещениеЗаказовОстатки
	|		
	|		СГРУППИРОВАТЬ ПО
	|			РазмещениеЗаказовОстатки.Организация,
	|			РазмещениеЗаказовОстатки.Номенклатура,
	|			РазмещениеЗаказовОстатки.Характеристика,
	|			РазмещениеЗаказовОстатки.ЗаказПокупателя,
	|			РазмещениеЗаказовОстатки.ИсточникОбеспечения) КАК РазмещениеЗаказовОстатки
	|		ПО ТаблицаРазмещениеЗаказов.Организация = РазмещениеЗаказовОстатки.Организация
	|			И ТаблицаРазмещениеЗаказов.Номенклатура = РазмещениеЗаказовОстатки.Номенклатура
	|			И ТаблицаРазмещениеЗаказов.Характеристика = РазмещениеЗаказовОстатки.Характеристика
	|			И ТаблицаРазмещениеЗаказов.Заказ = РазмещениеЗаказовОстатки.ИсточникОбеспечения
	|ГДЕ
	|	РазмещениеЗаказовОстатки.ЗаказПокупателя ЕСТЬ НЕ NULL ";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("МоментКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментКонтроля);
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.ПериодКонтроля);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРазмещениеЗаказов", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаРазмещениеЗаказов()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ВозникновениеОбязательствПередПоставщиком", НСтр("ru='Возникновение обязательств перед поставщиком'"));
	Запрос.УстановитьПараметр("ЗачетАванса", НСтр("ru='Зачет предоплаты'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Дата,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.Контрагент КАК Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчета,
	|	ТаблицаДокумента.Договор КАК Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ КАК Заказ,
	|	ТаблицаДокумента.ВалютаРасчетов КАК Валюта,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетов,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаДокумента.Сумма + ТаблицаДокумента.СуммаРасходов
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаДокумента.СуммаВал + ТаблицаДокумента.СуммаРасходовВал
	|			ИНАЧЕ ТаблицаДокумента.СуммаВал
	|		КОНЕЦ) КАК СуммаВал,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаДокумента.Сумма + ТаблицаДокумента.СуммаРасходов
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК СуммаДляОстатка,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаДокумента.СуммаВал + ТаблицаДокумента.СуммаРасходовВал
	|			ИНАЧЕ ТаблицаДокумента.СуммаВал
	|		КОНЕЦ) КАК СуммаВалДляОстатка,
	|	ВЫРАЗИТЬ(&ВозникновениеОбязательствПередПоставщиком КАК СТРОКА(100)) КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаРасчетыСПоставщиками
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|ГДЕ
	|	НЕ ТаблицаДокумента.ЭтоДопРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ВозникновениеОбязательствПередПоставщиком КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаДокумента
	|ГДЕ
	|	(ТаблицаДокумента.ЭтоДопРасходы
	|			ИЛИ НЕ ТаблицаДокумента.ВключатьРасходыВСебестоимость)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.ЗаказПоставщику
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетов,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ТипРасчетов,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход),
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.ДокументКуда
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда,
	|	СУММА(ТаблицаДокумента.Сумма),
	|	СУММА(ТаблицаДокумента.СуммаВал),
	|	-СУММА(ТаблицаДокумента.Сумма),
	|	-СУММА(ТаблицаДокумента.СуммаВал),
	|	ВЫРАЗИТЬ(&ЗачетАванса КАК СТРОКА(100))
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.Контрагент,
	|	ТаблицаДокумента.ВестиРасчетыПоДокументам,
	|	ТаблицаДокумента.Договор,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.ДокументКуда
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ТаблицаДокумента.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|				И ТаблицаДокумента.ВестиРасчетыПоЗаказам
	|			ТОГДА ТаблицаДокумента.Заказ
	|		ИНАЧЕ ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ТаблицаДокумента.ВалютаРасчетов,
	|	ТаблицаДокумента.ТипРасчетовКуда
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Валюта,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков расчетов с контрагентами.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРасчетыСПоставщиками.Организация КАК Организация,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Контрагент КАК Контрагент,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Договор КАК Договор,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Документ КАК Документ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.Заказ КАК Заказ,
	|	ВременнаяТаблицаРасчетыСПоставщиками.ТипРасчетов КАК ТипРасчетов
	|ИЗ
	|	ВременнаяТаблицаРасчетыСПоставщиками КАК ВременнаяТаблицаРасчетыСПоставщиками";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПоставщиками");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыРасчетыСПоставщиками(Запрос.МенеджерВременныхТаблиц, Истина, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаРасчетыСПоставщиками", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.НаправлениеДеятельностиПродажи КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|	И НЕ ТаблицаДокумента.ЭтоДопРасходы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход),
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА &Ссылка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ТаблицаДокумента.НаправлениеДеятельности,
	|	ТаблицаДокумента.Сумма
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.Организация КАК Организация,
	|	СУММА(ТаблицаДокумента.Сумма) КАК СуммаКСписанию
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Статья КАК Статья
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";

	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ТаблицаЗапасыДоходыИРасходыОтложенные = МассивРезультатов[0].Выгрузить();
	ВыборкаРезультатаЗапроса = МассивРезультатов[1].Выбрать();
	
	ТаблицаПредоплатаДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Скопировать();
	ТаблицаПредоплатаДоходыИРасходыОтложенные.Очистить();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		СуммаКСписанию = ВыборкаРезультатаЗапроса.СуммаКСписанию;
		Для каждого СтрокаЗапасыДоходыИРасходыОтложенные Из ТаблицаЗапасыДоходыИРасходыОтложенные Цикл
			Если СуммаКСписанию = 0 Тогда
				Продолжить
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов <= СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СуммаКСписанию = СуммаКСписанию - СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов;
			ИначеЕсли СтрокаЗапасыДоходыИРасходыОтложенные.СуммаРасходов > СуммаКСписанию Тогда
				СтрокаПредоплатаДоходыИРасходыОтложенные = ТаблицаПредоплатаДоходыИРасходыОтложенные.Добавить();
				ЗаполнитьЗначенияСвойств(СтрокаПредоплатаДоходыИРасходыОтложенные, СтрокаЗапасыДоходыИРасходыОтложенные);
				СтрокаПредоплатаДоходыИРасходыОтложенные.СуммаРасходов = СуммаКСписанию;
				СуммаКСписанию = 0;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Для каждого СтрокаПредоплатаДоходыИРасходыОтложенные Из ТаблицаПредоплатаДоходыИРасходыОтложенные Цикл
		СтрокаЗапасыДоходыИРасходыОтложенные = ТаблицаЗапасыДоходыИРасходыОтложенные.Добавить();
		ЗаполнитьЗначенияСвойств(СтрокаЗапасыДоходыИРасходыОтложенные, СтрокаПредоплатаДоходыИРасходыОтложенные);
		СтрокаЗапасыДоходыИРасходыОтложенные.ВидДвижения = ВидДвиженияНакопления.Расход;
	КонецЦикла;
	
	ВыборкаРезультатаЗапроса = МассивРезультатов[2].Выбрать();
	
	Если ВыборкаРезультатаЗапроса.Следующий() Тогда
		Статья = ВыборкаРезультатаЗапроса.Статья;
	Иначе
		Статья = Справочники.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам;
	КонецЕсли;

	Запрос.Текст =
	"ВЫБРАТЬ
	|	Таблица.НомерСтроки КАК НомерСтроки,
	|	Таблица.Период КАК Период,
	|	Таблица.Организация КАК Организация,
	|	Таблица.Документ КАК Документ,
	|	&Статья КАК Статья,
	|	Таблица.НаправлениеДеятельности КАК НаправлениеДеятельности,
	|	Таблица.СуммаРасходов КАК СуммаРасходов
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные
	|ИЗ
	|	&Таблица КАК Таблица";
	Запрос.УстановитьПараметр("Таблица", ТаблицаПредоплатаДоходыИРасходыОтложенные);
	Запрос.УстановитьПараметр("Статья", Статья);
	
	Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыОтложенные", ТаблицаЗапасыДоходыИРасходыОтложенные);
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыОтложенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Расход) КАК ВидДвижения,
	|	ТаблицаДокумента.Период КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.ВестиРасчетыПоДокументам
	|			ТОГДА ТаблицаДокумента.Документ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Документ,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|	И НЕ ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыНераспределенные", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыНераспределенные()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("КассовыйМетодУчетаДоходовИРасходов", СтруктураДополнительныеСвойства.УчетнаяПолитика.КассовыйМетодУчетаДоходовИРасходов);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.ДокументДата КАК Период,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ТаблицаДокумента.Статья КАК Статья,
	|	-ТаблицаДокумента.Сумма КАК СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплата КАК ТаблицаДокумента,
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	&КассовыйМетодУчетаДоходовИРасходов
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	Таблица.Период,
	|	Таблица.Организация,
	|	Таблица.НаправлениеДеятельности,
	|	Таблица.Статья,
	|	Таблица.СуммаРасходов
	|ИЗ
	|	ВременнаяТаблицаПредоплатаДоходыИРасходыОтложенные КАК Таблица";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаДоходыИРасходыКассовыйМетод", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходыКассовыйМетод() 

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаСуммовойУчетВРознице(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ПериодКонтроля", СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени.Дата);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	Запрос.УстановитьПараметр("ПоступлениеВРозницу", НСтр("ru = 'Поступление в розницу'"));
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаДокумента.Период КАК Дата,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ТаблицаДокумента.НомерСтроки КАК НомерСтроки,
	|	ТаблицаДокумента.Организация КАК Организация,
	|	ТаблицаДокумента.РозничныйВидЦен КАК РозничныйВидЦен,
	|	ТаблицаДокумента.Номенклатура КАК Номенклатура,
	|	ТаблицаДокумента.Характеристика КАК Характеристика,
	|	ТаблицаДокумента.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ТаблицаДокумента.ВалютаЦены КАК Валюта,
	|	ТаблицаДокумента.СчетУчетаВРознице КАК СчетУчета,
	|	ТаблицаДокумента.СчетУчетаНаценки КАК СчетУчетаНаценки,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество * КурсыЦенВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыЦенВалюты.Кратность) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК СуммаВал,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество * КурсыЦенВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыЦенВалюты.Кратность) / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК СуммаДляОстатка,
	|	СУММА(ВЫРАЗИТЬ(ЦеныНоменклатурыСрезПоследних.Цена * ТаблицаДокумента.Количество / ЕСТЬNULL(ЦеныНоменклатурыСрезПоследних.ЕдиницаИзмерения.Коэффициент, 1) КАК ЧИСЛО(15, 2))) КАК СуммаВалДляОстатка,
	|	СУММА(ВЫБОР
	|			КОГДА ТаблицаДокумента.ВключатьРасходыВСебестоимость
	|				ТОГДА ТаблицаДокумента.Сумма + ТаблицаДокумента.СуммаРасходов
	|			ИНАЧЕ ТаблицаДокумента.Сумма
	|		КОНЕЦ) КАК Себестоимость,
	|	&ПоступлениеВРозницу КАК СодержаниеПроводки
	|ПОМЕСТИТЬ ВременнаяТаблицаСуммовойУчетВРознице
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаДокумента
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатуры.СрезПоследних(
	|				&МоментВремени,
	|				(ВидЦен, Номенклатура, Характеристика) В
	|					(ВЫБРАТЬ
	|						ВременнаяТаблицаЗапасы.РозничныйВидЦен,
	|						ВременнаяТаблицаЗапасы.Номенклатура,
	|						ВременнаяТаблицаЗапасы.Характеристика
	|					ИЗ
	|						ВременнаяТаблицаЗапасы)) КАК ЦеныНоменклатурыСрезПоследних
	|		ПО ТаблицаДокумента.Номенклатура = ЦеныНоменклатурыСрезПоследних.Номенклатура
	|			И ТаблицаДокумента.РозничныйВидЦен = ЦеныНоменклатурыСрезПоследних.ВидЦен
	|			И ТаблицаДокумента.Характеристика = ЦеныНоменклатурыСрезПоследних.Характеристика
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(
	|				&МоментВремени,
	|				Валюта В
	|					(ВЫБРАТЬ
	|						КонстантаВалютаУчета.Значение
	|					ИЗ
	|						Константа.ВалютаУчета КАК КонстантаВалютаУчета)) КАК КурсыУпрВалюты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, ) КАК КурсыЦенВалюты
	|		ПО ТаблицаДокумента.ВалютаЦены = КурсыЦенВалюты.Валюта
	|ГДЕ
	|	ТаблицаДокумента.ПеремещениеВРозницуСуммовойУчет
	|	И НЕ ТаблицаДокумента.ЭтоДопРасходы
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.НомерСтроки,
	|	ТаблицаДокумента.Организация,
	|	ТаблицаДокумента.РозничныйВидЦен,
	|	ТаблицаДокумента.Номенклатура,
	|	ТаблицаДокумента.Характеристика,
	|	ТаблицаДокумента.СтруктурнаяЕдиница,
	|	ТаблицаДокумента.ВалютаЦены,
	|	ТаблицаДокумента.СчетУчетаВРознице,
	|	ТаблицаДокумента.СчетУчетаНаценки
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Валюта,
	|	СчетУчета";
	
	Запрос.Выполнить();
	
	// Установка исключительной блокировки контролируемых остатков денежных средств.
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВременнаяТаблицаСуммовойУчетВРознице.Организация КАК Организация,
	|	ВременнаяТаблицаСуммовойУчетВРознице.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ВременнаяТаблицаСуммовойУчетВРознице.Валюта КАК Валюта
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ВременнаяТаблицаСуммовойУчетВРознице";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.СуммовойУчетВРознице");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.ИсточникДанных = РезультатЗапроса;
	
	Для каждого КолонкаРезультатЗапроса Из РезультатЗапроса.Колонки Цикл
		ЭлементБлокировки.ИспользоватьИзИсточникаДанных(КолонкаРезультатЗапроса.Имя, КолонкаРезультатЗапроса.Имя);
	КонецЦикла;
	Блокировка.Заблокировать();
	
	НомерЗапроса = 0;
	Запрос.Текст = УправлениеНебольшойФирмойСервер.ПолучитьТекстЗапросаКурсовыеРазницыСуммовойУчетВРознице(Запрос.МенеджерВременныхТаблиц, НомерЗапроса);
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаСуммовойУчетВРознице", МассивРезультатов[НомерЗапроса].Выгрузить());
	
КонецПроцедуры // СформироватьТаблицаДоходыИРасходы()

// Формирует таблицу значений, содержащую данные для проведения по регистру.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура СформироватьТаблицаУправленческий(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	1 КАК Порядок,
	|	ТаблицаУправленческий.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУправленческий.Период КАК Период,
	|	ТаблицаУправленческий.Организация КАК Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический) КАК СценарийПланирования,
	|	ТаблицаУправленческий.СчетУчета КАК СчетДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаДт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалДт,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком КАК СчетКт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ВалютаКт,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаВалКт,
	|	ТаблицаУправленческий.Сумма КАК Сумма,
	|	&ОприходованиеЗапасов КАК Содержание
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	НЕ ТаблицаУправленческий.ВключатьРасходыВСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	2,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал + ТаблицаУправленческий.СуммаРасходовВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал + ТаблицаУправленческий.СуммаРасходовВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.Сумма + ТаблицаУправленческий.СуммаРасходов,
	|	&ОприходованиеЗапасов
	|ИЗ
	|	ВременнаяТаблицаЗапасы КАК ТаблицаУправленческий
	|ГДЕ
	|	ТаблицаУправленческий.ВключатьРасходыВСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	3,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Период,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаУправленческий.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаРасчетовСПоставщиком.Валютный
	|			ТОГДА ТаблицаУправленческий.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаУправленческий.Сумма,
	|	&ПрочиеРасходы
	|ИЗ
	|	ВременнаяТаблицаРасходы КАК ТаблицаУправленческий
	|ГДЕ
	|	НЕ ТаблицаУправленческий.ВключатьРасходыВСебестоимость
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	4,
	|	1,
	|	ТаблицаДокумента.Период,
	|	ТаблицаДокумента.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный
	|			ТОГДА ТаблицаДокумента.ВалютаРасчетов
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный
	|			ТОГДА ТаблицаДокумента.СуммаВал
	|		ИНАЧЕ 0
	|	КОНЕЦ,
	|	ТаблицаДокумента.Сумма,
	|	&ЗачетПредоплаты
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаДокумента.Период КАК Период,
	|		ТаблицаДокумента.Организация КАК Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный КАК СчетУчетаАвансовПоставщикуВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|		СУММА(ТаблицаДокумента.СуммаВал) КАК СуммаВал,
	|		СУММА(ТаблицаДокумента.Сумма) КАК Сумма
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Период КАК Период,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|			ТаблицаДокумента.СчетУчетаАвансовПоставщику.Валютный КАК СчетУчетаАвансовПоставщикуВалютный,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|			ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком.Валютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|			ТаблицаДокумента.ВалютаРасчетов КАК ВалютаРасчетов,
	|			ТаблицаДокумента.СуммаВал КАК СуммаВал,
	|			ТаблицаДокумента.Сумма КАК Сумма
	|		ИЗ
	|			ВременнаяТаблицаПредоплата КАК ТаблицаДокумента
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику,
	|			ТаблицаДокумента.Контрагент.СчетУчетаАвансовПоставщику.Валютный,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|			ТаблицаДокумента.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			0,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаДокумента
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаДокумента.Период,
	|		ТаблицаДокумента.Организация,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщику,
	|		ТаблицаДокумента.СчетУчетаАвансовПоставщикуВалютный,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщиком,
	|		ТаблицаДокумента.СчетУчетаРасчетовСПоставщикомВалютный,
	|		ТаблицаДокумента.ВалютаРасчетов
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаДокумента.Сумма) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.Сумма) <= -0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) >= 0.005
	|			ИЛИ СУММА(ТаблицаДокумента.СуммаВал) <= -0.005)) КАК ТаблицаДокумента
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	5,
	|	1,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА &ОтрицательнаяКурсоваяРазницаСчетУчета
	|		ИНАЧЕ ТаблицаУправленческий.СчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы < 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СчетУчета
	|		ИНАЧЕ &ПоложительнаяКурсоваяРазницаСчетУчета
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|				И ТаблицаУправленческий.СчетУчетаВалютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СуммаКурсовойРазницы > 0
	|			ТОГДА ТаблицаУправленческий.СуммаКурсовойРазницы
	|		ИНАЧЕ -ТаблицаУправленческий.СуммаКурсовойРазницы
	|	КОНЕЦ,
	|	&КурсоваяРазница
	|ИЗ
	|	(ВЫБРАТЬ
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата КАК Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация КАК Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета КАК СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный КАК СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта КАК Валюта,
	|		СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) КАК СуммаКурсовойРазницы
	|	ИЗ
	|		(ВЫБРАТЬ
	|			ТаблицаДокумента.Дата КАК Дата,
	|			ТаблицаДокумента.Организация КАК Организация,
	|			ТаблицаДокумента.СчетУчета КАК СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный КАК СчетУчетаВалютный,
	|			ТаблицаДокумента.Валюта КАК Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы КАК СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг)
	|		
	|		ОБЪЕДИНИТЬ ВСЕ
	|		
	|		ВЫБРАТЬ
	|			ТаблицаДокумента.Дата,
	|			ТаблицаДокумента.Организация,
	|			ТаблицаДокумента.СчетУчета,
	|			ТаблицаДокумента.СчетУчета.Валютный,
	|			ТаблицаДокумента.Валюта,
	|			ТаблицаДокумента.СуммаКурсовойРазницы
	|		ИЗ
	|			ВременнаяТаблицаКурсовыхРазницРасчетыСПоставщиками КАК ТаблицаДокумента
	|		ГДЕ
	|			ТаблицаДокумента.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)) КАК ТаблицаКурсовыхРазницРасчетыСПоставщиками
	|	
	|	СГРУППИРОВАТЬ ПО
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Дата,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Организация,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчета,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.СчетУчетаВалютный,
	|		ТаблицаКурсовыхРазницРасчетыСПоставщиками.Валюта
	|	
	|	ИМЕЮЩИЕ
	|		(СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) >= 0.005
	|			ИЛИ СУММА(ТаблицаКурсовыхРазницРасчетыСПоставщиками.СуммаКурсовойРазницы) <= -0.005)) КАК ТаблицаУправленческий
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	6,
	|	ТаблицаУправленческий.НомерСтроки,
	|	ТаблицаУправленческий.Дата,
	|	ТаблицаУправленческий.Организация,
	|	ЗНАЧЕНИЕ(Справочник.СценарииПланирования.Фактический),
	|	ТаблицаУправленческий.СчетУчета,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчета.Валютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ТаблицаУправленческий.СчетУчетаНаценки,
	|	ВЫБОР
	|		КОГДА ТаблицаУправленческий.СчетУчетаНаценки.Валютный
	|			ТОГДА ТаблицаУправленческий.Валюта
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ,
	|	0,
	|	ТаблицаУправленческий.Сумма - ТаблицаУправленческий.Себестоимость,
	|	&Наценка
	|ИЗ
	|	ВременнаяТаблицаСуммовойУчетВРознице КАК ТаблицаУправленческий
	|
	|УПОРЯДОЧИТЬ ПО
	|	Порядок,
	|	НомерСтроки";
		
	Запрос.УстановитьПараметр("ОприходованиеЗапасов", НСтр("ru = 'Оприходование запасов'"));
	Запрос.УстановитьПараметр("ПрочиеРасходы", НСтр("ru = 'Отражение затрат'"));
	Запрос.УстановитьПараметр("ЗачетПредоплаты", НСтр("ru = 'Зачет предоплаты'"));
	Запрос.УстановитьПараметр("Наценка", НСтр("ru = 'Наценка'"));
	Запрос.УстановитьПараметр("ПоложительнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеДоходы);
	Запрос.УстановитьПараметр("ОтрицательнаяКурсоваяРазницаСчетУчета", ПланыСчетов.Управленческий.ПрочиеРасходы);
	Запрос.УстановитьПараметр("КурсоваяРазница", НСтр("ru='Курсовая разница'"));
	
	РезультатЗапроса = Запрос.Выполнить();
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл  
		НоваяПроводка = СтруктураДополнительныеСвойства.ТаблицыДляДвижений.ТаблицаУправленческий.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяПроводка, Выборка);
	КонецЦикла;
	
КонецПроцедуры // СформироватьТаблицаУправленческий()

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА КорректировкаПоступления.Ссылка.Договор.ЭтоДоговорОбслуживания
	|				И КорректировкаПоступления.Ссылка.Договор.ДоговорОбслуживанияНаправлениеДеятельности <> ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.ПустаяСсылка)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ВестиУчетРасходовПоДоговорамОбслуживания,
	|	КорректировкаПоступления.Ссылка.Договор.ДоговорОбслуживанияНаправлениеДеятельности КАК НаправлениеДеятельности
	|ПОМЕСТИТЬ ВременнаяТаблицаШапка
	|ИЗ
	|	Документ.КорректировкаПоступления КАК КорректировкаПоступления
	|ГДЕ
	|	КорректировкаПоступления.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Валюта КАК Валюта,
	|	КурсыВалютСрезПоследних.Курс КАК Курс,
	|	КурсыВалютСрезПоследних.Кратность КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаКурсыВалютСрезПоследних
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&МоментВремени, Валюта В (&ВалютаУчета, &ВалютаНациональная)) КАК КурсыВалютСрезПоследних
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаПоступленияЗапасы.Ссылка,
	|	КорректировкаПоступленияЗапасы.Ссылка.Договор КАК Договор,
	|	КорректировкаПоступленияЗапасы.Номенклатура,
	|	КорректировкаПоступленияЗапасы.Характеристика,
	|	КорректировкаПоступленияЗапасы.Партия,
	|	КорректировкаПоступленияЗапасы.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияЗапасы.Количество
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.Количество - КорректировкаПоступленияЗапасы.КоличествоДоИзменения
	|	КОНЕЦ КАК Количество,
	|	КорректировкаПоступленияЗапасы.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияЗапасы.Сумма
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.Сумма - КорректировкаПоступленияЗапасы.СуммаДоИзменения
	|	КОНЕЦ КАК Сумма,
	|	КорректировкаПоступленияЗапасы.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияЗапасы.СуммаНДС
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаНДС - КорректировкаПоступленияЗапасы.СуммаНДСДоИзменения
	|	КОНЕЦ КАК СуммаНДС,
	|	КорректировкаПоступленияЗапасы.Заказ,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияЗапасы.Всего
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.Всего - КорректировкаПоступленияЗапасы.ВсегоДоИзменения
	|	КОНЕЦ КАК Всего,
	|	КорректировкаПоступленияЗапасы.Себестоимость,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияЗапасы.СуммаРасходов
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаРасходов - КорректировкаПоступленияЗапасы.СуммаРасходовДоИзменения
	|	КОНЕЦ КАК СуммаРасходов,
	|	КорректировкаПоступленияЗапасы.Содержание,
	|	КорректировкаПоступленияЗапасы.НомерСтроки,
	|	КорректировкаПоступленияЗапасы.СтранаПроисхождения,
	|	КорректировкаПоступленияЗапасы.НомерГТД
	|ПОМЕСТИТЬ ВременнаяТаблицаИзмененияЗапасов
	|ИЗ
	|	Документ.КорректировкаПоступления.Запасы КАК КорректировкаПоступленияЗапасы
	|ГДЕ
	|	КорректировкаПоступленияЗапасы.Ссылка = &Ссылка
	|	И (ВЫБОР
	|				КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|					ТОГДА КорректировкаПоступленияЗапасы.Количество
	|				ИНАЧЕ КорректировкаПоступленияЗапасы.Количество - КорректировкаПоступленияЗапасы.КоличествоДоИзменения
	|			КОНЕЦ <> 0
	|			ИЛИ ВЫБОР
	|				КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|					ТОГДА КорректировкаПоступленияЗапасы.Сумма
	|				ИНАЧЕ КорректировкаПоступленияЗапасы.Сумма - КорректировкаПоступленияЗапасы.СуммаДоИзменения
	|			КОНЕЦ <> 0
	|			ИЛИ ВЫБОР
	|				КОГДА НЕ КорректировкаПоступленияЗапасы.ЕстьВДокументеПоступления
	|					ТОГДА КорректировкаПоступленияЗапасы.СуммаРасходов
	|				ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаРасходов - КорректировкаПоступленияЗапасы.СуммаРасходовДоИзменения
	|			КОНЕЦ <> 0)
	|	И КорректировкаПоступленияЗапасы.Ссылка.ОтражатьВУчете
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаПоступленияРасходы.Ссылка,
	|	КорректировкаПоступленияРасходы.Ссылка.Договор КАК Договор,
	|	КорректировкаПоступленияРасходы.Номенклатура,
	|	КорректировкаПоступленияРасходы.ЕдиницаИзмерения,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияРасходы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияРасходы.Количество
	|		ИНАЧЕ КорректировкаПоступленияРасходы.Количество - КорректировкаПоступленияРасходы.КоличествоДоИзменения
	|	КОНЕЦ КАК Количество,
	|	КорректировкаПоступленияРасходы.Цена КАК Цена,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияРасходы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияРасходы.Сумма
	|		ИНАЧЕ КорректировкаПоступленияРасходы.Сумма - КорректировкаПоступленияРасходы.СуммаДоИзменения
	|	КОНЕЦ КАК Сумма,
	|	КорректировкаПоступленияРасходы.СтавкаНДС,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияРасходы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияРасходы.СуммаНДС
	|		ИНАЧЕ КорректировкаПоступленияРасходы.СуммаНДС - КорректировкаПоступленияРасходы.СуммаНДСДоИзменения
	|	КОНЕЦ КАК СуммаНДС,
	|	КорректировкаПоступленияРасходы.Заказ,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияРасходы.ЕстьВДокументеПоступления
	|			ТОГДА КорректировкаПоступленияРасходы.Всего
	|		ИНАЧЕ КорректировкаПоступленияРасходы.Всего - КорректировкаПоступленияРасходы.ВсегоДоИзменения
	|	КОНЕЦ КАК Всего,
	|	КорректировкаПоступленияРасходы.Содержание,
	|	КорректировкаПоступленияРасходы.ЗаказПоставщику,
	|	КорректировкаПоступленияРасходы.СтруктурнаяЕдиница,
	|	КорректировкаПоступленияРасходы.НаправлениеДеятельности,
	|	КорректировкаПоступленияРасходы.НомерСтроки
	|ПОМЕСТИТЬ ВременнаяТаблицаИзмененияРасходов
	|ИЗ
	|	Документ.КорректировкаПоступления.Расходы КАК КорректировкаПоступленияРасходы
	|ГДЕ
	|	КорректировкаПоступленияРасходы.Ссылка = &Ссылка
	|	И (ВЫБОР
	|				КОГДА НЕ КорректировкаПоступленияРасходы.ЕстьВДокументеПоступления
	|					ТОГДА КорректировкаПоступленияРасходы.Количество
	|				ИНАЧЕ КорректировкаПоступленияРасходы.Количество - КорректировкаПоступленияРасходы.КоличествоДоИзменения
	|			КОНЕЦ <> 0
	|			ИЛИ ВЫБОР
	|				КОГДА НЕ КорректировкаПоступленияРасходы.ЕстьВДокументеПоступления
	|					ТОГДА КорректировкаПоступленияРасходы.Сумма
	|				ИНАЧЕ КорректировкаПоступленияРасходы.Сумма - КорректировкаПоступленияРасходы.СуммаДоИзменения
	|			КОНЕЦ <> 0)
	|	И КорректировкаПоступленияРасходы.Ссылка.ОтражатьВУчете
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененияЗапасов.НомерСтроки КАК НомерСтроки,
	|	ИзмененияЗапасов.Ссылка.ВидОперации КАК ВидОперации,
	|	&ДокументПоступления КАК Документ,
	|	ИзмененияЗапасов.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ИзмененияЗапасов.Ссылка.Контрагент КАК Контрагент,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ИзмененияЗапасов.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ИзмененияЗапасов.Ссылка.Договор КАК Договор,
	|	ИзмененияЗапасов.Ссылка.Ответственный КАК Ответственный,
	|	ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.СчетУчетаНаценки КАК СчетУчетаНаценки,
	|	ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.РозничныйВидЦен КАК РозничныйВидЦен,
	|	ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.РозничныйВидЦен.ВалютаЦены КАК ВалютаЦены,
	|	ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.СчетУчетаВРознице КАК СчетУчетаВРознице,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПеремещениеВРозницуСуммовойУчет,
	|	ИзмененияЗапасов.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	НЕОПРЕДЕЛЕНО КАК КоррОрганизация,
	|	ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	НЕОПРЕДЕЛЕНО КАК КоррСтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.ОрдерныйСклад
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ОрдерныйСклад,
	|	ВЫБОР
	|		КОГДА &УчетПоЯчейкам
	|			ТОГДА ИзмененияЗапасов.Ссылка.Ячейка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Ячейка,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет)
	|			ТОГДА ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.СчетУчетаВРознице
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.СчетУчетаЗапасов
	|	КОНЕЦ КАК СчетУчета,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы = ЗНАЧЕНИЕ(Перечисление.ТипыСтруктурныхЕдиниц.РозницаСуммовойУчет)
	|			ТОГДА ИзмененияЗапасов.Ссылка.СтруктурнаяЕдиница.СчетУчетаВРознице.Валютный
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.СчетУчетаЗапасов.Валютный
	|	КОНЕЦ КАК СчетУчетаВалютный,
	|	НЕОПРЕДЕЛЕНО КАК КоррСчетУчета,
	|	ИзмененияЗапасов.Номенклатура КАК Номенклатура,
	|	НЕОПРЕДЕЛЕНО КАК КоррНоменклатура,
	|	ВЫБОР
	|		КОГДА &ИспользоватьХарактеристики
	|			ТОГДА ИзмененияЗапасов.Характеристика
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Характеристика,
	|	НЕОПРЕДЕЛЕНО КАК КоррХарактеристика,
	|	ВЫБОР
	|		КОГДА &ИспользоватьПартии
	|			ТОГДА ИзмененияЗапасов.Партия
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка)
	|	КОНЕЦ КАК Партия,
	|	НЕОПРЕДЕЛЕНО КАК КоррПартия,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ИзмененияЗапасов.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПокупателя.ПустаяСсылка)
	|			ТОГДА ИзмененияЗапасов.Заказ
	|		КОГДА ИзмененияЗапасов.Заказ ССЫЛКА Документ.ЗаказПоставщику
	|				И ИзмененияЗапасов.Заказ <> ЗНАЧЕНИЕ(Документ.ЗаказПоставщику.ПустаяСсылка)
	|			ТОГДА ИзмененияЗапасов.Заказ
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Заказ,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Заказ ССЫЛКА Документ.ЗаказПокупателя
	|				И ИзмененияЗапасов.Заказ.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЗаказПокупателя.ЗаказНаряд)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЗаказНаряд,
	|	НЕОПРЕДЕЛЕНО КАК КоррЗаказ,
	|	ЛОЖЬ КАК ТоварыНаКомиссии,
	|	ИзмененияЗапасов.Ссылка.Подразделение КАК ПодразделениеПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.НаправлениеДеятельности
	|	КОНЕЦ КАК НаправлениеДеятельностиПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.НаправлениеДеятельности.СчетУчетаВыручкиОтПродаж
	|	КОНЕЦ КАК СчетУчетаПродажи,
	|	ВЫБОР
	|		КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|			ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|		ИНАЧЕ ИзмененияЗапасов.Номенклатура.НаправлениеДеятельности.СчетУчетаСебестоимостиПродаж
	|	КОНЕЦ КАК СчетУчетаСебестоимость,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ИзмененияЗапасов.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ИзмененияЗапасов.Количество
	|		ИНАЧЕ ИзмененияЗапасов.Количество * ИзмененияЗапасов.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	ИзмененияЗапасов.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ИзмененияЗапасов.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ИзмененияЗапасов.СуммаНДС * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.СуммаНДС * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСЗакупкиПродажи,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.Всего * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.СуммаРасходов * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.СуммаРасходов * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРасходов,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.СуммаРасходов * КурсыРегВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность / (ИзмененияЗапасов.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.СуммаРасходов
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаРасходовВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.Себестоимость * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.Себестоимость * ИзмененияЗапасов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Себестоимость,
	|	ИзмененияЗапасов.Всего КАК СуммаРасчетовПринятыеПереданные,
	|	ИзмененияЗапасов.Ссылка.ВключатьРасходыВСебестоимость КАК ВключатьРасходыВСебестоимость,
	|	ИСТИНА КАК ФиксированнаяСтоимость,
	|	ВЫБОР
	|		КОГДА ИзмененияЗапасов.Количество > 0
	|				ИЛИ ИзмененияЗапасов.Сумма > 0
	|				ИЛИ ИзмененияЗапасов.СуммаРасходов > 0
	|			ТОГДА ВЫРАЗИТЬ(&ОприходованиеЗапасов КАК СТРОКА(100))
	|		ИНАЧЕ ВЫРАЗИТЬ(&СписаниеЗапасов КАК СТРОКА(100))
	|	КОНЕЦ КАК СодержаниеПроводки,
	|	ИзмененияЗапасов.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	ИзмененияЗапасов.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ИзмененияЗапасов.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком.Валютный КАК СчетУчетаРасчетовСПоставщикомВалютный,
	|	ИзмененияЗапасов.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияЗапасов.Всего * КурсыРегВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность / (ИзмененияЗапасов.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияЗапасов.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияЗапасов.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ИзмененияЗапасов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ИзмененияЗапасов.СуммаНДС * КурсыРегВалюты.Курс * ИзмененияЗапасов.Ссылка.Кратность / (ИзмененияЗапасов.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ИзмененияЗапасов.СуммаНДС
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСВал,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ИзмененияЗапасов.Ссылка.ДокументОснование) = ТИП(Документ.ДополнительныеРасходы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоДопРасходы
	|ПОМЕСТИТЬ ВременнаяТаблицаЗапасы
	|ИЗ
	|	ВременнаяТаблицаИзмененияЗапасов КАК ИзмененияЗапасов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыУпрВалюты
	|		ПО (КурсыУпрВалюты.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыРегВалюты
	|		ПО (КурсыРегВалюты.Валюта = &ВалютаНациональная),
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	ИзмененияЗапасов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИзмененияРасходов.НомерСтроки КАК НомерСтроки,
	|	ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения,
	|	ИзмененияРасходов.Ссылка.Дата КАК Период,
	|	&ДокументПоступления КАК Документ,
	|	ИзмененияРасходов.Ссылка.ВидОперации КАК ВидОперации,
	|	&Организация КАК Организация,
	|	ИзмененияРасходов.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ИзмененияРасходов.Ссылка.ВалютаДокумента КАК Валюта,
	|	ИзмененияРасходов.Номенклатура.СчетУчетаЗатрат КАК СчетУчета,
	|	ИзмененияРасходов.Номенклатура КАК Номенклатура,
	|	ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка) КАК ЗапасыНоменклатура,
	|	ЗНАЧЕНИЕ(Справочник.ХарактеристикиНоменклатуры.ПустаяСсылка) КАК Характеристика,
	|	ЗНАЧЕНИЕ(Справочник.ПартииНоменклатуры.ПустаяСсылка) КАК Партия,
	|	ИзмененияРасходов.Заказ КАК Заказ,
	|	ИзмененияРасходов.ЗаказПоставщику КАК ЗаказПоставщику,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ИзмененияРасходов.ЕдиницаИзмерения) = ТИП(Справочник.КлассификаторЕдиницИзмерения)
	|			ТОГДА ИзмененияРасходов.Количество
	|		ИНАЧЕ ИзмененияРасходов.Количество * ИзмененияРасходов.ЕдиницаИзмерения.Коэффициент
	|	КОНЕЦ КАК Количество,
	|	ИзмененияРасходов.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияРасходов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияРасходов.Всего * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияРасходов.Всего * ИзмененияРасходов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияРасходов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК Сумма,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияРасходов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияРасходов.Всего * КурсыРегВалюты.Курс * ИзмененияРасходов.Ссылка.Кратность / (ИзмененияРасходов.Ссылка.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияРасходов.Всего
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаВал,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияРасходов.Ссылка.НДСВключатьВСтоимость
	|				ТОГДА 0
	|			ИНАЧЕ ВЫБОР
	|					КОГДА ИзмененияРасходов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|						ТОГДА ИзмененияРасходов.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|					ИНАЧЕ ИзмененияРасходов.СуммаНДС * ИзмененияРасходов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияРасходов.Ссылка.Кратность)
	|				КОНЕЦ
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДС,
	|	ВЫРАЗИТЬ(ВЫБОР
	|			КОГДА ИзмененияРасходов.Ссылка.ВалютаДокумента = &ВалютаНациональная
	|				ТОГДА ИзмененияРасходов.СуммаНДС * КурсыРегВалюты.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * КурсыРегВалюты.Кратность)
	|			ИНАЧЕ ИзмененияРасходов.СуммаНДС * ИзмененияРасходов.Ссылка.Курс * КурсыУпрВалюты.Кратность / (КурсыУпрВалюты.Курс * ИзмененияРасходов.Ссылка.Кратность)
	|		КОНЕЦ КАК ЧИСЛО(15, 2)) КАК СуммаНДСЗакупкиПродажи,
	|	ИзмененияРасходов.Ссылка.ВключатьРасходыВСебестоимость КАК ВключатьРасходыВСебестоимость,
	|	ВЫБОР
	|		КОГДА ИзмененияРасходов.Номенклатура.СчетУчетаЗатрат.ТипСчета = ЗНАЧЕНИЕ(Перечисление.ТипыСчетов.Расходы)
	|				И НЕ ИзмененияРасходов.Ссылка.ВключатьРасходыВСебестоимость
	|			ТОГДА ИзмененияРасходов.НаправлениеДеятельности
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ВременнаяТаблицаШапка.ВестиУчетРасходовПоДоговорамОбслуживания
	|					ТОГДА ВременнаяТаблицаШапка.НаправлениеДеятельности
	|				ИНАЧЕ ИзмененияРасходов.Номенклатура.НаправлениеДеятельности
	|			КОНЕЦ
	|	КОНЕЦ КАК НаправлениеДеятельности,
	|	ИзмененияРасходов.Ссылка.Контрагент КАК Контрагент,
	|	ИзмененияРасходов.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	ИзмененияРасходов.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	ИзмененияРасходов.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	ИзмененияРасходов.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	ИзмененияРасходов.Ссылка.Договор КАК Договор,
	|	ИзмененияРасходов.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	ИзмененияРасходов.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	ИзмененияРасходов.Номенклатура.СчетУчетаЗатрат.ТипСчета КАК ТипСчета,
	|	ВЫБОР
	|		КОГДА ТИПЗНАЧЕНИЯ(ИзмененияРасходов.Ссылка.ДокументОснование) = ТИП(Документ.ДополнительныеРасходы)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ЭтоДопРасходы
	|ПОМЕСТИТЬ ВременнаяТаблицаРасходы
	|ИЗ
	|	ВременнаяТаблицаИзмененияРасходов КАК ИзмененияРасходов
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыУпрВалюты
	|		ПО (КурсыУпрВалюты.Валюта = &ВалютаУчета)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыРегВалюты
	|		ПО (КурсыРегВалюты.Валюта = &ВалютаНациональная),
	|	ВременнаяТаблицаШапка КАК ВременнаяТаблицаШапка
	|ГДЕ
	|	ИзмененияРасходов.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(КорректировкаПоступленияПредоплата.НомерСтроки) КАК НомерСтроки,
	|	КорректировкаПоступленияПредоплата.Ссылка.ВидОперации КАК ВидОперации,
	|	КорректировкаПоступленияПредоплата.Ссылка.Дата КАК Период,
	|	&Организация КАК Организация,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент КАК Контрагент,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДоговорам КАК ВестиРасчетыПоДоговорам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам КАК ВестиРасчетыПоДокументам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоЗаказам КАК ВестиРасчетыПоЗаказам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам КАК ВестиУчетОплатыПоСчетам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем КАК СчетУчетаРасчетовСПокупателем,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПокупателя КАК СчетУчетаАвансовПокупателя,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком КАК СчетУчетаРасчетовСПоставщиком,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПоставщику КАК СчетУчетаАвансовПоставщику,
	|	КорректировкаПоступленияПредоплата.Ссылка.Договор КАК Договор,
	|	КорректировкаПоступленияПредоплата.Ссылка.Договор.ВалютаРасчетов КАК ВалютаРасчетов,
	|	КорректировкаПоступленияПредоплата.Заказ КАК Заказ,
	|	ЗНАЧЕНИЕ(Справочник.НаправленияДеятельности.Прочее) КАК НаправлениеДеятельностиПродажи,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс) КАК ТипРасчетов,
	|	ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Долг) КАК ТипРасчетовКуда,
	|	&ДокументПоступления КАК ДокументКуда,
	|	КорректировкаПоступленияПредоплата.Документ КАК Документ,
	|	КорректировкаПоступленияПредоплата.Ссылка.ДокументОснование КАК ДокументОснование,
	|	ВЫБОР
	|		КОГДА НЕ КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|				ИЛИ ТИПЗНАЧЕНИЯ(КорректировкаПоступленияПредоплата.Документ) = ТИП(Документ.АвансовыйОтчет)
	|				ИЛИ ТИПЗНАЧЕНИЯ(КорректировкаПоступленияПредоплата.Документ) = ТИП(Документ.Взаимозачет)
	|			ТОГДА ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам)
	|		ИНАЧЕ ВЫБОР
	|				КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.РасходСоСчета
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.РасходСоСчета).Статья
	|				КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.ПоступлениеНаСчет
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.ПоступлениеНаСчет).Статья
	|				КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.ПоступлениеВКассу
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.ПоступлениеВКассу).Статья
	|				КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.РасходИзКассы
	|					ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.РасходИзКассы).Статья
	|				ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СтатьиДвиженияДенежныхСредств.ОплатаПоставщикам)
	|			КОНЕЦ
	|	КОНЕЦ КАК Статья,
	|	ВЫБОР
	|		КОГДА КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам
	|			ТОГДА ВЫБОР
	|					КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.РасходСоСчета
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.РасходСоСчета).Дата
	|					КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.ПоступлениеНаСчет
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.ПоступлениеНаСчет).Дата
	|					КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.ПоступлениеВКассу
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.ПоступлениеВКассу).Дата
	|					КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.РасходИзКассы
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.РасходИзКассы).Дата
	|					КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.АвансовыйОтчет
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.АвансовыйОтчет).Дата
	|					КОГДА КорректировкаПоступленияПредоплата.Документ ССЫЛКА Документ.Взаимозачет
	|						ТОГДА ВЫРАЗИТЬ(КорректировкаПоступленияПредоплата.Документ КАК Документ.Взаимозачет).Дата
	|				КОНЕЦ
	|		ИНАЧЕ КорректировкаПоступленияПредоплата.Ссылка.Дата
	|	КОНЕЦ КАК ДокументДата,
	|	СУММА(ВЫРАЗИТЬ(ВЫБОР
	|				КОГДА НЕ КорректировкаПоступленияПредоплата.ЕстьВДокументеПоступления
	|					ТОГДА КорректировкаПоступленияПредоплата.СуммаПлатежа
	|				ИНАЧЕ КорректировкаПоступленияПредоплата.СуммаПлатежа - КорректировкаПоступленияПредоплата.СуммаПлатежаДоИзменения
	|			КОНЕЦ * КорректировкаПоступленияПредоплата.Ссылка.Курс * КурсыВалютУчетаСрезПоследних.Кратность / (КурсыВалютУчетаСрезПоследних.Курс * КорректировкаПоступленияПредоплата.Ссылка.Кратность) КАК ЧИСЛО(15, 2))) КАК Сумма,
	|	СУММА(ВЫБОР
	|			КОГДА НЕ КорректировкаПоступленияПредоплата.ЕстьВДокументеПоступления
	|				ТОГДА КорректировкаПоступленияПредоплата.СуммаРасчетов
	|			ИНАЧЕ КорректировкаПоступленияПредоплата.СуммаРасчетов - КорректировкаПоступленияПредоплата.СуммаРасчетовДоИзменения
	|		КОНЕЦ) КАК СуммаВал
	|ПОМЕСТИТЬ ВременнаяТаблицаПредоплата
	|ИЗ
	|	Документ.КорректировкаПоступления.Предоплата КАК КорректировкаПоступленияПредоплата
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВременнаяТаблицаКурсыВалютСрезПоследних КАК КурсыВалютУчетаСрезПоследних
	|		ПО (КурсыВалютУчетаСрезПоследних.Валюта = &ВалютаУчета)
	|ГДЕ
	|	КорректировкаПоступленияПредоплата.Ссылка = &Ссылка
	|	И КорректировкаПоступленияПредоплата.Ссылка.ОтражатьВУчете
	|
	|СГРУППИРОВАТЬ ПО
	|	КорректировкаПоступленияПредоплата.Ссылка,
	|	КорректировкаПоступленияПредоплата.Документ,
	|	КорректировкаПоступленияПредоплата.Ссылка.Дата,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент,
	|	КорректировкаПоступленияПредоплата.Ссылка.Договор,
	|	КорректировкаПоступленияПредоплата.Заказ,
	|	КорректировкаПоступленияПредоплата.Ссылка.Договор.ВалютаРасчетов,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПокупателем,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПокупателя,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаРасчетовСПоставщиком,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.СчетУчетаАвансовПоставщику,
	|	КорректировкаПоступленияПредоплата.Ссылка.ВидОперации,
	|	КорректировкаПоступленияПредоплата.Ссылка.ДокументОснование,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДоговорам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоДокументам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиРасчетыПоЗаказам,
	|	КорректировкаПоступленияПредоплата.Ссылка.Контрагент.ВестиУчетОплатыПоСчетам";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("ДокументПоступления", ПолучитьИсправляемыйДокументПоступления(ДокументСсылкаКорректировкаПоступления, Истина));
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.Дляпроведения.Организация);
	Запрос.УстановитьПараметр("МоментВремени", Новый Граница(СтруктураДополнительныеСвойства.ДляПроведения.МоментВремени, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("ИспользоватьХарактеристики", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьХарактеристики);
	Запрос.УстановитьПараметр("ИспользоватьПартии", СтруктураДополнительныеСвойства.УчетнаяПолитика.ИспользоватьПартии);
	Запрос.УстановитьПараметр("УчетПоЯчейкам", СтруктураДополнительныеСвойства.УчетнаяПолитика.УчетПоЯчейкам);
	
	Запрос.УстановитьПараметр("ОприходованиеЗапасов", НСтр("ru = 'Оприходование запасов'"));
	Запрос.УстановитьПараметр("СписаниеЗапасов", НСтр("ru = 'Списание запасов'"));
	
	Запрос.УстановитьПараметр("ВалютаУчета", Константы.ВалютаУчета.Получить());
	Запрос.УстановитьПараметр("ВалютаНациональная", Константы.НациональнаяВалюта.Получить());
	
	Запрос.ВыполнитьПакет();
	
	// Формирование проводок документа.
	УправлениеНебольшойФирмойСервер.СформироватьТаблицуПроводок(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаПоступления, "ДокументОснование");
	СтруктураДополнительныеСвойства.ДляПроведения.Вставить("ДокументОснование", ДокументОснование);
	
	СформироватьТаблицаЗакупки(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыНаСкладах(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыКПоступлениюНаСклады(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗаказыПоставщикам(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаРазмещениеЗаказов(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасы(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства);
	СформироватьТаблицаРасчетыСПоставщиками(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходы(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыОтложенные(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыНераспределенные(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаДоходыИРасходыКассовыйМетод(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	СформироватьТаблицаСуммовойУчетВРознице(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	
	СформироватьТаблицаУправленческий(ДокументСсылкаКорректировкаПоступления, СтруктураДополнительныеСвойства);
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

// Выполняет контроль возникновения отрицательных остатков.
//
Процедура ВыполнитьКонтроль(ДокументСсылкаКорректировкаПоступления, ДополнительныеСвойства, Отказ, УдалениеПроведения = Ложь) Экспорт
	
	Если НЕ УправлениеНебольшойФирмойСервер.ВыполнитьКонтрольОстатков() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Если временные таблицы "ДвиженияЗапасыИзменение", "ДвиженияЗапасыНаСкладахИзменение", 
	// "ДвиженияЗапасыКПоступлениюНаСкладыИзменение", "ДвиженияРазмещениеЗаказовИзменение",
	// содержат записи, необходимо выполнить контроль реализации товаров.
	Если СтруктураВременныеТаблицы.ДвиженияЗапасыИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыНаСкладахИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыКПоступлениюНаСкладыИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияРазмещениеЗаказовИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗаказыПоставщикамИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияРасчетыСПоставщикамиИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияСуммовойУчетВРозницеИзменение
		ИЛИ СтруктураВременныеТаблицы.ДвиженияЗапасыВРазрезеГТДИзменение
		Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыНаСкладахИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыНаСкладахИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыНаСкладахИзменение.Ячейка КАК ЯчейкаПредставление,
		|	ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыНаСкладахОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыНаСкладахИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыНаСкладах,
		|	ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыНаСкладах
		|ИЗ
		|	ДвиженияЗапасыНаСкладахИзменение КАК ДвиженияЗапасыНаСкладахИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыНаСкладах.Остатки(&МоментКонтроля, ) КАК ЗапасыНаСкладахОстатки
		|		ПО ДвиженияЗапасыНаСкладахИзменение.Организация = ЗапасыНаСкладахОстатки.Организация
		|			И ДвиженияЗапасыНаСкладахИзменение.СтруктурнаяЕдиница = ЗапасыНаСкладахОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыНаСкладахИзменение.Номенклатура = ЗапасыНаСкладахОстатки.Номенклатура
		|			И ДвиженияЗапасыНаСкладахИзменение.Характеристика = ЗапасыНаСкладахОстатки.Характеристика
		|			И ДвиженияЗапасыНаСкладахИзменение.Партия = ЗапасыНаСкладахОстатки.Партия
		|			И ДвиженияЗапасыНаСкладахИзменение.Ячейка = ЗапасыНаСкладахОстатки.Ячейка
		|			И (ЕСТЬNULL(ЗапасыНаСкладахОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗапасыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияЗапасыИзменение.СчетУчета КАК СчетУчетаПредставление,
		|	ДвиженияЗапасыИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗапасыИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияЗапасыИзменение.Партия КАК ПартияПредставление,
		|	ДвиженияЗапасыИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ЗапасыОстатки.СтруктурнаяЕдиница.ТипСтруктурнойЕдиницы КАК ТипСтруктурнойЕдиницы,
		|	ЗапасыОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасы,
		|	ЕСТЬNULL(ЗапасыОстатки.СуммаОстаток, 0) КАК СуммаОстатокЗапасы
		|ИЗ
		|	ДвиженияЗапасыИзменение КАК ДвиженияЗапасыИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.Запасы.Остатки(&МоментКонтроля, ) КАК ЗапасыОстатки
		|		ПО ДвиженияЗапасыИзменение.Организация = ЗапасыОстатки.Организация
		|			И ДвиженияЗапасыИзменение.СтруктурнаяЕдиница = ЗапасыОстатки.СтруктурнаяЕдиница
		|			И ДвиженияЗапасыИзменение.СчетУчета = ЗапасыОстатки.СчетУчета
		|			И ДвиженияЗапасыИзменение.Номенклатура = ЗапасыОстатки.Номенклатура
		|			И ДвиженияЗапасыИзменение.Характеристика = ЗапасыОстатки.Характеристика
		|			И ДвиженияЗапасыИзменение.Партия = ЗапасыОстатки.Партия
		|			И ДвиженияЗапасыИзменение.ЗаказПокупателя = ЗапасыОстатки.ЗаказПокупателя
		|			И (ЕСТЬNULL(ЗапасыОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗаказыПоставщикамИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗаказыПоставщикамИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику КАК ЗаказПоставщикуПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияЗаказыПоставщикамИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ЗаказыПоставщикамОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗаказыПоставщикамИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) КАК ОстатокЗаказыПоставщикам,
		|	ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗаказыПоставщикам
		|ИЗ
		|	ДвиженияЗаказыПоставщикамИзменение КАК ДвиженияЗаказыПоставщикамИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.ЗаказыПоставщикам.Остатки(&МоментКонтроля, ) КАК ЗаказыПоставщикамОстатки
		|		ПО ДвиженияЗаказыПоставщикамИзменение.Организация = ЗаказыПоставщикамОстатки.Организация
		|			И ДвиженияЗаказыПоставщикамИзменение.ЗаказПоставщику = ЗаказыПоставщикамОстатки.ЗаказПоставщику
		|			И ДвиженияЗаказыПоставщикамИзменение.Номенклатура = ЗаказыПоставщикамОстатки.Номенклатура
		|			И ДвиженияЗаказыПоставщикамИзменение.Характеристика = ЗаказыПоставщикамОстатки.Характеристика
		|			И (ЕСТЬNULL(ЗаказыПоставщикамОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРазмещениеЗаказовИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРазмещениеЗаказовИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя КАК ЗаказПокупателяПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.Номенклатура КАК НоменклатураПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.Характеристика КАК ХарактеристикаПредставление,
		|	ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения КАК ИсточникОбеспеченияПредставление,
		|	РазмещениеЗаказовОстатки.Номенклатура.ЕдиницаИзмерения КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияРазмещениеЗаказовИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) КАК ОстатокРазмещениеЗаказов,
		|	ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокРазмещениеЗаказов
		|ИЗ
		|	ДвиженияРазмещениеЗаказовИзменение КАК ДвиженияРазмещениеЗаказовИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РазмещениеЗаказов.Остатки(&МоментКонтроля, ) КАК РазмещениеЗаказовОстатки
		|		ПО ДвиженияРазмещениеЗаказовИзменение.Организация = РазмещениеЗаказовОстатки.Организация
		|			И ДвиженияРазмещениеЗаказовИзменение.ЗаказПокупателя = РазмещениеЗаказовОстатки.ЗаказПокупателя
		|			И ДвиженияРазмещениеЗаказовИзменение.Номенклатура = РазмещениеЗаказовОстатки.Номенклатура
		|			И ДвиженияРазмещениеЗаказовИзменение.Характеристика = РазмещениеЗаказовОстатки.Характеристика
		|			И ДвиженияРазмещениеЗаказовИзменение.ИсточникОбеспечения = РазмещениеЗаказовОстатки.ИсточникОбеспечения
		|			И (ЕСТЬNULL(РазмещениеЗаказовОстатки.КоличествоОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияРасчетыСПоставщикамиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Контрагент КАК КонтрагентПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Договор КАК ДоговорПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Договор.ВалютаРасчетов КАК ВалютаПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Документ КАК ДокументПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.Заказ КАК ЗаказПредставление,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетовПредставление,
		|	ЛОЖЬ КАК ДвиженияДенежныхДокументов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалПриЗаписи - ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВыданныхАвансов,
		|	ДвиженияРасчетыСПоставщикамиИзменение.СуммаВалИзменение + ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаНепогашеннойЗадолженности,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) КАК СуммаВалОстаток,
		|	ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов КАК ТипРасчетов
		|ИЗ
		|	ДвиженияРасчетыСПоставщикамиИзменение КАК ДвиженияРасчетыСПоставщикамиИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.РасчетыСПоставщиками.Остатки(&МоментКонтроля, ) КАК РасчетыСПоставщикамиОстатки
		|		ПО ДвиженияРасчетыСПоставщикамиИзменение.Организация = РасчетыСПоставщикамиОстатки.Организация
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Контрагент = РасчетыСПоставщикамиОстатки.Контрагент
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Договор = РасчетыСПоставщикамиОстатки.Договор
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Документ = РасчетыСПоставщикамиОстатки.Документ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.Заказ = РасчетыСПоставщикамиОстатки.Заказ
		|			И ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = РасчетыСПоставщикамиОстатки.ТипРасчетов
		|			И (ВЫБОР
		|				КОГДА ДвиженияРасчетыСПоставщикамиИзменение.ТипРасчетов = ЗНАЧЕНИЕ(Перечисление.ТипыРасчетов.Аванс)
		|					ТОГДА ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) > 0
		|				ИНАЧЕ ЕСТЬNULL(РасчетыСПоставщикамиОстатки.СуммаВалОстаток, 0) < 0
		|			КОНЕЦ)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияСуммовойУчетВРозницеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияСуммовойУчетВРозницеИзменение.Организация КАК ОрганизацияПредставление,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаПредставление,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница.РозничныйВидЦен.ВалютаЦены КАК ВалютаПредставление,
		|	ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаОстаток, 0) КАК СуммаОстаток,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалИзменение + ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) КАК ОстатокВРознице,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаПриЗаписи КАК СуммаПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаИзменение КАК СуммаИзменение,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалПриЗаписи КАК СуммаВалПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СуммаВалИзменение КАК СуммаВалИзменение,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьПередЗаписью КАК СебестоимостьПередЗаписью,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьПриЗаписи КАК СебестоимостьПриЗаписи,
		|	ДвиженияСуммовойУчетВРозницеИзменение.СебестоимостьИзменение КАК СебестоимостьИзменение
		|ИЗ
		|	ДвиженияСуммовойУчетВРозницеИзменение КАК ДвиженияСуммовойУчетВРозницеИзменение
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрНакопления.СуммовойУчетВРознице.Остатки(&МоментКонтроля, ) КАК СуммовойУчетВРозницеОстатки
		|		ПО ДвиженияСуммовойУчетВРозницеИзменение.Организация = СуммовойУчетВРозницеОстатки.Организация
		|			И ДвиженияСуммовойУчетВРозницеИзменение.СтруктурнаяЕдиница = СуммовойУчетВРозницеОстатки.СтруктурнаяЕдиница
		|			И (ЕСТЬNULL(СуммовойУчетВРозницеОстатки.СуммаВалОстаток, 0) < 0)
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ДвиженияЗапасыВРазрезеГТДИзменение.НомерСтроки КАК НомерСтроки,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Организация) КАК ОрганизацияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД) КАК НомерГТДПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура) КАК НоменклатураПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика) КАК ХарактеристикаПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.Партия) КАК ПартияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения) КАК СтранаПроисхожденияПредставление,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ЗапасыВРазрезеГТДОстатки.Номенклатура.ЕдиницаИзмерения) КАК ЕдиницаИзмеренияПредставление,
		|	ЕСТЬNULL(ДвиженияЗапасыВРазрезеГТДИзменение.КоличествоИзменение, 0) + ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК ОстатокЗапасыВРазрезеГТД,
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) КАК КоличествоОстатокЗапасыВРазрезеГТД
		|ИЗ
		|	ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ЗапасыВРазрезеГТД.Остатки(
		|				&МоментКонтроля,
		|				(Организация, НомерГТД, Номенклатура, Характеристика, Партия, СтранаПроисхождения) В
		|					(ВЫБРАТЬ
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Организация КАК Организация,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД КАК НомерГТД,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура КАК Номенклатура,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика КАК Характеристика,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.Партия КАК Партия,
		|						ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения КАК СтранаПроисхождения
		|					ИЗ
		|						ДвиженияЗапасыВРазрезеГТДИзменение КАК ДвиженияЗапасыВРазрезеГТДИзменение)) КАК ЗапасыВРазрезеГТДОстатки
		|		ПО ДвиженияЗапасыВРазрезеГТДИзменение.Организация = ЗапасыВРазрезеГТДОстатки.Организация
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.НомерГТД = ЗапасыВРазрезеГТДОстатки.НомерГТД
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Номенклатура = ЗапасыВРазрезеГТДОстатки.Номенклатура
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Характеристика = ЗапасыВРазрезеГТДОстатки.Характеристика
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.Партия = ЗапасыВРазрезеГТДОстатки.Партия
		|			И ДвиженияЗапасыВРазрезеГТДИзменение.СтранаПроисхождения = ЗапасыВРазрезеГТДОстатки.СтранаПроисхождения
		|ГДЕ
		|	ЕСТЬNULL(ЗапасыВРазрезеГТДОстатки.КоличествоОстаток, 0) < 0
		|
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки");
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.УстановитьПараметр("МоментКонтроля", ДополнительныеСвойства.ДляПроведения.МоментКонтроля);
		
		МассивРезультатов = Запрос.ВыполнитьПакет();
		
		Если НЕ МассивРезультатов[0].Пустой()
			ИЛИ НЕ МассивРезультатов[1].Пустой()
			ИЛИ НЕ МассивРезультатов[2].Пустой()
			ИЛИ НЕ МассивРезультатов[3].Пустой()
			ИЛИ НЕ МассивРезультатов[4].Пустой()
			ИЛИ НЕ МассивРезультатов[5].Пустой()
			ИЛИ НЕ МассивРезультатов[6].Пустой()
			Тогда
			
			ДокументОбъектПриходнаяНакладная = ДокументСсылкаКорректировкаПоступления.ПолучитьОбъект()
			
		КонецЕсли;
		
		// Отрицательный остаток запасов на складе.
		Если НЕ МассивРезультатов[0].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[0].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасыНаСкладах(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток учета запасов.
		Если НЕ МассивРезультатов[1].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[1].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасы(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по заказу поставщику.
		Если НЕ МассивРезультатов[2].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[2].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗаказыПоставщикам(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток размещение заказов.
		Если НЕ МассивРезультатов[3].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[3].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРазмещениеЗаказов(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по расчетам с поставщиками.
		Если НЕ МассивРезультатов[4].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[4].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструРасчетыСПоставщиками(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по суммовому учету в рознице.
		Если НЕ МассивРезультатов[5].Пустой() Тогда
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[5].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструСуммовойУчетВРознице(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
		КонецЕсли;
		
		// Отрицательный остаток по остаткам запасов в разрезе номеров ГТД.
		Если Константы.КонтролироватьОстаткиПоНомерамГТД.Получить()
			И НЕ МассивРезультатов[6].Пустой() Тогда
			
			ВыборкаИзРезультатаЗапроса = МассивРезультатов[6].Выбрать();
			УправлениеНебольшойФирмойСервер.СообщитьОбОшибкахПроведенияПоРегиструЗапасыВРазрезеГТД(ДокументОбъектПриходнаяНакладная, ВыборкаИзРезультатаЗапроса, Отказ);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры // ВыполнитьКонтроль()

// Делает записи в регистр сведений Цены номенклатуры контрагентов.
//
Процедура ЗарегистрироватьЦеныПоставщика(ДокументСсылкаКорректировкаПоступления) Экспорт

	Если ДокументСсылкаКорректировкаПоступления.Проведен Тогда
		УдалитьЦеныПоставщика(ДокументСсылкаКорректировкаПоступления);
	КонецЕсли;
	
	ДокументОснование = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ДокументСсылкаКорректировкаПоступления, "ДокументОснование");
	Если ТипЗнч(ДокументОснование) <> Тип("ДокументСсылка.ПриходнаяНакладная")
		ИЛИ НЕ ЗначениеЗаполнено(ДокументСсылкаКорректировкаПоступления.ВидЦенКонтрагента) Тогда
		Возврат;
	КонецЕсли; 
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ТаблицаЦены.Ссылка.Дата КАК Период,
	|	ТаблицаЦены.Ссылка.ВидЦенКонтрагента КАК ВидЦенКонтрагента,
	|	ТаблицаЦены.Номенклатура КАК Номенклатура,
	|	ТаблицаЦены.Характеристика КАК Характеристика,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ТаблицаЦены.Ссылка.СуммаВключаетНДС = ТаблицаЦены.Ссылка.ВидЦенКонтрагента.ЦенаВключаетНДС
	|				ТОГДА ЕСТЬNULL(ТаблицаЦены.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс * КурсВалютыДокумента.Кратность), 0)
	|			КОГДА ТаблицаЦены.Ссылка.СуммаВключаетНДС > ТаблицаЦены.Ссылка.ВидЦенКонтрагента.ЦенаВключаетНДС
	|				ТОГДА ЕСТЬNULL(ТаблицаЦены.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс * КурсВалютыДокумента.Кратность) * 100 / (100 + ТаблицаЦены.СтавкаНДС.Ставка), 0)
	|			ИНАЧЕ ЕСТЬNULL(ТаблицаЦены.Цена * КурсВалютыДокумента.Курс * КурсВалютыВидЦен.Кратность / (КурсВалютыВидЦен.Курс * КурсВалютыДокумента.Кратность) * (100 + ТаблицаЦены.СтавкаНДС.Ставка) / 100, 0)
	|		КОНЕЦ) КАК Цена,
	|	ТаблицаЦены.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ИСТИНА КАК Актуальность,
	|	ТаблицаЦены.Ссылка КАК ДокументРегистратор,
	|	ТаблицаЦены.Ссылка.Автор КАК Автор
	|ИЗ
	|	Документ.КорректировкаПоступления.Запасы КАК ТаблицаЦены
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ЦеныНоменклатурыКонтрагентов КАК ЦеныНоменклатурыКонтрагентов
	|		ПО ТаблицаЦены.Ссылка.ВидЦенКонтрагента = ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента
	|			И ТаблицаЦены.Номенклатура = ЦеныНоменклатурыКонтрагентов.Номенклатура
	|			И ТаблицаЦены.Характеристика = ЦеныНоменклатурыКонтрагентов.Характеристика
	|			И (НАЧАЛОПЕРИОДА(ТаблицаЦены.Ссылка.Дата, ДЕНЬ) = ЦеныНоменклатурыКонтрагентов.Период)
	|			И ТаблицаЦены.Ссылка.Дата <= ЦеныНоменклатурыКонтрагентов.ДокументРегистратор.Дата
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки, ) КАК КурсВалютыВидЦен
	|		ПО ТаблицаЦены.Ссылка.ВидЦенКонтрагента.ВалютаЦены = КурсВалютыВидЦен.Валюта,
	|	РегистрСведений.КурсыВалют.СрезПоследних(&ДатаОбработки, Валюта = &ВалютаДокумента) КАК КурсВалютыДокумента
	|ГДЕ
	|	ТаблицаЦены.Ссылка.РегистрироватьЦеныПоставщика
	|	И ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента ЕСТЬ NULL 
	|	И ТаблицаЦены.Ссылка = &Ссылка
	|	И ТаблицаЦены.Цена <> 0
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЦены.Номенклатура,
	|	ТаблицаЦены.Характеристика,
	|	ТаблицаЦены.ЕдиницаИзмерения,
	|	ТаблицаЦены.Ссылка.Дата,
	|	ТаблицаЦены.Ссылка.ВидЦенКонтрагента,
	|	ТаблицаЦены.Ссылка,
	|	ТаблицаЦены.Ссылка.Автор";
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылкаКорректировкаПоступления);
	Запрос.УстановитьПараметр("ВалютаДокумента", ДокументСсылкаКорректировкаПоступления.ВалютаДокумента);
	Запрос.УстановитьПараметр("ДатаОбработки", ДокументСсылкаКорректировкаПоступления.Дата);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаписей = РезультатЗапроса.Выгрузить();
	
	// Запись набора РС
	НаборЗаписей = РегистрыСведений.ЦеныНоменклатурыКонтрагентов.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Период.Установить(ДокументСсылкаКорректировкаПоступления.Дата);
	НаборЗаписей.Отбор.ВидЦенКонтрагента.Установить(ДокументСсылкаКорректировкаПоступления.ВидЦенКонтрагента);
	Для каждого СтрокаТаблицы Из ТаблицаЗаписей Цикл
		НоваяЗапись = НаборЗаписей.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяЗапись, СтрокаТаблицы);
	КонецЦикла; 
	НаборЗаписей.Записать();
	
КонецПроцедуры // ЗарегистрироватьЦеныПоставщика()

// Удаляет записи из регистра сведений Цены номенклатуры контрагентов.
//
Процедура УдалитьЦеныПоставщика(ДокументСсылкаКорректировкаПоступления) Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ЦеныНоменклатурыКонтрагентов.Период,
	|	ЦеныНоменклатурыКонтрагентов.ВидЦенКонтрагента,
	|	ЦеныНоменклатурыКонтрагентов.Номенклатура,
	|	ЦеныНоменклатурыКонтрагентов.Характеристика
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатурыКонтрагентов КАК ЦеныНоменклатурыКонтрагентов
	|ГДЕ
	|	ЦеныНоменклатурыКонтрагентов.ДокументРегистратор = &ДокументРегистратор";
	
	Запрос.УстановитьПараметр("ДокументРегистратор", ДокументСсылкаКорректировкаПоступления);
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаЗаписей = РезультатЗапроса.Выгрузить();	
	
	Для каждого СтрокаТаблицы Из ТаблицаЗаписей Цикл
		НаборЗаписей = РегистрыСведений.ЦеныНоменклатурыКонтрагентов.СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.Период.Установить(СтрокаТаблицы.Период);
		НаборЗаписей.Отбор.ВидЦенКонтрагента.Установить(СтрокаТаблицы.ВидЦенКонтрагента);
		НаборЗаписей.Отбор.Номенклатура.Установить(СтрокаТаблицы.Номенклатура);
		НаборЗаписей.Отбор.Характеристика.Установить(СтрокаТаблицы.Характеристика);
		НаборЗаписей.Записать();
	КонецЦикла;	

КонецПроцедуры // УдалитьЦеныПоставщика()

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

Функция ТекстЗапросаДанныеДляПечатиКорректировочныхСчетовФактур(НомераТаблиц) Экспорт

	НомераТаблиц.Вставить("ВалютаРегламентированногоУчета",    НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты",         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты",                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаНоменклатуры",      НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаДокумента",         НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	КонстантаВалютаРегламентированногоУчета.Значение КАК ВалютаРеглУчета
	|ПОМЕСТИТЬ КонстантаВалютаРегламентированногоУчета
	|ИЗ
	|	Константа.НациональнаяВалюта КАК КонстантаВалютаРегламентированногоУчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка,
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	Реквизиты.Подразделение,
	|	Реквизиты.Контрагент,
	|	ВЫБОР
	|		КОГДА КонстантаВалютаРегламентированногоУчета.ВалютаРеглУчета <> Реквизиты.ВалютаДокумента
	|				И Реквизиты.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА КонстантаВалютаРегламентированногоУчета.ВалютаРеглУчета
	|		ИНАЧЕ Реквизиты.ВалютаДокумента
	|	КОНЕЦ КАК ВалютаДокумента,
	|	Реквизиты.Договор,
	|	Реквизиты.СуммаВключаетНДС,
	|	ВЫБОР
	|		КОГДА КонстантаВалютаРегламентированногоУчета.ВалютаРеглУчета <> Реквизиты.ВалютаДокумента
	|				И Реквизиты.Договор.РасчетыВУсловныхЕдиницах
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ИспользоватьПересчет,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийИсправленияПоступленияРеализации.ИсправлениеОшибки)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Исправление,
	|	ВЫБОР
	|		КОГДА Реквизиты.Курс = 0
	|			ТОГДА 1
	|		ИНАЧЕ Реквизиты.Курс
	|	КОНЕЦ КАК Курс,
	|	ВЫБОР
	|		КОГДА Реквизиты.Кратность = 0
	|			ТОГДА 1
	|		ИНАЧЕ Реквизиты.Кратность
	|	КОНЕЦ КАК Кратность
	|ПОМЕСТИТЬ ВременнаяТаблицаРеквизиты
	|ИЗ
	|	Документ.КорректировкаПоступления КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ КонстантаВалютаРегламентированногоУчета КАК КонстантаВалютаРегламентированногоУчета
	|		ПО (ИСТИНА)
	|ГДЕ
	|	Реквизиты.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК ДатаОснования,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Подразделение КАК Подразделение,
	|	"""" КАК ЦифровойИндексОбособленногоПодразделения,
	|	Реквизиты.Организация КАК Поставщик,
	|	ВЫБОР
	|		КОГДА &АдресПоставщика <> """"
	|			ТОГДА &АдресПоставщика
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК АдресПоставщика,
	|	ВЫБОР
	|		КОГДА &ИННКПППоставщика <> """"
	|			ТОГДА &ИННКПППоставщика
	|		ИНАЧЕ """"
	|	КОНЕЦ КАК ИННКПППоставщика,
	|	Реквизиты.Организация.ИНН КАК ИННПоставщика,
	|	Реквизиты.Организация КАК ОбособленноеПодразделениеПоставщика,
	|	Реквизиты.Контрагент КАК Покупатель,
	|	Реквизиты.Контрагент.ИНН КАК ИННпокупателя,
	|	Реквизиты.Контрагент КАК ОбособленноеПодразделениеПокупателя,
	|	Реквизиты.ВалютаДокумента КАК Валюта,
	|	Реквизиты.Договор.ВалютаРасчетов КАК ВалютаВзаиморасчетов,
	|	Реквизиты.Договор.РасчетыВУсловныхЕдиницах КАК РасчетыВУсловныхЕдиницах,
	|	Реквизиты.Договор.Представление КАК Основание
	|ИЗ
	|	ВременнаяТаблицаРеквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.СчетФактураПолученный КАК СчетФактураПолученный
	|		ПО Реквизиты.Ссылка = СчетФактураПолученный.ДокументОснование
	|			И (НЕ СчетФактураПолученный.ПометкаУдаления)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КорректировкаПоступленияЗапасы.Ссылка КАК Ссылка,
	|	1 КАК НомерТабЧасти,
	|	КорректировкаПоступленияЗапасы.НомерСтроки КАК НомерСтроки,
	|	КорректировкаПоступленияЗапасы.Номенклатура КАК Товар,
	|	КорректировкаПоступленияЗапасы.Номенклатура.Код КАК ТоварКод,
	|	КорректировкаПоступленияЗапасы.Номенклатура.Артикул КАК ТоварАртикул,
	|	КорректировкаПоступленияЗапасы.Номенклатура.НаименованиеПолное КАК ТоварНаименование,
	|	НЕОПРЕДЕЛЕНО КАК СтранаПроисхождения,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеСтраны,
	|	НЕОПРЕДЕЛЕНО КАК НомерГТД,
	|	НЕОПРЕДЕЛЕНО КАК ПредставлениеГТД,
	|	ВЫРАЗИТЬ(КорректировкаПоступленияЗапасы.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	КорректировкаПоступленияЗапасы.СтавкаНДС КАК СтавкаНДС,
	|	ЛОЖЬ КАК ЭтоУслуга,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаПоступленияЗапасы.КоличествоДоКорректировки
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.КоличествоДоИзменения
	|	КОНЕЦ КАК КоличествоДоИзменения,
	|	КорректировкаПоступленияЗапасы.Количество КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаПоступленияЗапасы.ЦенаДоКорректировки
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.ЦенаДоИзменения
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЦенаДоИзменения,
	|	КорректировкаПоступленияЗапасы.Цена * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК ЦенаПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаПоступленияЗапасы.СуммаНДСДоКорректировки
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаНДСДоИзменения
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаНДСДоИзменения,
	|	КорректировкаПоступленияЗапасы.СуммаНДС * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СуммаНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаПоступленияЗапасы.СуммаДоКорректировки - КорректировкаПоступленияЗапасы.СуммаНДСДоКорректировки
	|					ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаПоступленияЗапасы.СуммаДоИзменения - КорректировкаПоступленияЗапасы.СуммаНДСДоИзменения
	|				ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтоимостьБезНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияЗапасы.Сумма - КорректировкаПоступленияЗапасы.СуммаНДС
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.Сумма
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтоимостьБезНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаПоступленияЗапасы.СуммаДоКорректировки
	|					ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаДоКорректировки + КорректировкаПоступленияЗапасы.СуммаНДСДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаПоступленияЗапасы.СуммаДоИзменения
	|				ИНАЧЕ КорректировкаПоступленияЗапасы.СуммаДоИзменения + КорректировкаПоступленияЗапасы.СуммаНДСДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтоимостьСНДСДоИзменения,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияЗапасы.Сумма
	|		ИНАЧЕ КорректировкаПоступленияЗапасы.Сумма + КорректировкаПоступленияЗапасы.СуммаНДС
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ КАК СтоимостьСНДСПослеИзменения
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.КорректировкаПоступления.Запасы КАК КорректировкаПоступленияЗапасы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРеквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = КорректировкаПоступленияЗапасы.Ссылка)
	|ГДЕ
	|	КорректировкаПоступленияЗапасы.Ссылка = &ДокументОснование
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	КорректировкаПоступленияРасходы.Ссылка,
	|	1,
	|	КорректировкаПоступленияРасходы.НомерСтроки,
	|	КорректировкаПоступленияРасходы.Номенклатура,
	|	КорректировкаПоступленияРасходы.Номенклатура.Код,
	|	КорректировкаПоступленияРасходы.Номенклатура.Артикул,
	|	КорректировкаПоступленияРасходы.Номенклатура.НаименованиеПолное,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	НЕОПРЕДЕЛЕНО,
	|	ВЫРАЗИТЬ(КорректировкаПоступленияРасходы.Номенклатура КАК Справочник.Номенклатура).ЕдиницаИзмерения,
	|	КорректировкаПоступленияРасходы.СтавкаНДС,
	|	ИСТИНА,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаПоступленияРасходы.КоличествоДоКорректировки
	|		ИНАЧЕ КорректировкаПоступленияРасходы.КоличествоДоИзменения
	|	КОНЕЦ,
	|	КорректировкаПоступленияРасходы.Количество,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаПоступленияРасходы.ЦенаДоКорректировки
	|		ИНАЧЕ КорректировкаПоступленияРасходы.ЦенаДоИзменения
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	КорректировкаПоступленияРасходы.Цена * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА КорректировкаПоступленияРасходы.СуммаНДСДоКорректировки
	|		ИНАЧЕ КорректировкаПоступленияРасходы.СуммаНДСДоИзменения
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	КорректировкаПоступленияРасходы.СуммаНДС * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаПоступленияРасходы.СуммаДоКорректировки - КорректировкаПоступленияРасходы.СуммаНДСДоКорректировки
	|					ИНАЧЕ КорректировкаПоступленияРасходы.СуммаДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаПоступленияРасходы.СуммаДоИзменения - КорректировкаПоступленияРасходы.СуммаНДСДоИзменения
	|				ИНАЧЕ КорректировкаПоступленияРасходы.СуммаДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияРасходы.Сумма - КорректировкаПоступленияРасходы.СуммаНДС
	|		ИНАЧЕ КорректировкаПоступленияРасходы.Сумма
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.Исправление
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.СуммаВключаетНДС
	|						ТОГДА КорректировкаПоступленияРасходы.СуммаДоКорректировки
	|					ИНАЧЕ КорректировкаПоступленияРасходы.СуммаДоКорректировки + КорректировкаПоступленияРасходы.СуммаНДСДоКорректировки
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА Реквизиты.СуммаВключаетНДС
	|					ТОГДА КорректировкаПоступленияРасходы.СуммаДоИзменения
	|				ИНАЧЕ КорректировкаПоступленияРасходы.СуммаДоИзменения + КорректировкаПоступленияРасходы.СуммаНДСДоИзменения
	|			КОНЕЦ
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ,
	|	ВЫБОР
	|		КОГДА Реквизиты.СуммаВключаетНДС
	|			ТОГДА КорректировкаПоступленияРасходы.Сумма
	|		ИНАЧЕ КорректировкаПоступленияРасходы.Сумма + КорректировкаПоступленияРасходы.СуммаНДС
	|	КОНЕЦ * ВЫБОР
	|		КОГДА Реквизиты.ИспользоватьПересчет
	|			ТОГДА Реквизиты.Курс / Реквизиты.Кратность
	|		ИНАЧЕ 1
	|	КОНЕЦ
	|ИЗ
	|	Документ.КорректировкаПоступления.Расходы КАК КорректировкаПоступленияРасходы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВременнаяТаблицаРеквизиты КАК Реквизиты
	|		ПО (Реквизиты.Ссылка = КорректировкаПоступленияРасходы.Ссылка)
	|ГДЕ
	|	КорректировкаПоступленияРасходы.Ссылка = &ДокументОснование
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаНоменклатуры.НомерТабЧасти КАК НомерТабЧасти,
	|	ТаблицаНоменклатуры.НомерСтроки КАК НомерСтроки,
	|	ТаблицаНоменклатуры.Товар КАК Товар,
	|	ТаблицаНоменклатуры.ТоварКод КАК ТоварКод,
	|	ТаблицаНоменклатуры.ТоварАртикул КАК ТоварАртикул,
	|	ТаблицаНоменклатуры.ТоварНаименование КАК ТоварНаименование,
	|	ТаблицаНоменклатуры.СтранаПроисхождения КАК СтранаПроисхождения,
	|	ТаблицаНоменклатуры.ПредставлениеСтраны КАК ПредставлениеСтраны,
	|	ТаблицаНоменклатуры.НомерГТД КАК НомерГТД,
	|	ТаблицаНоменклатуры.ПредставлениеГТД КАК ПредставлениеГТД,
	|	ТаблицаНоменклатуры.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	ТаблицаНоменклатуры.КоличествоДоИзменения КАК КоличествоДоИзменения,
	|	ТаблицаНоменклатуры.КоличествоПослеИзменения КАК КоличествоПослеИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоДоИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения / ТаблицаНоменклатуры.КоличествоДоИзменения
	|	КОНЕЦ КАК ЦенаДоИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.КоличествоПослеИзменения = 0
	|			ТОГДА 0
	|		ИНАЧЕ ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения / ТаблицаНоменклатуры.КоличествоПослеИзменения
	|	КОНЕЦ КАК ЦенаПослеИзменения,
	|	ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения КАК СтоимостьБезНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения КАК СтоимостьБезНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДСПослеИзменения КАК СуммаНДСПослеИзменения,
	|	ТаблицаНоменклатуры.СуммаНДСДоИзменения КАК СуммаНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения КАК СтоимостьСНДСДоИзменения,
	|	ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения КАК СтоимостьСНДСПослеИзменения,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения > ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения - ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения > ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения - ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаБезНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СуммаНДСДоИзменения > ТаблицаНоменклатуры.СуммаНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСДоИзменения - ТаблицаНоменклатуры.СуммаНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СуммаНДСПослеИзменения > ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СуммаНДСПослеИзменения - ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаНДСУвеличение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения > ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения - ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУменьшение,
	|	ВЫБОР
	|		КОГДА ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения > ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения
	|			ТОГДА ТаблицаНоменклатуры.СтоимостьСНДСПослеИзменения - ТаблицаНоменклатуры.СтоимостьСНДСДоИзменения
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК РазницаСНДСУвеличение,
	|	ТаблицаНоменклатуры.СтавкаНДС КАК СтавкаНДС,
	|	ТаблицаНоменклатуры.ЭтоУслуга КАК ЭтоУслуга,
	|	ТаблицаНоменклатуры.Ссылка КАК Ссылка
	|ПОМЕСТИТЬ ТаблицаДокумента
	|ИЗ
	|	ТаблицаНоменклатуры КАК ТаблицаНоменклатуры
	|ГДЕ
	|	(ТаблицаНоменклатуры.КоличествоПослеИзменения <> ТаблицаНоменклатуры.КоличествоДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.СтоимостьБезНДСПослеИзменения <> ТаблицаНоменклатуры.СтоимостьБезНДСДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.СуммаНДСПослеИзменения <> ТаблицаНоменклатуры.СуммаНДСДоИзменения
	|			ИЛИ ТаблицаНоменклатуры.ЦенаПослеИзменения <> ТаблицаНоменклатуры.ЦенаДоИзменения)";
	
	Возврат ТекстЗапроса + УправлениеНебольшойФирмойСервер.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура СформироватьТаблицаЗапасыВРазрезеГТД(СтруктураДополнительныеСвойства)
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = СтруктураДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(ТаблицаЗапасыНаСкладах.НомерСтроки) КАК НомерСтроки
	|	,ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход) КАК ВидДвижения
	|	,ТаблицаЗапасыНаСкладах.Ссылка.Дата КАК Период
	|	,&Организация КАК Организация
	|	,ТаблицаЗапасыНаСкладах.Номенклатура КАК Номенклатура
	|	,ТаблицаЗапасыНаСкладах.Характеристика КАК Характеристика
	|	,ТаблицаЗапасыНаСкладах.Партия КАК Партия
	|	,ТаблицаЗапасыНаСкладах.НомерГТД КАК НомерГТД
	|	,ТаблицаЗапасыНаСкладах.СтранаПроисхождения КАК СтранаПроисхождения
	|	,СУММА(ТаблицаЗапасыНаСкладах.Количество) КАК Количество
	|ИЗ
	|	ВременнаяТаблицаИзмененияЗапасов КАК ТаблицаЗапасыНаСкладах
	|ГДЕ
	|	ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.Россия)
	|	И ТаблицаЗапасыНаСкладах.СтранаПроисхождения <> Значение(Справочник.СтраныМира.ПустаяССылка)
	|	И ТаблицаЗапасыНаСкладах.НомерГТД <> Значение(Справочник.НомераГТД.ПустаяССылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТаблицаЗапасыНаСкладах.Ссылка.Дата
	|	,ТаблицаЗапасыНаСкладах.Номенклатура
	|	,ТаблицаЗапасыНаСкладах.Характеристика
	|	,ТаблицаЗапасыНаСкладах.Партия
	|	,ТаблицаЗапасыНаСкладах.НомерГТД
	|	,ТаблицаЗапасыНаСкладах.СтранаПроисхождения";
	
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.Дляпроведения.Организация);
	РезультатЗапроса = Запрос.Выполнить();
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаЗапасыВРазрезеГТД", РезультатЗапроса.Выгрузить());
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли