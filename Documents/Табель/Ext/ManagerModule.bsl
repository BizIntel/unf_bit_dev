#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Инициализирует таблицы значений, содержащие данные табличных частей документа.
// Таблицы значений сохраняет в свойствах структуры "ДополнительныеСвойства".
//
Процедура ИнициализироватьДанныеДокумента(ДокументТабель, СтруктураДополнительныеСвойства) Экспорт

	Если ДокументТабель.СпособВводаДанных = Перечисления.СпособыВводаДанныхОВремени.ВцеломЗаПериод Тогда
	
		ТекстЗапроса = "";
		Для Счетчик = 1 По 6 Цикл
		
			ТекстЗапроса = 	ТекстЗапроса + ?(Счетчик > 1, "	
			|ОБЪЕДИНИТЬ ВСЕ
			| 
			|", "") + 
			"ВЫБРАТЬ
			|	&Организация КАК Организация,
			|	ТабельОтработанноеВремяЗаПериод.Ссылка.ПериодРегистрации 	КАК Период,
			|	ИСТИНА									 					КАК ВЦеломЗаПериод,
			|	ТабельОтработанноеВремяЗаПериод.Сотрудник 					КАК Сотрудник,
			|	ТабельОтработанноеВремяЗаПериод.Ссылка.СтруктурнаяЕдиница 	КАК СтруктурнаяЕдиница,
			|	ТабельОтработанноеВремяЗаПериод.Должность 					КАК Должность,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени" + Счетчик + " 	КАК ВидВремени,
			|	ТабельОтработанноеВремяЗаПериод.Дней" + Счетчик + " 		КАК Дней,
			|	ТабельОтработанноеВремяЗаПериод.Часов" + Счетчик + " 		КАК Часов
			|ИЗ
			|	Документ.Табель.ОтработанноеВремяЗаПериод КАК ТабельОтработанноеВремяЗаПериод
			|ГДЕ
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени" + Счетчик + " <> ЗНАЧЕНИЕ(Справочник.ВидыРабочегоВремени.ПустаяСсылка)
			|	И ТабельОтработанноеВремяЗаПериод.Ссылка = &Ссылка
			|";
		
		КонецЦикла; 
		
	Иначе        
		
		ТекстЗапроса = "";
		Для Счетчик = 1 По 31 Цикл
		
			ТекстЗапроса = ТекстЗапроса + ?(Счетчик > 1, "	
			|ОБЪЕДИНИТЬ ВСЕ
			| 
			|", "") + 
			"ВЫБРАТЬ
			|	&Организация 															КАК Организация,
			|	ЛОЖЬ									 								КАК ВЦеломЗаПериод,
			|	ТабельОтработанноеВремяПоДням.Сотрудник 								КАК Сотрудник,
			|	ТабельОтработанноеВремяПоДням.Ссылка.СтруктурнаяЕдиница 				КАК СтруктурнаяЕдиница,
			|	ТабельОтработанноеВремяПоДням.Должность 								КАК Должность,
			|	ДОБАВИТЬКДАТЕ(ТабельОтработанноеВремяПоДням.Ссылка.ПериодРегистрации, ДЕНЬ, " + (Счетчик - 1) + ") КАК Период,
			|	1 																		КАК Дней,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени" + Счетчик + " 			КАК ВидВремени,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы" + Счетчик + " 				КАК Часов
			|ИЗ
			|	Документ.Табель.ОтработанноеВремяПоДням КАК ТабельОтработанноеВремяПоДням
			|ГДЕ
			|	ТабельОтработанноеВремяПоДням.Ссылка = &Ссылка
			|	И ТабельОтработанноеВремяПоДням.ПервыйВидВремени" + Счетчик + " <> ЗНАЧЕНИЕ(Справочник.ВидыРабочегоВремени.ПустаяСсылка)
			|		
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	&Организация,
			|	ЛОЖЬ,
			|	ТабельОтработанноеВремяПоДням.Сотрудник,
			|	ТабельОтработанноеВремяПоДням.Ссылка.СтруктурнаяЕдиница,
			|	ТабельОтработанноеВремяПоДням.Должность,
			|	ДОБАВИТЬКДАТЕ(ТабельОтработанноеВремяПоДням.Ссылка.ПериодРегистрации, ДЕНЬ, " + (Счетчик - 1) + "),
			|	1,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени" + Счетчик + ",
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы" + Счетчик + "
			|ИЗ
			|	Документ.Табель.ОтработанноеВремяПоДням КАК ТабельОтработанноеВремяПоДням
			|ГДЕ
			|	ТабельОтработанноеВремяПоДням.Ссылка = &Ссылка
			|	И ТабельОтработанноеВремяПоДням.ВторойВидВремени" + Счетчик + " <> ЗНАЧЕНИЕ(Справочник.ВидыРабочегоВремени.ПустаяСсылка)
			|		
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	&Организация,
			|	ЛОЖЬ,
			|	ТабельОтработанноеВремяПоДням.Сотрудник,
			|	ТабельОтработанноеВремяПоДням.Ссылка.СтруктурнаяЕдиница,
			|	ТабельОтработанноеВремяПоДням.Должность,
			|	ДОБАВИТЬКДАТЕ(ТабельОтработанноеВремяПоДням.Ссылка.ПериодРегистрации, ДЕНЬ, " + (Счетчик - 1) + "),
			|	1,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени" + Счетчик + ",
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы" + Счетчик + "
			|ИЗ
			|	Документ.Табель.ОтработанноеВремяПоДням КАК ТабельОтработанноеВремяПоДням
			|ГДЕ
			|	ТабельОтработанноеВремяПоДням.Ссылка = &Ссылка
			|	И ТабельОтработанноеВремяПоДням.ТретийВидВремени" + Счетчик + " <> ЗНАЧЕНИЕ(Справочник.ВидыРабочегоВремени.ПустаяСсылка)
			|";	
		
		КонецЦикла;
		
	КонецЕсли; 
		
	Запрос = Новый Запрос(ТекстЗапроса);
	
	Запрос.УстановитьПараметр("Ссылка", ДокументТабель);
	Запрос.УстановитьПараметр("Организация", СтруктураДополнительныеСвойства.ДляПроведения.Организация);
	
	СтруктураДополнительныеСвойства.ТаблицыДляДвижений.Вставить("ТаблицаТабель", Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры // ИнициализироватьДанныеДокумента()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция проверяет, необходимо ли данные прибавлять к отработанному времени
Функция ПрибавитьКОтработанномуВремени(ВидВремени)
	
	Если ВидВремени = Справочники.ВидыРабочегоВремени.Праздники
		ИЛИ ВидВремени = Справочники.ВидыРабочегоВремени.Сверхурочные
		ИЛИ ВидВремени = Справочники.ВидыРабочегоВремени.Работа Тогда
	
		Возврат Истина;	
	Иначе	
		Возврат Ложь;	
	КонецЕсли; 
	
КонецФункции

#КонецОбласти

#Область ИнтерфейсПечати

Функция ПечатнаяФорма(МассивОбъектов, ОбъектыПечати)
	
	ТабличныйДокумент = Новый ТабличныйДокумент;
	ТабличныйДокумент.КлючПараметровПечати = "ПараметрыПечати_Табель";
	
	ПервыйДокумент = Истина;
	
	Для Каждого ТекущийДокумент Из МассивОбъектов Цикл
		
		Если Не ПервыйДокумент Тогда
			ТабличныйДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		ПервыйДокумент = Ложь;
		
		НомерСтрокиНачало = ТабличныйДокумент.ВысотаТаблицы + 1;
		
		Запрос = Новый Запрос();
		Запрос.УстановитьПараметр("ТекущийДокумент", ТекущийДокумент);
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	Табель.Дата КАК ДатаДокумента,
		|	Табель.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	Табель.ПериодРегистрации КАК ПериодРегистрации,
		|	Табель.Номер,
		|	Табель.Организация.Префикс КАК Префикс,
		|	Табель.Организация.НаименованиеПолное,
		|	Табель.Организация,
		|	Табель.СпособВводаДанных
		|ИЗ
		|	Документ.Табель КАК Табель
		|ГДЕ
		|	Табель.Ссылка = &ТекущийДокумент";
		
		Шапка = Запрос.Выполнить().Выбрать();
		Шапка.Следующий();
		
		Если Шапка.СпособВводаДанных = Перечисления.СпособыВводаДанныхОВремени.ПоДням Тогда
			ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Табель_Макет";		
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Табель.ПФ_MXL_Макет");
		Иначе
			ТабличныйДокумент.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Табель_МакетСводно";		
			Макет = УправлениеПечатью.МакетПечатнойФормы("Документ.Табель.ПФ_MXL_МакетСводно");
		КонецЕсли;
		
		ОбластьШапкаДокумента = Макет.ПолучитьОбласть("ШапкаДокумента");
		ОбластьШапка          = Макет.ПолучитьОбласть("Шапка");
		ОбластьДетали         = Макет.ПолучитьОбласть("Детали");
		ОбластьПодвал         = Макет.ПолучитьОбласть("Подвал");
		
		Если Шапка.ДатаДокумента < Дата('20110101') Тогда
			НомерДокумента = УправлениеНебольшойФирмойСервер.ПолучитьНомерНаПечать(Шапка.Номер, Шапка.Префикс);
		Иначе
			НомерДокумента = ПрефиксацияОбъектовКлиентСервер.НомерНаПечать(Шапка.Номер, Истина, Истина);
		КонецЕсли;		
		
		ОбластьШапкаДокумента.Параметры.ИмяОрганизации = Шапка.ОрганизацияНаименованиеПолное;
		ОбластьШапкаДокумента.Параметры.ИмяПодразделения = Шапка.СтруктурнаяЕдиница;
		ОбластьШапкаДокумента.Параметры.НомерДокумента = НомерДокумента;
		ОбластьШапкаДокумента.Параметры.ДатаЗаполнения = Шапка.ДатаДокумента;
		ОбластьШапкаДокумента.Параметры.ДатаНач = Шапка.ПериодРегистрации;
		ОбластьШапкаДокумента.Параметры.ДатаКон = КонецМесяца(Шапка.ПериодРегистрации);
				
		ТабличныйДокумент.Вывести(ОбластьШапкаДокумента);
		ТабличныйДокумент.Вывести(ОбластьШапка);
		                                             
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Ссылка",   ТекущийДокумент);
		Запрос.УстановитьПараметр("ПериодРегистрации",   Шапка.ПериодРегистрации);
		
		Если Шапка.СпособВводаДанных = Перечисления.СпособыВводаДанныхОВремени.ПоДням Тогда
				
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ФИОФизЛицСрезПоследних.Фамилия,
			|	ФИОФизЛицСрезПоследних.Имя,
			|	ФИОФизЛицСрезПоследних.Отчество,
			|	ТабельОтработанноеВремяПоДням.Сотрудник,
			|	ТабельОтработанноеВремяПоДням.Должность,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени1,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени2,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени3,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени4,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени5,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени6,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени7,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени8,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени9,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени10,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени11,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени12,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени13,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени14,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени15,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени16,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени17,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени18,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени19,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени20,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени21,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени22,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени23,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени24,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени25,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени26,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени27,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени28,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени29,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени30,
			|	ТабельОтработанноеВремяПоДням.ПервыйВидВремени31,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени1,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени2,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени3,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени4,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени5,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени6,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени7,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени8,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени9,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени10,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени11,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени12,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени13,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени14,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени15,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени16,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени17,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени18,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени19,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени20,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени21,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени22,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени23,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени24,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени25,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени26,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени27,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени28,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени29,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени30,
			|	ТабельОтработанноеВремяПоДням.ВторойВидВремени31,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени1,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени2,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени3,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени4,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени5,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени6,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени7,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени8,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени9,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени10,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени11,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени12,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени13,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени14,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени15,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени16,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени17,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени18,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени19,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени20,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени21,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени22,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени23,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени24,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени25,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени26,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени27,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени28,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени29,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени30,
			|	ТабельОтработанноеВремяПоДням.ТретийВидВремени31,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы1,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы2,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы3,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы4,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы5,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы6,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы7,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы8,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы9,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы10,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы11,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы12,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы13,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы14,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы15,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы16,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы17,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы18,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы19,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы20,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы21,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы22,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы23,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы24,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы25,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы26,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы27,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы28,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы29,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы30,
			|	ТабельОтработанноеВремяПоДням.ПервыйЧасы31,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы1,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы2,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы3,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы4,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы5,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы6,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы7,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы8,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы9,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы10,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы11,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы12,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы13,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы14,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы15,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы16,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы17,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы18,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы19,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы20,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы21,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы22,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы23,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы24,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы25,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы26,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы27,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы28,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы29,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы30,
			|	ТабельОтработанноеВремяПоДням.ВторойЧасы31,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы1,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы2,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы3,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы4,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы5,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы6,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы7,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы8,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы9,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы10,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы11,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы12,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы13,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы14,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы15,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы16,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы17,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы18,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы19,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы20,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы21,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы22,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы23,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы24,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы25,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы26,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы27,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы28,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы29,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы30,
			|	ТабельОтработанноеВремяПоДням.ТретийЧасы31,
			|	ТабельОтработанноеВремяПоДням.Сотрудник.Код КАК ТабНомер
			|ИЗ
			|	Документ.Табель.ОтработанноеВремяПоДням КАК ТабельОтработанноеВремяПоДням
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ПериодРегистрации, ) КАК ФИОФизЛицСрезПоследних
			|		ПО ТабельОтработанноеВремяПоДням.Сотрудник.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
			|ГДЕ
			|	ТабельОтработанноеВремяПоДням.Ссылка = &Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТабельОтработанноеВремяПоДням.НомерСтроки";
			
			Выборка = Запрос.Выполнить().Выбрать();
            				
			НПП = 0;
			Пока Выборка.Следующий() Цикл
				ЧасыПерваяПоловина = 0;
				ДниПерваяПоловина = 0;
				ЧасыВтораяПоловина = 0;
				ДниВтораяПоловина = 0;
				НПП = НПП + 1;				
				ОбластьДетали.Параметры.НомерПП = НПП;
				ОбластьДетали.Параметры.Заполнить(Выборка);
				Если ЗначениеЗаполнено(Выборка.Фамилия) Тогда
					ФИО = УправлениеНебольшойФирмойСервер.ПолучитьФамилиюИмяОтчество(Выборка.Фамилия, Выборка.Имя, Выборка.Отчество, Истина);
				Иначе
					ФИО = "";
				КонецЕсли;
				ОбластьДетали.Параметры.НаименованиеСотрудник = ?(ЗначениеЗаполнено(ФИО), ФИО, Выборка.Сотрудник);
				
				Для Счетчик = 1 По 15 Цикл
					
				    СтрокаВидВремени = "" + Выборка["ПервыйВидВремени" + Счетчик] + ?(ЗначениеЗаполнено(Выборка["ВторойВидВремени" + Счетчик]), "/" + Выборка["ВторойВидВремени" + Счетчик], "") + ?(ЗначениеЗаполнено(Выборка["ТретийВидВремени" + Счетчик]), "/" + Выборка["ТретийВидВремени" + Счетчик], "");
					СтрокаЧасы = "" + ?(Выборка["ПервыйЧасы" + Счетчик] = 0, "", Выборка["ПервыйЧасы" + Счетчик]) + ?(ЗначениеЗаполнено(Выборка["ВторойВидВремени" + Счетчик]), "/" + Выборка["ВторойЧасы" + Счетчик], "") + ?(ЗначениеЗаполнено(Выборка["ТретийВидВремени" + Счетчик]), "/" + Выборка["ТретийЧасы" + Счетчик], "");
								 
					ОбластьДетали.Параметры["Символ" + Счетчик] = СтрокаВидВремени;			 
					ОбластьДетали.Параметры["ДополнительноеЗначение" + Счетчик] = СтрокаЧасы;			 
								 
					Часы = ?(ПрибавитьКОтработанномуВремени(Выборка["ПервыйВидВремени" + Счетчик]), Выборка["ПервыйЧасы" + Счетчик], 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка["ВторойВидВремени" + Счетчик]), Выборка["ВторойЧасы" + Счетчик], 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка["ТретийВидВремени" + Счетчик]), Выборка["ТретийЧасы" + Счетчик], 0);
					ЧасыПерваяПоловина = ЧасыПерваяПоловина +  Часы;
					ДниПерваяПоловина = ДниПерваяПоловина + ?(Часы > 0, 1, 0);
					
				КонецЦикла; 
				
				Для Счетчик = 16 По День(КонецМесяца(ТекущийДокумент.ПериодРегистрации)) Цикл
					
				    СтрокаВидВремени = "" + Выборка["ПервыйВидВремени" + Счетчик] + ?(ЗначениеЗаполнено(Выборка["ВторойВидВремени" + Счетчик]), "/" + Выборка["ВторойВидВремени" + Счетчик], "") + ?(ЗначениеЗаполнено(Выборка["ТретийВидВремени" + Счетчик]), "/" + Выборка["ТретийВидВремени" + Счетчик], "");
					СтрокаЧасы = "" + ?(Выборка["ПервыйЧасы" + Счетчик] = 0, "", Выборка["ПервыйЧасы" + Счетчик]) + ?(ЗначениеЗаполнено(Выборка["ВторойВидВремени" + Счетчик]), "/" + Выборка["ВторойЧасы" + Счетчик], "") + ?(ЗначениеЗаполнено(Выборка["ТретийВидВремени" + Счетчик]), "/" + Выборка["ТретийЧасы" + Счетчик], "");
								 
					ОбластьДетали.Параметры["Символ" + Счетчик] = СтрокаВидВремени;			 
					ОбластьДетали.Параметры["ДополнительноеЗначение" + Счетчик] = СтрокаЧасы;			 
								 
					Часы = ?(ПрибавитьКОтработанномуВремени(Выборка["ПервыйВидВремени" + Счетчик]), Выборка["ПервыйЧасы" + Счетчик], 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка["ВторойВидВремени" + Счетчик]), Выборка["ВторойЧасы" + Счетчик], 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка["ТретийВидВремени" + Счетчик]), Выборка["ТретийЧасы" + Счетчик], 0);
					ЧасыВтораяПоловина = ЧасыВтораяПоловина + Часы;
					ДниВтораяПоловина = ДниВтораяПоловина + ?(Часы > 0, 1, 0);
					
				КонецЦикла; 
				
				Для Счетчик = День(КонецМесяца(ТекущийДокумент.ПериодРегистрации)) + 1 По 31 Цикл
					
				    ОбластьДетали.Параметры["Символ" + Счетчик] = "Х";			 
					ОбластьДетали.Параметры["ДополнительноеЗначение" + Счетчик] = "Х";
					
				КонецЦикла; 
				
				ОбластьДетали.Параметры.ЧасыПерваяПоловина = ЧасыПерваяПоловина;
				ОбластьДетали.Параметры.ДниПерваяПоловина = ДниПерваяПоловина;
				ОбластьДетали.Параметры.ЧасыВтораяПоловина = ЧасыВтораяПоловина;
				ОбластьДетали.Параметры.ДниВтораяПоловина = ДниВтораяПоловина;
				ОбластьДетали.Параметры.ДниЗаМесяц = ДниПерваяПоловина + ДниВтораяПоловина;
				ОбластьДетали.Параметры.ЧасыЗаМесяц = ЧасыПерваяПоловина + ЧасыВтораяПоловина;
				
				ТабличныйДокумент.Вывести(ОбластьДетали);
			КонецЦикла;
			
		Иначе
		
			Запрос.Текст =
			"ВЫБРАТЬ
			|	ФИОФизЛицСрезПоследних.Фамилия,
			|	ФИОФизЛицСрезПоследних.Имя,
			|	ФИОФизЛицСрезПоследних.Отчество,
			|	ТабельОтработанноеВремяЗаПериод.Сотрудник,
			|	ТабельОтработанноеВремяЗаПериод.Должность,
			|	ТабельОтработанноеВремяЗаПериод.Сотрудник.Код КАК ТабНомер,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени1,
			|	ТабельОтработанноеВремяЗаПериод.Часов1,
			|	ТабельОтработанноеВремяЗаПериод.Дней1,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени2,
			|	ТабельОтработанноеВремяЗаПериод.Часов2,
			|	ТабельОтработанноеВремяЗаПериод.Дней2,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени3,
			|	ТабельОтработанноеВремяЗаПериод.Часов3,
			|	ТабельОтработанноеВремяЗаПериод.Дней3,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени4,
			|	ТабельОтработанноеВремяЗаПериод.Часов4,
			|	ТабельОтработанноеВремяЗаПериод.Дней4,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени5,
			|	ТабельОтработанноеВремяЗаПериод.Часов5,
			|	ТабельОтработанноеВремяЗаПериод.Дней5,
			|	ТабельОтработанноеВремяЗаПериод.ВидВремени6,
			|	ТабельОтработанноеВремяЗаПериод.Часов6,
			|	ТабельОтработанноеВремяЗаПериод.Дней6
			|ИЗ
			|	Документ.Табель.ОтработанноеВремяЗаПериод КАК ТабельОтработанноеВремяЗаПериод
			|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц.СрезПоследних(&ПериодРегистрации, ) КАК ФИОФизЛицСрезПоследних
			|		ПО ТабельОтработанноеВремяЗаПериод.Сотрудник.Физлицо = ФИОФизЛицСрезПоследних.ФизЛицо
			|ГДЕ
			|	ТабельОтработанноеВремяЗаПериод.Ссылка = &Ссылка
			|
			|УПОРЯДОЧИТЬ ПО
			|	ТабельОтработанноеВремяЗаПериод.НомерСтроки";
			
			Выборка = Запрос.Выполнить().Выбрать();		
				
			НПП = 0;
			Пока Выборка.Следующий() Цикл
				НПП = НПП + 1;
				ОбластьДетали.Параметры.НомерПП = НПП;
				ОбластьДетали.Параметры.Заполнить(Выборка);
				Если ЗначениеЗаполнено(Выборка.Фамилия) Тогда
					ФИО = УправлениеНебольшойФирмойСервер.ПолучитьФамилиюИмяОтчество(Выборка.Фамилия, Выборка.Имя, Выборка.Отчество, Истина);
				Иначе
					ФИО = "";
				КонецЕсли;
				ОбластьДетали.Параметры.НаименованиеСотрудник = ?(ЗначениеЗаполнено(ФИО), ФИО, Выборка.Сотрудник);
				
				СтрокаВидВремени = "" + Выборка.ВидВремени1 + ?(ЗначениеЗаполнено(Выборка.ВидВремени2), "/" + Выборка.ВидВремени2, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени3), "/" + Выборка.ВидВремени3, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени4), "/" + Выборка.ВидВремени4, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени5), "/" + Выборка.ВидВремени5, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени6), "/" + Выборка.ВидВремени6, "");
				СтрокаЧасы = "" + ?(Выборка.ВидВремени1 = 0, "", Выборка.Часов1) + ?(ЗначениеЗаполнено(Выборка.ВидВремени2), "/" + Выборка.Часов2, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени3), "/" + Выборка.Часов3, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени4), "/" + Выборка.Часов4, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени5), "/" + Выборка.Часов5, "") + ?(ЗначениеЗаполнено(Выборка.ВидВремени6), "/" + Выборка.Часов6, "");
				
				ОбластьДетали.Параметры.Символ1 = СтрокаВидВремени;			 
				ОбластьДетали.Параметры.ДополнительноеЗначение1 = СтрокаЧасы;					 
				
				ОбластьДетали.Параметры.ЧасыЗаМесяц = ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени1), Выборка.Часов1, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени2), Выборка.Часов2, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени3), Выборка.Часов3, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени4), Выборка.Часов4, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени5), Выборка.Часов5, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени6), Выборка.Часов6, 0);
					
				ОбластьДетали.Параметры.ДниЗаМесяц = ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени1), Выборка.Дней1, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени2), Выборка.Дней2, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени3), Выборка.Дней3, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени4), Выборка.Дней4, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени5), Выборка.Дней5, 0) 
							+ ?(ПрибавитьКОтработанномуВремени(Выборка.ВидВремени6), Выборка.Дней6, 0);
					
				ТабличныйДокумент.Вывести(ОбластьДетали);
			КонецЦикла;
		
		КонецЕсли; 			
		
		Руководители = УправлениеНебольшойФирмойСервер.ОтветственныеЛицаОрганизационнойЕдиницы(ТекущийДокумент.Организация, ТекущийДокумент.Дата);
		ОбластьПодвал.Параметры.Заполнить(Руководители);
		ТабличныйДокумент.Вывести(ОбластьПодвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабличныйДокумент, НомерСтрокиНачало, ОбъектыПечати, ТекущийДокумент);
	
	КонецЦикла;
	
	ТабличныйДокумент.АвтоМасштаб = Истина;
	
	Возврат ТабличныйДокумент;
	
КонецФункции // ПечатнаяФорма()

// Сформировать печатные формы объектов
//
// ВХОДЯЩИЕ:
//   ИменаМакетов    - Строка    - Имена макетов, перечисленные через запятую
//   МассивОбъектов  - Массив    - Массив ссылок на объекты которые нужно распечатать
//   ПараметрыПечати - Структура - Структура дополнительных параметров печати
//
// ИСХОДЯЩИЕ:
//   КоллекцияПечатныхФорм - Таблица значений - Сформированные табличные документы
//   ПараметрыВывода       - Структура        - Параметры сформированных табличных документов
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, 
	
	КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Табель") Тогда
		
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(КоллекцияПечатныхФорм, "Табель", "Табель учета рабочего времени", ПечатнаяФорма(МассивОбъектов, ОбъектыПечати));
		
	КонецЕсли;
	
	// параметры отправки печатных форм по электронной почте
	УправлениеНебольшойФирмойСервер.ЗаполнитьПараметрыОтправки(ПараметрыВывода.ПараметрыОтправки, МассивОбъектов, КоллекцияПечатныхФорм);
	
КонецПроцедуры

// Заполняет список команд печати Заказа покупателя
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати.
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Табель";
	КомандаПечати.Представление = НСтр("ru = 'Табель учета рабочего времени'");
	КомандаПечати.СписокФорм = "ФормаДокумента,ФормаСписка";
	КомандаПечати.ПроверкаПроведенияПередПечатью = Ложь;
	КомандаПечати.Порядок = 1;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли