&НаСервере
// Возвращает тип денежных средств
//
Функция ПолучитьДанныеПоДокументу(ДокументСсылка)
	
	ДанныеПоДокументу = Новый Структура;
	
	ДанныеПоДокументу.Вставить("ТипДенежныхСредств", ДокументСсылка.ТипДенежныхСредств);
	ДанныеПоДокументу.Вставить("СтатусУтвержденияПлатежаНеУтвержден", ДокументСсылка.СтатусУтвержденияПлатежа = Перечисления.СтатусыУтвержденияПлатежей.НеУтвержден);
	
	Возврат ДанныеПоДокументу;
	
КонецФункции // ПолучитьТипДенежныхСредств()

&НаКлиенте
// Процедура обработки команды.
//
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	Параметры = Новый Структура("Основание", ПараметрКоманды);
	ДанныеПоДокументу = ПолучитьДанныеПоДокументу(ПараметрКоманды);
	
	Если ДанныеПоДокументу.СтатусУтвержденияПлатежаНеУтвержден Тогда
		ВызватьИсключение НСтр("ru = 'Нельзя ввести расход денег на основании неутвержденной заявки!'");
		Возврат;
	КонецЕсли;
	
	ТипДенежныхСредств = ДанныеПоДокументу.ТипДенежныхСредств;
	
	Если НЕ ЗначениеЗаполнено(ТипДенежныхСредств) Тогда
		ОткрытьФорму(
		"ОбщаяФорма.ФормаТипыДенежныхСредств",,,,,,
		Новый ОписаниеОповещения("ОбработкаКомандыЗавершение", ЭтотОбъект, Параметры));
		Возврат;
	КонецЕсли;
	
	ОбработкаКомандыФрагмент(Параметры, ТипДенежныхСредств);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыЗавершение(Результат, Параметры) Экспорт
	
	ТипДенежныхСредств = Результат;
	
	ОбработкаКомандыФрагмент(Параметры, ТипДенежныхСредств);

КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКомандыФрагмент(Параметры, ТипДенежныхСредств)
	
	Если ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Наличные") Тогда
		ОткрытьФорму("Документ.РасходИзКассы.ФормаОбъекта", Параметры);
	ИначеЕсли ТипДенежныхСредств = ПредопределенноеЗначение("Перечисление.ТипыДенежныхСредств.Безналичные") Тогда
		ОткрытьФорму("Документ.РасходСоСчета.ФормаОбъекта", Параметры);
	КонецЕсли;
	
КонецПроцедуры
