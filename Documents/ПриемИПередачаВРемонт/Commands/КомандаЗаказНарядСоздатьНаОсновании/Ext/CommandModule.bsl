
&НаСервере
Процедура ПолучитьПараметрыЗаполнения(СтруктураЗаполнения)
	
	Контрагент = СтруктураЗаполнения.ДокументСсылка.Контрагент;
	Организация = СтруктураЗаполнения.ДокументСсылка.Организация;
	
	Если Не Контрагент.ВестиРасчетыПоДоговорам Тогда
		Договор = Контрагент.ДоговорПоУмолчанию;
	Иначе
		МенеджерСправочника = Справочники.ДоговорыКонтрагентов;
		СписокВидовДоговоров = МенеджерСправочника.ПолучитьСписокВидовДоговораДляДокумента(СтруктураЗаполнения);
		Договор = МенеджерСправочника.ПолучитьДоговорПоУмолчаниюПоОрганизацииВидуДоговора(Контрагент, Организация, СписокВидовДоговоров);
	КонецЕсли;
	
	ВидЦен = Договор.ВидЦен;
	ВидСкидкиНаценки = Договор.ВидСкидкиНаценки;
	ВалютаДокумента = Договор.ВалютаРасчетов;
	СуммаВключаетНДС = ?(ЗначениеЗаполнено(Договор.ВидЦен), Договор.ВидЦен.ЦенаВключаетНДС, Неопределено);
	НалогообложениеНДС = УправлениеНебольшойФирмойСервер.НалогообложениеНДС(Организация,, СтруктураЗаполнения.ДокументСсылка.Дата);
	
	СтруктураПоВалюте = РегистрыСведений.КурсыВалют.ПолучитьПоследнее(СтруктураЗаполнения.ДокументСсылка.Дата, Новый Структура("Валюта", Договор.ВалютаРасчетов));
	Курс = СтруктураПоВалюте.Курс;
	Кратность = СтруктураПоВалюте.Кратность;
	
	СтруктураЗаполнения.Вставить("ДокументОснование", СтруктураЗаполнения.ДокументСсылка);
	СтруктураЗаполнения.Вставить("ВидОперации", Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд);
	СтруктураЗаполнения.Вставить("Контрагент", Контрагент);
	СтруктураЗаполнения.Вставить("Организация", Организация);
	СтруктураЗаполнения.Вставить("Договор", Договор);
	СтруктураЗаполнения.Вставить("ВидЦен", ВидЦен);
	СтруктураЗаполнения.Вставить("ВидСкидкиНаценки", ВидСкидкиНаценки);
	СтруктураЗаполнения.Вставить("ВалютаДокумента", ВалютаДокумента);
	СтруктураЗаполнения.Вставить("СуммаВключаетНДС", СуммаВключаетНДС);
	СтруктураЗаполнения.Вставить("НалогообложениеНДС", НалогообложениеНДС);
	СтруктураЗаполнения.Вставить("Курс", Курс);
	СтруктураЗаполнения.Вставить("Кратность", Кратность);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)

	Если Не ЗначениеЗаполнено(ПараметрКоманды) Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураЗаполнения = Новый Структура();
	СтруктураЗаполнения.Вставить("ДокументСсылка", ПараметрКоманды);
	ПолучитьПараметрыЗаполнения(СтруктураЗаполнения);
	
	ОткрытьФорму("Документ.ЗаказПокупателя.ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", СтруктураЗаполнения));
	
КонецПроцедуры
