#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// Серийные номера
	Если РаботаССерийнымиНомерами.ИспользоватьСерийныеНомераОстатки() = Истина 
		И ЭтотОбъект.Номенклатура.ИспользоватьСерийныеНомера Тогда
		
		ПроверяемыеРеквизиты.Добавить("СерийныйНомер");
		
	КонецЕсли;
	
	Если НЕ Константы.ФункциональнаяОпцияУчетПоНесколькимНаправлениямДеятельности.Получить() Тогда
		Если РемонтВыполнен Тогда
			
			ПроверяемыеРеквизиты.Добавить("НаправлениеДеятельности");
			
		КонецЕсли;
	КонецЕсли;
	
	Если ПередачаВСервисныйЦентр И ВариантРемонта = Перечисления.ВариантыРемонта.СервисЦентр Тогда
	
		ПроверяемыеРеквизиты.Добавить("СервисЦентр");
	
	КонецЕсли;
	
	
	Если ВыдачаИзРемонта И (НЕ РемонтВыполнен) Тогда
		Если ВариантРемонта = Перечисления.ВариантыРемонта.СервисЦентр Тогда
		    ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не установлен ""Принят из сервисного центра""!'"));
		Иначе	
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не установлен ""Ремонт выполнен""!'"));
		КонецЕсли;
		Отказ = Истина;
	КонецЕсли;
	Если РемонтВыполнен И ВариантРемонта = Перечисления.ВариантыРемонта.СервисЦентр И (НЕ ПередачаВСервисныйЦентр) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не установлен ""Передан в сервисный центр""!'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если ПередачаВСервисныйЦентр И РемонтВыполнен И ДатаПередачаВСервисныйЦентр > ДатаРемонтВыполнен Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата передачи в сервисный центр не может быть больше даты приема из сервисного центра!'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если РемонтВыполнен И ВыдачаИзРемонта И ДатаРемонтВыполнен > ДатаВыдачаИзРемонта Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата выпонения ремонта не может быть больше даты выдачи из ремонта!'"));
		Отказ = Истина;
	КонецЕсли;
	
	Если ПередачаВСервисныйЦентр И Дата > ДатаПередачаВСервисныйЦентр Тогда
		Если НЕ ЗначениеЗаполнено(Ссылка) Тогда
			//Если пользователь установил ДатаПередачаВСервисныйЦентр в новом документе до записи, она будет меньше даты и времени записи документа
			Дата = ДатаПередачаВСервисныйЦентр;
		Иначе
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Дата передачи в сервисный центр не может быть меньше даты приема в ремонт (даты документа)!'"));
			Отказ = Истина;
		КонецЕсли; 
	КонецЕсли;
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаПроведения объекта.
//
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Инициализация данных документа
	Документы.ПриемИПередачаВРемонт.ИнициализироватьДанныеДокумента(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Отражение в разделах учета.
	
	УправлениеНебольшойФирмойСервер.ОтразитьТоварыВРемонте(ДополнительныеСвойства, Движения, Отказ);
	// СерийныеНомера
	УправлениеНебольшойФирмойСервер.ОтразитьСерийныеНомераГарантии(ДополнительныеСвойства, Движения, Отказ);
		
	Если (ДополнительныеСвойства.ВариантРемонта = Перечисления.ВариантыРемонта.НашаМастерскаяПростойРемонт
		ИЛИ ДополнительныеСвойства.ВариантРемонта = Перечисления.ВариантыРемонта.СервисЦентр) Тогда
		
		Если ДополнительныеСвойства.РемонтВыполнен Тогда
			
			УправлениеНебольшойФирмойСервер.ОтразитьТоварыВРемонте(ДополнительныеСвойства, Движения, Отказ);
			// СерийныеНомера
			УправлениеНебольшойФирмойСервер.ОтразитьСерийныеНомераГарантии(ДополнительныеСвойства, Движения, Отказ);
			
			УправлениеНебольшойФирмойСервер.ОтразитьРасчетыСПокупателями(ДополнительныеСвойства, Движения, Отказ);
			
			УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходы(ДополнительныеСвойства, Движения, Отказ);
			УправлениеНебольшойФирмойСервер.ОтразитьДоходыИРасходыОтложенные(ДополнительныеСвойства, Движения, Отказ);
			
			УправлениеНебольшойФирмойСервер.ОтразитьУправленческий(ДополнительныеСвойства, Движения, Отказ);
			
		КонецЕсли;
	
		УправлениеНебольшойФирмойСервер.ОтразитьОплатаСчетовИЗаказов(ДополнительныеСвойства, Движения, Отказ);
		
	КонецЕсли;
	
	Если ДополнительныеСвойства.ВариантРемонта = Перечисления.ВариантыРемонта.СервисЦентр Тогда
		
		УправлениеНебольшойФирмойСервер.ОтразитьТоварыПереданныеВРемонте(ДополнительныеСвойства, Движения, Отказ);
		
	КонецЕсли;
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.ПриемИПередачаВРемонт.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
	ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы.МенеджерВременныхТаблиц.Закрыть();
	
КонецПроцедуры

// Процедура - обработчик события ОбработкаУдаленияПроведения объекта.
//
Процедура ОбработкаУдаленияПроведения(Отказ)
	
	// Инициализация дополнительных свойств для проведения документа
	УправлениеНебольшойФирмойСервер.ИнициализироватьДополнительныеСвойстваДляПроведения(Ссылка, ДополнительныеСвойства);
	
	// Подготовка наборов записей
	УправлениеНебольшойФирмойСервер.ПодготовитьНаборыЗаписейКРегистрацииДвижений(ЭтотОбъект);
	
	// Запись наборов записей
	УправлениеНебольшойФирмойСервер.ЗаписатьНаборыЗаписей(ЭтотОбъект);
	
	// Контроль
	Документы.ПриемИПередачаВРемонт.ВыполнитьКонтроль(Ссылка, ДополнительныеСвойства, Отказ, Истина);
	
КонецПроцедуры // ОбработкаУдаленияПроведения()

// Обработчик заполнения документа по структуре
//
// Параметры:
//
Процедура ЗаполнитьПоСтруктуре(ДанныеЗаполнения) Экспорт
	
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	СтратегияЗаполнения = Новый Соответствие;
	СтратегияЗаполнения[Тип("Структура")] = "ЗаполнитьПоСтруктуре";
	ЗаполнениеОбъектовУНФ.ЗаполнитьДокумент(ЭтотОбъект, ДанныеЗаполнения, СтратегияЗаполнения);	
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РежимЗаписи = РежимЗаписиДокумента.Проведение ИЛИ РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
		ДополнительныеСвойства.Вставить("ВариантРемонта", ВариантРемонта);
		ДополнительныеСвойства.Вставить("ПередачаВСервисныйЦентр", ПередачаВСервисныйЦентр);
		ДополнительныеСвойства.Вставить("ДатаПередачаВСервисныйЦентр", ДатаПередачаВСервисныйЦентр);
		ДополнительныеСвойства.Вставить("РемонтВыполнен", РемонтВыполнен);
		ДополнительныеСвойства.Вставить("ДатаРемонтВыполнен", ДатаРемонтВыполнен);
		ДополнительныеСвойства.Вставить("ВыдачаИзРемонта", ВыдачаИзРемонта);
		ДополнительныеСвойства.Вставить("ДатаВыдачаИзРемонта", ДатаВыдачаИзРемонта);
		ДополнительныеСвойства.Вставить("СуммаДокумента", СуммаДокумента);
	КонецЕсли;
	ДополнительныеСвойства.Вставить("РежимЗаписи", РежимЗаписи);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ЭтотОбъект.ПередачаВСервисныйЦентр	= Ложь;
	ЭтотОбъект.РемонтВыполнен			= Ложь;
	ЭтотОбъект.ВыдачаИзРемонта			= Ложь;
	ЭтотОбъект.ДатаПередачаВСервисныйЦентр	= Дата("00010101");
	ЭтотОбъект.ДатаРемонтВыполнен			= Дата("00010101");
	ЭтотОбъект.ДатаВыдачаИзРемонта			= Дата("00010101");
	
	ЭтотОбъект.Предоплата.Очистить();
	
КонецПроцедуры

#КонецЕсли
