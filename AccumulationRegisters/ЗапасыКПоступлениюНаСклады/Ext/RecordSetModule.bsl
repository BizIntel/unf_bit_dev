#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗапасыКПоступлениюНаСклады.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыКПоступлениюНаСкладыИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыКПоступлениюНаСкладыИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияЗапасыКПоступлениюНаСкладыИзменение Тогда
		
		// Если временная таблица "ДвиженияЗапасыКПоступлениюНаСкладыИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗапасыКПоступлениюНаСклады.НомерСтроки КАК НомерСтроки,
		|	ЗапасыКПоступлениюНаСклады.Организация КАК Организация,
		|	ЗапасыКПоступлениюНаСклады.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ЗапасыКПоступлениюНаСклады.Номенклатура КАК Номенклатура,
		|	ЗапасыКПоступлениюНаСклады.Характеристика КАК Характеристика,
		|	ЗапасыКПоступлениюНаСклады.Партия КАК Партия,
		|	ВЫБОР
		|		КОГДА ЗапасыКПоступлениюНаСклады.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗапасыКПоступлениюНаСклады.Количество
		|		ИНАЧЕ -ЗапасыКПоступлениюНаСклады.Количество
		|	КОНЕЦ КАК КоличествоПередЗаписью
		|ПОМЕСТИТЬ ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью
		|ИЗ
		|	РегистрНакопления.ЗапасыКПоступлениюНаСклады КАК ЗапасыКПоступлениюНаСклады
		|ГДЕ
		|	ЗапасыКПоступлениюНаСклады.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияЗапасыКПоступлениюНаСкладыИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Организация КАК Организация,
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Номенклатура КАК Номенклатура,
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Характеристика КАК Характеристика,
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Партия КАК Партия,
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.КоличествоПередЗаписью КАК КоличествоПередЗаписью
		|ПОМЕСТИТЬ ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью
		|ИЗ
		|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение КАК ДвиженияЗапасыКПоступлениюНаСкладыИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыКПоступлениюНаСклады.НомерСтроки,
		|	ЗапасыКПоступлениюНаСклады.Организация,
		|	ЗапасыКПоступлениюНаСклады.СтруктурнаяЕдиница,
		|	ЗапасыКПоступлениюНаСклады.Номенклатура,
		|	ЗапасыКПоступлениюНаСклады.Характеристика,
		|	ЗапасыКПоступлениюНаСклады.Партия,
		|	ВЫБОР
		|		КОГДА ЗапасыКПоступлениюНаСклады.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗапасыКПоступлениюНаСклады.Количество
		|		ИНАЧЕ -ЗапасыКПоступлениюНаСклады.Количество
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ЗапасыКПоступлениюНаСклады КАК ЗапасыКПоступлениюНаСклады
		|ГДЕ
		|	ЗапасыКПоступлениюНаСклады.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияЗапасыКПоступлениюНаСкладыИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыКПоступлениюНаСкладыИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияЗапасыКПоступлениюНаСкладыИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияЗапасыКПоступлениюНаСкладыИзменение");
	
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияЗапасыКПоступлениюНаСкладыИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияЗапасыКПоступлениюНаСкладыИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Характеристика КАК Характеристика,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Партия КАК Партия,
	|	СУММА(ДвиженияЗапасыКПоступлениюНаСкладыИзменение.КоличествоПередЗаписью) КАК КоличествоПередЗаписью,
	|	СУММА(ДвиженияЗапасыКПоступлениюНаСкладыИзменение.КоличествоИзменение) КАК КоличествоИзменение,
	|	СУММА(ДвиженияЗапасыКПоступлениюНаСкладыИзменение.КоличествоПриЗаписи) КАК КоличествоПриЗаписи
	|ПОМЕСТИТЬ ДвиженияЗапасыКПоступлениюНаСкладыИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.Организация КАК Организация,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.Партия КАК Партия,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.КоличествоПередЗаписью КАК КоличествоПередЗаписью,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		0 КАК КоличествоПриЗаписи
	|	ИЗ
	|		ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью КАК ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.НомерСтроки,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Организация,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.СтруктурнаяЕдиница,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Номенклатура,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Характеристика,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Партия,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Количество
	|			ИНАЧЕ ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Количество
	|		КОНЕЦ,
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Количество
	|	ИЗ
	|		РегистрНакопления.ЗапасыКПоступлениюНаСклады КАК ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи
	|	ГДЕ
	|		ДвиженияЗапасыКПоступлениюНаСкладыПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияЗапасыКПоступлениюНаСкладыИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Организация,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.СтруктурнаяЕдиница,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Номенклатура,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Характеристика,
	|	ДвиженияЗапасыКПоступлениюНаСкладыИзменение.Партия
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияЗапасыКПоступлениюНаСкладыИзменение.КоличествоИзменение) <> 0
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	СтруктурнаяЕдиница,
	|	Номенклатура,
	|	Характеристика,
	|	Партия");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияЗапасыКПоступлениюНаСкладыИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЗапасыКПоступлениюНаСкладыИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияЗапасыКПоступлениюНаСкладыПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли