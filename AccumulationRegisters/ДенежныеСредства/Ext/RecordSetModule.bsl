#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДенежныеСредства.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияДенежныеСредстваИзменение Тогда
		
		// Если временная таблица "ДвиженияДенежныеСредстваИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияДенежныеСредстваПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РегистрНакопленияДенежныеСредства.НомерСтроки КАК НомерСтроки,
		|	РегистрНакопленияДенежныеСредства.Организация КАК Организация,
		|	РегистрНакопленияДенежныеСредства.ТипДенежныхСредств КАК ТипДенежныхСредств,
		|	РегистрНакопленияДенежныеСредства.БанковскийСчетКасса КАК БанковскийСчетКасса,
		|	РегистрНакопленияДенежныеСредства.Валюта КАК Валюта,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредства.Сумма
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредства.Сумма
		|	КОНЕЦ КАК СуммаПередЗаписью,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредства.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредства.СуммаВал
		|	КОНЕЦ КАК СуммаВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияДенежныеСредстваПередЗаписью
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства КАК РегистрНакопленияДенежныеСредства
		|ГДЕ
		|	РегистрНакопленияДенежныеСредства.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияДенежныеСредстваИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияДенежныеСредстваПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияДенежныеСредстваИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияДенежныеСредстваИзменение.Организация КАК Организация,
		|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств КАК ТипДенежныхСредств,
		|	ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса КАК БанковскийСчетКасса,
		|	ДвиженияДенежныеСредстваИзменение.Валюта КАК Валюта,
		|	ДвиженияДенежныеСредстваИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияДенежныеСредстваИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияДенежныеСредстваПередЗаписью
		|ИЗ
		|	ДвиженияДенежныеСредстваИзменение КАК ДвиженияДенежныеСредстваИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрНакопленияДенежныеСредства.НомерСтроки,
		|	РегистрНакопленияДенежныеСредства.Организация,
		|	РегистрНакопленияДенежныеСредства.ТипДенежныхСредств,
		|	РегистрНакопленияДенежныеСредства.БанковскийСчетКасса,
		|	РегистрНакопленияДенежныеСредства.Валюта,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредства.Сумма
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредства.Сумма
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредства.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредства.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредства.СуммаВал
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ДенежныеСредства КАК РегистрНакопленияДенежныеСредства
		|ГДЕ
		|	РегистрНакопленияДенежныеСредства.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияДенежныеСредстваИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияДенежныеСредстваИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияДенежныеСредстваИзменение");
	
	КонецЕсли;
	
	// Хозяйственная операция
	ХозяйственныеОперацииСервер.ЗаполнитьХозяйственнуюОперациюВНабореЗаписей(ЭтотОбъект);
	// Конец Хозяйственная операция
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда	
		Возврат;		
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияДенежныеСредстваИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияДенежныеСредстваИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияДенежныеСредстваИзменение.Организация КАК Организация,
	|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|	ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|	ДвиженияДенежныеСредстваИзменение.Валюта КАК Валюта,
	|	СУММА(ДвиженияДенежныеСредстваИзменение.СуммаПередЗаписью) КАК СуммаПередЗаписью,
	|	СУММА(ДвиженияДенежныеСредстваИзменение.СуммаИзменение) КАК СуммаИзменение,
	|	СУММА(ДвиженияДенежныеСредстваИзменение.СуммаПриЗаписи) КАК СуммаПриЗаписи,
	|	СУММА(ДвиженияДенежныеСредстваИзменение.СуммаВалПередЗаписью) КАК СуммаВалПередЗаписью,
	|	СУММА(ДвиженияДенежныеСредстваИзменение.СуммаВалИзменение) КАК СуммаВалИзменение,
	|	СУММА(ДвиженияДенежныеСредстваИзменение.СуммаВалПриЗаписи) КАК СуммаВалПриЗаписи
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияДенежныеСредстваПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияДенежныеСредстваПередЗаписью.Организация КАК Организация,
	|		ДвиженияДенежныеСредстваПередЗаписью.ТипДенежныхСредств КАК ТипДенежныхСредств,
	|		ДвиженияДенежныеСредстваПередЗаписью.БанковскийСчетКасса КАК БанковскийСчетКасса,
	|		ДвиженияДенежныеСредстваПередЗаписью.Валюта КАК Валюта,
	|		ДвиженияДенежныеСредстваПередЗаписью.СуммаПередЗаписью КАК СуммаПередЗаписью,
	|		ДвиженияДенежныеСредстваПередЗаписью.СуммаПередЗаписью КАК СуммаИзменение,
	|		0 КАК СуммаПриЗаписи,
	|		ДвиженияДенежныеСредстваПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
	|		ДвиженияДенежныеСредстваПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалИзменение,
	|		0 КАК СуммаВалПриЗаписи
	|	ИЗ
	|		ДвиженияДенежныеСредстваПередЗаписью КАК ДвиженияДенежныеСредстваПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДенежныеСредстваПриЗаписи.НомерСтроки,
	|		ДвиженияДенежныеСредстваПриЗаписи.Организация,
	|		ДвиженияДенежныеСредстваПриЗаписи.ТипДенежныхСредств,
	|		ДвиженияДенежныеСредстваПриЗаписи.БанковскийСчетКасса,
	|		ДвиженияДенежныеСредстваПриЗаписи.Валюта,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияДенежныеСредстваПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияДенежныеСредстваПриЗаписи.Сумма
	|			ИНАЧЕ ДвиженияДенежныеСредстваПриЗаписи.Сумма
	|		КОНЕЦ,
	|		ДвиженияДенежныеСредстваПриЗаписи.Сумма,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияДенежныеСредстваПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияДенежныеСредстваПриЗаписи.СуммаВал
	|			ИНАЧЕ ДвиженияДенежныеСредстваПриЗаписи.СуммаВал
	|		КОНЕЦ,
	|		ДвиженияДенежныеСредстваПриЗаписи.СуммаВал
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредства КАК ДвиженияДенежныеСредстваПриЗаписи
	|	ГДЕ
	|		ДвиженияДенежныеСредстваПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияДенежныеСредстваИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныеСредстваИзменение.Организация,
	|	ДвиженияДенежныеСредстваИзменение.ТипДенежныхСредств,
	|	ДвиженияДенежныеСредстваИзменение.БанковскийСчетКасса,
	|	ДвиженияДенежныеСредстваИзменение.Валюта
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДвиженияДенежныеСредстваИзменение.СуммаИзменение) <> 0
	|		ИЛИ СУММА(ДвиженияДенежныеСредстваИзменение.СуммаВалИзменение) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	ТипДенежныхСредств,
	|	БанковскийСчетКасса,
	|	Валюта");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияДенежныеСредстваИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияДенежныеСредстваПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияДенежныеСредстваПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли