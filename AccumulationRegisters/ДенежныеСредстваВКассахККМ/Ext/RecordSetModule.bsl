#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;

	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ДенежныеСредстваВКассахККМ.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваВКассахККМИзменение")
		ИЛИ	СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваВКассахККМИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияДенежныеСредстваВКассахККМИзменение Тогда
		
		// Если временная таблица "ДвиженияДенежныеСредстваВКассахККМИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияДенежныеСредстваВКассахККМПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.НомерСтроки КАК НомерСтроки,
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.Организация КАК Организация,
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.КассаККМ КАК КассаККМ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.Сумма
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредстваВКассахККМ.Сумма
		|	КОНЕЦ КАК СуммаПередЗаписью,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредстваВКассахККМ.СуммаВал
		|	КОНЕЦ КАК СуммаВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияДенежныеСредстваВКассахККМПередЗаписью
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВКассахККМ КАК РегистрНакопленияДенежныеСредстваВКассахККМ
		|ГДЕ
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияДенежныеСредстваВКассахККМИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияДенежныеСредстваВКассахККМПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.Организация КАК Организация,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.КассаККМ КАК КассаККМ,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияДенежныеСредстваВКассахККМПередЗаписью
		|ИЗ
		|	ДвиженияДенежныеСредстваВКассахККМИзменение КАК ДвиженияДенежныеСредстваВКассахККМИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.НомерСтроки,
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.Организация,
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.КассаККМ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.Сумма
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредстваВКассахККМ.Сумма
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияДенежныеСредстваВКассахККМ.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияДенежныеСредстваВКассахККМ.СуммаВал
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ДенежныеСредстваВКассахККМ КАК РегистрНакопленияДенежныеСредстваВКассахККМ
		|ГДЕ
		|	РегистрНакопленияДенежныеСредстваВКассахККМ.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияДенежныеСредстваВКассахККМИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияДенежныеСредстваВКассахККМИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияДенежныеСредстваВКассахККМИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияДенежныеСредстваВКассахККМИзменение");
	
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда	
		Возврат;		
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияДенежныеСредстваВКассахККМИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияДенежныеСредстваВКассахККМИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияДенежныеСредстваВКассахККМИзменение.Организация КАК Организация,
	|	ДвиженияДенежныеСредстваВКассахККМИзменение.КассаККМ КАК КассаККМ,
	|	СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаПередЗаписью) КАК СуммаПередЗаписью,
	|	СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаИзменение) КАК СуммаИзменение,
	|	СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаПриЗаписи) КАК СуммаПриЗаписи,
	|	СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалПередЗаписью) КАК СуммаВалПередЗаписью,
	|	СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалИзменение) КАК СуммаВалИзменение,
	|	СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалПриЗаписи) КАК СуммаВалПриЗаписи
	|ПОМЕСТИТЬ ДвиженияДенежныеСредстваВКассахККМИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.Организация КАК Организация,
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.КассаККМ КАК КассаККМ,
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.СуммаПередЗаписью КАК СуммаПередЗаписью,
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.СуммаПередЗаписью КАК СуммаИзменение,
	|		0 КАК СуммаПриЗаписи,
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалИзменение,
	|		0 КАК СуммаВалПриЗаписи
	|	ИЗ
	|		ДвиженияДенежныеСредстваВКассахККМПередЗаписью КАК ДвиженияДенежныеСредстваВКассахККМПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияДенежныеСредстваВКассахККМПриЗаписи.НомерСтроки,
	|		ДвиженияДенежныеСредстваВКассахККМПриЗаписи.Организация,
	|		ДвиженияДенежныеСредстваВКассахККМПриЗаписи.КассаККМ,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияДенежныеСредстваВКассахККМПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияДенежныеСредстваВКассахККМПриЗаписи.Сумма
	|			ИНАЧЕ ДвиженияДенежныеСредстваВКассахККМПриЗаписи.Сумма
	|		КОНЕЦ,
	|		ДвиженияДенежныеСредстваВКассахККМПриЗаписи.Сумма,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияДенежныеСредстваВКассахККМПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияДенежныеСредстваВКассахККМПриЗаписи.СуммаВал
	|			ИНАЧЕ ДвиженияДенежныеСредстваВКассахККМПриЗаписи.СуммаВал
	|		КОНЕЦ,
	|		ДвиженияДенежныеСредстваВКассахККМПриЗаписи.СуммаВал
	|	ИЗ
	|		РегистрНакопления.ДенежныеСредстваВКассахККМ КАК ДвиженияДенежныеСредстваВКассахККМПриЗаписи
	|	ГДЕ
	|		ДвиженияДенежныеСредстваВКассахККМПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияДенежныеСредстваВКассахККМИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияДенежныеСредстваВКассахККМИзменение.Организация,
	|	ДвиженияДенежныеСредстваВКассахККМИзменение.КассаККМ
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаИзменение) <> 0
	|		ИЛИ СУММА(ДвиженияДенежныеСредстваВКассахККМИзменение.СуммаВалИзменение) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	КассаККМ");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияДенежныеСредстваВКассахККМИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияДенежныеСредстваВКассахККМИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияДенежныеСредстваВКассахККМПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияДенежныеСредстваВКассахККМПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли