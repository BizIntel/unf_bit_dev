#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.ЗапасыПереданные.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыПереданныеИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыПереданныеИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияЗапасыПереданныеИзменение Тогда
		
		// Если временная таблица "ДвиженияЗапасыПереданныеИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияЗапасыПереданныеПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ЗапасыПереданные.НомерСтроки КАК НомерСтроки,
		|	ЗапасыПереданные.Организация КАК Организация,
		|	ЗапасыПереданные.Номенклатура КАК Номенклатура,
		|	ЗапасыПереданные.Характеристика КАК Характеристика,
		|	ЗапасыПереданные.Партия КАК Партия,
		|	ЗапасыПереданные.Контрагент КАК Контрагент,
		|	ЗапасыПереданные.Договор КАК Договор,
		|	ЗапасыПереданные.Заказ КАК Заказ,
		|	ЗапасыПереданные.ТипПриемаПередачи КАК ТипПриемаПередачи,
		|	ВЫБОР
		|		КОГДА ЗапасыПереданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗапасыПереданные.Количество
		|		ИНАЧЕ -ЗапасыПереданные.Количество
		|	КОНЕЦ КАК КоличествоПередЗаписью,
		|	ВЫБОР
		|		КОГДА ЗапасыПереданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗапасыПереданные.СуммаРасчетов
		|		ИНАЧЕ -ЗапасыПереданные.СуммаРасчетов
		|	КОНЕЦ КАК СуммаРасчетовПередЗаписью
		|ПОМЕСТИТЬ ДвиженияЗапасыПереданныеПередЗаписью
		|ИЗ
		|	РегистрНакопления.ЗапасыПереданные КАК ЗапасыПереданные
		|ГДЕ
		|	ЗапасыПереданные.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияЗапасыПереданныеИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияЗапасыПереданныеПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияЗапасыПереданныеИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияЗапасыПереданныеИзменение.Организация КАК Организация,
		|	ДвиженияЗапасыПереданныеИзменение.Номенклатура КАК Номенклатура,
		|	ДвиженияЗапасыПереданныеИзменение.Характеристика КАК Характеристика,
		|	ДвиженияЗапасыПереданныеИзменение.Партия КАК Партия,
		|	ДвиженияЗапасыПереданныеИзменение.Контрагент КАК Контрагент,
		|	ДвиженияЗапасыПереданныеИзменение.Договор КАК Договор,
		|	ДвиженияЗапасыПереданныеИзменение.Заказ КАК Заказ,
		|	ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачи,
		|	ДвиженияЗапасыПереданныеИзменение.КоличествоПередЗаписью КАК КоличествоПередЗаписью,
		|	ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовПередЗаписью КАК СуммаРасчетовПередЗаписью
		|ПОМЕСТИТЬ ДвиженияЗапасыПереданныеПередЗаписью
		|ИЗ
		|	ДвиженияЗапасыПереданныеИзменение КАК ДвиженияЗапасыПереданныеИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	ЗапасыПереданные.НомерСтроки,
		|	ЗапасыПереданные.Организация,
		|	ЗапасыПереданные.Номенклатура,
		|	ЗапасыПереданные.Характеристика,
		|	ЗапасыПереданные.Партия,
		|	ЗапасыПереданные.Контрагент,
		|	ЗапасыПереданные.Договор,
		|	ЗапасыПереданные.Заказ,
		|	ЗапасыПереданные.ТипПриемаПередачи,
		|	ВЫБОР
		|		КОГДА ЗапасыПереданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗапасыПереданные.Количество
		|		ИНАЧЕ -ЗапасыПереданные.Количество
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА ЗапасыПереданные.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА ЗапасыПереданные.СуммаРасчетов
		|		ИНАЧЕ -ЗапасыПереданные.СуммаРасчетов
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.ЗапасыПереданные КАК ЗапасыПереданные
		|ГДЕ
		|	ЗапасыПереданные.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияЗапасыПереданныеИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияЗапасыПереданныеИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияЗапасыПереданныеИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияЗапасыПереданныеИзменение");
		
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияЗапасыПереданныеИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияЗапасыПереданныеИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияЗапасыПереданныеИзменение.Организация КАК Организация,
	|	ДвиженияЗапасыПереданныеИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияЗапасыПереданныеИзменение.Характеристика КАК Характеристика,
	|	ДвиженияЗапасыПереданныеИзменение.Партия КАК Партия,
	|	ДвиженияЗапасыПереданныеИзменение.Контрагент КАК Контрагент,
	|	ДвиженияЗапасыПереданныеИзменение.Договор КАК Договор,
	|	ДвиженияЗапасыПереданныеИзменение.Заказ КАК Заказ,
	|	ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи КАК ТипПриемаПередачи,
	|	СУММА(ДвиженияЗапасыПереданныеИзменение.КоличествоПередЗаписью) КАК КоличествоПередЗаписью,
	|	СУММА(ДвиженияЗапасыПереданныеИзменение.КоличествоИзменение) КАК КоличествоИзменение,
	|	СУММА(ДвиженияЗапасыПереданныеИзменение.КоличествоПриЗаписи) КАК КоличествоПриЗаписи,
	|	СУММА(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовПередЗаписью) КАК СуммаРасчетовПередЗаписью,
	|	СУММА(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовИзменение) КАК СуммаРасчетовИзменение,
	|	СУММА(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовПриЗаписи) КАК СуммаРасчетовПриЗаписи
	|ПОМЕСТИТЬ ДвиженияЗапасыПереданныеИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияЗапасыПереданныеПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Организация КАК Организация,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Партия КАК Партия,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Контрагент КАК Контрагент,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Договор КАК Договор,
	|		ДвиженияЗапасыПереданныеПередЗаписью.Заказ КАК Заказ,
	|		ДвиженияЗапасыПереданныеПередЗаписью.ТипПриемаПередачи КАК ТипПриемаПередачи,
	|		ДвиженияЗапасыПереданныеПередЗаписью.КоличествоПередЗаписью КАК КоличествоПередЗаписью,
	|		ДвиженияЗапасыПереданныеПередЗаписью.КоличествоПередЗаписью КАК КоличествоИзменение,
	|		0 КАК КоличествоПриЗаписи,
	|		ДвиженияЗапасыПереданныеПередЗаписью.СуммаРасчетовПередЗаписью КАК СуммаРасчетовПередЗаписью,
	|		ДвиженияЗапасыПереданныеПередЗаписью.СуммаРасчетовПередЗаписью КАК СуммаРасчетовИзменение,
	|		0 КАК СуммаРасчетовПриЗаписи
	|	ИЗ
	|		ДвиженияЗапасыПереданныеПередЗаписью КАК ДвиженияЗапасыПереданныеПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияЗапасыПереданныеПриЗаписи.НомерСтроки,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Организация,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Номенклатура,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Характеристика,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Партия,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Контрагент,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Договор,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Заказ,
	|		ДвиженияЗапасыПереданныеПриЗаписи.ТипПриемаПередачи,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияЗапасыПереданныеПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияЗапасыПереданныеПриЗаписи.Количество
	|			ИНАЧЕ ДвиженияЗапасыПереданныеПриЗаписи.Количество
	|		КОНЕЦ,
	|		ДвиженияЗапасыПереданныеПриЗаписи.Количество,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияЗапасыПереданныеПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияЗапасыПереданныеПриЗаписи.СуммаРасчетов
	|			ИНАЧЕ ДвиженияЗапасыПереданныеПриЗаписи.СуммаРасчетов
	|		КОНЕЦ,
	|		ДвиженияЗапасыПереданныеПриЗаписи.СуммаРасчетов
	|	ИЗ
	|		РегистрНакопления.ЗапасыПереданные КАК ДвиженияЗапасыПереданныеПриЗаписи
	|	ГДЕ
	|		ДвиженияЗапасыПереданныеПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияЗапасыПереданныеИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияЗапасыПереданныеИзменение.Организация,
	|	ДвиженияЗапасыПереданныеИзменение.Номенклатура,
	|	ДвиженияЗапасыПереданныеИзменение.Характеристика,
	|	ДвиженияЗапасыПереданныеИзменение.Партия,
	|	ДвиженияЗапасыПереданныеИзменение.Контрагент,
	|	ДвиженияЗапасыПереданныеИзменение.Договор,
	|	ДвиженияЗапасыПереданныеИзменение.Заказ,
	|	ДвиженияЗапасыПереданныеИзменение.ТипПриемаПередачи
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДвиженияЗапасыПереданныеИзменение.КоличествоИзменение) <> 0
	|		ИЛИ СУММА(ДвиженияЗапасыПереданныеИзменение.СуммаРасчетовИзменение) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Номенклатура,
	|	Характеристика,
	|	Партия,
	|	Контрагент,
	|	Договор,
	|	Заказ,
	|	ТипПриемаПередачи");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияЗапасыПереданныеИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияЗапасыПереданныеИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияЗапасыПереданныеПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияЗапасыПереданныеПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли