#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
	 ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
	 ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыПоЭквайрингу.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияРасчетыПоЭквайрингуИзменение")
		ИЛИ	СтруктураВременныеТаблицы.Свойство("ДвиженияРасчетыПоЭквайрингуИзменение")
	   И НЕ СтруктураВременныеТаблицы.ДвиженияРасчетыПоЭквайрингуИзменение Тогда
		
		// Если временная таблица "ДвиженияРасчетыПоЭквайрингуИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияРасчетыПоЭквайрингуПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РасчетыПоЭквайрингу.НомерСтроки КАК НомерСтроки,
		|	РасчетыПоЭквайрингу.ВидОперацииЭквайринга,
		|	РасчетыПоЭквайрингу.Организация,
		|	РасчетыПоЭквайрингу.ЭквайринговыйТерминал,
		|	РасчетыПоЭквайрингу.ДатаПлатежа,
		|	РасчетыПоЭквайрингу.Документ,
		|	РасчетыПоЭквайрингу.Заказ,
		|	РасчетыПоЭквайрингу.ВидПлатежнойКарты,
		|	РасчетыПоЭквайрингу.НомерПлатежнойКарты,
		|	ВЫБОР
		|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоЭквайрингу.Сумма
		|		ИНАЧЕ -РасчетыПоЭквайрингу.Сумма
		|	КОНЕЦ КАК СуммаПередЗаписью,
		|	ВЫБОР
		|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоЭквайрингу.СуммаВал
		|		ИНАЧЕ -РасчетыПоЭквайрингу.СуммаВал
		|	КОНЕЦ КАК СуммаВалПередЗаписью,
		|	ВЫБОР
		|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоЭквайрингу.Комиссия
		|		ИНАЧЕ -РасчетыПоЭквайрингу.Комиссия
		|	КОНЕЦ КАК КомиссияПередЗаписью,
		|	ВЫБОР
		|		КОГДА РасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РасчетыПоЭквайрингу.КомиссияВал
		|		ИНАЧЕ -РасчетыПоЭквайрингу.КомиссияВал
		|	КОНЕЦ КАК КомиссияВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРасчетыПоЭквайрингуПередЗаписью
		|ИЗ
		|	РегистрНакопления.РасчетыПоЭквайрингу КАК РасчетыПоЭквайрингу
		|ГДЕ
		|	РасчетыПоЭквайрингу.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияРасчетыПоЭквайрингуИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияРасчетыПоЭквайрингуПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияРасчетыПоЭквайрингуИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидОперацииЭквайринга КАК ВидОперацииЭквайринга,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.Организация КАК Организация,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ДатаПлатежа КАК ДатаПлатежа,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.Документ КАК Документ,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.Заказ КАК Заказ,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидПлатежнойКарты,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.НомерПлатежнойКарты,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияПередЗаписью КАК КомиссияПередЗаписью,
		|	ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалПередЗаписью КАК КомиссияВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРасчетыПоЭквайрингуПередЗаписью
		|ИЗ
		|	ДвиженияРасчетыПоЭквайрингуИзменение КАК ДвиженияРасчетыПоЭквайрингуИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрНакопленияРасчетыПоЭквайрингу.НомерСтроки,
		|	РегистрНакопленияРасчетыПоЭквайрингу.ВидОперацииЭквайринга,
		|	РегистрНакопленияРасчетыПоЭквайрингу.Организация,
		|	РегистрНакопленияРасчетыПоЭквайрингу.ЭквайринговыйТерминал,
		|	РегистрНакопленияРасчетыПоЭквайрингу.ДатаПлатежа,
		|	РегистрНакопленияРасчетыПоЭквайрингу.Документ,
		|	РегистрНакопленияРасчетыПоЭквайрингу.Заказ,
		|	РегистрНакопленияРасчетыПоЭквайрингу.ВидПлатежнойКарты,
		|	РегистрНакопленияРасчетыПоЭквайрингу.НомерПлатежнойКарты,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыПоЭквайрингу.Сумма
		|		ИНАЧЕ -РегистрНакопленияРасчетыПоЭквайрингу.Сумма
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыПоЭквайрингу.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияРасчетыПоЭквайрингу.СуммаВал
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыПоЭквайрингу.Комиссия
		|		ИНАЧЕ -РегистрНакопленияРасчетыПоЭквайрингу.Комиссия
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыПоЭквайрингу.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыПоЭквайрингу.КомиссияВал
		|		ИНАЧЕ -РегистрНакопленияРасчетыПоЭквайрингу.КомиссияВал
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.РасчетыПоЭквайрингу КАК РегистрНакопленияРасчетыПоЭквайрингу
		|ГДЕ
		|	РегистрНакопленияРасчетыПоЭквайрингу.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияРасчетыПоЭквайрингуИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияРасчетыПоЭквайрингуИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРасчетыПоЭквайрингуИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияРасчетыПоЭквайрингуИзменение");
	
	КонецЕсли;
	
	// Хозяйственная операция
	ХозяйственныеОперацииСервер.ЗаполнитьХозяйственнуюОперациюВНабореЗаписей(ЭтотОбъект);
	// Конец Хозяйственная операция
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
  ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
  ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияРасчетыПоЭквайрингуИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияРасчетыПоЭквайрингуИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидОперацииЭквайринга КАК ВидОперацииЭквайринга,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.Организация КАК Организация,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.Документ КАК Документ,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.Заказ КАК Заказ,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ДатаПлатежа КАК ДатаПлатежа,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидПлатежнойКарты,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.НомерПлатежнойКарты,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаПередЗаписью) КАК СуммаПередЗаписью,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаИзменение) КАК СуммаИзменение,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаПриЗаписи) КАК СуммаПриЗаписи,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалПередЗаписью) КАК СуммаВалПередЗаписью,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалИзменение) КАК СуммаВалИзменение,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалПриЗаписи) КАК СуммаВалПриЗаписи,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияПередЗаписью) КАК КомиссияПередЗаписью,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияИзменение) КАК КомиссияИзменение,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияПриЗаписи) КАК КомиссияПриЗаписи,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалПередЗаписью) КАК КомиссияВалПередЗаписью,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалИзменение) КАК КомиссияВалИзменение,
	|	СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалПриЗаписи) КАК КомиссияВалПриЗаписи
	|ПОМЕСТИТЬ ДвиженияРасчетыПоЭквайрингуИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.ВидОперацииЭквайринга КАК ВидОперацииЭквайринга,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.Организация КАК Организация,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.Документ КАК Документ,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.Заказ КАК Заказ,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.ЭквайринговыйТерминал КАК ЭквайринговыйТерминал,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.ДатаПлатежа КАК ДатаПлатежа,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.ВидПлатежнойКарты КАК ВидПлатежнойКарты,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.НомерПлатежнойКарты КАК НомерПлатежнойКарты,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.СуммаПередЗаписью КАК СуммаПередЗаписью,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.СуммаПередЗаписью КАК СуммаИзменение,
	|		0 КАК СуммаПриЗаписи,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалИзменение,
	|		0 КАК СуммаВалПриЗаписи,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.КомиссияПередЗаписью КАК КомиссияПередЗаписью,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.КомиссияПередЗаписью КАК КомиссияИзменение,
	|		0 КАК КомиссияПриЗаписи,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.КомиссияВалПередЗаписью КАК КомиссияВалПередЗаписью,
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью.КомиссияВалПередЗаписью КАК КомиссияВалИзменение,
	|		0 КАК КомиссияВалПриЗаписи
	|	ИЗ
	|		ДвиженияРасчетыПоЭквайрингуПередЗаписью КАК ДвиженияРасчетыПоЭквайрингуПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.НомерСтроки,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.ВидОперацииЭквайринга,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.Организация,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.Документ,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.Заказ,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.ЭквайринговыйТерминал,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.ДатаПлатежа,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.ВидПлатежнойКарты,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.НомерПлатежнойКарты,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРасчетыПоЭквайрингуПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРасчетыПоЭквайрингуПриЗаписи.Сумма
	|			ИНАЧЕ ДвиженияРасчетыПоЭквайрингуПриЗаписи.Сумма
	|		КОНЕЦ,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.Сумма,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРасчетыПоЭквайрингуПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРасчетыПоЭквайрингуПриЗаписи.СуммаВал
	|			ИНАЧЕ ДвиженияРасчетыПоЭквайрингуПриЗаписи.СуммаВал
	|		КОНЕЦ,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.СуммаВал,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРасчетыПоЭквайрингуПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРасчетыПоЭквайрингуПриЗаписи.Комиссия
	|			ИНАЧЕ ДвиженияРасчетыПоЭквайрингуПриЗаписи.Комиссия
	|		КОНЕЦ,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.Комиссия,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРасчетыПоЭквайрингуПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРасчетыПоЭквайрингуПриЗаписи.КомиссияВал
	|			ИНАЧЕ ДвиженияРасчетыПоЭквайрингуПриЗаписи.КомиссияВал
	|		КОНЕЦ,
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.КомиссияВал
	|	ИЗ
	|		РегистрНакопления.РасчетыПоЭквайрингу КАК ДвиженияРасчетыПоЭквайрингуПриЗаписи
	|	ГДЕ
	|		ДвиженияРасчетыПоЭквайрингуПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияРасчетыПоЭквайрингуИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидОперацииЭквайринга,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.Организация,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.Документ,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.Заказ,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ЭквайринговыйТерминал,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ДатаПлатежа,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидПлатежнойКарты,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.НомерПлатежнойКарты
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаИзменение) <> 0
	|		ИЛИ СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.СуммаВалИзменение) <> 0
	|		ИЛИ (СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияИзменение) <> 0
	|			ИЛИ СУММА(ДвиженияРасчетыПоЭквайрингуИзменение.КомиссияВалИзменение) <> 0))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Документ,
	|	Заказ,
	|	ЭквайринговыйТерминал,
	|	ДатаПлатежа,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.ВидПлатежнойКарты,
	|	ДвиженияРасчетыПоЭквайрингуИзменение.НомерПлатежнойКарты");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияРасчетыПоЭквайрингуИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРасчетыПоЭквайрингуИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияРасчетыПоЭквайрингуПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРасчетыПоЭквайрингуПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли