#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("КонтрагентыСИзмененнымиВзаиморасчетами", КонтрагентыСИзмененнымиВзаиморасчетами());
	
	Если Не ДополнительныеСвойства.Свойство("ДляПроведения")
		Или Не ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Установка исключительной блокировки текущего набора записей регистратора.
	Блокировка = Новый БлокировкаДанных;
	ЭлементБлокировки = Блокировка.Добавить("РегистрНакопления.РасчетыСПокупателями.НаборЗаписей");
	ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
	ЭлементБлокировки.УстановитьЗначение("Регистратор", Отбор.Регистратор.Значение);
	Блокировка.Заблокировать();
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияРасчетыСПокупателямиИзменение")
		ИЛИ	СтруктураВременныеТаблицы.Свойство("ДвиженияРасчетыСПокупателямиИзменение")
	   И НЕ СтруктураВременныеТаблицы.ДвиженияРасчетыСПокупателямиИзменение Тогда
		
		// Если временная таблица "ДвиженияРасчетыСПокупателямиИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияРасчетыСПокупателямиПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	РегистрНакопленияРасчетыСПокупателями.НомерСтроки КАК НомерСтроки,
		|	РегистрНакопленияРасчетыСПокупателями.Организация КАК Организация,
		|	РегистрНакопленияРасчетыСПокупателями.Контрагент КАК Контрагент,
		|	РегистрНакопленияРасчетыСПокупателями.Договор КАК Договор,
		|	РегистрНакопленияРасчетыСПокупателями.Документ КАК Документ,
		|	РегистрНакопленияРасчетыСПокупателями.Заказ КАК Заказ,
		|	РегистрНакопленияРасчетыСПокупателями.ТипРасчетов КАК ТипРасчетов,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыСПокупателями.Сумма
		|		ИНАЧЕ -РегистрНакопленияРасчетыСПокупателями.Сумма
		|	КОНЕЦ КАК СуммаПередЗаписью,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыСПокупателями.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияРасчетыСПокупателями.СуммаВал
		|	КОНЕЦ КАК СуммаВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРасчетыСПокупателямиПередЗаписью
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями КАК РегистрНакопленияРасчетыСПокупателями
		|ГДЕ
		|	РегистрНакопленияРасчетыСПокупателями.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияРасчетыСПокупателямиИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияРасчетыСПокупателямиПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияРасчетыСПокупателямиИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияРасчетыСПокупателямиИзменение.Организация КАК Организация,
		|	ДвиженияРасчетыСПокупателямиИзменение.Контрагент КАК Контрагент,
		|	ДвиженияРасчетыСПокупателямиИзменение.Договор КАК Договор,
		|	ДвиженияРасчетыСПокупателямиИзменение.Документ КАК Документ,
		|	ДвиженияРасчетыСПокупателямиИзменение.Заказ КАК Заказ,
		|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетов,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаПередЗаписью КАК СуммаПередЗаписью,
		|	ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью
		|ПОМЕСТИТЬ ДвиженияРасчетыСПокупателямиПередЗаписью
		|ИЗ
		|	ДвиженияРасчетыСПокупателямиИзменение КАК ДвиженияРасчетыСПокупателямиИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	РегистрНакопленияРасчетыСПокупателями.НомерСтроки,
		|	РегистрНакопленияРасчетыСПокупателями.Организация,
		|	РегистрНакопленияРасчетыСПокупателями.Контрагент,
		|	РегистрНакопленияРасчетыСПокупателями.Договор,
		|	РегистрНакопленияРасчетыСПокупателями.Документ,
		|	РегистрНакопленияРасчетыСПокупателями.Заказ,
		|	РегистрНакопленияРасчетыСПокупателями.ТипРасчетов,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыСПокупателями.Сумма
		|		ИНАЧЕ -РегистрНакопленияРасчетыСПокупателями.Сумма
		|	КОНЕЦ,
		|	ВЫБОР
		|		КОГДА РегистрНакопленияРасчетыСПокупателями.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
		|			ТОГДА РегистрНакопленияРасчетыСПокупателями.СуммаВал
		|		ИНАЧЕ -РегистрНакопленияРасчетыСПокупателями.СуммаВал
		|	КОНЕЦ
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями КАК РегистрНакопленияРасчетыСПокупателями
		|ГДЕ
		|	РегистрНакопленияРасчетыСПокупателями.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
				
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияРасчетыСПокупателямиИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияРасчетыСПокупателямиИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРасчетыСПокупателямиИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияРасчетыСПокупателямиИзменение");
	
	КонецЕсли;
	
	// Хозяйственная операция
	ХозяйственныеОперацииСервер.ЗаполнитьХозяйственнуюОперациюВНабореЗаписей(ЭтотОбъект);
	// Конец Хозяйственная операция
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеСвойства.Свойство("КонтрагентыСИзмененнымиВзаиморасчетами") Тогда
		РегистрыСведений.ОстаткиВзаиморасчетов.ОбновитьОстаткиВзаиморасчетов(ДополнительныеСвойства.КонтрагентыСИзмененнымиВзаиморасчетами);
	КонецЕсли;
	
	Если Не ДополнительныеСвойства.Свойство("ДляПроведения")
		Или Не ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияРасчетыСПокупателямиИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	МИНИМУМ(ДвиженияРасчетыСПокупателямиИзменение.НомерСтроки) КАК НомерСтроки,
	|	ДвиженияРасчетыСПокупателямиИзменение.Организация КАК Организация,
	|	ДвиженияРасчетыСПокупателямиИзменение.Контрагент КАК Контрагент,
	|	ДвиженияРасчетыСПокупателямиИзменение.Договор КАК Договор,
	|	ДвиженияРасчетыСПокупателямиИзменение.Документ КАК Документ,
	|	ДвиженияРасчетыСПокупателямиИзменение.Заказ КАК Заказ,
	|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов КАК ТипРасчетов,
	|	СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаПередЗаписью) КАК СуммаПередЗаписью,
	|	СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаИзменение) КАК СуммаИзменение,
	|	СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаПриЗаписи) КАК СуммаПриЗаписи,
	|	СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПередЗаписью) КАК СуммаВалПередЗаписью,
	|	СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение) КАК СуммаВалИзменение,
	|	СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаВалПриЗаписи) КАК СуммаВалПриЗаписи
	|ПОМЕСТИТЬ ДвиженияРасчетыСПокупателямиИзменение
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.НомерСтроки КАК НомерСтроки,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.Организация КАК Организация,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.Контрагент КАК Контрагент,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.Договор КАК Договор,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.Документ КАК Документ,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.Заказ КАК Заказ,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.ТипРасчетов КАК ТипРасчетов,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.СуммаПередЗаписью КАК СуммаПередЗаписью,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.СуммаПередЗаписью КАК СуммаИзменение,
	|		0 КАК СуммаПриЗаписи,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалПередЗаписью,
	|		ДвиженияРасчетыСПокупателямиПередЗаписью.СуммаВалПередЗаписью КАК СуммаВалИзменение,
	|		0 КАК СуммаВалПриЗаписи
	|	ИЗ
	|		ДвиженияРасчетыСПокупателямиПередЗаписью КАК ДвиженияРасчетыСПокупателямиПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.НомерСтроки,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Организация,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Контрагент,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Договор,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Документ,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Заказ,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.ТипРасчетов,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРасчетыСПокупателямиПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРасчетыСПокупателямиПриЗаписи.Сумма
	|			ИНАЧЕ ДвиженияРасчетыСПокупателямиПриЗаписи.Сумма
	|		КОНЕЦ,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Сумма,
	|		0,
	|		ВЫБОР
	|			КОГДА ДвиженияРасчетыСПокупателямиПриЗаписи.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияНакопления.Приход)
	|				ТОГДА -ДвиженияРасчетыСПокупателямиПриЗаписи.СуммаВал
	|			ИНАЧЕ ДвиженияРасчетыСПокупателямиПриЗаписи.СуммаВал
	|		КОНЕЦ,
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.СуммаВал
	|	ИЗ
	|		РегистрНакопления.РасчетыСПокупателями КАК ДвиженияРасчетыСПокупателямиПриЗаписи
	|	ГДЕ
	|		ДвиженияРасчетыСПокупателямиПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияРасчетыСПокупателямиИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияРасчетыСПокупателямиИзменение.Организация,
	|	ДвиженияРасчетыСПокупателямиИзменение.Контрагент,
	|	ДвиженияРасчетыСПокупателямиИзменение.Договор,
	|	ДвиженияРасчетыСПокупателямиИзменение.Документ,
	|	ДвиженияРасчетыСПокупателямиИзменение.Заказ,
	|	ДвиженияРасчетыСПокупателямиИзменение.ТипРасчетов
	|
	|ИМЕЮЩИЕ
	|	(СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаИзменение) <> 0
	|		ИЛИ СУММА(ДвиженияРасчетыСПокупателямиИзменение.СуммаВалИзменение) <> 0)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Организация,
	|	Контрагент,
	|	Договор,
	|	Документ,
	|	Заказ,
	|	ТипРасчетов");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	ВыборкаИзРезультатаЗапроса.Следующий();
	
	// Новые изменения были помещены во временную таблицу "ДвиженияРасчетыСПокупателямиИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	СтруктураВременныеТаблицы.Вставить("ДвиженияРасчетыСПокупателямиИзменение", ВыборкаИзРезультатаЗапроса.Количество > 0);
	
	// Временная таблица "ДвиженияРасчетыСПокупателямиПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияРасчетыСПокупателямиПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция КонтрагентыСИзмененнымиВзаиморасчетами()
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗЛИЧНЫЕ
		|	РасчетыСПокупателями.Контрагент КАК Контрагент
		|ИЗ
		|	РегистрНакопления.РасчетыСПокупателями КАК РасчетыСПокупателями
		|ГДЕ
		|	РасчетыСПокупателями.Регистратор = &Регистратор";
	
	Запрос.УстановитьПараметр("Регистратор", ЭтотОбъект.Отбор.Регистратор.Значение);
	
	КонтрагентыПрошлогоНабора = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("Контрагент");
	КонтрагентыТекущегоНабора = ЭтотОбъект.ВыгрузитьКолонку("Контрагент");
	
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(КонтрагентыПрошлогоНабора, КонтрагентыТекущегоНабора, Истина);
	
	Возврат КонтрагентыПрошлогоНабора;
	
КонецФункции

#КонецОбласти

#КонецЕсли