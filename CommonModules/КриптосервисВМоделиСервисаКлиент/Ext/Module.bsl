////////////////////////////////////////////////////////////////////////////////
// Подсистема "Криптосервис".
//  
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

Функция Инициализация(УчетнаяЗапись, ФормаВладелец = Неопределено) Экспорт
	
	ИнициализацияВыполнена = Ложь;
	Если ТипЗнч(УчетнаяЗапись) = Тип("Структура") Тогда
		ПараметрыАвторизации = УчетнаяЗапись;
	Иначе
		ПараметрыАвторизации = Новый Структура("УчетнаяЗапись", УчетнаяЗапись);
	КонецЕсли;
	Если КриптосервисВМоделиСервисаВызовСервера.ГотовКРаботе(УчетнаяЗапись) Тогда
		ИнициализацияВыполнена = Истина;
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю("Срок действия временного пароля истек. Повторите операцию.");
	КонецЕсли;
	
	Возврат ИнициализацияВыполнена;
	
КонецФункции

Процедура ИнициализацияСАвторизацией(УчетнаяЗапись, ФормаВладелец, ВыполняемоеОповещение) Экспорт
	
	ИнициализацияВыполнена = Ложь;
	Если ТипЗнч(УчетнаяЗапись) = Тип("Структура") Тогда
		ПараметрыАвторизации = УчетнаяЗапись;
	Иначе
		ПараметрыАвторизации = Новый Структура("УчетнаяЗапись", УчетнаяЗапись);
	КонецЕсли;
	Если КриптосервисВМоделиСервисаВызовСервера.ГотовКРаботе(УчетнаяЗапись) Тогда
		ИнициализацияВыполнена = Истина;
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ИнициализацияВыполнена);
	Иначе
		ЗапроситьАвторизацию(ПараметрыАвторизации, ФормаВладелец, ВыполняемоеОповещение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗапроситьАвторизацию(ПараметрыАвторизации, ФормаВладелец, ВыполняемоеОповещение)
	
	ДополнительныеПараметры = Новый Структура("ВыполняемоеОповещение", ВыполняемоеОповещение);
	ОписаниеОповещения = Новый ОписаниеОповещения("ЗапроситьАвторизациюЗавершение", ЭтотОбъект, ДополнительныеПараметры);
	ОткрытьФорму(
		"ОбщаяФорма.ВводВременногоПароляКЭлектроннойПодписиВМоделиСервиса", 
		ПараметрыАвторизации,
		?(ТипЗнч(ФормаВладелец) = Тип("УправляемаяФорма") И ФормаВладелец.Открыта(), ФормаВладелец, Неопределено),
		,
		,
		,
		ОписаниеОповещения, 
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ЗапроситьАвторизациюЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если Результат = КодВозвратаДиалога.ОК Тогда
		ИнициализацияВыполнена = Истина;
	Иначе 
		ИнициализацияВыполнена = Ложь;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, ИнициализацияВыполнена);
	
КонецПроцедуры

#КонецОбласти