////////////////////////////////////////////////////////////////////////////////
// Процедуры и функции, предназначенные для группового изменения строк в табличной
//  части и управления оформлением панели редактирования.
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция НастроитьОформлениеПанелиРедактирования(ЭтаФорма, НаборЭлементов, Состояние, Значение, ИзменяетДанные = Неопределено, СвязиПараметровВыбора = Неопределено) Экспорт
	
	Если Состояние = 2 И НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки") Тогда
		НаборЭлементов.ЗначениеЭлемент.Видимость = Ложь;
		Состояние = 3;
	КонецЕсли;
	
	УстановитьСвязиПараметровВыбора = Неопределено;
	Если Состояние = 0 Тогда
		
		// 0. Панель скрыта
		Если ЗначениеЗаполнено(ИзменяетДанные) И ИзменяетДанные Тогда
			ЭтаФорма.Модифицированность = Истина;
		КонецЕсли;
		
		НаборЭлементов.ПанельРедактирования.Видимость = Ложь;
		НаборЭлементов.КолонкаПометка.Видимость = Ложь;
		НаборЭлементов.КолонкаНомерСтроки.Видимость = Истина;
		
	ИначеЕсли Состояние = 1 Тогда
		
		// 1. Выбор действия
		НаборЭлементов.КолонкаПометка.Видимость = Истина;
		НаборЭлементов.КолонкаНомерСтроки.Видимость = Ложь;
		НаборЭлементов.ПанельРедактирования.Видимость = Истина;
		
		Значение = НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа.ПривестиЗначение("");
		
	ИначеЕсли Состояние = 2 Тогда
		
		// 2. Выбор значения
		Если НаборЭлементов.Действие <> ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки") Тогда
			НаборЭлементов.ЗначениеЭлемент.Видимость = Истина;
		КонецЕсли;
		
		УстановитьСвязиПараметровВыбора = Ложь;
		УстановитьПредставлениеДействия(НаборЭлементов, УстановитьСвязиПараметровВыбора);
		
		Значение = НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа.ПривестиЗначение("");
		
		// Курсор на ввод значения
		ЭтаФорма.ТекущийЭлемент = НаборЭлементов.ЗначениеЭлемент;
		
	ИначеЕсли Состояние = 3 Тогда
		
		// 3. Осталось нажать на кнопку "Выполнить"
		// Курсор на кнопке выполнить
		ЭтаФорма.ТекущийЭлемент = НаборЭлементов.КнопкаВыполнитьДействие;
		
	ИначеЕсли Состояние = 4 Тогда
		
		// 4. Представление изменений
		// Сдвиг табличной части на измененную колонку
		Если ЗначениеЗаполнено(НаборЭлементов.ОбъектИзменений) Тогда
			ЭтаФорма.ТекущийЭлемент = НаборЭлементов.КолонкаОбъектИзменений;
		КонецЕсли;
		
	КонецЕсли;
	
	Результат = Новый Структура;
	Если УстановитьСвязиПараметровВыбора <> Неопределено И УстановитьСвязиПараметровВыбора Тогда
		Результат.Вставить("УстановитьСвязиПараметровВыбора", УстановитьСвязиПараметровВыбора);
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура СтрокаПометкаПриИзменении(Таблица, ЭлементУстановитьПометки, ЭлементСнятьПометки) Экспорт
	
	Если Таблица.НайтиСтроки(Новый Структура("Пометка", Истина)).Количество() = 0 Тогда
		ЭлементУстановитьПометки.Видимость = Истина;
		ЭлементСнятьПометки.Видимость = Ложь;
	Иначе
		ЭлементУстановитьПометки.Видимость = Ложь;
		ЭлементСнятьПометки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьДействиеЗначение(Действие, Значение) Экспорт
	
	Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ПустаяСсылка");
	Значение = "";
	
КонецПроцедуры

// Получает строковое представление действия.
//
// Параметры:
//  ДействиеПеречисление - ПеречислениеСсылка.ДействияГрупповогоИзмененияСтрок - Действие, описание которого необходимо получить.
// Возвращаемое значение:
//  Строка - Представление действия.
Функция ПредставлениеДействия(ДействиеПеречисление) Экспорт
	
	ДействиеПредставление = Строка(ДействиеПеречисление);
	Возврат Лев(ДействиеПредставление, СтрНайти(ДействиеПредставление, "@") - 1);
	
КонецФункции


#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Устанавливает ограничение типа выбираемому значению в зависимости от выбранного действия
//
// Параметры:
//  НаборЭлементов   - Структура - набор необходимых реквизитов и элементов формы, входящих в панель редактирования.
//    * КнопкаВыполнитьДействие - КнопкаФормы - Кнопка формы, запускающая обработку ТЧ.
//    * Действие - ПеречислениеСсылка.ДействияГрупповогоИзмененияСтрок - Выбранное действие для группового изменения строк.
//    * ЗначениеЭлемент - ПолеФормы - Поле ввода значения для группового изменения строк.
//
Процедура УстановитьПредставлениеДействия(НаборЭлементов, УстановитьСвязиПараметровВыбора)
	
	Если НЕ ЗначениеЗаполнено(НаборЭлементов.Действие) Тогда
		
		МассивТипов = Новый Массив;
		МассивТипов.Добавить(Тип("Строка"));
		НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа = Новый ОписаниеТипов(МассивТипов);
		НаборЭлементов.ЗначениеЭлемент.Видимость = Истина;
		НаборЭлементов.ЗначениеЭлемент.Заголовок = "Значение";
		Возврат;
		
	КонецЕсли;
	
	ПараметрыВыбора = ?(НаборЭлементов.КолонкаОбъектИзменений = Неопределено, Неопределено, НаборЭлементов.КолонкаОбъектИзменений.ПараметрыВыбора);
	СвязиПараметровВыбора = ?(НаборЭлементов.КолонкаОбъектИзменений = Неопределено, Неопределено, НаборЭлементов.КолонкаОбъектИзменений.СвязиПараметровВыбора);
	
	ТипЗначения = ТипОбъектаДействия(НаборЭлементов.Действие, НаборЭлементов.КолонкаОбъектИзменений);
	
	Если ЗначениеЗаполнено(ТипЗначения) Тогда
		
		НаборЭлементов.ЗначениеЭлемент.ОграничениеТипа = ТипЗначения;
		Если ПараметрыВыбора <> Неопределено Тогда
			НаборЭлементов.ЗначениеЭлемент.ПараметрыВыбора = ПараметрыВыбора;
		КонецЕсли;
		Если НаборЭлементов.ЗначениеЭлемент.СвязиПараметровВыбора.Количество() <> 0 Тогда
			УстановитьСвязиПараметровВыбора = Истина;
		ИначеЕсли СвязиПараметровВыбора <> Неопределено Тогда
			УстановитьСвязиПараметровВыбора = СвязиПараметровВыбора.Количество() <> 0;
		Иначе
			УстановитьСвязиПараметровВыбора = Ложь;
		КонецЕсли;
		НаборЭлементов.ЗначениеЭлемент.Заголовок = ПредставлениеЗаголовкаОбъектаДействия(НаборЭлементов.Действие);
		
		Если НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ИзменитьЦеныНаПроцент") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьЦены")
			ИЛИ НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьСуммы") Тогда
			
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Ложь;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоКоличеству") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСтавкуНДС") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Ложь;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦены") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦеныПоВиду") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЗаказПокупателя") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуОтгрузки")
			ИЛИ НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуПоступления")
			ИЛИ НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуРеализации")
			ИЛИ НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуПланирования") Тогда
			
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Ложь;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПокупателя") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЗаказПоставщикуЗаказПокупателя") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		ИначеЕсли НаборЭлементов.Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьНовоеМесто") Тогда
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыбора = Истина;
			НаборЭлементов.ЗначениеЭлемент.КнопкаВыпадающегоСписка = Истина;
		КонецЕсли;
		
	Иначе
		
		НаборЭлементов.ЗначениеЭлемент.Видимость = Ложь;
		НаборЭлементов.КнопкаВыполнитьДействие.Видимость = Истина;
		
	КонецЕсли;
	
	НаборЭлементов.ЗначениеЭлемент.КнопкаОткрытия = Ложь;
	
КонецПроцедуры

// Определяет тип объекта действия.
//
Функция ТипОбъектаДействия(Действие, ОбъектИзменений)
	
	МассивТипов = Новый Массив;
	КвалификаторыЧисла = Неопределено;
	ОписаниеТипов = Неопределено;
	
	Если Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ДобавитьИзДокумента") Тогда
		
		МассивТипов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ИнвентаризацияЗапасов"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ОприходованиеЗапасов"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ПеремещениеЗапасов"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		МассивТипов.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
		МассивТипов.Добавить(Тип("ДокументСсылка.СборкаЗапасов"));
		МассивТипов.Добавить(Тип("ДокументСсылка.СписаниеЗапасов"));
		МассивТипов.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
		МассивТипов.Добавить(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ИзменитьЦеныНаПроцент") Тогда
		
		МассивТипов.Добавить(Тип("Число"));
		КвалификаторыЧисла = Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Любой);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьЦены")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.ОкруглитьСуммы") Тогда
		
		МассивТипов.Добавить(Тип("ПеречислениеСсылка.ПорядкиОкругления"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоКоличеству") Тогда
		
		МассивТипов.Добавить(Тип("Число"));
		КвалификаторыЧисла = Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.РаспределитьСуммуПоСуммам") Тогда
		
		МассивТипов.Добавить(Тип("Число"));
		КвалификаторыЧисла = Новый КвалификаторыЧисла(15, 2, ДопустимыйЗнак.Неотрицательный);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьСтавкуНДС") Тогда
		
		МассивТипов.Добавить(Тип("СправочникСсылка.СтавкиНДС"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПроцентСкидкиНаценки") Тогда
		
		МассивТипов.Добавить(Тип("Число"));
		КвалификаторыЧисла = Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Неотрицательный);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦены") Тогда
		
		МассивТипов.Добавить(Тип("Число"));
		КвалификаторыЧисла = Новый КвалификаторыЧисла(10, 2, ДопустимыйЗнак.Неотрицательный);
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЦеныПоВиду") Тогда
		
		МассивТипов.Добавить(Тип("СправочникСсылка.ВидыЦен"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УдалитьСтроки") Тогда
		
		МассивТипов.Очистить();
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЗаказПокупателя") Тогда
		
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуОтгрузки")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуПоступления")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуРеализации")
		ИЛИ Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьДатуПланирования") Тогда
		
		МассивТипов.Добавить(Тип("Дата"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьПокупателя") Тогда
		
		МассивТипов.Добавить(Тип("СправочникСсылка.Контрагенты"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьЗаказПоставщикуЗаказПокупателя") Тогда
		
		ОписаниеТипов = ОбъектИзменений.ОграничениеТипа;
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьИсходноеМесто") Тогда
		
		МассивТипов.Добавить(Тип("СправочникСсылка.СтруктурныеЕдиницы"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		
	ИначеЕсли Действие = ПредопределенноеЗначение("Перечисление.ДействияГрупповогоИзмененияСтрок.УстановитьНовоеМесто") Тогда
		
		МассивТипов.Добавить(Тип("СправочникСсылка.СтруктурныеЕдиницы"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказНаПроизводство"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		МассивТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		
	КонецЕсли;
	
	Если ОписаниеТипов = Неопределено Тогда
		ТипОбъекта = Новый ОписаниеТипов(МассивТипов,,, КвалификаторыЧисла);
	Иначе
		ТипОбъекта = ОписаниеТипов;
	КонецЕсли;
	
	Возврат ТипОбъекта;
	
КонецФункции

// Определяет название объекта действия.
//
Функция ПредставлениеЗаголовкаОбъектаДействия(ДействиеПеречисление)
	
	ДействиеПредставление = Строка(ДействиеПеречисление);
	Возврат Прав(ДействиеПредставление, СтрДлина(ДействиеПредставление) - СтрНайти(ДействиеПредставление, "@"));
	
КонецФункции

#КонецОбласти
