////////////////////////////////////////////////////////////////////////////////
// Подсистема "Отправка SMS"
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция СтатусДоставки(ИдентификаторСообщения) Экспорт
	
	ОтправкаSMS.ПроверитьПрава();
	
	Если ПустаяСтрока(ИдентификаторСообщения) Тогда
		Возврат "НеОтправлялось";
	КонецЕсли;
	
	Результат = Неопределено;
	НастройкиОтправкиSMS = НастройкиОтправкиSMS();
	
	Если НастройкиОтправкиSMS.Провайдер = Перечисления.ПровайдерыSMS.МТС Тогда
		Результат = ОтправкаSMSЧерезМТС.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS);
	ИначеЕсли НастройкиОтправкиSMS.Провайдер = Перечисления.ПровайдерыSMS.Билайн Тогда
		Результат = ОтправкаSMSЧерезБилайн.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS);
	ИначеЕсли НастройкиОтправкиSMS.Провайдер = Перечисления.ПровайдерыSMS.SMSRU Тогда
		Результат = ОтправкаSMSЧерезSMSRU.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS);
	ИначеЕсли НастройкиОтправкиSMS.Провайдер = Перечисления.ПровайдерыSMS.SMSЦЕНТР Тогда
		Результат = ОтправкаSMSЧерезSMSЦЕНТР.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS);
	ИначеЕсли НастройкиОтправкиSMS.Провайдер = Перечисления.ПровайдерыSMS.СМСУслуги Тогда
		Результат = ОтправкаSMSЧерезСМСУслуги.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS);
	ИначеЕсли НастройкиОтправкиSMS.Провайдер = Перечисления.ПровайдерыSMS.GSMINFORM Тогда
		Результат = ОтправкаSMSЧерезGSMINFORM.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS);
	ИначеЕсли ЗначениеЗаполнено(НастройкиОтправкиSMS.Провайдер) Тогда
		ОтправкаSMSПереопределяемый.СтатусДоставки(ИдентификаторСообщения, НастройкиОтправкиSMS.Провайдер,
			НастройкиОтправкиSMS.Логин, НастройкиОтправкиSMS.Пароль, Результат);
	Иначе // провайдер не выбран
		Результат = "Ошибка";
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

Функция НастройкиОтправкиSMS() Экспорт
	Результат = Новый Структура("Логин, Пароль, Провайдер, ИмяОтправителя");
	Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		Владелец = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("Константа.ПровайдерSMS");
		УстановитьПривилегированныйРежим(Истина);
		НастройкиПровайдера = ОбщегоНазначения.ПрочитатьДанныеИзБезопасногоХранилища(Владелец, "Пароль,Логин,ИмяОтправителя");
		УстановитьПривилегированныйРежим(Ложь);
		Результат.Логин = НастройкиПровайдера.Логин;
		Результат.Пароль = НастройкиПровайдера.Пароль;
		Результат.ИмяОтправителя = НастройкиПровайдера.ИмяОтправителя;
		Результат.Провайдер =Константы.ПровайдерSMS.Получить();
	КонецЕсли;
	Возврат Результат;
КонецФункции

#КонецОбласти