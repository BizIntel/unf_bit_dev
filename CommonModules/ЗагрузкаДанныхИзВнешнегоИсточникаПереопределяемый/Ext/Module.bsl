
Функция МаксимумПолезныхКолонокТабличногоДокумента() ЭКспорт
	
	// Самый большой справочник по количеству реквизитов: Контрагенты (43 полезных) + 10 на КИ + Дополнительные реквизиты.
	// Остальное регулируем в ЗагрузкаДанныхИзВнешнегоИсточника.ОптимизироватьТабличныйДокумент()
	
	Возврат 43 + 10 + МаксимумДополнительныхРеквизитовТабличногоДокумента();
	
КонецФункции

Функция МаксимумДополнительныхРеквизитовТабличногоДокумента() ЭКспорт
	
	Возврат 10;
	
КонецФункции

Функция МаксимумЗаписейВОднойТранзакции() Экспорт
	
	Возврат 137;
	
КонецФункции

Функция СтандартныеПодсистемыСервер_ПреобразоватьСтрокуВДопустимоеНаименованиеКолонки(Строка) Экспорт
	
	НедопустимыеСимволы = "<>:;!@#$%^&-~`'.,?{}[]+=*/|\ ()_""«»";
	Результат = "";
	Для Индекс = 1 По СтрДлина(Строка) Цикл
		Символ =  Сред(Строка, Индекс, 1);
		Если СтрНайти(НедопустимыеСимволы, Символ) > 0 Тогда
			Результат = Результат + "_" + КодСимвола(Символ) + "_";
		Иначе
			Результат = Результат + Символ;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Процедура ДобавитьУсловноеОформлениеДополнительныхРеквизитов(ЭтотОбъект, ПутьРеквизита, НастройкиЗагрузкиДанных)
	
	
	
КонецПроцедуры

Процедура ДобавитьУсловноеОформлениеТаблицыСопоставлений(ЭтотОбъект, ПутьРеквизита, НастройкиЗагрузкиДанных) Экспорт
	
	Если НастройкиЗагрузкиДанных.ЭтоЗагрузкаТабличнойЧасти Тогда
		
		Возврат;
		
	ИначеЕсли НастройкиЗагрузкиДанных.ЭтоЗагрузкаСправочника Тогда
		
		Если НастройкиЗагрузкиДанных.ОписаниеДополнительныхРеквизитов.Количество() > 0 Тогда
			
			ДобавитьУсловноеОформлениеДополнительныхРеквизитов(ЭтотОбъект, ПутьРеквизита, НастройкиЗагрузкиДанных);
			
		КонецЕсли;
		
		Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
			
			ИмяПоля = "Номенклатура";
			
		ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты" Тогда
			
			ИмяПоля = "Контрагент";
			
		КонецЕсли;
		
		ТекстНовыйЭлемент	= Нстр("ru ='<Будет создан новый элемент>'");
		ТекстПропущены		= Нстр("ru ='<Данные будут пропущены>'");
		ТекстУсловногоОформления = ?(НастройкиЗагрузкиДанных.СоздаватьЕслиНеСопоставлено, ТекстНовыйЭлемент, ТекстПропущены);
		
	ИначеЕсли НастройкиЗагрузкиДанных.ЭтоЗагрузкаРегистраСведений Тогда
		
		Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "РегистрСведений.ЦеныНоменклатуры" Тогда
			
			ИмяПоля = "Номенклатура";
			
		КонецЕсли;
		
		ТекстУсловногоОформления = Нстр("ru ='<Строка будет пропущена...>'");
		
	КонецЕсли;
	
	ЭлементУсловногоОформленияКД = ЭтотОбъект.УсловноеОформление.Элементы.Добавить();
	ЭлементУсловногоОформленияКД.Использование = Истина;
	
	ЭлементОтбораКД = ЭлементУсловногоОформленияКД.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбораКД.ЛевоеЗначение = Новый ПолеКомпоновкиДанных(ПутьРеквизита + "." + ИмяПоля);
	ЭлементОтбораКД.ВидСравнения = ВидСравненияКомпоновкиДанных.НеЗаполнено;
	
	ЭлементУсловногоОформленияКД.Оформление.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Текст"), ТекстУсловногоОформления);
	ЭлементУсловногоОформленияКД.Оформление.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("ЦветТекста"), Новый Цвет(175, 175, 175));
	
	ОформляемоеПолеКД = ЭлементУсловногоОформленияКД.Поля.Элементы.Добавить();
	ОформляемоеПолеКД.Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	
КонецПроцедуры

Процедура ИзменитьТекстУсловногоОформления(УсловноеОформление, НастройкиЗагрузкиДанных) Экспорт
	
	Если НастройкиЗагрузкиДанных.ЭтоЗагрузкаТабличнойЧасти Тогда
		
		Возврат;
		
	ИначеЕсли НастройкиЗагрузкиДанных.ЭтоЗагрузкаРегистраСведений Тогда
		
		Возврат;
		
	ИначеЕсли НастройкиЗагрузкиДанных.ЭтоЗагрузкаСправочника Тогда
		
		Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
			
			ИмяПоля = "Номенклатура";
			
		ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты" Тогда
			
			ИмяПоля = "Контрагент";
			
		КонецЕсли;
		
		ТекстНовыйЭлемент	= Нстр("ru ='<Будет создан новый элемент>'");
		ТекстПропущены		= Нстр("ru ='<Данные будут пропущены>'");
		ТекстУсловногоОформления = ?(НастройкиЗагрузкиДанных.СоздаватьЕслиНеСопоставлено, ТекстНовыйЭлемент, ТекстПропущены);
		
	КонецЕсли;
	
	ИскомыйЭлемент = Новый ПолеКомпоновкиДанных(ИмяПоля);
	Для каждого ЭлементУсловногоОформления Из УсловноеОформление.Элементы Цикл
		
		ЭтоИскомоеОформление = Ложь;
		Для каждого ОформляемоеПоле Из ЭлементУсловногоОформления.Поля.Элементы Цикл
			
			Если ОформляемоеПоле.Поле = ИскомыйЭлемент Тогда
				
				ЭтоИскомоеОформление = Истина;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
		Если ЭтоИскомоеОформление Тогда
			
			ЭлементУсловногоОформления.Оформление.УстановитьЗначениеПараметра(Новый ПараметрКомпоновкиДанных("Текст"), ТекстУсловногоОформления);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриОпределенииФормыЗагрузкиДанных(ИмяФормыЗагрузкиДанныхИзВнешнихИсточников, ПолноеИмяОбъектаЗаполнения, ОбъектЗаполнения) Экспорт
	
	
	
КонецПроцедуры

Процедура ПереопределитьОбразцыЗагрузкиДанных(НастройкиЗагрузкиДанных, УникальныйИдентификатор) Экспорт
	
	
	
КонецПроцедуры

Процедура ПереопределитьЗаполнениеПолейЗагрузкиДанных(ТаблицаПолейЗагрузки, НастройкиЗагрузкиДанных) Экспорт
	
	
	
КонецПроцедуры

Процедура ПриДобавленииСлужебныхПолей(ГруппаСлужебныхПолей, ПолноеИмяОбъектаЗаполнения) Экспорт
	
	
	
КонецПроцедуры

Процедура ПослеДобавленияЭлементовТаблицыСопоставлений(ЭтотОбъект, НастройкиЗагрузкиДанных) Экспорт
	
	Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
		
		ЭтотОбъект.Элементы["Родитель"].ФормаВыбора = "Справочник.Номенклатура.Форма.ФормаВыбораГруппы";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты"  Тогда
		
		ЭтотОбъект.Элементы["Родитель"].ФормаВыбора = "Справочник.Контрагенты.Форма.ФормаВыбораГруппы.";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Запасы" Тогда
		
		МассивТиповНоменклатуры = Новый Массив;
		МассивТиповНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Запас);
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипНоменклатуры", МассивТиповНоменклатуры);
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(НовыйПараметр);
		
		ЭтотОбъект.Элементы["Номенклатура"].ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Документ.ПриходнаяНакладная.ТабличнаяЧасть.Расходы" Тогда
		
		МассивТиповНоменклатуры = Новый Массив;
		МассивТиповНоменклатуры.Добавить(Перечисления.ТипыНоменклатуры.Услуга);
		
		НовыйПараметр = Новый ПараметрВыбора("Отбор.ТипНоменклатуры", МассивТиповНоменклатуры);
		
		МассивПараметров = Новый Массив;
		МассивПараметров.Добавить(НовыйПараметр);
		
		ЭтотОбъект.Элементы["Номенклатура"].ПараметрыВыбора = Новый ФиксированныйМассив(МассивПараметров);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОпределенииРежимаИспользования(ИспользоватьСовместно) Экспорт
	
	ИспользоватьСовместно = Ложь;
	
КонецПроцедуры

Функция СтандартныеИменаПолейНеподлежащихОбновлению(НастройкиЗагрузкиДанных) Экспорт
	
	// При более сложной настройке допускается индивидуальная настройка исключаемых полей
	// Описание таких полей можно разместить непосредственно в менеджерах объектов (Справочники.Номенклатура.<ИмяПроцедурыИлиФункции>)
	
	СтандартныеИсключаемыеСвойства = "";
	Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты" Тогда
		
		СтандартныеИсключаемыеСвойства = "Родитель";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
		
		СтандартныеИсключаемыеСвойства = "Код,Родитель";
		
	КонецЕсли;
	
	Возврат СтандартныеИсключаемыеСвойства;
	
КонецФункции

Функция ИменаПолейНеУчаствующиеВЗаполненииСвойств() Экспорт
	
	Возврат
	"АдресФакт
	|АдресЮр
	|АдресЭП
	|ИНН_КПП
	|КонтрагентНаименование
	|НоменклатураНаименование
	|НоменклатураНаименованиеПолное
	|РасчетныйСчет
	|Родитель
	|Телефон
	|СерийныйНомер
	|Штрихкод"
	
КонецФункции

Функция ОсновнойВидЦен() Экспорт
	
	Возврат Справочники.Контрагенты.ПолучитьОсновнойВидЦенПродажи();
	
КонецФункции

#Область МетодыСопоставлений

//:::Общее

Процедура СправочникПоНаименованию(ИмяСправочника, СправочникЗначение, СправочникНаименование, ЗначениеПоУмолчанию = Неопределено)
	
	Если НЕ ПустаяСтрока(СправочникНаименование) Тогда
		
		СправочникСсылка = Справочники[ИмяСправочника].НайтиПоНаименованию(СправочникНаименование, Ложь);
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникЗначение = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СправочникЗначение) Тогда
		
		СправочникЗначение = ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьПеречисление(ИмяПеречисления, ЗначениеПеречисления, ВходящиеДанные, ЗначениеПоУмолчанию)
	
	Если ЗначениеЗаполнено(ВходящиеДанные) Тогда
		
		Для каждого ЭлементПеречисления Из Метаданные.Перечисления[ИмяПеречисления].ЗначенияПеречисления Цикл
			
			Синоним = ЭлементПеречисления.Синоним;
			Если СтрНайти(ВРег(Синоним), ВРег(ВходящиеДанные)) > 0 Тогда
				
				ЗначениеПеречисления = Перечисления[ИмяПеречисления][ЭлементПеречисления.Имя];
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЗначениеПеречисления) Тогда
		
		ЗначениеПеречисления = ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьСчетУчета(СчетУчета, СчетУчета_ВходящиеДанные, ЗначениеПоУмолчанию)
	
	Если НЕ ПустаяСтрока(СчетУчета_ВходящиеДанные) Тогда
		
		НайденныйСчетУчета = ПланыСчетов.Управленческий.НайтиПоКоду(СчетУчета_ВходящиеДанные);
		Если НайденныйСчетУчета = Неопределено
			ИЛИ НайденныйСчетУчета = ПланыСчетов.Управленческий.ПустаяСсылка() Тогда
			
			НайденныйСчетУчета = ПланыСчетов.Управленческий.НайтиПоНаименованию(СчетУчета_ВходящиеДанные);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(НайденныйСчетУчета) Тогда
			
			СчетУчета = НайденныйСчетУчета
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СчетУчета) Тогда
		
		СчетУчета = ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПреобразоватьСтрокуВБулево(ЗначениеБулево, ВходящиеДанные) Экспорт
	
	ВходящиеДанные = ВРЕГ(СокрЛП(ВходящиеДанные));
	
	МассивИстина = Новый Массив;
	МассивИстина.Добавить("+");
	МассивИстина.Добавить("1");
	МассивИстина.Добавить("ИСТИНА");
	МассивИстина.Добавить("ДА");
	МассивИстина.Добавить("TRUE");
	МассивИстина.Добавить("YES");
	
	МассивЛожь = Новый Массив;
	МассивЛожь.Добавить("-");
	МассивЛожь.Добавить("0");
	МассивЛожь.Добавить("ЛОЖЬ");
	МассивЛожь.Добавить("НЕТ");
	МассивЛожь.Добавить("FALSE");
	МассивЛожь.Добавить("NO");
	
	Если МассивИстина.Найти(ВходящиеДанные) <> Неопределено Тогда
		
		ЗначениеБулево = Истина;
		
	ИначеЕсли МассивЛожь.Найти(ВходящиеДанные) <> Неопределено Тогда
		
		ЗначениеБулево = Ложь;
		
	Иначе
		
		ЗначениеБулево = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПреобразоватьСтрокуВЧисло(ЧислоРезультат, ЧислоСтрокой, ЗначениеПоУмолчанию = 0) Экспорт
	
	Если ПустаяСтрока(ЧислоСтрокой) Тогда
		
		ЧислоРезультат = ЗначениеПоУмолчанию;
		Возврат;
		
	КонецЕсли;
	
	ЧислоСтрокойКопия = СтрЗаменить(ЧислоСтрокой, ".", "");
	ЧислоСтрокойКопия = СтрЗаменить(ЧислоСтрокойКопия, ",", "");
	ЧислоСтрокойКопия = СтрЗаменить(ЧислоСтрокойКопия, Символ(32), "");
	ЧислоСтрокойКопия = СтрЗаменить(ЧислоСтрокойКопия, Символ(160), "");
	Если СтроковыеФункцииКлиентСервер.ТолькоЦифрыВСтроке(ЧислоСтрокойКопия) Тогда
		
		ЧислоСтрокойКопия = СтрЗаменить(ЧислоСтрокой, " ", "");
		Попытка // через попытку, например, на случай нескольких точек в выражении
			
			ЧислоРезультат = Число(ЧислоСтрокойКопия);
			
		Исключение
			
			ЧислоРезультат = 0; // Если передали мусор, значит ноль
			
		КонецПопытки;
		
	Иначе
		
		ЧислоРезультат = 0; // Если передали мусор, значит ноль
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПреобразоватьСтрокуВДату(ДатаРезультат, ДатаСтрокой) Экспорт
	
	Если ПустаяСтрока(ДатаСтрокой) Тогда
		
		ДатаРезультат = Дата(0001, 01, 01);
		
	Иначе
		
		КопияДатаСтрокой = ДатаСтрокой;
		
		МассивРазделителей = Новый Массив;
		МассивРазделителей.Добавить(".");
		МассивРазделителей.Добавить("/");
		
		Для каждого Разделитель Из МассивРазделителей Цикл
			
			ЧислоСтрокой = "";
			МесяцСтрокой = "";
			ГодСтрокой = "";
			
			ПозицияРазделителя = СтрНайти(КопияДатаСтрокой, Разделитель);
			Если ПозицияРазделителя > 0 Тогда
				
				ЧислоСтрокой = Лев(КопияДатаСтрокой, ПозицияРазделителя - 1);
				КопияДатаСтрокой = Сред(КопияДатаСтрокой, ПозицияРазделителя + 1);
				
			КонецЕсли;
			
			ПозицияРазделителя = СтрНайти(КопияДатаСтрокой, Разделитель);
			Если ПозицияРазделителя > 0 Тогда
				
				МесяцСтрокой = Лев(КопияДатаСтрокой, ПозицияРазделителя - 1);
				КопияДатаСтрокой = Сред(КопияДатаСтрокой, ПозицияРазделителя + 1);
				
			КонецЕсли;
			
			ГодСтрокой = КопияДатаСтрокой;
			
			Если НЕ ПустаяСтрока(ЧислоСтрокой) 
				И НЕ ПустаяСтрока(МесяцСтрокой) 
				И НЕ ПустаяСтрока(ГодСтрокой) Тогда
				
				Попытка
					
					ДатаРезультат = Дата(Число(ГодСтрокой), Число(МесяцСтрокой), Число(ЧислоСтрокой));
					
				Исключение
					
					ДатаРезультат = Дата(0001, 01, 01);
					
				КонецПопытки;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СкопироватьСтрокуВЗначениеСтроковогоТипа(ЗначениеСтроковогоТипа, Строка) Экспорт
	
	ЗначениеСтроковогоТипа = СокрЛП(Строка);
	
КонецПроцедуры

Процедура СопоставитьНоменклатуру(Номенклатура, Штрихкод, Артикул, НоменклатураНаименование, НоменклатураНаименованиеПолное = Неопределено, Код = Неопределено) Экспорт
	
	ЗначениеБылоСопоставлено = Ложь;
	Если ЗначениеЗаполнено(Код) Тогда
		
		СправочникСсылка = Справочники.Номенклатура.НайтиПоКоду(Код, Ложь);
		Если НЕ СправочникСсылка.Пустая() Тогда
			
			ЗначениеБылоСопоставлено = Истина;
			Номенклатура = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеБылоСопоставлено 
		И ЗначениеЗаполнено(Штрихкод) Тогда
		
		Запрос = Новый Запрос("ВЫБРАТЬ ШК.Номенклатура ИЗ РегистрСведений.ШтрихкодыНоменклатуры КАК ШК ГДЕ ШК.Штрихкод = &Штрихкод");
		Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗначениеБылоСопоставлено = Истина;
			Номенклатура = Выборка.Номенклатура;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеБылоСопоставлено 
		И ЗначениеЗаполнено(Артикул) Тогда
		
		СправочникСсылка = Справочники.Номенклатура.НайтиПоРеквизиту("Артикул", Артикул);
		Если НЕ СправочникСсылка.Пустая() Тогда
			
			ЗначениеБылоСопоставлено = Истина;
			Номенклатура = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеБылоСопоставлено 
		И ЗначениеЗаполнено(НоменклатураНаименование) Тогда
		
		СправочникСсылка = Справочники.Номенклатура.НайтиПоНаименованию(НоменклатураНаименование, Истина);
		Если ЗначениеЗаполнено(СправочникСсылка)
			И НЕ СправочникСсылка.ЭтоГруппа Тогда
			
			ЗначениеБылоСопоставлено = Истина;
			Номенклатура = СправочникСсылка;
			
		Иначе
			
			Запрос = Новый Запрос("Выбрать Справочник.Номенклатура.Ссылка ГДЕ НЕ Справочник.Номенклатура.ЭтоГруппа И Справочник.Номенклатура.Наименование ПОДОБНО &Наименование");
			Запрос.УстановитьПараметр("Наименование", НоменклатураНаименование + "%");
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				
				ЗначениеБылоСопоставлено = Истина;
				Номенклатура = Выборка.Ссылка;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеБылоСопоставлено 
		И ЗначениеЗаполнено(НоменклатураНаименованиеПолное) Тогда
		
		Запрос = Новый Запрос("Выбрать Справочник.Номенклатура.Ссылка ГДЕ НЕ Справочник.Номенклатура.ЭтоГруппа И Справочник.Номенклатура.НаименованиеПолное ПОДОБНО &НаименованиеПолное");
		Запрос.УстановитьПараметр("НаименованиеПолное", НоменклатураНаименованиеПолное + "%");
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗначениеБылоСопоставлено = Истина;
			Номенклатура = Выборка.Ссылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Группы для справочника номенклатуры на текущий момент не используется.
	Если ЗначениеЗаполнено(Номенклатура)
		И Номенклатура.ЭтоГруппа Тогда
		
		Номенклатура = Справочники.Номенклатура.ПустаяСсылка();
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьХарактеристику(Характеристика, Номенклатура, Штрихкод, Характеристика_ВходящиеДанные) Экспорт
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		ЗначениеБылоСопоставлено = Ложь;
		Если ЗначениеЗаполнено(Штрихкод) Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ ШК.Характеристика ИЗ РегистрСведений.ШтрихкодыНоменклатуры КАК ШК ГДЕ ШК.Штрихкод = &Штрихкод И ШК.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
			Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				
				ЗначениеБылоСопоставлено = Истина;
				Характеристика = Выборка.Характеристика;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеБылоСопоставлено
			И ЗначениеЗаполнено(Характеристика_ВходящиеДанные) Тогда
			
			// Владельцем характеристики может быть номенклатура или категория номенклатуры.
			//
			
			СправочникСсылка = Неопределено;
			СправочникСсылка = Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Характеристика_ВходящиеДанные, Ложь, , Номенклатура);
			Если НЕ ЗначениеЗаполнено(СправочникСсылка)
				И ЗначениеЗаполнено(Номенклатура.КатегорияНоменклатуры) Тогда
				
				СправочникСсылка = Справочники.ХарактеристикиНоменклатуры.НайтиПоНаименованию(Характеристика_ВходящиеДанные, Ложь, , Номенклатура.КатегорияНоменклатуры);
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				Характеристика = СправочникСсылка;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьПартию(Партия, Номенклатура, Штрихкод, Партия_ВходящиеДанные) Экспорт
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		ЗначениеБылоСопоставлено = Ложь;
		Если ЗначениеЗаполнено(Штрихкод) Тогда
			
			Запрос = Новый Запрос("ВЫБРАТЬ ШК.Партия ИЗ РегистрСведений.ШтрихкодыНоменклатуры КАК ШК ГДЕ ШК.Штрихкод = &Штрихкод И ШК.Номенклатура = &Номенклатура");
			Запрос.УстановитьПараметр("Штрихкод", Штрихкод);
			Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
			
			Выборка = Запрос.Выполнить().Выбрать();
			Если Выборка.Следующий() Тогда
				
				ЗначениеБылоСопоставлено = Истина;
				Партия = Выборка.Партия;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеБылоСопоставлено
			И ЗначениеЗаполнено(Партия_ВходящиеДанные) Тогда
			
			СправочникСсылка = Справочники.ПартииНоменклатуры.НайтиПоНаименованию(Партия_ВходящиеДанные, Ложь, , Номенклатура);
			Если ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				Партия = СправочникСсылка;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьЕдиницыИзмерения(Номенклатура, ЕдиницаИзмерения, ЕдиницаИзмерения_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	Если НЕ ПустаяСтрока(ЕдиницаИзмерения_ВходящиеДанные) Тогда
		
		Если ЗначениеЗаполнено(Номенклатура) Тогда
			
			НаименованиеТекущейЕдиницыИзмерения = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Номенклатура, "ЕдиницаИзмерения.Наименование");
			Если ВРег(СокрЛП(НаименованиеТекущейЕдиницыИзмерения)) = ВРег(СокрЛП(ЕдиницаИзмерения_ВходящиеДанные)) Тогда
				
				ЕдиницаИзмерения = Номенклатура.ЕдиницаИзмерения;
				
			КонецЕсли;
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
			
			СправочникСсылка = Справочники.КлассификаторЕдиницИзмерения.НайтиПоНаименованию(ЕдиницаИзмерения_ВходящиеДанные, Ложь);
			Если ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				ЕдиницаИзмерения = СправочникСсылка;
				
			КонецЕсли;
			
		КонецЕсли;
			
		Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения)
			И ЗначениеЗаполнено(Номенклатура) Тогда
			
			СправочникСсылка = Справочники.ЕдиницыИзмерения.НайтиПоНаименованию(ЕдиницаИзмерения_ВходящиеДанные, Ложь, , Номенклатура);
			Если ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				ЕдиницаИзмерения = СправочникСсылка;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЕдиницаИзмерения) Тогда
		
		ЕдиницаИзмерения = ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьРодителя(ИмяСправочника, Родитель, Родитель_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	Если НЕ ПустаяСтрока(Родитель_ВходящиеДанные) Тогда
		
		Запрос = Новый Запрос("Выбрать Справочник." + ИмяСправочника + ".Ссылка ГДЕ Справочник." + ИмяСправочника + ".ЭтоГруппа И Справочник." + ИмяСправочника + ".Наименование ПОДОБНО &Наименование");
		Запрос.УстановитьПараметр("Наименование", Родитель_ВходящиеДанные + "%");
		Выборка = Запрос.Выполнить().Выбрать();
		
		Если Выборка.Следующий() Тогда
			
			Родитель = Выборка.Ссылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Родитель) Тогда
		
		Родитель = ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьВалюту(ЗначениеДопРеквизита, СтроковоеЗначение) Экспорт
	
	Если ПустаяСтрока(СтроковоеЗначение) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"Выбрать СпрВалюты.Ссылка КАК Ссылка Из Справочник.Валюты КАК СпрВалюты 
	|Где СпрВалюты.Код = &КодВалюты 
	|	ИЛИ СпрВалюты.Наименование Подобно &НаименованиеВалюты
	|	ИЛИ СпрВалюты.НаименованиеПолное Подобно &НаименованиеПолное
	|	ИЛИ СпрВалюты.СимвольноеПредставление = &СимвольноеПредставление");
	
	Запрос.УстановитьПараметр("КодВалюты", СокрЛП(СтроковоеЗначение));
	Запрос.УстановитьПараметр("НаименованиеВалюты", СокрЛП(СтроковоеЗначение));
	Запрос.УстановитьПараметр("НаименованиеПолное", СокрЛП(СтроковоеЗначение));
	Запрос.УстановитьПараметр("СимвольноеПредставление", СокрЛП(СтроковоеЗначение));
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗначениеДопРеквизита = Выборка.Ссылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьДополнительноеСвойство(ЗначениеДопРеквизита, Свойство, ИспользоватьИерархию, СтроковоеЗначение) Экспорт
	
	Запрос = Новый Запрос("Выбрать Справочник.ЗначенияСвойствОбъектов.Ссылка КАК ЗначениеСвойства Где Наименование Подобно &Наименование И Справочник.ЗначенияСвойствОбъектов.Ссылка В(&МассивЗначений)");
	
	Если ИспользоватьИерархию Тогда
		
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "Справочник.ЗначенияСвойствОбъектов", "Справочник.ЗначенияСвойствОбъектовИерархия");
		
	КонецЕсли;
	
	МассивЗначений = УправлениеСвойствами.ПолучитьСписокЗначенийСвойств(Свойство);
	
	Запрос.УстановитьПараметр("Наименование", СокрЛП(СтроковоеЗначение));
	Запрос.УстановитьПараметр("МассивЗначений", МассивЗначений);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗначениеДопРеквизита = Выборка.ЗначениеСвойства;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьПользователя(ЗначениеДопРеквизита, СтроковоеЗначение) Экспорт
	
	НайденныйПользователь = Пользователи.НайтиПоИмени(СокрЛП(СтроковоеЗначение));
	Если НЕ ЗначениеЗаполнено(НайденныйПользователь) Тогда
		
		НайденныйПользователь = Справочники.Пользователи.НайтиПоНаименованию(СокрЛП(СтроковоеЗначение), Ложь);
		
	КонецЕсли;
	
	ЗначениеДопРеквизита = ?(ЗначениеЗаполнено(НайденныйПользователь), НайденныйПользователь, Неопределено);
	
КонецПроцедуры

Процедура СопоставитьПроект(ЗначениеДопРеквизита, СтроковоеЗначение) Экспорт
	
	Если ПустаяСтрока(СтроковоеЗначение) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос("Выбрать Справочник.Проекты.Ссылка КАК Проект Где Наименование Подобно &Наименование");
	
	Запрос.УстановитьПараметр("Наименование", СокрЛП(СтроковоеЗначение));
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗначениеДопРеквизита = Выборка.Проект;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьСотрудника(ЗначениеДопРеквизита, СтроковоеЗначение) Экспорт
	
	Если ПустаяСтрока(СтроковоеЗначение) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос("Выбрать Справочник.Сотрудники.Ссылка КАК Сотрудник Где Код = &ТабельныйНомер ИЛИ Наименование Подобно &ПредставлениеСотрудника");
	
	Запрос.УстановитьПараметр("ТабельныйНомер", СокрЛП(СтроковоеЗначение));
	Запрос.УстановитьПараметр("ПредставлениеСотрудника", СокрЛП(СтроковоеЗначение));
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			ЗначениеДопРеквизита = Выборка.Сотрудник;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьДополнительноеСвойство(ЗначениеДопРеквизита, Свойство, ИспользоватьИерархию, СтроковоеЗначение) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ЗначениеДопРеквизита) Тогда
		
		ИмяСправочника = ?(ИспользоватьИерархию, "ЗначенияСвойствОбъектовИерархия", "ЗначенияСвойствОбъектов");
		
		СпрОбъект = Справочники[ИмяСправочника].СоздатьЭлемент();
		СпрОбъект.Владелец = Свойство;
		СпрОбъект.Наименование = СтроковоеЗначение;
		
		ОбновлениеИнформационнойБазы.ЗаписатьОбъект(СпрОбъект, Истина, Истина);
		
		ЗначениеДопРеквизита = СпрОбъект.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СоздатьЗаполнитьТаблицуВозможныхТиповЗначенийДопРеквизита(Свойство, ТаблицаТипов) Экспорт
	
	ТаблицаТипов = Новый ТаблицаЗначений;
	ТаблицаТипов.Колонки.Добавить("Тип");
	ТаблицаТипов.Колонки.Добавить("Приоритет");
	
	МассивТиповЗначений = Свойство.ТипЗначения.Типы();
	Для каждого ЭлементМассива Из МассивТиповЗначений Цикл
		
		НоваяСтрока = ТаблицаТипов.Добавить();
		НоваяСтрока.Тип = ЭлементМассива;
		Если ЭлементМассива = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") 
			ИЛИ ЭлементМассива = Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия") Тогда
			
			НоваяСтрока.Приоритет = 1;
			
		ИначеЕсли ЭлементМассива = Тип("Булево")
			ИЛИ ЭлементМассива = Тип("Дата")
			ИЛИ ЭлементМассива = Тип("Число") Тогда
			
			НоваяСтрока.Приоритет = 3;
			
		ИначеЕсли ЭлементМассива = Тип("Строка") Тогда
			
			НоваяСтрока.Приоритет = 4;
			
		Иначе
			
			НоваяСтрока.Приоритет = 2;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ТаблицаТипов.Сортировать("Приоритет");
	
КонецПроцедуры

Процедура СопоставитьДополнительныеРеквизиты(СтрокаТаблицыФормы, ВыбранныеДополнительныеРеквизиты) Экспорт
	Перем ТаблицаТипов;
	
	ПостФикс = "_ВходящиеДанные";
	Для каждого ЭлементСоответствия Из ВыбранныеДополнительныеРеквизиты Цикл
		
		СтроковоеЗначение = СтрокаТаблицыФормы[ЭлементСоответствия.Значение + ПостФикс];
		Если ПустаяСтрока(СтроковоеЗначение) Тогда
			
			Продолжить;
			
		КонецЕсли;
		
		Свойство = ЭлементСоответствия.Ключ;
		
		СоздатьЗаполнитьТаблицуВозможныхТиповЗначенийДопРеквизита(Свойство, ТаблицаТипов);
		
		ЗначениеДопРеквизита = Неопределено;
		Для каждого СтрокаТаблицы Из ТаблицаТипов Цикл
			
			Если СтрокаТаблицы.Тип = Тип("СправочникСсылка.Валюты") Тогда
				
				СопоставитьВалюту(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.ЗначенияСвойствОбъектов") Тогда
				
				СопоставитьДополнительноеСвойство(ЗначениеДопРеквизита, Свойство, Ложь, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.ЗначенияСвойствОбъектовИерархия") Тогда
				
				СопоставитьДополнительноеСвойство(ЗначениеДопРеквизита, Свойство, Истина, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.Контрагенты") Тогда
				
				СопоставитьКонтрагента(ЗначениеДопРеквизита, СтроковоеЗначение, СтроковоеЗначение, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.Пользователи") Тогда
				
				СопоставитьПользователя(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.Проекты") Тогда
				
				СопоставитьПроект(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.Сотрудники") Тогда
				
				СопоставитьСотрудника(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("СправочникСсылка.ФизическиеЛица") Тогда
				
				СопоставитьФизическоеЛицо(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("Булево") Тогда
				
				ПреобразоватьСтрокуВБулево(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("Строка") Тогда
				
				СкопироватьСтрокуВЗначениеСтроковогоТипа(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("Дата") Тогда
				
				ПреобразоватьСтрокуВДату(ЗначениеДопРеквизита, СтроковоеЗначение);
				
			ИначеЕсли СтрокаТаблицы.Тип = Тип("Число") Тогда
				
				ПреобразоватьСтрокуВЧисло(ЗначениеДопРеквизита, СтроковоеЗначение);
				Если ЗначениеДопРеквизита = 0 Тогда // 0 игнорируем
					
					ЗначениеДопРеквизита = Неопределено;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеДопРеквизита <> Неопределено Тогда
				
				СтрокаТаблицыФормы[ЭлементСоответствия.Значение] = ЗначениеДопРеквизита;
				Прервать;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

Функция НайтиКонтрагентаПоИНН_КПП(ИНН, КПП) Экспорт
	Перем Контрагент;
	
	Запрос = Новый Запрос("ВЫБРАТЬ Справочник.Контрагенты.Ссылка ГДЕ НЕ ЭтоГруппа И ИНН = &ИНН");
	Запрос.УстановитьПараметр("ИНН", ИНН);
	
	Если НЕ ПустаяСтрока(КПП) Тогда
		
		Запрос.Текст = Запрос.Текст + " И КПП = &КПП";
		Запрос.УстановитьПараметр("КПП", КПП);
		
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Контрагент = Выборка.Ссылка;
		
	КонецЕсли;
	
	Возврат Контрагент;
	
КонецФункции

Процедура СопоставитьСтрануПроисхождения(СтранаПроисхождения, СтранаПроисхождения_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	Перем СправочникСсылка;
	
	// Справочник страны мира
	Если НЕ ПустаяСтрока(СтранаПроисхождения_ВходящиеДанные) Тогда
		
		СправочникСсылка = Справочники.СтраныМира.НайтиПоНаименованию(СтранаПроисхождения_ВходящиеДанные, Ложь);
		Если НЕ ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникСсылка = Справочники.СтраныМира.НайтиПоРеквизиту("КодАльфа3", СтранаПроисхождения_ВходящиеДанные);
			Если НЕ ЗначениеЗаполнено(СправочникСсылка) Тогда
				
				СправочникСсылка = Справочники.СтраныМира.НайтиПоКоду(СтранаПроисхождения_ВходящиеДанные, Истина);
				
			КонецЕсли;
			
		КонецЕсли;
		
		// Справочник классификатор стран мира
		Если НЕ ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			УстановитьПривилегированныйРежим(Истина);
			
			ДанныеКласификатора = Справочники.СтраныМира.ДанныеКлассификатораСтранМираПоКоду(СтранаПроисхождения_ВходящиеДанные);
			Если ДанныеКласификатора = Неопределено Тогда
				
				ДанныеКласификатора = Справочники.СтраныМира.ДанныеКлассификатораСтранМираПоНаименованию(ВРЕГ(СтранаПроисхождения_ВходящиеДанные));
				
			КонецЕсли;
			
			Если ДанныеКласификатора <> Неопределено Тогда
				
				СправочникСсылка = Справочники.СтраныМира.СсылкаПоДаннымКлассификатора(ДанныеКласификатора);
				
			КонецЕсли;
			
			УстановитьПривилегированныйРежим(Ложь);
			
		КонецЕсли;
		
	КонецЕсли;
	
	СтранаПроисхождения = ?(ЗначениеЗаполнено(СправочникСсылка), СправочникСсылка, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьНомерГТД(НомерГТД, НомерГТД_ВходящиеДанные) Экспорт
	
	Если НЕ ПустаяСтрока(НомерГТД_ВходящиеДанные) Тогда
		
		НомерГТД = Справочники.НомераГТД.НайтиПоКоду(НомерГТД_ВходящиеДанные);
		
	КонецЕсли;
	
КонецПроцедуры

//:::Спецификация

Процедура СопоставитьТипСтроки(ТипСтроки, ТипСтроки_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьПеречисление("ТипыСтрокСоставаСпецификации", ТипСтроки, ТипСтроки_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСпецификацию(Спецификация, Спецификация_ВходящиеДанные, Номенклатура) Экспорт
	
	Если ЗначениеЗаполнено(Номенклатура) 
		И НЕ ПустаяСтрока(Спецификация_ВходящиеДанные) Тогда
		
		СправочникСсылка = Справочники.Спецификации.НайтиПоНаименованию(Спецификация_ВходящиеДанные, Ложь, , Номенклатура);
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			Спецификация = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

//:::Номенклатура

Процедура СопоставитьТипНоменклатуры(ТипНоменклатуры, ТипНоменклатуры_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьПеречисление("ТипыНоменклатуры", ТипНоменклатуры, ТипНоменклатуры_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьМетодОценки(МетодОценки, МетодОценки_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьПеречисление("МетодОценкиЗапасов", МетодОценки, МетодОценки_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьНаправлениеДеятельности(НаправлениеДеятельности, НаправлениеДеятельности_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	ИспользованиеВключено = ПолучитьФункциональнуюОпцию("УчетПоНесколькимНаправлениямДеятельности");
	Если НЕ ИспользованиеВключено Тогда
		
		// Нельзя заполнять значением по умолчанию, например, потому, что оно может прийти из пользовательских настроек.
		//
		НаправлениеДеятельности = Справочники.НаправленияДеятельности.ОсновноеНаправление;
		
	Иначе
		
		СправочникПоНаименованию("НаправленияДеятельности", НаправлениеДеятельности, НаправлениеДеятельности_ВходящиеДанные, ЗначениеПоУмолчанию);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьКатегориюНоменклатуры(КатегорияНоменклатуры, КатегорияНоменклатуры_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СправочникПоНаименованию("КатегорииНоменклатуры", КатегорияНоменклатуры, КатегорияНоменклатуры_ВходящиеДанные, ЗначениеПоУмолчанию)
	
КонецПроцедуры

Процедура СопоставитьПродукциюПоЕГАИС(КодПродукцииЕГАИС, КодПродукцииЕГАИС_ВходящиеДанные) Экспорт
	
	Если ПустаяСтрока(КодПродукцииЕГАИС_ВходящиеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос("Выбрать Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.Ссылка КАК ПродукцияЕГАИС Где Справочник.КлассификаторАлкогольнойПродукцииЕГАИС.Код = &КодПродукцииЕГАИС");
	Запрос.УстановитьПараметр("КодПродукцииЕГАИС", КодПродукцииЕГАИС_ВходящиеДанные);
	РезультатЗапроса = Запрос.Выполнить();
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Если Выборка.Следующий() Тогда
			
			КодПродукцииЕГАИС = Выборка.ПродукцияЕГАИС;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьПоставщика(Поставщик, Поставщик_ВходящиеДанные) Экспорт
	
	Если ПустаяСтрока(Поставщик_ВходящиеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	//:::Поиск ИНН и КПП
	Разделители = Новый Массив;
	Разделители.Добавить("/");
	Разделители.Добавить("\");
	Разделители.Добавить("-");
	Разделители.Добавить("|");
	
	ИНН = "";
	КПП = "";
	
	Для каждого ЗначениеРазделителя Из Разделители Цикл
		
		ПозицияРазделителя = СтрНайти(Поставщик_ВходящиеДанные, ЗначениеРазделителя);
		Если ПозицияРазделителя = 0 Тогда 
			
			Продолжить;
			
		КонецЕсли;
		
		ИНН = Лев(Поставщик_ВходящиеДанные, ПозицияРазделителя - 1);
		КПП = Сред(Поставщик_ВходящиеДанные, ПозицияРазделителя + 1);
		
		Запрос = Новый Запрос("ВЫБРАТЬ Справочник.Контрагенты.Ссылка ГДЕ НЕ ЭтоГруппа И ИНН = &ИНН И КПП = &КПП");
		Запрос.УстановитьПараметр("ИНН", ИНН);
		Запрос.УстановитьПараметр("КПП", КПП);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			Поставщик = Выборка.Ссылка;
			Возврат;
			
		КонецЕсли;
		
	КонецЦикла;
	
	//:::Поиск ИНН
	Запрос = Новый Запрос("ВЫБРАТЬ Справочник.Контрагенты.Ссылка ГДЕ НЕ ЭтоГруппа И ИНН = &ИНН");
	Запрос.УстановитьПараметр("ИНН", Поставщик_ВходящиеДанные);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Поставщик = Выборка.Ссылка;
		Возврат;
		
	КонецЕсли;
	
	//:::Поиск Наименование
	СправочникСсылка = Справочники.Контрагенты.НайтиПоНаименованию(Поставщик_ВходящиеДанные, Ложь);
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		
		Поставщик = СправочникСсылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьСтруктурнуюЕдиницу(Склад, Склад_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СправочникПоНаименованию("СтруктурныеЕдиницы", Склад, Склад_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСпособПополнения(СпособПополнения, СпособПополнения_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьПеречисление("СпособыПополненияЗапасов", СпособПополнения, СпособПополнения_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСтавкуНДС(СтавкаНДС, СтавкаНДС_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СправочникПоНаименованию("СтавкиНДС", СтавкаНДС, СтавкаНДС_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСчетУчетаЗапасов(СчетУчетаЗапасов, СчетУчетаЗапасов_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьСчетУчета(СчетУчетаЗапасов, СчетУчетаЗапасов_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСчетУчетаЗатрат(СчетУчетаЗатрат, СчетУчетаЗатрат_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьСчетУчета(СчетУчетаЗатрат, СчетУчетаЗатрат_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьЯчейку(Ячейка, Ячейка_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СправочникПоНаименованию("Ячейки", Ячейка, Ячейка_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьЦеновуюГруппу(ЦеноваяГруппа, ЦеноваяГруппа_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СправочникПоНаименованию("ЦеновыеГруппы", ЦеноваяГруппа, ЦеноваяГруппа_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура ДобавитьЗаписьСопоставленияПоИНН_КПП(ОрганизацияЕГАИС, Контрагент)
	
	// Пробуем сами сопоставить
	Контрагент = НайтиКонтрагентаПоИНН_КПП(ОрганизацияЕГАИС.ИНН, ОрганизацияЕГАИС.КПП);
	Если ЗначениеЗаполнено(Контрагент) Тогда
		
		СоответствиеОрганизацийЕГАИС = РегистрыСведений.СоответствиеОрганизацийЕГАИС.СоздатьМенеджерЗаписи();
		СоответствиеОрганизацийЕГАИС.ОрганизацияЕГАИС = ОрганизацияЕГАИС;
		СоответствиеОрганизацийЕГАИС.Контрагент = Контрагент;
		СоответствиеОрганизацийЕГАИС.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьКонтрагентовПоСоответствиюЕГАИС(Производитель, Импортер) Экспорт
	
	СопоставленныеДанные = Новый Структура("Производитель, Импортер");
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Производитель", Производитель);
	Запрос.УстановитьПараметр("Импортер", Импортер);
	
	Запрос.Текст = 
	"Выбрать РегистрСведений.СоответствиеОрганизацийЕГАИС.* Где ОрганизацияЕГАИС = &Производитель;
	|Выбрать РегистрСведений.СоответствиеОрганизацийЕГАИС.* Где ОрганизацияЕГАИС = &Импортер";
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	//:::Производитель
	ВыборкаПроизводитель = РезультатЗапроса[0].Выбрать();
	Если ВыборкаПроизводитель.Следующий() Тогда
		
		СопоставленныеДанные.Производитель = ВыборкаПроизводитель.Контрагент;
		
	ИначеЕсли ЗначениеЗаполнено(Производитель) Тогда
		
		ДобавитьЗаписьСопоставленияПоИНН_КПП(Производитель, СопоставленныеДанные.Производитель);
		
	КонецЕсли;
	
	ВыборкаИмпортер = РезультатЗапроса[1].Выбрать();
	Если ВыборкаИмпортер.Следующий() Тогда
		
		СопоставленныеДанные.Импортер = ВыборкаПроизводитель.Контрагент;
		
	ИначеЕсли ЗначениеЗаполнено(Импортер) Тогда
		
		ДобавитьЗаписьСопоставленияПоИНН_КПП(Импортер, СопоставленныеДанные.Импортер);
		
	КонецЕсли;
		
	Возврат СопоставленныеДанные;
	
КонецФункции

Процедура СопоставитьСерийныйНомер(НоменклатураСсылка, СерийныйНомер, СерийныйНомер_ВходящиеДанные) Экспорт
	
	СерийныйНомер = Справочники.СерийныеНомера.НайтиПоНаименованию(СерийныйНомер_ВходящиеДанные, Истина, , НоменклатураСсылка);
	
КонецПроцедуры

//:::Заказ поставщику

Процедура СопоставитьЗаказ(Заказ, Заказ_ВходящиеДанные) Экспорт
	
	Если ПустаяСтрока(Заказ_ВходящиеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	МассивТеговПоставщиков = Новый Массив;
	МассивТеговПоставщиков.Добавить("Заказ поставщику");
	МассивТеговПоставщиков.Добавить("ЗаказПоставщику");
	МассивТеговПоставщиков.Добавить("Поставщику");
	МассивТеговПоставщиков.Добавить("Поставщик");
	МассивТеговПоставщиков.Добавить("Пост");
	
	НомерДляПоиска	= Заказ_ВходящиеДанные;
	ВидДокумента	= "ЗаказПокупателя";
	Для каждого ТегИзМассива Из МассивТеговПоставщиков Цикл
		
		Если СтрНайти(Заказ_ВходящиеДанные, ТегИзМассива) > 0 Тогда
			
			ВидДокумента = "ЗаказПоставщику";
			НомерДляПоиска = СокрЛП(СтрЗаменить(НомерДляПоиска, "", ТегИзМассива));
			
		КонецЕсли;
		
	КонецЦикла;
	
	Запрос = Новый Запрос("Выбрать Документ.ЗаказПокупателя.Ссылка Где Номер ПОДОБНО &Номер УПОРЯДОЧИТЬ ПО Дата Убыв");
	Запрос.УстановитьПараметр("Номер", "%" + НомерДляПоиска + "%");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Заказ = Выборка.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

//:::Контрагент

Процедура СопоставитьКонтрагента(Контрагент, ИНН_КПП, КонтрагентНаименование, РасчетныйСчет) Экспорт
	
	//:::Поиск ИНН и КПП
	Если НЕ ПустаяСтрока(ИНН_КПП) Тогда
		
		Разделители = Новый Массив;
		Разделители.Добавить("/");
		Разделители.Добавить("\");
		Разделители.Добавить("-");
		Разделители.Добавить("|");
		
		ИНН = "";
		КПП = "";
		
		Для каждого ЗначениеРазделителя Из Разделители Цикл
			
			ПозицияРазделителя = СтрНайти(ИНН_КПП, ЗначениеРазделителя);
			Если ПозицияРазделителя = 0 Тогда 
				
				Продолжить;
				
			КонецЕсли;
			
			ИНН = Лев(ИНН_КПП, ПозицияРазделителя - 1);
			КПП = Сред(ИНН_КПП, ПозицияРазделителя + 1);
			
		КонецЦикла;
	
		// Если не было разделителя, то предполагаю, что это все ИНН
		Если ИНН = "" И КПП = "" Тогда
			
			ИНН = ИНН_КПП;
			
		КонецЕсли;
		
		Контрагент = НайтиКонтрагентаПоИНН_КПП(ИНН, КПП);
		Если ЗначениеЗаполнено(Контрагент) Тогда
			
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	//:::Поиск Наименование
	Если НЕ ПустаяСтрока(КонтрагентНаименование) Тогда
		
		СправочникСсылка = Справочники.Контрагенты.НайтиПоНаименованию(КонтрагентНаименование, Ложь);
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			Контрагент = СправочникСсылка;
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	//:::Номер расчетного счета
	Если НЕ ПустаяСтрока(РасчетныйСчет) Тогда
		
		СправочникСсылка = Справочники.БанковскиеСчета.НайтиПоРеквизиту("НомерСчета", РасчетныйСчет);
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			Контрагент = СправочникСсылка.Владелец;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьФизическоеЛицо(ФизическоеЛицо, ФизическоеЛицо_ВходящиеДанные) Экспорт
	
	СправочникПоНаименованию("ФизическиеЛица", ФизическоеЛицо, ФизическоеЛицо_ВходящиеДанные, Неопределено);
	
КонецПроцедуры

Процедура СопоставитьГруппуДоступа(ГруппаДоступа, ГруппаДоступа_ВходящиеДанные) Экспорт
	
	СправочникПоНаименованию("ГруппыДоступаКонтрагентов", ГруппаДоступа, ГруппаДоступа_ВходящиеДанные);
	
КонецПроцедуры

Процедура СопоставитьСчетУчетаРасчетовСПокупателем(СчетУчетаРасчетовСПокупателем, СчетУчетаРасчетовСПокупателем_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьСчетУчета(СчетУчетаРасчетовСПокупателем, СчетУчетаРасчетовСПокупателем_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСчетУчетаАвансовПокупателя(СчетУчетаАвансовПокупателя, СчетУчетаАвансовПокупателя_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьСчетУчета(СчетУчетаАвансовПокупателя, СчетУчетаАвансовПокупателя_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСчетУчетаРасчетовСПоставщиком(СчетУчетаРасчетовСПоставщиком, СчетУчетаРасчетовСПоставщиком_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьСчетУчета(СчетУчетаРасчетовСПоставщиком, СчетУчетаРасчетовСПоставщиком_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьСчетУчетаАвансовПоставщику(СчетУчетаАвансовПоставщику, СчетУчетаАвансовПоставщику_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	СопоставитьСчетУчета(СчетУчетаАвансовПоставщику, СчетУчетаАвансовПоставщику_ВходящиеДанные, ЗначениеПоУмолчанию);
	
КонецПроцедуры

Процедура СопоставитьВидыКонтрагентов(ВидыКонтрагентов, ВидыКонтрагентов_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	Если ПустаяСтрока(ВидыКонтрагентов_ВходящиеДанные) Тогда
		
		ВидыКонтрагентов = ЗначениеПоУмолчанию;
		
	Иначе
		
		Если ВРЕГ(ВидыКонтрагентов_ВходящиеДанные) = ВРЕГ("Юр. лицо")
			ИЛИ ВидыКонтрагентов_ВходящиеДанные = "1" Тогда
			
			ВидыКонтрагентов = Перечисления.ВидыКонтрагентов.ЮридическоеЛицо;
			
		ИначеЕсли ВРЕГ(ВидыКонтрагентов_ВходящиеДанные) = ВРЕГ("ИП")
			ИЛИ ВидыКонтрагентов_ВходящиеДанные = "2" Тогда
			
			ВидыКонтрагентов = Перечисления.ВидыКонтрагентов.ИндивидуальныйПредприниматель;
			
		ИначеЕсли ВРЕГ(ВидыКонтрагентов_ВходящиеДанные) = ВРЕГ("Физ. лицо")
			ИЛИ ВидыКонтрагентов_ВходящиеДанные = "3" Тогда
			
			ВидыКонтрагентов = Перечисления.ВидыКонтрагентов.ФизическоеЛицо;
			
		Иначе
			
			ВидыКонтрагентов = ЗначениеПоУмолчанию;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьПолФизическогоЛица(Пол, Пол_ВходящиеДанные) Экспорт
	
	Если ВРЕГ(Пол_ВходящиеДанные) = ВРЕГ("Муж.")
		ИЛИ ВРЕГ(Пол_ВходящиеДанные) = ВРЕГ("Мужской")
		ИЛИ ВРЕГ(Пол_ВходящиеДанные) = ВРЕГ("+") Тогда
		
		Пол = Перечисления.ПолФизическогоЛица.Мужской;
		
	ИначеЕсли ВРЕГ(Пол_ВходящиеДанные) = ВРЕГ("Жен.")
		ИЛИ ВРЕГ(Пол_ВходящиеДанные) = ВРЕГ("Женский")
		ИЛИ ВРЕГ(Пол_ВходящиеДанные) = ВРЕГ("-") Тогда
		
		Пол = Перечисления.ПолФизическогоЛица.Женский;
		
	КонецЕсли;
	
КонецПроцедуры

//:::Цены номенклатуры

Процедура СопоставитьВидЦен(ВидЦен, ВидЦен_ВходящиеДанные, ЗначениеПоУмолчанию) Экспорт
	
	Если НЕ ПустаяСтрока(ВидЦен_ВходящиеДанные) Тогда
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ВидЦен_ВходящиеДанные", СокрЛП(ВидЦен_ВходящиеДанные));
		Запрос.Текст = 
		"Выбрать СпрВидыЦен.Ссылка Из Справочник.ВидыЦен КАК СпрВидыЦен
		|	Где ТипВидаЦен = Значение(Перечисление.ТипыВидовЦен.Статический) И Наименование Подобно &ВидЦен_ВходящиеДанные";
		Выборка = Запрос.Выполнить().Выбрать();
		
		ВидЦен = ?(Выборка.Следующий(), Выборка.Ссылка, Неопределено);
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ВидЦен) Тогда
		
		ВидЦен = ЗначениеПоУмолчанию;
		
	КонецЕсли;
	
КонецПроцедуры

//:::Ввод начальных остатков

Процедура СопоставитьДоговор(Контрагент, Договор, Договор_ВходящиеДанные) Экспорт
	
	Если ЗначениеЗаполнено(Контрагент) 
		И ЗначениеЗаполнено(Договор_ВходящиеДанные) Тогда
		
		СправочникСсылка = Неопределено;
		СправочникСсылка = Справочники.ДоговорыКонтрагентов.НайтиПоНаименованию(Договор_ВходящиеДанные, Ложь, , Контрагент);
		Если НЕ ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникСсылка = Справочники.ДоговорыКонтрагентов.НайтиПоРеквизиту("НомерДоговора", Договор_ВходящиеДанные, , Контрагент);
			
		КонецЕсли;
		
		Если ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			Договор = СправочникСсылка;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьЗаказПоНомеруДате(Заказ, НазваниеТипаДокумента, Контрагент, Номер_ВходящиеДанные, Дата_ВходящиеДанные) Экспорт
	
	Если ПустаяСтрока(Номер_ВходящиеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если НазваниеТипаДокумента <> "ЗаказПоставщику" Тогда
		
		НазваниеТипаДокумента = "ЗаказПокупателя"
		
	КонецЕсли;
	
	ИмяТаблицы = "Документ." + НазваниеТипаДокумента;
	
	Запрос = Новый Запрос("Выбрать Заказ.Ссылка ИЗ &ИмяТаблицы КАК Заказ Где Заказ.Контрагент = &Контрагент И Заказ.Номер ПОДОБНО &Номер");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицы", ИмяТаблицы);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Номер", "%" + Номер_ВходящиеДанные + "%");
	
	Если НЕ ПустаяСтрока(Дата_ВходящиеДанные) Тогда
		
		ДатаИзСтроки = Дата('00010101');
		ПреобразоватьСтрокуВДату(ДатаИзСтроки, Дата_ВходящиеДанные);
		
		Если ЗначениеЗаполнено(ДатаИзСтроки) Тогда
			
			Запрос.Текст = Запрос.Текст + " И Заказ.Дата Между &ДатаНачала И &ДатаОкончания";
			Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаИзСтроки));
			Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаИзСтроки));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + " УПОРЯДОЧИТЬ ПО Заказ.Дата Убыв";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Заказ = Выборка.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьРасчетныйДокументПоНомеруДате(Документ, НазваниеТипаДокумента, Контрагент, Номер_ВходящиеДанные, Дата_ВходящиеДанные) Экспорт
	
	Если ПустаяСтрока(НазваниеТипаДокумента) 
		ИЛИ ПустаяСтрока(Номер_ВходящиеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СоответствиеИменДокументов = Новый Соответствие;
	
	СоответствиеИменДокументов.Вставить("ЗаказПокупателя", 		"ЗаказПокупателя");
	СоответствиеИменДокументов.Вставить("Заказы покупателей",	"ЗаказПокупателя");
	СоответствиеИменДокументов.Вставить("Заказ покупателя",		"ЗаказПокупателя");
	
	СоответствиеИменДокументов.Вставить("Взаимозачет",			"Взаимозачет");
	СоответствиеИменДокументов.Вставить("Корректировка долга",	"Взаимозачет");
	СоответствиеИменДокументов.Вставить("Корректировки долга",	"Взаимозачет");
	
	СоответствиеИменДокументов.Вставить("ОтчетКомиссионера",	"ОтчетКомиссионера");
	СоответствиеИменДокументов.Вставить("Отчет комиссионера",	"ОтчетКомиссионера");
	СоответствиеИменДокументов.Вставить("Отчеты комиссионеров",	"ОтчетКомиссионера");
	
	СоответствиеИменДокументов.Вставить("ОтчетОПереработке",	"ОтчетОПереработке");
	СоответствиеИменДокументов.Вставить("Отчет о переработке",	"ОтчетОПереработке");
	СоответствиеИменДокументов.Вставить("Отчеты о переработке",	"ОтчетОПереработке");
	
	СоответствиеИменДокументов.Вставить("ПоступлениеВКассу",	"ПоступлениеВКассу");
	СоответствиеИменДокументов.Вставить("Поступление в кассу",	"ПоступлениеВКассу");
	СоответствиеИменДокументов.Вставить("Поступления в кассу",	"ПоступлениеВКассу");
	СоответствиеИменДокументов.Вставить("ПКО",	"ПоступлениеВКассу");
	
	СоответствиеИменДокументов.Вставить("ПоступлениеНаСчет",	"ПоступлениеНаСчет");
	СоответствиеИменДокументов.Вставить("Поступление на счет",	"ПоступлениеНаСчет");
	СоответствиеИменДокументов.Вставить("Поступления на счет",	"ПоступлениеНаСчет");
	
	СоответствиеИменДокументов.Вставить("ПередачаВА",			"ПередачаВА");
	СоответствиеИменДокументов.Вставить("Продажа имущества",	"ПередачаВА");
	СоответствиеИменДокументов.Вставить("Продажи имущества",	"ПередачаВА");
	
	СоответствиеИменДокументов.Вставить("РасходнаяНакладная",	"РасходнаяНакладная");
	СоответствиеИменДокументов.Вставить("РН",					"РасходнаяНакладная");
	СоответствиеИменДокументов.Вставить("Расходная накладная",	"РасходнаяНакладная");
	СоответствиеИменДокументов.Вставить("Расходные накладные",	"РасходнаяНакладная");
	
	СоответствиеИменДокументов.Вставить("АвансовыйОтчет", "АвансовыйОтчет");
	СоответствиеИменДокументов.Вставить("Авансовый отчет", "АвансовыйОтчет");
	СоответствиеИменДокументов.Вставить("Авансовые отчеты", "АвансовыйОтчет");
	
	СоответствиеИменДокументов.Вставить("ДополнительныеРасходы", "ДополнительныеРасходы");
	СоответствиеИменДокументов.Вставить("Дополнительные расходы", "ДополнительныеРасходы");
	
	СоответствиеИменДокументов.Вставить("ОтчетКомитенту", "ОтчетКомитенту");
	СоответствиеИменДокументов.Вставить("Отчет комитенту", "ОтчетКомитенту");
	СоответствиеИменДокументов.Вставить("Отчеты комитентам", "ОтчетКомитенту");
	
	СоответствиеИменДокументов.Вставить("ОтчетПереработчика", "ОтчетПереработчика");
	СоответствиеИменДокументов.Вставить("Отчет переработчика", "ОтчетПереработчика");
	СоответствиеИменДокументов.Вставить("Отчеты переработчиков", "ОтчетПереработчика");
	
	СоответствиеИменДокументов.Вставить("ПриходнаяНакладная", "ПриходнаяНакладная");
	СоответствиеИменДокументов.Вставить("Приходная накладная", "ПриходнаяНакладная");
	СоответствиеИменДокументов.Вставить("Приходные накладные", "ПриходнаяНакладная");
	СоответствиеИменДокументов.Вставить("ПН", "ПриходнаяНакладная");
	
	СоответствиеИменДокументов.Вставить("РасходИзКассы", "РасходИзКассы");
	СоответствиеИменДокументов.Вставить("Расход из кассы", "РасходИзКассы");
	СоответствиеИменДокументов.Вставить("РКО", "РасходИзКассы");
	
	СоответствиеИменДокументов.Вставить("РасходСоСчета", "РасходСоСчета");
	СоответствиеИменДокументов.Вставить("Расход со счета", "РасходСоСчета");
	
	ТипДокумента = СоответствиеИменДокументов.Получить(НазваниеТипаДокумента);
	Если ТипДокумента = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ИмяТаблицы = "Документ." + ТипДокумента;
	
	Запрос = Новый Запрос("Выбрать ДокументРасчетов.Ссылка ИЗ &ИмяТаблицы КАК ДокументРасчетов Где ДокументРасчетов.Контрагент = &Контрагент И ДокументРасчетов.Номер ПОДОБНО &Номер");
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&ИмяТаблицы", ИмяТаблицы);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Номер", "%" + Номер_ВходящиеДанные + "%");
	
	Если НЕ ПустаяСтрока(Дата_ВходящиеДанные) Тогда
		
		ДатаИзСтроки = Дата('00010101');
		ПреобразоватьСтрокуВДату(ДатаИзСтроки, Дата_ВходящиеДанные);
		
		Если ЗначениеЗаполнено(ДатаИзСтроки) Тогда
			
			Запрос.Текст = Запрос.Текст + " И ДокументРасчетов.Дата Между &ДатаНачала И &ДатаОкончания";
			Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаИзСтроки));
			Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаИзСтроки));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + " УПОРЯДОЧИТЬ ПО ДокументРасчетов.Дата Убыв";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Документ = Выборка.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьСчетПоНомеруДате(Счет, Контрагент, Номер_ВходящиеДанные, Дата_ВходящиеДанные) Экспорт
	
	Если ПустаяСтрока(Номер_ВходящиеДанные) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Запрос = Новый Запрос("Выбрать Счет.Ссылка ИЗ Документ.СчетНаОплату КАК Счет Где Счет.Контрагент = &Контрагент И Счет.Номер ПОДОБНО &Номер");
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("Номер", "%" + Номер_ВходящиеДанные + "%");
	
	Если НЕ ПустаяСтрока(Дата_ВходящиеДанные) Тогда
		
		ДатаИзСтроки = Дата('00010101');
		ПреобразоватьСтрокуВДату(ДатаИзСтроки, Дата_ВходящиеДанные);
		
		Если ЗначениеЗаполнено(ДатаИзСтроки) Тогда
			
			Запрос.Текст = Запрос.Текст + " И Счет.Дата Между &ДатаНачала И &ДатаОкончания";
			Запрос.УстановитьПараметр("ДатаНачала", НачалоДня(ДатаИзСтроки));
			Запрос.УстановитьПараметр("ДатаОкончания", КонецДня(ДатаИзСтроки));
			
		КонецЕсли;
		
	КонецЕсли;
	
	Запрос.Текст = Запрос.Текст + " УПОРЯДОЧИТЬ ПО Счет.Дата Убыв";
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		
		Счет = Выборка.Ссылка;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура СопоставитьВнеоборотныйАктив(ВнеоборотныйАктив, ВнеоборотныйАктив_ВходящиеДанные) Экспорт
	
	Если НЕ ПустаяСтрока(ВнеоборотныйАктив_ВходящиеДанные) Тогда
		
		СправочникСсылка = Справочники.ВнеоборотныеАктивы.НайтиПоНаименованию(ВнеоборотныйАктив_ВходящиеДанные, Истина);
		
		Если НЕ ЗначениеЗаполнено(СправочникСсылка) Тогда
			
			СправочникСсылка = Справочники.ВнеоборотныеАктивы.НайтиПоКоду(ВнеоборотныйАктив_ВходящиеДанные);
			
		КонецЕсли; 
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СправочникСсылка) Тогда
		
		ВнеоборотныйАктив = СправочникСсылка;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
