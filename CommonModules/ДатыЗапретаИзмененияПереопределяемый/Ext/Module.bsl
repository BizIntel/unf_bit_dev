////////////////////////////////////////////////////////////////////////////////
// Подсистема "Даты запрета изменения".
// 
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Позволяет изменить работу интерфейса при встраивании.
//
// Параметры:
//  НастройкиРаботыИнтерфейса - Структура - содержит свойство:
//   * ИспользоватьВнешнихПользователей - Булево - (начальное значение Ложь),
//     если установить Истина, тогда даты запрета можно будет настраивать для внешних пользователей.
//
Процедура НастройкаИнтерфейса(НастройкиРаботыИнтерфейса) Экспорт
	
	НастройкиРаботыИнтерфейса.ИспользоватьВнешнихПользователей = Ложь;
	
КонецПроцедуры

// Содержит описание таблиц и полей объектов для проверки запретов изменения данных.
//   Вызывается из процедуры ИзменениеЗапрещено общего модуля ДатыЗапретаИзменения,
//   используемой в подписке на событие ПередЗаписью объекта для проверки наличия
//   запретов и отказа от изменений запрещенного объекта.
//
// Параметры:
//  ИсточникиДанных - ТаблицаЗначений - с колонками:
//   * Таблица     - Строка - полное имя объекта метаданных,
//                   например, Метаданные.Документы.ПриходнаяНакладная.ПолноеИмя().
//   * ПолеДаты    - Строка - имя реквизита объекта или табличной части,
//                   например "Дата", "Товары.ДатаОтгрузки".
//   * Раздел      - Строка - имя предопределенного элемента
//                   "ПланВидовХарактеристикСсылка.РазделыДатЗапрета".
//   * ПолеОбъекта - Строка - имя реквизита объекта или реквизита табличной части,
//                   например "Организация", "Товары.Склад".
//
//  Для добавления строки имеется процедура ДобавитьСтроку в общем модуле ДатыЗапретаИзменения.
//
Процедура ЗаполнитьИсточникиДанныхДляПроверкиЗапретаИзменения(ИсточникиДанных) Экспорт
	
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АвансовыйОтчет", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АктВыполненныхРабот",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.АмортизацияВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Бюджет", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВводНачальныхОстатков",	 		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Взаимозачет", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ВыработкаВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Доверенность", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ДополнительныеРасходы", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаданиеНаРаботу", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказНаПроизводство", 			"Дата", "УправленческийУчет", "Организация");
	// "Документ.ЗаказПокупателя" - вынесен в отдельный раздел
	// "Документ.ЗаказПоставщику" - вынесен в отдельный раздел
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗакрытиеМесяца", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ИзменениеПараметровВА", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ИнвентаризацияЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КадровоеПеремещение", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачислениеЗарплаты", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачислениеНалогов", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Операция", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОприходованиеЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомиссионера", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетКомитенту", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетОПереработке", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетОРозничныхПродажах",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОтчетПереработчика", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПередачаВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеДС", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеДСПлан", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПеремещениеПоЯчейкам", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПереоценкаВРозницеСуммовойУчет", "Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПланПродаж", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПлатежнаяВедомость", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПлатежноеПоручение", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеВКассу", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеДСПлан", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПоступлениеНаСчет", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриемНаРаботу", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПринятиеКУчетуВА", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриходнаяНакладная", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПриходныйОрдер", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ПрочиеРасходы", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РаспределениеЗатрат", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходДСПлан", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходИзКассы", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходнаяНакладная", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходныйОрдер", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходСоСчета", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РасходыПриИмпорте", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.РезервированиеЗапасов", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СборкаЗапасов", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СдельныйНаряд", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеВА", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СписаниеЗапасов", 				"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетНаОплату", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетНаОплатуПоставщика",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетФактура", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.СчетФактураПолученный", 			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Табель", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.Увольнение", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.УчетВремени", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЧекККМ", 						"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЧекККМВозврат", 					"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ОперацияПоПлатежнымКартам",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ДоговорКредитаИЗайма",			"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.НачисленияПоКредитамИЗаймам",	"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаПоступления",		"Дата", "УправленческийУчет", "Организация");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.КорректировкаРеализации",		"Дата", "УправленческийУчет", "Организация");
	
	// Дополнительные разделы для документов Заказ покупателя, Заказ поставщику
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказПокупателя", 				"Дата", "ЗаказыПокупателей", "СостояниеЗаказа");
	ДатыЗапретаИзменения.ДобавитьСтроку(ИсточникиДанных, "Документ.ЗаказПоставщику", 				"Дата", "ЗаказыПоставщиков", "СостояниеЗаказа");
	
КонецПроцедуры

// Позволяет переопределить выполнение проверок запретов по произвольному условию.
//
// Параметры:
//  Объект       - СправочникОбъект,
//                 ДокументОбъект,
//                 ПланВидовХарактеристикОбъект,
//                 ПланСчетовОбъект,
//                 ПланВидовРасчетаОбъект,
//                 БизнесПроцессОбъект,
//                 ЗадачаОбъект,
//                 ПланОбменаОбъект - объект данных (ПередЗаписью или ПриЧтенииНаСервере).
//               - РегистрСведенийНаборЗаписей,
//                 РегистрНакопленияНаборЗаписей,
//                 РегистрБухгалтерииНаборЗаписей,
//                 РегистрРасчетаНаборЗаписей - набор записей (ПередЗаписью или ПриЧтенииНаСервере).
//
//                 Для документов в свойство ДополнительныеСвойства вставляется свойство РежимЗаписи,
//                 если вызов делается в процессе записи документа.
//  
//  ПроверкаЗапретаИзменения    - Булево - когда Истина, тогда проверка изменения выполняется.
//                                Если установить Ложь, тогда проверка запрета изменения будет пропущена.
//
//  УзелПроверкиЗапретаЗагрузки - Неопределено - проверка запрета загрузки не выполняется.
//                              - ПланыОбменаСсылка - проверка запрета загрузки выполняется. Если
//                                установить Неопределено, проверка запрета загрузки будет пропущена.
//
//  ВерсияОбъекта               - Строка - начальное значение "". Проверяются обе версии объекта.
//                                Если установить "СтараяВерсия" или "НоваяВерсия", тогда будет
//                                выполнена проверка только старой или только новой версии объекта.
//
Процедура ПередПроверкойЗапретаИзменения(Объект,
                                         ПроверкаЗапретаИзменения,
                                         УзелПроверкиЗапретаЗагрузки,
                                         ВерсияОбъекта) Экспорт
	
	Если Объект.Метаданные().ПолноеИмя() = "Документ.ЗаказПокупателя" Тогда
		
		Если Объект.ВидОперации = Перечисления.ВидыОперацийЗаказПокупателя.ЗаказНаряд Тогда
			
			ПроверкаЗапретаИзменения = Ложь;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФукнции
////////////////////////////////////////////////////////////////////////////////
// УНФ

// Позволяет переопределить выполнение проверок запретов по произвольному условию.
//
Процедура ПроверитьДатуЗапретаРедактированияЗаказНарядов(Форма, ТекущийОбъект, ОписаниеОшибки = Ложь, УзелПроверкиЗапретаЗагрузки = Неопределено) Экспорт
	
	ДанныеДляПроверки	= ДатыЗапретаИзменения.ШаблонДанныхДляПроверки();
	
	НоваяСтрока			= ДанныеДляПроверки.Добавить();
	НоваяСтрока.Дата	= ТекущийОбъект.Финиш;
	Новаястрока.Раздел	= ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.УправленческийУчет;
	НоваяСтрока.Объект	= ТекущийОбъект.Организация;
	
	НайденныеЗапреты	= Неопределено;
	СтандартнаяОбработка= Истина;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("НоваяВерсия", Истина);
	ОписаниеДанных.Вставить("Данные", ТекущийОбъект.Ссылка);
	
	Форма.ТолькоПросмотр= ДатыЗапретаИзменения.НайденЗапретИзмененияДанных(ДанныеДляПроверки, ОписаниеДанных, ОписаниеОшибки, УзелПроверкиЗапретаЗагрузки);
	
КонецПроцедуры // ПроверитьЗаказНаряд()

// Начиная с версии 1.5.3.6 доступны для использования новые разделы.
//
// - ЗаказыПокупателей;
// - ЗаказыПоставщиков;
//
// Соответственно возможность редактирования (или запрет) для указанных документов устанавливается раздельно.
//
Процедура ОбновлениеРазделовДатЗапретаИзменений(ОбработкаЗавершена) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	РазделЗаказыПокупателей.Пользователь,
	|	ИСТИНА КАК Использование
	|ПОМЕСТИТЬ РазделЗаказыПокупателей
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК РазделЗаказыПокупателей
	|ГДЕ
	|	РазделЗаказыПокупателей.Раздел = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ЗаказыПокупателей)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазделЗаказыПоставщиков.Пользователь,
	|	ИСТИНА КАК Использование
	|ПОМЕСТИТЬ РазделЗаказыПоставщиков
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК РазделЗаказыПоставщиков
	|ГДЕ
	|	РазделЗаказыПоставщиков.Раздел = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.ЗаказыПоставщиков)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	РазделОсновной.Пользователь КАК Пользователь,
	|	РазделОсновной.ОписаниеДатыЗапрета КАК ОписаниеДатыЗапрета,
	|	РазделОсновной.ДатаЗапрета КАК ДатаЗапрета,
	|	ЕСТЬNULL(РазделЗаказыПокупателей.Использование, ЛОЖЬ) КАК РазделЗаказыПокупателейИспользуется,
	|	ЕСТЬNULL(РазделЗаказыПоставщиков.Использование, ЛОЖЬ) КАК РазделЗаказыПоставщиковИспользуется
	|ИЗ
	|	РегистрСведений.ДатыЗапретаИзменения КАК РазделОсновной
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазделЗаказыПокупателей КАК РазделЗаказыПокупателей
	|		ПО РазделОсновной.Пользователь = РазделЗаказыПокупателей.Пользователь
	|		ЛЕВОЕ СОЕДИНЕНИЕ РазделЗаказыПоставщиков КАК РазделЗаказыПоставщиков
	|		ПО РазделОсновной.Пользователь = РазделЗаказыПоставщиков.Пользователь
	|ГДЕ
	|	РазделОсновной.Раздел = ЗНАЧЕНИЕ(ПланВидовХарактеристик.РазделыДатЗапретаИзменения.УправленческийУчет)"
	);
	
	НачатьТранзакцию();
	Попытка
		
		ОбработкаЗавершена = Истина;
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.РазделЗаказыПокупателейИспользуется
				ИЛИ Выборка.РазделЗаказыПоставщиковИспользуется Тогда
				
				Продолжить;
				
			КонецЕсли;
			
			Комментарий = НСтр("ru = 'Раздел добавлен автоматически при обновлении УНФ до версии 1.5.3.6'");
			
			МенеджерРегистра = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
			МенеджерРегистра.Пользователь = Выборка.Пользователь;
			МенеджерРегистра.ДатаЗапрета = Выборка.ДатаЗапрета;
			МенеджерРегистра.Раздел = ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ЗаказыПокупателей;
			МенеджерРегистра.Объект = ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ЗаказыПокупателей;
			МенеджерРегистра.ОписаниеДатыЗапрета = Выборка.ОписаниеДатыЗапрета;
			МенеджерРегистра.Комментарий = Комментарий;
			МенеджерРегистра.Записать();
			
			МенеджерРегистра = РегистрыСведений.ДатыЗапретаИзменения.СоздатьМенеджерЗаписи();
			МенеджерРегистра.Пользователь = Выборка.Пользователь;
			МенеджерРегистра.ДатаЗапрета = Выборка.ДатаЗапрета;
			МенеджерРегистра.Раздел = ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ЗаказыПоставщиков;
			МенеджерРегистра.Объект = ПланыВидовХарактеристик.РазделыДатЗапретаИзменения.ЗаказыПоставщиков;
			МенеджерРегистра.ОписаниеДатыЗапрета = Выборка.ОписаниеДатыЗапрета;
			МенеджерРегистра.Комментарий = Комментарий;
			МенеджерРегистра.Записать();
			
			ОбработкаЗавершена = Ложь; // Обработчик обновления считается выполненым, если записи не добавляются
			
		КонецЦикла;
		
		ЗафиксироватьТранзакцию();
		
	Исключение
		
		ОбработкаЗавершена = Ложь;
		ЗаписьЖурналаРегистрации("Обновление разделов дат запрета изменений", УровеньЖурналаРегистрации.Ошибка, Метаданные.РегистрыСведений.ДатыЗапретаИзменения, , ОписаниеОшибки());
		
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти