
#Область РаботаСТабличнойЧастьюНоменклатуры

// Процедура рассчитывает сумму НДС в строке табличной части.
Процедура РассчитатьСуммуНДС(Объект, СтрокаТабличнойЧасти) Экспорт
	
	СтавкаНДС = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеСтавкиНДС(СтрокаТабличнойЧасти.СтавкаНДС);
	
	СтрокаТабличнойЧасти.СуммаНДС = ?(Объект.СуммаВключаетНДС, 
									  СтрокаТабличнойЧасти.Сумма - (СтрокаТабличнойЧасти.Сумма) / ((СтавкаНДС + 100) / 100),
									  СтрокаТабличнойЧасти.Сумма * СтавкаНДС / 100);
											
КонецПроцедуры // ПересчитатьСуммыДокумента() 

// Процедура рассчитывает сумму в строке табличной части.
Процедура РассчитатьСуммуВСтрокеТабличнойЧасти(Объект, СтрокаТабличнойЧасти, ИмяТабличнойЧасти = "Запасы") Экспорт
	
	// Сумма.
	Если ИмяТабличнойЧасти = "Работы" Тогда
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Кратность * СтрокаТабличнойЧасти.Коэффициент * СтрокаТабличнойЧасти.Цена;
	Иначе
		СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Количество * СтрокаТабличнойЧасти.Цена;
	КонецЕсли;
	
	// Скидки.
	Если ЕстьРеквизитОбъекта("ИспользоватьСкидки", СтрокаТабличнойЧасти) И СтрокаТабличнойЧасти.ИспользоватьСкидки Тогда
		Если СтрокаТабличнойЧасти.ПроцентСкидкиНаценки = 100 Тогда
			СтрокаТабличнойЧасти.Сумма = 0;
		ИначеЕсли СтрокаТабличнойЧасти.ПроцентСкидкиНаценки <> 0 И СтрокаТабличнойЧасти.Количество <> 0 Тогда
			СтрокаТабличнойЧасти.Сумма = СтрокаТабличнойЧасти.Сумма * (1 - СтрокаТабличнойЧасти.ПроцентСкидкиНаценки / 100);
		КонецЕсли;
		
		СтрокаТабличнойЧасти.ПроцентАвтоматическойСкидки = 0;
		СтрокаТабличнойЧасти.СуммаАвтоматическойСкидки = 0;
	КонецЕсли; 
	
	// Сумма НДС.
	РассчитатьСуммуНДС(Объект, СтрокаТабличнойЧасти);
	
	// Всего.
	Если ЕстьРеквизитОбъекта("Всего", СтрокаТабличнойЧасти) Тогда
		СтрокаТабличнойЧасти.Всего = СтрокаТабличнойЧасти.Сумма + ?(Объект.СуммаВключаетНДС, 0, СтрокаТабличнойЧасти.СуммаНДС);
	КонецЕсли;
	
КонецПроцедуры

//Обновляет итоги подобранных товаров в форме Корзина справочника Номенклатура
Процедура ОбновитьИтогиПодобранныхТоваров(Форма) Экспорт
	
	Если Форма.ЕстьДоступКЦенам Тогда
		Форма.НадписьПодобраноТоваров = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Подобрано: %1 на сумму %2 %3'"),
			Форма.Корзина.Итог("Количество"),
			Формат(Форма.Корзина.Итог("Сумма"),"ЧДЦ=2; ЧН=0"),
			?(ЗначениеЗаполнено(Форма.ВалютаПредставление),Форма.ВалютаПредставление,"")
			);
	Иначе		
		Форма.НадписьПодобраноТоваров = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Подобрано: %1'"),
			Форма.Корзина.Итог("Количество")
			);
	КонецЕсли;
						
КонецПроцедуры

#КонецОбласти

#Область ОбщиеПроцедурыИФункции

//Проверяет наличие реквизита у объекта по строковому имени реквизита
Функция ЕстьРеквизитОбъекта(Знач ИмяРеквизита, Знач Объект) Экспорт
	ПроверкаРеквизита = Новый Структура(ИмяРеквизита, Неопределено);
	ЗаполнитьЗначенияСвойств(ПроверкаРеквизита, Объект);
	Если ПроверкаРеквизита[ИмяРеквизита] <> Неопределено Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции	

Процедура УстановитьФлагУПодчиненных(СписокЭлементов, ИмяФлага, ЗначениеФлага) Экспорт
	
	Для Каждого СтрокаДерева Из СписокЭлементов Цикл
		
		СтрокаДерева[ИмяФлага] = ЗначениеФлага;
		
		ДочерниеСтроки = СтрокаДерева.ПолучитьЭлементы();
		Если ДочерниеСтроки.Количество() > 0 Тогда
			УстановитьФлагУПодчиненных(ДочерниеСтроки, ИмяФлага, ЗначениеФлага);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьФлагУРодителей(ТекущиеДанные, ИмяФлага, ЗначениеФлага) Экспорт
	
	Родитель = ТекущиеДанные.ПолучитьРодителя();
	Если Родитель <> Неопределено Тогда
		Родитель[ИмяФлага] = ЗначениеФлага;
		УстановитьФлагУРодителей(Родитель, ИмяФлага, ЗначениеФлага);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
