#Область ПрограммныйИнтерфейс

Функция ВидимостьКраснойПанели(СтруктураРеквизитовФормы, ОтчетВРосстат) Экспорт

	ОтчетВФНС = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	Если ЗначениеЗаполнено(ОтчетВФНС) Тогда
	
		// Если отчет отправлен в ФНС, а в Росстат еще не создан, то эту ссылку не показываем.
		// Вместо нее - красная панель.
		ОтчетВФНСОтправлен = ОтчетОтправленВФНС(СтруктураРеквизитовФормы);
		
		КраснаяПанельСкрыта = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(
			"ОтправкаБухгалтерскойОтчетностиВРосстат_БольшеНеПоказыватьПанель", 
			Строка(ОтчетВФНС.УникальныйИдентификатор())) = Истина; // может быть Неопределено.
		
		ЕстьКраснаяПанель = ОтчетВФНСОтправлен И НЕ ЗначениеЗаполнено(ОтчетВРосстат);
		
		Возврат ЕстьКраснаяПанель И НЕ КраснаяПанельСкрыта;
		
	Иначе
		
		Возврат Ложь;
		
	КонецЕсли;

КонецФункции

Функция ОтчетОтправленВФНС(СтруктураРеквизитовФормы) Экспорт

	ОтчетВФНС = СтруктураРеквизитовФормы.мСохраненныйДок;
	
	// Если отчет отправлен в ФНС, а в Росстат еще не создан, то эту ссылку не показываем.
	// Вместо нее - красная панель.
	Если ЗначениеЗаполнено(ОтчетВФНС) Тогда
		
		ЦиклОбмена = ДокументооборотСКОВызовСервера.ПолучитьПоследнийЦиклОбмена(ОтчетВФНС);
		Если ЗначениеЗаполнено(ЦиклОбмена) Тогда
			
			СообщенияЦикла = ДокументооборотСКОВызовСервера.ПолучитьСообщенияЦиклаОбмена(ЦиклОбмена);
			СтрЗаявленияНП = СообщенияЦикла.НайтиСтроки(Новый Структура("Тип", Перечисления.ТипыТранспортныхСообщений.ДекларацияНП));
			
			Если СтрЗаявленияНП.Количество() > 0 Тогда
				
				ПервичноеТранспортноеСообщение = СтрЗаявленияНП[0];
				ОтчетВФНСОтправлен = ПервичноеТранспортноеСообщение.Статус = Перечисления.СтатусыПисем.Отправленное;
				
			Иначе
				ОтчетВФНСОтправлен = Ложь;
			КонецЕсли;

		Иначе
			ОтчетВФНСОтправлен = Ложь;
		КонецЕсли;
			
	Иначе
		ОтчетВФНСОтправлен = Ложь;
	КонецЕсли;
	
	Возврат ОтчетВФНСОтправлен;

КонецФункции

Процедура БольшеНеПоказыватьПанель(мСохраненныйДок) Экспорт

	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(
		"ОтправкаБухгалтерскойОтчетностиВРосстат_БольшеНеПоказыватьПанель", 
		Строка(мСохраненныйДок.УникальныйИдентификатор()), 
		Истина);

КонецПроцедуры
 

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#КонецОбласти