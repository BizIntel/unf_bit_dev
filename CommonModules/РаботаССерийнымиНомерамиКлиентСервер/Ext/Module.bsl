
Процедура ДобавитьСерийныйНомерВСтроку(НоваяСтрока, СерийныйНомер, ДокОбъект, ИмяПоляКлючСвязи = "КлючСвязи") Экспорт
	
	ОтборСерийныйНомер = Новый Структура("СерийныйНомер", СерийныйНомер);
	Если ДокОбъект.СерийныеНомера.НайтиСтроки(ОтборСерийныйНомер).Количество()>0 Тогда
		//Серийный номер уже есть, пропускаем
		Возврат;
	КонецЕсли;
	
	Если НоваяСтрока[ИмяПоляКлючСвязи]=0 Тогда
		ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(ДокОбъект, "Запасы", ,ИмяПоляКлючСвязи);
	КонецЕсли;
	
	СтрСерийныеНомера = ДокОбъект.СерийныеНомера.Добавить();
	СтрСерийныеНомера.СерийныйНомер = СерийныйНомер;
	СтрСерийныеНомера.КлючСвязи = НоваяСтрока[ИмяПоляКлючСвязи];
	
	Если ДокОбъект.СерийныеНомера.Количество()>1 Тогда
		ОбновитьСтроковоеПредставлениеСерийныхНомеровСтроки(НоваяСтрока, ДокОбъект, ИмяПоляКлючСвязи);
	Иначе
		НоваяСтрока.СерийныеНомера = Строка(СерийныйНомер);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСтроковоеПредставлениеСерийныхНомеровСтроки(СтрокаЗапасы, ДокОбъект, ИмяПоляКлючСвязи, ИмяТабличнойЧастиСерийныеНомера="СерийныеНомера") Экспорт
	
	ОтборСерийныеНомераТекущейСтроки = Новый Структура("КлючСвязи", СтрокаЗапасы[ИмяПоляКлючСвязи]);
	СтроковоеПредставлениеСерийныхНомеров = "";
	
	Для каждого СтрокаЗагрузки Из ДокОбъект[ИмяТабличнойЧастиСерийныеНомера].НайтиСтроки(ОтборСерийныеНомераТекущейСтроки) Цикл
		СтроковоеПредставлениеСерийныхНомеров = СтроковоеПредставлениеСерийныхНомеров + СтрокаЗагрузки.СерийныйНомер+"; ";
	КонецЦикла;
	
	СтроковоеПредставлениеСерийныхНомеров = Лев(СтроковоеПредставлениеСерийныхНомеров, Мин(СтрДлина(СтроковоеПредставлениеСерийныхНомеров)-2,150));
	Если СтрНайти(ИмяТабличнойЧастиСерийныеНомера, "Оприходование") = 0 Тогда
		СтрокаЗапасы.СерийныеНомера = СтроковоеПредставлениеСерийныхНомеров;
	Иначе
		СтрокаЗапасы.СерийныеНомераОприходование = СтроковоеПредставлениеСерийныхНомеров;
	КонецЕсли;
	
КонецПроцедуры

Функция СтроковоеПредставлениеСерийныхНомеровСтроки(СерийныеНомера, КлючСвязи) Экспорт
	
	ОтборКлючСвязи = Новый Структура("КлючСвязи", КлючСвязи);
	
	СтроковоеПредставлениеСерийныхНомеров = "";
	Для каждого стр Из СерийныеНомера.НайтиСтроки(ОтборКлючСвязи) Цикл
		СтроковоеПредставлениеСерийныхНомеров = СтроковоеПредставлениеСерийныхНомеров + стр.СерийныйНомер+"; ";
	КонецЦикла;
	
	СтроковоеПредставлениеСерийныхНомеров = Лев(СтроковоеПредставлениеСерийныхНомеров, Мин(СтрДлина(СтроковоеПредставлениеСерийныхНомеров)-2,150));
	Возврат СтроковоеПредставлениеСерийныхНомеров;
	
КонецФункции

// Функция заполняет ключи связи в табличной части "Товары" документа.
Процедура ЗаполнитьКлючиСвязиВТабличнойЧастиТовары(Объект, ИмяТЧ, ИмяТЧ2 = Неопределено, ИмяПоляКлючСвязи = "КлючСвязи") Экспорт
	
	Для Каждого СтрокаТЧ Из Объект[ИмяТЧ] Цикл
		Если Не ЗначениеЗаполнено(СтрокаТЧ[ИмяПоляКлючСвязи]) Тогда
			УправлениеНебольшойФирмойКлиентСервер.ЗаполнитьКлючСвязи(Объект[ИмяТЧ], СтрокаТЧ, ИмяПоляКлючСвязи);
		КонецЕсли;
	КонецЦикла;
	
	Индекс = 0;
	Если Не ИмяТЧ2 = Неопределено Тогда
		Для Каждого СтрокаТЧ Из Объект[ИмяТЧ2] Цикл
			Индекс = Индекс + 1;
			СтрокаТЧ.КлючСвязиДляСкидокНаценок = Индекс;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьКлючиСвязиВТабличнойЧастиТовары()

Процедура ОбновитьСерийныеНомераКоличество(Объект, СтрокаТабличнойЧасти, ИмяТабличнойЧастиСерийныеНомера="СерийныеНомера", ИмяПоляКлючСвязи = "КлючСвязи") Экспорт

	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти[ИмяПоляКлючСвязи]) Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("КлючСвязи", СтрокаТабличнойЧасти[ИмяПоляКлючСвязи]);
	МассивСНСтроки = Новый ФиксированныйМассив(Объект[ИмяТабличнойЧастиСерийныеНомера].НайтиСтроки(СтруктураПоиска));
	
	Если ТипЗнч(СтрокаТабличнойЧасти.ЕдиницаИзмерения)=Тип("СправочникСсылка.ЕдиницыИзмерения") Тогда
		Коэффициент = РаботаССерийнымиНомерами.КоэффициентЕдиницы(СтрокаТабличнойЧасти.ЕдиницаИзмерения);
	Иначе
		Коэффициент = 1;
	КонецЕсли;
	
	СтрокаЗапасыКоличество = СтрокаТабличнойЧасти.Количество * Коэффициент;

	Если СтрокаЗапасыКоличество < МассивСНСтроки.Количество() И СтрокаЗапасыКоличество > 0 Тогда
		Для н=СтрокаЗапасыКоличество По МассивСНСтроки.Количество()-1 Цикл
			СтрокаУдалить = МассивСНСтроки[н];
			Объект[ИмяТабличнойЧастиСерийныеНомера].Удалить(СтрокаУдалить);	
		КонецЦикла;
	КонецЕсли;

	ОбновитьСтроковоеПредставлениеСерийныхНомеровСтроки(СтрокаТабличнойЧасти, Объект, ИмяПоляКлючСвязи, ИмяТабличнойЧастиСерийныеНомера);
	
КонецПроцедуры

Функция СимволЧисла() Экспорт
	возврат "#";
КонецФункции

Функция СтрокаМаскиПоШаблону(ШаблонСерийногоНомера) Экспорт
	
	//Экранируем спец.символы
	спСпецСимволов = Новый Соответствие;
	спСпецСимволов.Вставить("9","\9");
	спСпецСимволов.Вставить("N","\N");
	спСпецСимволов.Вставить("U","\U");
	спСпецСимволов.Вставить("X","\X");
	спСпецСимволов.Вставить("h","\h");
	спСпецСимволов.Вставить("@","\@");
	
	СтрокаМаски = "";
	Для н=1 По СтрДлина(ШаблонСерийногоНомера) Цикл
		симв = Сред(ШаблонСерийногоНомера, н, 1);
		
		Если спСпецСимволов.Получить(симв)<>Неопределено Тогда
			СтрокаМаски = СтрокаМаски + спСпецСимволов.Получить(симв);
		ИначеЕсли симв=СимволЧисла() Тогда
			СтрокаМаски = СтрокаМаски + "9";
		Иначе
			СтрокаМаски = СтрокаМаски + симв;
		КонецЕсли;
	КонецЦикла;
	
	Возврат СтрокаМаски;
	
КонецФункции

// Удаляет строки по ключу связи в табличной части СерийныеНомера, очистка строки представления серийных номеров
Процедура УдалитьСерийныеНомераПоКлючуСвязи(ТабличнаяЧастьСН, СтрокаТабличнойЧасти, ИмяПоляКлючСвязи = "КлючСвязи", ИспользоватьСерийныеНомераОстатки,
	ИмяТабличнойЧастиСерийныеНомера = "СерийныеНомера") Экспорт
	
	Если ИспользоватьСерийныеНомераОстатки=Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти[ИмяПоляКлючСвязи]) Тогда
		Возврат;
	КонецЕсли; 
	
	СтруктураПоиска = Новый Структура("КлючСвязи", СтрокаТабличнойЧасти[ИмяПоляКлючСвязи]);
	УдаляемыеСтроки = ТабличнаяЧастьСН.НайтиСтроки(СтруктураПоиска);
	Для каждого СтрокаТаблицы Из УдаляемыеСтроки Цикл
		
		ТабличнаяЧастьСН.Удалить(СтрокаТаблицы);
		
	КонецЦикла;
	
	Если СтрНайти(ИмяТабличнойЧастиСерийныеНомера, "Оприходование") = 0 Тогда
		СтрокаТабличнойЧасти.СерийныеНомера = "";
	Иначе
		СтрокаТабличнойЧасти.СерийныеНомераОприходование = "";
	КонецЕсли;
	
КонецПроцедуры
