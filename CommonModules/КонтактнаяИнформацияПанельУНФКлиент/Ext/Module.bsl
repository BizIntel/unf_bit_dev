
#Область СобытияЭлементовФормы

Процедура ДанныеПанелиКонтактнойИнформацииВыбор(Форма, Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	СтрокаКИ = Форма.ДанныеПанелиКонтактнойИнформации.НайтиПоИдентификатору(ВыбраннаяСтрока);
	
	Если СтрокаКИ.ТипОтображаемыхДанных = "ЗначениеКИ" Тогда
		
		Если СтрокаКИ.ИндексПиктограммы = 20 Тогда // Skype
			Параметры = Новый Структура("ЛогинSkype");
			Параметры.ЛогинSkype = СтрокаКИ.ПредставлениеКИ;
			Список = Новый СписокЗначений;
			Список.Добавить("Позвонить", НСтр("ru = 'Позвонить'"));
			Список.Добавить("НачатьЧат", НСтр("ru = 'Начать чат'"));
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораИзМенюSkype", КонтактнаяИнформацияУНФКлиент, Параметры);
			Форма.ПоказатьВыборИзМеню(ОписаниеОповещения, Список);
			Возврат;
		КонецЕсли;
		
		ЗначенияЗаполнения = Новый Структура("ТипСобытия,Контакт,ЗначениеКИ", 
			ТипСобытияПоИндексуПиктограммы(СтрокаКИ.ИндексПиктограммы),
			СтрокаКИ.ВладелецКИ,
			СтрокаКИ.ПредставлениеКИ
		);
			
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы, Форма);
		
	ИначеЕсли СтрокаКИ.ТипОтображаемыхДанных = "КонтактноеЛицо" Тогда
		
		ПоказатьЗначение(,СтрокаКИ.ВладелецКИ);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДанныеПанелиКонтактнойИнформацииПриАктивизацииСтроки(Форма, Элемент) Экспорт
	
	СтрокаКИ = Форма.Элементы.ДанныеПанелиКонтактнойИнформации.ТекущиеДанные;
	Если СтрокаКИ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КнопкаЯндекс = Форма.Элементы.Найти("КонтекстноеМенюПанелиКартаЯндекс");
	Если КнопкаЯндекс <> Неопределено Тогда
		КнопкаЯндекс.Доступность = СтрокаКИ.ТипОтображаемыхДанных = "ЗначениеКИ"
			И СтрокаКИ.ИндексПиктограммы = 12; // Адрес
	КонецЕсли;
	
	КнопкаGoogle = Форма.Элементы.Найти("КонтекстноеМенюПанелиКартаGoogle");
	Если КнопкаGoogle <> Неопределено Тогда
		КнопкаGoogle.Доступность = СтрокаКИ.ТипОтображаемыхДанных = "ЗначениеКИ"
			И СтрокаКИ.ИндексПиктограммы = 12; // Адрес
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьКоманду(Форма, Команда) Экспорт
	
	СтрокаКИ = Форма.Элементы.ДанныеПанелиКонтактнойИнформации.ТекущиеДанные;
	Если СтрокаКИ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Команда.Имя = "КонтекстноеМенюПанелиКартаЯндекс" Тогда
		УправлениеКонтактнойИнформациейКлиент.ПоказатьАдресНаКарте(СтрокаКИ.ПредставлениеКИ, "Яндекс.Карты");
	ИначеЕсли Команда.Имя = "КонтекстноеМенюПанелиКартаGoogle" Тогда
		УправлениеКонтактнойИнформациейКлиент.ПоказатьАдресНаКарте(СтрокаКИ.ПредставлениеКИ, "GoogleMaps");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Функция ОбрабатыватьОповещения(Форма, ИмяСобытия, Параметр) Экспорт
	
	Результат = ИмяСобытия = "Запись_Контрагент"
			Или ИмяСобытия = "Запись_КонтактноеЛицо";
		
	Возврат Результат;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ТипСобытияПоИндексуПиктограммы(ИндексПиктограммы)
	
	Если ИндексПиктограммы = 12 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ЛичнаяВстреча");
	ИначеЕсли ИндексПиктограммы = 8 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ЭлектронноеПисьмо");
	ИначеЕсли ИндексПиктограммы = 9 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Прочее");
	ИначеЕсли ИндексПиктограммы = 20 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Прочее");
	ИначеЕсли ИндексПиктограммы = 11 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.Прочее");
	ИначеЕсли ИндексПиктограммы = 7 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок");
	ИначеЕсли ИндексПиктограммы = 10 Тогда
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок");
	Иначе
		ТипСобытия = ПредопределенноеЗначение("Перечисление.ТипыСобытий.ПустаяСсылка");
	КонецЕсли;
	
	Возврат ТипСобытия;
	
КонецФункции

#КонецОбласти
