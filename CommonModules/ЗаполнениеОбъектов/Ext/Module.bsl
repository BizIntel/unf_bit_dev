////////////////////////////////////////////////////////////////////////////////
// Подсистема "Заполнение объектов" (сервер).
// Обслуживает команды заполнения.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

////////////////////////////////////////////////////////////////////////////////
// Обработчики условных вызовов.

// См. описание этой же процедуры в модуле ИнтеграцияСтандартныхПодсистем.
Процедура ПриОпределенииСтруктурыНастроекПодключаемыхОбъектов(СтруктураНастроек) Экспорт
	СтруктураНастроек.Вставить("ДобавитьКомандыЗаполнения", Новый ОписаниеТипов("Булево"));
КонецПроцедуры

// См. описание этой же процедуры в модуле ИнтеграцияСтандартныхПодсистем.
Процедура ПриОпределенииИспользуемыхПодменю(Форма, ИспользуемыеПодменю) Экспорт
	Если Форма.Элементы.Найти("ПодменюЗаполнить") <> Неопределено Тогда
		ИспользуемыеПодменю.Добавить("ПодменюЗаполнить");
	КонецЕсли;
	Если Форма.Элементы.Найти("ПодменюЗаполнитьОбычное") <> Неопределено Тогда
		ИспользуемыеПодменю.Добавить("ПодменюЗаполнитьОбычное");
	КонецЕсли;
КонецПроцедуры

// Формирование списка команд, подключенных к форме.
//   Событие возникает в процессе вызова модуля повторного использования.
//   Подробнее - см. описание этой же процедуры в модуле ИнтеграцияСтандартныхПодсистем.
//
// Параметры:
//   НастройкиФормы - Структура - Сведения о форме, в которой выводятся команды. Для чтения.
//       * ИмяФормы            - Строка          - Полное имя формы, в которой выводятся подключаемые команды.
//       * ИспользуемыеПодменю - Массив из Строк - Имена групп формы, которые используются в этой форме.
//   НастройкиОбъекта - Структура - Сведения о родительском объекте, к которому принадлежит форма. Для чтения.
//       Состав полей см. в ПодключаемыеКоманды.НастройкиОбъекта().
//   НастройкиРасширений - ТаблицаЗначений - Настройки расширений, подключенных к этому объекту метаданных. Для чтения.
//       * ПолноеИмя - Строка       - Полное имя объекта расширения.
//       * Менеджер  - Произвольный - Модуль менеджера объекта расширения.
//       Состав колонок определяется в процедуре ПриОпределенииИменИТиповНастроекПоставщиков.
//   Команды - ТаблицаЗначений - Таблица команд для вывода в подменю. Для изменения.
//       Состав колонок см. в ПодключаемыеКоманды.ТаблицаКоманд().
//
Процедура ПриОпределенииКомандПодключенныхКОбъекту(НастройкиФормы, НастройкиОбъекта, НастройкиРасширений, Команды) Экспорт
	Если Не ПравоДоступа("Изменение", НастройкиОбъекта.Метаданные) Тогда
		Возврат;
	КонецЕсли;
	КомандыЗаполнения = Команды.СкопироватьКолонки();
	
	ЕстьПодменюОбычное = НастройкиФормы.ИспользуемыеПодменю.Найти("ПодменюЗаполнитьОбычное") <> Неопределено;
	ЕстьПодменюЗаполнить = НастройкиФормы.ИспользуемыеПодменю.Найти("ПодменюЗаполнить") <> Неопределено;
	ГруппаПоУмолчанию = ?(ЕстьПодменюОбычное, "ПодменюЗаполнитьОбычное", ?(ЕстьПодменюЗаполнить, "ПодменюЗаполнить", ""));
	СтандартнаяОбработка = ЗначениеЗаполнено(ГруппаПоУмолчанию) И НастройкиОбъекта.Менеджер <> Неопределено;
	
	ЗаполнениеОбъектовПереопределяемый.ПередДобавлениемКомандЗаполнения(КомандыЗаполнения, НастройкиОбъекта, СтандартнаяОбработка);
	
	Если СтандартнаяОбработка Тогда
		НастройкиОбъекта.Менеджер.ДобавитьКомандыЗаполнения(КомандыЗаполнения, НастройкиОбъекта);
		ДобавленныеКоманды = КомандыЗаполнения.НайтиСтроки(Новый Структура("Менеджер", ""));
		Для Каждого КомандаЗаполнения Из ДобавленныеКоманды Цикл
			КомандаЗаполнения.Менеджер = НастройкиОбъекта.ПолноеИмя;
		КонецЦикла;
	КонецЕсли;
	
	РасширенияЗаполнения = НастройкиРасширений.НайтиСтроки(Новый Структура("ДобавитьКомандыЗаполнения", Истина));
	Для Каждого Расширение Из РасширенияЗаполнения Цикл
		Расширение.Менеджер.ДобавитьКомандыЗаполнения(КомандыЗаполнения, НастройкиОбъекта);
		ДобавленныеКоманды = КомандыЗаполнения.НайтиСтроки(Новый Структура("Менеджер", ""));
		Для Каждого КомандаЗаполнения Из ДобавленныеКоманды Цикл
			КомандаЗаполнения.Менеджер = Расширение.ПолноеИмя;
		КонецЦикла;
	КонецЦикла;
	
	Если КомандыЗаполнения.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого КомандаЗаполнения Из КомандыЗаполнения Цикл
		Команда = Команды.Добавить();
		ЗаполнитьЗначенияСвойств(Команда, КомандаЗаполнения);
		Если Команда.Порядок = 0 Тогда
			Команда.Порядок = 50;
		КонецЕсли;
		Если ПустаяСтрока(Команда.Группа) Или (Команда.Группа = "ПодменюЗаполнить" И ЕстьПодменюОбычное) Тогда
			Команда.Группа = ГруппаПоУмолчанию;
		КонецЕсли;
		Если Команда.РежимЗаписи = "" Тогда
			Команда.РежимЗаписи = "Записывать";
		КонецЕсли;
		Если Команда.МножественныйВыбор = Неопределено Тогда
			Команда.МножественныйВыбор = Истина;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
