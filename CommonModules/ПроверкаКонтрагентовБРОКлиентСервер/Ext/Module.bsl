////////////////////////////////////////////////////////////////////////////////
// Проверка одного или нескольких контрагентов при помощи веб-сервиса ФНС
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

Функция ПроверяемыеРазделыДекларацииПоНДС() Экспорт
	
	Разделы = Новый Структура;
	Разделы.Вставить("Начальный", 8);
	Разделы.Вставить("Конечный",  12);
	
	Возврат Разделы;
	
КонецФункции

Функция ЭтоПроверяемыйРаздел(ИмяРаздела) Экспорт
	
	Разделы = ПроверяемыеРазделыДекларацииПоНДС();
	
	Для НомерРаздела = Разделы.Начальный По Разделы.Конечный Цикл
		
		Если ИмяРаздела = "Раздел" + Строка(НомерРаздела) Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Процедура ИнициализироватьРеквизитыПроверкиКонтрагентов(Форма, ЭтоДекларацияПоНДС = Истина) Экспорт
	
	Реквизиты = Новый Структура;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Реквизиты, РеквизитыФоновогоЗадания(), Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Реквизиты, ПустойРезультатПроверки(), Истина);
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Реквизиты, ПрочиеРеквизиты(ЭтоДекларацияПоНДС), Истина);

	Форма.РеквизитыПроверкиКонтрагентов = Реквизиты;
	
КонецПроцедуры

Функция ПустойРезультатПроверки() Экспорт
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("ВсегоКонтрагентов",					0);
	Реквизиты.Вставить("КоличествоНекорректныхКонтрагентов",0);
	Реквизиты.Вставить("ЕстьДоступКВебСервисуФНС",			Ложь);
	Реквизиты.Вставить("ПроверкаВыполнилась", 				Ложь);
	
	Возврат Реквизиты;
	
КонецФункции

Функция ПрочиеРеквизиты(ЭтоДекларацияПоНДС) Экспорт
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("ПроверкаИспользуется", ПроверкаКонтрагентовБРОВызовСервера.ИспользованиеПроверкиВозможно());
	
	Если ЭтоДекларацияПоНДС Тогда
		Реквизиты.Вставить("ЭтоДекларацияПоНДС", Истина);
	Иначе
		Реквизиты.Вставить("ТабДок", Неопределено);
	КонецЕсли;
	
	Возврат Реквизиты;
	
КонецФункции

Функция РезультатПроверкиКонтрагентов(Форма) Экспорт
	
	РезультатПроверки = Новый Структура;
	РезультатПроверки.Вставить("ОшибкаПодключенияКСервису",
		НЕ Форма.РеквизитыПроверкиКонтрагентов.ЕстьДоступКВебСервисуФНС);
	РезультатПроверки.Вставить("КоличествоОбнаруженныхОшибок",
		Форма.РеквизитыПроверкиКонтрагентов.КоличествоНекорректныхКонтрагентов);
	РезультатПроверки.Вставить("ПроверкаВыполнена", 
		Форма.РеквизитыПроверкиКонтрагентов.ПроверкаВыполнилась);
	
	Возврат РезультатПроверки;
	
КонецФункции

#КонецОбласти

#Область ВспомогательныеПроцедурыИФункции

Функция РеквизитыФоновогоЗадания()
	
	Реквизиты = Новый Структура;
	
	Реквизиты.Вставить("АдресХранилища", 		Неопределено);
	Реквизиты.Вставить("ИдентификаторЗадания", 	Неопределено);

	Возврат Реквизиты;
	
КонецФункции

#КонецОбласти