////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
// Подсистема "Адресный классификатор".
////////////////////////////////////////////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

// Данные для запроса разрешений профилей безопасности для обновления на сайте 1С.
//
// Возвращаемое значение:
//     Массив - идентификаторы необходимых разрешений.
// 
Функция ЗапросРазрешенийБезопасностиОбновления() Экспорт
	
	ВладелецРазрешения = ОбщегоНазначения.ИдентификаторОбъектаМетаданных("РегистрСведений.АдресныеОбъекты"); 
	Разрешения         = АдресныйКлассификаторСлужебный.РазрешенияБезопасностиОбновления();
	
	Результат = Новый Массив;
	Результат.Добавить(РаботаВБезопасномРежиме.ЗапросНаИспользованиеВнешнихРесурсов(Разрешения, ВладелецРазрешения, Истина));
	
	Возврат Результат;
КонецФункции

// Возвращает представление региона по коду.
//
// Параметры:
//     КодРегиона - Число, Строка, Соответствие - код региона.
//
// Возвращаемое значение:
//     Строка, Соответствие - представления.
//
Функция ПредставлениеРегионаПоКоду(КодРегиона) Экспорт
	
	Если ТипЗнч(КодРегиона) <> Тип("Соответствие") Тогда
		Возврат ПредставлениеРегиона(КодРегиона);
	КонецЕсли;
	
	Результат = Новый Соответствие;
	Для Каждого КлючЗначение Из КодРегиона Цикл
		Код = КлючЗначение.Ключ;
		Результат.Вставить(Код, ПредставлениеРегиона(Код) );
	КонецЦикла;
	
	Возврат Результат;
КонецФункции

Функция ПредставлениеРегиона(КодРегиона)
	
	Возврат Формат(КодРегиона, "ЧЦ=2; ЧН=; ЧВН=; ЧГ=") + " " + АдресныйКлассификатор.НаименованиеРегионаПоКоду(КодРегиона);
	
КонецФункции

#КонецОбласти

