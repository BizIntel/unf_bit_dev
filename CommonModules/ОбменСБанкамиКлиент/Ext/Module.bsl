////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиКлиент: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Создает настройку обмена с банком или открывает существующую.
// Если банк не известен системе, то открывается форма новой настройки обмена с банком,
// в которой заполнена только Организация и Банк.
// Если банк известен системе, но не поддерживает автоматическое получение настроек,
// то предлагается выбор файла настроек, загружает настройки из файла
// и открывает заполненную форму настроек обмена с банком.
// Если банк известен системе и поддерживает автоматическое получение настроек,
// то настройка создается автоматически и производится тест настроек.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - ссылка на организацию.
//  Банк - СправочникСсылка.КлассификаторБанковРФ - ссылка на банк.
//  НомерБанковскогоСчета - Строка - номер банковского счета.
//  ОбработчикОповещения - ОписаниеОповещения - содержит описание процедуры,
//   которая будет вызвана после создания настройки обмена с банком, не вызывается если настройка обмена уже существует. Параметры процедуры:
//    * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - созданная настройка обмена с банком.
//    * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//
Процедура ОткрытьСоздатьНастройкуОбмена(Организация, Банк, НомерБанковскогоСчета = "", ОбработчикОповещения = Неопределено) Экспорт
	
	ЕстьПравоПросмотраНастройкиЭДО = Ложь;
	
	ТекущаяНастройка = ОбменСБанкамиСлужебныйВызовСервера.НастройкаОбмена(
		Организация, Банк, ЕстьПравоПросмотраНастройкиЭДО);
		
	ТекстСообщения = НСтр("ru = 'Недостаточно прав для настройки прямого обмена с банком.
								|Обратитесь к администратору.'");
		
	Если ЗначениеЗаполнено(ТекущаяНастройка) Тогда
		Если Не ЕстьПравоПросмотраНастройкиЭДО Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		ПоказатьЗначение( , ТекущаяНастройка);
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Организация", Организация);
	ПараметрыФормы.Вставить("Банк", Банк);
	ПараметрыФормы.Вставить("НомерБанковскогоСчета", НомерБанковскогоСчета);
	
	ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.ПомощникСозданияНастройкиОбмена", ПараметрыФормы, , , , ,
		ОбработчикОповещения);
	
КонецПроцедуры

// Отправляет запрос выписки в банк, а после получения выписки вызывает оповещение о выборе.
// для формы или элемента формы, указанного в параметре Владелец.
// Может возвращать ДокументСсылка.СообщениеОбменСБанками или Массив с элементами указанного типа.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком;
//  ДатаНачала - Дата - начало периода запроса.
//  ДатаОкончания - Дата - окончание периода запроса.
//  Владелец - УправляемаяФорма, ЭлементФормы - получатель оповещения о выборе элемента - выписки банка.
//  НомерСчета - Строка - номер банковского счета организации. Если не указан, то запрос по всем счетам.
//  ОткрыватьФормуУточненияПериода - Булево - необходимо установить Истина,
//                                           если на форме получения выписки нет возможности вручную изменить период запроса.
//
Процедура ПолучитьВыпискуБанка(НастройкаОбмена, ДатаНачала, Знач ДатаОкончания, Владелец, НомерСчета = Неопределено, ОткрыватьФормуУточненияПериода = Ложь) Экспорт
	
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	ТекущаяДатаСеанса = ОбщегоНазначенияКлиент.ДатаСеанса();
	
	Если ДатаНачала > ТекущаяДатаСеанса Тогда
		СообщениеТекст = НСтр("ru = 'Дата начала периода запроса не должна быть позднее текущей даты.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "НачалоПериода");
		Возврат;
	КонецЕсли;
	
	Если ДатаОкончания > КонецДня(ТекущаяДатаСеанса) Тогда
		СообщениеТекст = НСтр("ru = 'Дата конца периода запроса не должна быть позднее текущей даты.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "КонецПериода");
		Возврат;
	КонецЕсли;
	
	Если ДатаНачала > ДатаОкончания Тогда
		СообщениеТекст = НСтр("ru = 'Дата начала периода запроса не должна быть больше даты конца периода.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "НачалоПериода");
		Возврат;
	КонецЕсли;
	
	Если КонецДня(ДатаОкончания) = КонецДня(ТекущаяДатаСеанса) Тогда
		ДатаОкончания = ТекущаяДатаСеанса;
	Иначе
		ДатаОкончания = КонецДня(ДатаОкончания);
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(НастройкаОбмена);
	
	Если РеквизитыНастройкиОбмена.Недействительна Тогда
		ТекстСообщения = НСтр("ru = 'Настройка обмена недействительна, операция невозможна'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Параметры = Новый Структура;
	
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("НомерСчета", НомерСчета);
	Параметры.Вставить("ДатаНачала", ДатаНачала);
	Параметры.Вставить("ДатаОкончания", ДатаОкончания);
	Параметры.Вставить("Владелец", Владелец);
	Параметры.Вставить("РеквизитыНастройкиОбмена", РеквизитыНастройкиОбмена);
	
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		ОбменСБанкамиСлужебныйКлиент.НачатьПроцессПолученияВыпискиЧерезВК(
			НастройкаОбмена, ДатаНачала, ДатаОкончания, НомерСчета, Владелец);
		Возврат;
	ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ОбработчикПослеПодключения = Новый ОписаниеОповещения(
			"ПолучитьВыпискуЧерезДополнительнуюОбработку", ОбменСБанкамиСлужебныйКлиент, Параметры);
		ОбменСБанкамиСлужебныйКлиент.ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(
			ОбработчикПослеПодключения,  РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
		Возврат;
	ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		
		// Максимальный период запроса - 14 дней.
		МинимальнаяДата = ДатаОкончания - 60*60*24*14;
		Если МинимальнаяДата > ДатаНачала Тогда
			Если ОткрыватьФормуУточненияПериода Тогда
				Оповещение = Новый ОписаниеОповещения(
					"ПослеУточненияПериодаЗапросаСбербанк", ОбменСБанкамиСлужебныйКлиент, Параметры);
				ПараметрыФормы = Новый Структура("ДатаНачала, ДатаОкончания", ДатаНачала, ДатаОкончания);
				ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборПериодаСбербанк", ПараметрыФормы, , , , , Оповещение);
				Возврат;
			Иначе
				СообщениеТекст = НСтр("ru = 'Период запроса выписки не должен превышать 14 дней.'");
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеТекст, , "КонецПериода");
				Возврат;
			КонецЕсли;
		КонецЕсли;
		
		ГотовыеВыписки = Неопределено;
		ЗапросВыписки = ОбменСБанкамиСлужебныйВызовСервера.ЗапросВыпискиСбербанк(
			НастройкаОбмена, ДатаНачала, ДатаОкончания, НомерСчета, , ГотовыеВыписки);
		Параметры.Вставить("ГотовыеВыписки", ГотовыеВыписки);
		Параметры.Вставить("СообщениеОбмена", ЗапросВыписки);
		Параметры.Вставить("НомерСчета", НомерСчета);
			
		Если ЗначениеЗаполнено(ЗапросВыписки) Тогда
			
			МассивСообщенийОбмена = Новый Массив;
			МассивСообщенийОбмена.Добавить(ЗапросВыписки);
			
			ОбработчикПослеПодписания = Новый ОписаниеОповещения(
				"ОтправитьЗапросВыпискиПослеПодписания", ОбменСБанкамиСлужебныйКлиент, Параметры);
				
			ОбменСБанкамиСлужебныйКлиент.ПодписатьЭДСбербанк(ОбработчикПослеПодписания, Параметры.НастройкаОбмена, МассивСообщенийОбмена);

		ИначеЕсли ГотовыеВыписки.Количество() Тогда
			ОписаниеОповещенияОЗавершении = Новый ОписаниеОповещения(
				"ПолучитьВыпискуСбербанкаПослеВопросаОбИхНаличии", ОбменСБанкамиСлужебныйКлиент, Параметры);
			ТекстВопроса = НСтр("ru = 'В базе уже есть выписки банка за указанный период.
									|Загрузить выписки из базы или получить новые из банка?'");
			Кнопки = Новый СписокЗначений;
			Кнопки.Добавить(Истина, НСтр("ru = 'Загрузить из базы'"));
			Кнопки.Добавить(Ложь, НСтр("ru = 'Получить из банка'"));
			Заголовок = НСтр("ru = 'Получение выписки'");
			ПоказатьВопрос(ОписаниеОповещенияОЗавершении, ТекстВопроса, Кнопки, , Истина, Заголовок);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если РеквизитыНастройкиОбмена.ИспользуетсяКриптография Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПолученияОтпечатковПолучитьВыпискуБанка", ОбменСБанкамиСлужебныйКлиент, Параметры);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Ложь);
		Возврат;
	Иначе
		МассивОтпечатковСертификатов = Новый Массив;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ПослеПолученияОтпечатковПолучитьВыпискуБанка(МассивОтпечатковСертификатов, Параметры);
	
КонецПроцедуры

// Отправляет подготовленные документы в банк и получает новые.
// Если параметры не переданы то выполняется синхронизация по всем настройкам с банками.
//
// Параметры:
//  Организация - СправочникСсылка.Организации - организация из расчетного счета.
//  Банк - СправочникСсылка.КлассификаторБанковРФ - банк из расчетного счета.
//
Процедура СинхронизироватьСБанком(Организация = Неопределено, Банк = Неопределено) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если НЕ ЭлектронноеВзаимодействиеПереопределяемый.ЕстьПравоОбработкиЭД() Тогда
			ЭлектронноеВзаимодействиеСлужебныйКлиент.СообщитьПользователюОНарушенииПравДоступа();
			Возврат;
		КонецЕсли;
		Если НЕ ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьОбменСБанками") Тогда
			ТекстСообщения = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ТекстСообщенияОНеобходимостиНастройкиСистемы("РАБОТАСБАНКАМИ");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	МассивНастроекОбмена = ОбменСБанкамиСлужебныйВызовСервера.НастройкиОбмена(Организация, Банк);
	
	ПараметрыСинхронизации = Новый Структура;
	ПараметрыСинхронизации.Вставить("НастройкиОбмена", МассивНастроекОбмена);
	ПараметрыСинхронизации.Вставить("ИтогКолПолученных", 0);
	ПараметрыСинхронизации.Вставить("ИтогКолОтправленных", 0);
	
	ОбменСБанкамиСлужебныйКлиент.ВыполнитьОбменСБанками(Неопределено, ПараметрыСинхронизации);

КонецПроцедуры

// Процедура создает, подписывает и отправляет электронный документ.
//
// Параметры:
//  ПараметрКоманды - ДокументСсылка, СправочникСсылка - ссылка на объект ИБ, электронные документы которого надо обработать.
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - электронный документ, который надо подписать, отправить.
//
Процедура СформироватьПодписатьОтправитьЭД(ПараметрКоманды, СообщениеОбмена = Неопределено) Экспорт
	
	МассивСсылок = ЭлектронноеВзаимодействиеСлужебныйКлиент.МассивПараметров(ПараметрКоманды);
	Если МассивСсылок = Неопределено Тогда
		Если СообщениеОбмена = Неопределено Тогда
			Возврат;
		Иначе
			МассивСсылок = Новый Массив;
		КонецЕсли;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "СформироватьУтвердитьПодписатьОтправить", СообщениеОбмена);
	
КонецПроцедуры

// Процедура отправляет повторно электронный документ.
//
// Параметры:
//  ПараметрКоманды - СсылкаНаОбъект - ссылка на объект ИБ, электронные документы которого нужно отправить,
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение, которые нужно отправить.
//
Процедура ОтправитьПовторноЭД(ПараметрКоманды, СообщениеОбмена = Неопределено) Экспорт
	
	МассивСсылок = ЭлектронноеВзаимодействиеСлужебныйКлиент.МассивПараметров(ПараметрКоманды);
	Если МассивСсылок = Неопределено Тогда
		Если СообщениеОбмена = Неопределено Тогда
			Возврат;
		Иначе
			МассивСсылок = Новый Массив;
		КонецЕсли;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "ОтправитьПовторно", СообщениеОбмена);
	
КонецПроцедуры

// Открывает актуальный ЭД по документу ИБ
//
// Параметры:
//  ПараметрКоманды - ДокументСсылка- ссылка на документ ИБ;
//  Источник - УправляемаяФорма - Форма источник;
//  ПараметрыОткрытия - Структура - дополнительные параметры просмотра.
//
Процедура ОткрытьАктуальныйЭД(ПараметрКоманды, Источник = Неопределено, ПараметрыОткрытия = Неопределено) Экспорт
	
	ОчиститьСообщения();
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если НЕ ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЕстьПравоЧтенияЭД() Тогда
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	МассивСсылок = ЭлектронноеВзаимодействиеСлужебныйКлиент.МассивПараметров(ПараметрКоманды);
	Если МассивСсылок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеВладельцевИСообщенийОбмена = ОбменСБанкамиСлужебныйВызовСервера.СообщенияОбменаПоВладельцам(МассивСсылок);
	Для Каждого ТекЭл Из МассивСсылок Цикл
		
		СсылкаНаСообщениеОбмена = СоответствиеВладельцевИСообщенийОбмена.Получить(ТекЭл);
		Если ЗначениеЗаполнено(СсылкаНаСообщениеОбмена) Тогда
			Если ТипЗнч(ПараметрыОткрытия) = Тип("ПараметрыВыполненияКоманды") Тогда
				ОбменСБанкамиСлужебныйКлиент.ОткрытьЭДДляПросмотра(
					СсылкаНаСообщениеОбмена, ПараметрыОткрытия, ПараметрыОткрытия.Источник);
			Иначе
				ОбменСБанкамиСлужебныйКлиент.ОткрытьЭДДляПросмотра(СсылкаНаСообщениеОбмена, , Источник);
			КонецЕсли;
			
		Иначе
			ТекстШаблона = НСтр("ru = '%1. Актуальный электронный документ не найден.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстШаблона, ТекЭл);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

// Открывает форму со списком электронных документов для данного владельца.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - Ссылка на объект ИБ, электронные документы которого надо увидеть или сообщение обмена.
//  ПараметрыОткрытия - Структура - дополнительные параметры просмотра списка электронных документов.
//
Процедура ОткрытьСписокЭД(СсылкаНаОбъект, ПараметрыОткрытия = Неопределено) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если НЕ ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЕстьПравоЧтенияЭД() Тогда
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	Если НЕ ЗначениеЗаполнено(СсылкаНаОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ОбъектОтбора", СсылкаНаОбъект);
	Если ПараметрыОткрытия = Неопределено Тогда
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.СписокЭД", ПараметрыФормы, , СсылкаНаОбъект.УникальныйИдентификатор());
	Иначе
		Окно = Неопределено;
		Если ТипЗнч(ПараметрыОткрытия) = Тип("ПараметрыВыполненияКоманды") Тогда
			Окно = ПараметрыОткрытия.Окно;
		КонецЕсли;
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.СписокЭД", ПараметрыФормы,
			ПараметрыОткрытия.Источник, СсылкаНаОбъект.УникальныйИдентификатор(), Окно);
	КонецЕсли;
	
КонецПроцедуры

// Процедура создает новый электронный документ.
//
// Параметры:
//  ПараметрКоманды - СсылкаНаОбъект - ссылка на объект ИБ, электронные документы которого надо отправить.
//  Показывать - Булево - признак того что созданный документ будет показан пользователю.
//
Процедура СформироватьНовыйЭД(ПараметрКоманды, Показывать=Истина) Экспорт
	
	МассивСсылок = ЭлектронноеВзаимодействиеСлужебныйКлиент.МассивПараметров(ПараметрКоманды);
	Если МассивСсылок = Неопределено Тогда
		Возврат;
	КонецЕсли;
	Если Показывать Тогда
		ОбменСБанкамиСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "СформироватьПоказать");
	Иначе
		ОбменСБанкамиСлужебныйКлиент.ОбработатьЭД(МассивСсылок, "Сформировать");
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из одноименной процедуры модуля ЭлектроннаяПодписьКлиентПереопределяемый.
//
// Параметры:
//  Параметры - Структура - со свойствами:
//  * ОжидатьПродолжения   - Булево - (возвращаемое значение) - если Истина, тогда дополнительная проверка
//                            будет выполнятся асинхронно, продолжение возобновится после выполнения оповещения.
//                            Начальное значение Ложь.
//  * Оповещение           - ОписаниеОповещения - обработка, которую нужно вызывать для продолжения
//                              после асинхронного выполнения дополнительной проверки.
//  * Сертификат           - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - проверяемый сертификат.
//  * Проверка             - Строка - имя проверки, добавленное в процедуре ПриСозданииФормыПроверкаСертификата
//                              общего модуля ЭлектроннаяПодписьПереопределяемый.
//  * МенеджерКриптографии - МенеджерКриптографии - подготовленный менеджер криптографии для
//                              выполнения проверки.
//                         - Неопределено - если стандартные проверки отключены в процедуре
//                              ПриСозданииФормыПроверкаСертификата общего модуля ЭлектроннаяПодписьПереопределяемый.
//  * ОписаниеОшибки       - Строка - (возвращаемое значение) - описание ошибки, полученной при выполнении проверки.
//                              Это описание сможет увидеть пользователь при нажатии на картинку результата.
//  * ЭтоПредупреждение    - Булево - (возвращаемое значение) - вид картинки Ошибка/Предупреждение 
//                            начальное значение Ложь.
//
Процедура ПриДополнительнойПроверкеСертификата(Параметры) Экспорт
	
	Если Параметры.Проверка = "УстановкаПодписиСбербанк" Тогда
		Параметры.ОжидатьПродолжения = Истина;
		ПроверитьУстановкуПодписиСбербанк(Параметры.Оповещение, Параметры.Сертификат, Параметры);
	ИначеЕсли Параметры.Проверка = "ПроверкаПодписиСбербанк" Тогда
		Параметры.ОжидатьПродолжения = Истина;
		ПроверитьПодписьСбербанк(Параметры.Оповещение, Параметры.Сертификат, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Открывает форму списка настроек обмена с отбором по организации или банку.
//
// Параметры:
//  СсылкаНаОбъект - СправочникСсылка - ссылка на объект отбора. Ссылка должна быть только на организацию или банк.
//
Процедура ОткрытьНастройкиDirectBankСОтбором(СсылкаНаОбъект) Экспорт
	
	НазваниеСправочникаБанки = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ИмяПрикладногоСправочника("Банки");
	Если Не ЗначениеЗаполнено(НазваниеСправочникаБанки) Тогда
		НазваниеСправочникаБанки = "КлассификаторБанковРФ";
	КонецЕсли;
	
	НазваниеСправочникаОрганизации = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ИмяПрикладногоСправочника(
		"Организации");
	Если Не ЗначениеЗаполнено(НазваниеСправочникаОрганизации) Тогда
		НазваниеСправочникаОрганизации = "Организации";
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	Если ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка." + НазваниеСправочникаОрганизации) Тогда
		ПараметрыФормы.Вставить("Организация", СсылкаНаОбъект);
	ИначеЕсли ТипЗнч(СсылкаНаОбъект) = Тип("СправочникСсылка." + НазваниеСправочникаБанки) Тогда
		ПараметрыФормы.Вставить("Банк", СсылкаНаОбъект);
	КонецЕсли;

	ОткрытьФорму("Справочник.НастройкиОбменСБанками.Форма.ФормаСписка", ПараметрыФормы);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет механизм установки подписи для сертификата сбербанка
// 
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, которое будет вызвано после установки подписи.
//    Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на сертификат.
//    Контекст - Структура - контекст из ЭлектроннаяПодписьКлиентПереопределяемый.ПриДополнительнойПроверкеСертификата.
//
Процедура ПроверитьУстановкуПодписиСбербанк(Оповещение, Сертификат, Контекст)

	НастройкаОбмена = ОбменСБанкамиСлужебныйВызовСервера.НастройкаОбменаПоСертификатуСбербанк(Сертификат);
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		ОписаниеОшибки = НСтр("ru = 'Не найдена действующая настройка обмена с сервисом 1С:ДиректБанка с данным сертификатом'");
		Контекст.ОписаниеОшибки = ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(Оповещение, Контекст);
		Возврат;
	КонецЕсли;
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПодписания", Оповещение);
	Параметры.Вставить("Сертификат", Сертификат);
	Параметры.Вставить("Контекст", Контекст);
		
	ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
		"ПроверитьУстановкуПодписиПослеАутентификацииНаТокенеСбербанк", ОбменСБанкамиСлужебныйКлиент, Параметры);

	ОбменСБанкамиСлужебныйКлиент.АутентифицироватьсяНаТокенеСбербанка(
		ОповещениеПослеАутентификации, НастройкаОбмена, Истина);
	
КонецПроцедуры

// Проверяет механизм установки подписи для сертификата сбербанка
// 
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, которое будет вызвано после установки подписи.
//    Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - ссылка на сертификат.
//    Контекст - Структура - контекст из ЭлектроннаяПодписьКлиентПереопределяемый.ПриДополнительнойПроверкеСертификата.
//
Процедура ПроверитьПодписьСбербанк(Оповещение, Сертификат, Контекст)

	Подпись = ОбменСБанкамиСлужебныйКлиент.ЗначениеИзКэшаСбербанк("ТестоваяПодпись");
	
	Если Не ЗначениеЗаполнено(Подпись) Тогда
		Возврат;
	КонецЕсли;
	
	СтрокаПодписиBase64 = "JiMxMDU4OyYjMTA3NzsmIzEwODk7JiMxMDkwOw==";
	
	РеквизитыСертификата = Новый Структура("СертификатBase64");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(Сертификат, РеквизитыСертификата);
		
	ПодключаемыйМодуль = ОбменСБанкамиСлужебныйКлиент.ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");

	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеТестаПодписиСертификата", Оповещение);
	Параметры.Вставить("Контекст", Контекст);
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗавершитьТестПодписиСертификатаСбербанк", ОбменСБанкамиСлужебныйКлиент, Параметры);
	
	ОбменСБанкамиСлужебныйКлиент.ПроверитьПодписьНаТокенеСбербанк(
		Оповещение, ПодключаемыйМодуль, СтрокаПодписиBase64, Подпись, РеквизитыСертификата.СертификатBase64);
		
КонецПроцедуры

#КонецОбласти

