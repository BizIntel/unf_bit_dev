#Область ПрограммныйИнтерфейс

Функция ВыгрузитьРегламентированныйОтчет(
		Отчет, 
		ПроверкаБлокировкиФормы, 
		ТекстВыгрузки = Неопределено, 
		ИмяФайлаВыгрузки = Неопределено, 
		КодировкаТекстаВыгрузки = Неопределено, 
		ТипФайлаВыгрузки = Неопределено, 
		парамДокументы = Неопределено, 
		УникальныйИдентификатор = Неопределено, 
		ВозвращатьИменаИТекстыФайловВыгрузкиВВидеМассивов = Ложь) Экспорт
	
	Если ТипЗнч(Отчет) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		// формируем файл выгрузки
		ФормаВыгрузкиРеглОтчета = ПолучитьФорму("Документ.ВыгрузкаРегламентированныхОтчетов.Форма.ФормаДокумента");
		ДанныеВыгрузки = ТекстВыгрузки;
		
		РезультатВыгрузки = ФормаВыгрузкиРеглОтчета.ВыгрузитьОтчет(Отчет, ПроверкаБлокировкиФормы, , ДанныеВыгрузки, ИмяФайлаВыгрузки, КодировкаТекстаВыгрузки, ВозвращатьИменаИТекстыФайловВыгрузкиВВидеМассивов);
		Если НЕ РезультатВыгрузки Тогда
			Возврат Ложь;
		КонецЕсли;
		
		парамДокументы = Новый Массив;
		
		Если ТипЗнч(ДанныеВыгрузки) = Тип("Массив") Тогда
			Для Каждого ЭлементВыгрузки Из ДанныеВыгрузки Цикл
				
				Если ТипЗнч(ЭлементВыгрузки) = Тип("Структура")
					И ЭлементВыгрузки.Свойство("ТипФайлаВыгрузки")
					И ЭлементВыгрузки.ТипФайлаВыгрузки = "НДС2015Разделы1_7" Тогда
					
					ТекстВыгрузки = ЭлементВыгрузки.АдресФайлаВыгрузки;
					ИмяФайлаВыгрузки = ЭлементВыгрузки.ИмяФайлаВыгрузки;
					КодировкаТекстаВыгрузки = ЭлементВыгрузки.КодировкаФайлаВыгрузки;
					
				Иначе
					парамДокументы.Добавить(ЭлементВыгрузки);
				КонецЕсли;
				
			КонецЦикла;
		Иначе
			ТекстВыгрузки = ДанныеВыгрузки;
		КонецЕсли;
	Иначе
		
		СвойстваФайлаВыгрузки = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ВыгрузитьДокумент(Отчет, УникальныйИдентификатор);
		Если НЕ ЗначениеЗаполнено(СвойстваФайлаВыгрузки) Тогда
			Возврат Ложь;
		КонецЕсли;
		
		Если ТипЗнч(СвойстваФайлаВыгрузки) = Тип("Структура") Тогда
			ТекстВыгрузки = СвойстваФайлаВыгрузки.АдресФайлаВыгрузки;
			ИмяФайлаВыгрузки = СвойстваФайлаВыгрузки.ИмяФайлаВыгрузки;
			КодировкаТекстаВыгрузки = СвойстваФайлаВыгрузки.КодировкаФайлаВыгрузки;
			ТипФайлаВыгрузки = СвойстваФайлаВыгрузки.ТипФайлаВыгрузки;
		Иначе
			
			ТекстВыгрузки = Новый Массив;
			ИмяФайлаВыгрузки = Новый Массив;
			КодировкаТекстаВыгрузки = Новый Массив;
			ТипФайлаВыгрузки = Новый Массив;
			парамДокументы = Новый Массив;
			
			Для Каждого Эл Из СвойстваФайлаВыгрузки Цикл
				ТекстВыгрузки.Добавить(Эл.АдресФайлаВыгрузки);
				ИмяФайлаВыгрузки.Добавить(Эл.ИмяФайлаВыгрузки);
				КодировкаТекстаВыгрузки.Добавить(Эл.КодировкаФайлаВыгрузки);
				ТипФайлаВыгрузки.Добавить(Эл.ТипФайлаВыгрузки);
				парамДокументы.Добавить(Эл.Ссылка);
			КонецЦикла;
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции

Процедура ПоказатьПротокол(Форма) Экспорт
	
	СсылкаОтчет = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СсылкаНаОтчетПоФорме(Форма);
	Если ЗначениеЗаполнено(СсылкаОтчет) Тогда
		ОткрытьФорму("ОбщаяФорма.ПротоколОшибок", Новый Структура("Отчет", СсылкаОтчет), Форма,,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ЗарегистрироватьЗаявкуНаОтправкуОтчета(Форма, КонтролирующийОрган = "ФНС", ВыполняемоеОповещение = Неопределено) Экспорт
	
	Отчет = ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.СсылкаНаОтчетПоФорме(Форма);
	
	// проверяем, отправлялся ли отчет
	ТекстВопроса = "";
	Если НЕ СдачаОтчетностиЧерезСервисСпецоператораВызовСервера.ПроверитьПередРегистрациейЗаявкиНаОтправкуОтчетности(Отчет, КонтролирующийОрган, ТекстВопроса) Тогда
		Если ЗначениеЗаполнено(ТекстВопроса) Тогда
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("Форма", Форма);
			ДополнительныеПараметры.Вставить("Отчет", Отчет);
			ДополнительныеПараметры.Вставить("КонтролирующийОрган", КонтролирующийОрган);
			ДополнительныеПараметры.Вставить("ВыполняемоеОповещение", ВыполняемоеОповещение);
			ОписаниеОповещения = Новый ОписаниеОповещения("ЗарегистрироватьЗаявкуНаОтправкуОтчетаЗавершение", ЭтотОбъект, ДополнительныеПараметры);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
		Иначе
			Результат = Ложь;
			Если ВыполняемоеОповещение <> Неопределено Тогда
				ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
			КонецЕсли;
		КонецЕсли;
	Иначе
		ФормируемПакетИРегистрируемОтправку(Форма, Отчет, КонтролирующийОрган, ВыполняемоеОповещение)
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗарегистрироватьЗаявкуНаОтправкуОтчетаЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Форма = ДополнительныеПараметры.Форма;
	Отчет = ДополнительныеПараметры.Отчет;
	КонтролирующийОрган = ДополнительныеПараметры.КонтролирующийОрган;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Результат = Ложь;
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
		КонецЕсли;
	Иначе
		ФормируемПакетИРегистрируемОтправку(Форма, Отчет, КонтролирующийОрган, ВыполняемоеОповещение)
	КонецЕсли;
	
КонецПроцедуры

Процедура ФормируемПакетИРегистрируемОтправку(Форма, Отчет, КонтролирующийОрган, ВыполняемоеОповещение)
	
	// формируем пакет
	ДвоичныеДанныеФайлаПакета = Неопределено;
	КороткоеИмяФайлаПакета = "";
	РезультатФормированияПакета = СформироватьПакетОтчетности(Форма, Отчет, КонтролирующийОрган, ДвоичныеДанныеФайлаПакета, КороткоеИмяФайлаПакета);
	Если НЕ РезультатФормированияПакета Тогда
		Результат = Ложь;
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	// регистрируем в базе отправку
	РезультатРегистрацииОтправки = СдачаОтчетностиЧерезСервисСпецоператораВызовСервера.ЗарегистрироватьОтправкуОтчетностиВСправочнике(ДвоичныеДанныеФайлаПакета, КороткоеИмяФайлаПакета, Отчет);
	Если НЕ РезультатРегистрацииОтправки Тогда
		Результат = Ложь;
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Если ВыполняемоеОповещение <> Неопределено Тогда
		ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Истина);
	КонецЕсли;

КонецПроцедуры

Функция СформироватьПакетОтчетности(Форма, Отчет, КонтролирующийОрган, ДвоичныеДанныеФайлаПакета = Неопределено, КороткоеИмяФайлаПакета = Неопределено)
	Перем КороткоеИмяФайлаВыгрузки, ТекстВыгрузки, КодировкаТекстаВыгрузки;
	
	// пытаемся выгрузить отчет
	РезультатВыгрузки = ВыгрузитьРегламентированныйОтчет(Отчет, Ложь, ТекстВыгрузки, КороткоеИмяФайлаВыгрузки, КодировкаТекстаВыгрузки, , , Форма.УникальныйИдентификатор);
	Если НЕ РезультатВыгрузки Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Возврат СдачаОтчетностиЧерезСервисСпецоператораВызовСервера.ВыгрузитьСведенияОДоверенностиИСформироватьФайлПакета(Отчет,
																													КонтролирующийОрган,
																													ТекстВыгрузки,
																													КороткоеИмяФайлаВыгрузки,
																													КодировкаТекстаВыгрузки,
																													ДвоичныеДанныеФайлаПакета,
																													КороткоеИмяФайлаПакета);
	
КонецФункции

#КонецОбласти