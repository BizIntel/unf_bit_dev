////////////////////////////////////////////////////////////////////////////////
// Подсистема "Базовая функциональность" (сервер, повт. исп.).
// Обслуживает подключаемые команды.
// Выполняется на сервере, возвращаемые значения кэшируются на время сеанса.
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныеПроцедурыИФункции

Функция НастройкиРазмещения(Знач ИмяФормы, Знач ИспользуемыеПодменю) Экспорт
	Возврат ПодключаемыеКоманды.НастройкиРазмещения(ИмяФормы, ИспользуемыеПодменю);
КонецФункции

Функция Параметры() Экспорт
	Параметры = Константы.ПараметрыПодключаемыхКоманд.Получить().Получить();
	Если Параметры = Неопределено Тогда
		ПодключаемыеКоманды.ОперативноеОбновлениеОбщихДанныхКонфигурации();
		Параметры = Константы.ПараметрыПодключаемыхКоманд.Получить().Получить();
		Если Параметры = Неопределено Тогда
			Возврат Новый Структура("ПодключенныеОбъекты", Новый Соответствие);
		КонецЕсли;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ПараметрыСеанса.ВерсияРасширений) Тогда
		ХранилищеЗначения = СтандартныеПодсистемыСервер.ПараметрРаботыРасширения(ПодключаемыеКоманды.ПолноеИмяПодсистемы());
		Если ХранилищеЗначения = Неопределено Тогда
			Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
				УстановитьПривилегированныйРежим(Истина);
				Если Не ПривилегированныйРежим() Тогда
					ВызватьИсключение НСтр("ru = 'Не удалось обновить вспомогательные данные расширений. Обратитесь к администратору.'");
				КонецЕсли;
			КонецЕсли;
			ПодключаемыеКоманды.ПриЗаполненииВсехПараметровРаботыРасширений();
			ХранилищеЗначения = СтандартныеПодсистемыСервер.ПараметрРаботыРасширения(ПодключаемыеКоманды.ПолноеИмяПодсистемы());
			Если ХранилищеЗначения = Неопределено Тогда
				Возврат Параметры;
			КонецЕсли;
		КонецЕсли;
		ПараметрыРасширений = ХранилищеЗначения.Получить();
		ДополнитьСоответствиеСМассивами(Параметры.ПодключенныеОбъекты, ПараметрыРасширений.ПодключенныеОбъекты);
	КонецЕсли;
	
	Возврат Параметры;
КонецФункции

Процедура ДополнитьСоответствиеСМассивами(СоответствиеПриемник, СоответствиеИсточник)
	Для Каждого КлючИЗначение Из СоответствиеИсточник Цикл
		МассивПриемника = СоответствиеПриемник[КлючИЗначение.Ключ];
		Если МассивПриемника = Неопределено Тогда
			СоответствиеПриемник.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		Иначе
			Для Каждого Значение Из КлючИЗначение.Значение Цикл
				Если МассивПриемника.Найти(Значение) = Неопределено Тогда
					МассивПриемника.Добавить(Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти
