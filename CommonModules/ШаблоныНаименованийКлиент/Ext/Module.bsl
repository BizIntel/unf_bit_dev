
#Область ПрограммныйИнтерфейс

Процедура НаименованиеОбработкаВыбора(Форма, ВыбранноеЗначение, КатегорияНоменклатуры, Наименование, ВидНаименования) Экспорт
	
	Если ВыбранноеЗначение = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Шаблон = ВыбранноеЗначение.ШаблонСсылка;
	
	Если ТипЗнч(Шаблон) <> Тип("СправочникСсылка.ШаблоныНаименований") Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Шаблон) Тогда
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("Отбор", Новый Структура("Владелец", КатегорияНоменклатуры));
		ПараметрыФормы.Вставить("ВидНаименования", ВидНаименования);
		ОткрытьФорму("Справочник.ШаблоныНаименований.ФормаСписка", ПараметрыФормы);
		Возврат;
	КонецЕсли;
	
	Наименование = ВыбранноеЗначение.Наименование;
	ДобавитьИнформациюОШаблонеВНастройкиФормы(Форма, Шаблон, ВыбранноеЗначение.ШаблонДляФормирования, ВидНаименования);
	
КонецПроцедуры

Процедура ОбновитьСписокСвойствОбъектаПриИзмененииСтандартногоРеквизита(Форма, ИмяРеквизита) Экспорт
	
	Если ИмяРеквизита = "Наименование" И Форма.ШаблоныНаименованийНастройки.НаименованиеПолучено Тогда
		Возврат;
	КонецЕсли;
	
	Если ИмяРеквизита = "Наименование" Тогда
		Форма.ШаблоныНаименованийНастройки.НаименованиеПолучено = Истина;
	КонецЕсли;
	
	Форма.ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования = Истина;
	
	РедактируемоеСвойствоЗначение = Форма.Объект[ИмяРеквизита];
	ЭлементСпискаСвойств = Форма.ШаблоныНаименованийНастройки.СписокСвойств.НайтиПоЗначению(ИмяРеквизита);
	Если ЭлементСпискаСвойств <> Неопределено Тогда
		ЭлементСпискаСвойств.Представление = РедактируемоеСвойствоЗначение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСписокСвойствОбъектаПриИзмененииДопРеквизита(Форма, Элемент) Экспорт
	
	Форма.ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования = Истина;
	
	Если НЕ Форма.Свойства_ИспользоватьСвойства
	 ИЛИ НЕ Форма.Свойства_ИспользоватьДопРеквизиты Тогда
		
		Возврат;
	КонецЕсли;
	
	РедактируемоеСвойствоСтроки = Форма.Свойства_ОписаниеДополнительныхРеквизитов.НайтиСтроки(Новый Структура("ИмяРеквизитаЗначение", Элемент.Имя));
	Если РедактируемоеСвойствоСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если Элемент.Вид = ВидПоляФормы.ПолеВвода Тогда
		Формат = Элемент.Формат;
	Иначе
		Формат = "";
	КонецЕсли;
	
	РедактируемоеСвойствоОписание = РедактируемоеСвойствоСтроки[0];
	РедактируемоеСвойствоЗначение = Форма[Элемент.Имя];
	Если ТипЗнч(РедактируемоеСвойствоЗначение) = Тип("Булево") Тогда
		РедактируемоеСвойствоЗначение = ?(РедактируемоеСвойствоЗначение = Истина, РедактируемоеСвойствоОписание.Наименование, "");
	Иначе
		РедактируемоеСвойствоЗначение = Формат(РедактируемоеСвойствоЗначение, Формат);
	КонецЕсли;
	РедактируемоеСвойствоНаименование = Строка(РедактируемоеСвойствоОписание.Свойство);
	ЭлементСпискаСвойств = Форма.ШаблоныНаименованийНастройки.СписокСвойств.НайтиПоЗначению(РедактируемоеСвойствоНаименование);
	Если ЭлементСпискаСвойств <> Неопределено Тогда
		ЭлементСпискаСвойств.Представление = РедактируемоеСвойствоЗначение;
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбновитьСписокСвойствОбъектаПриИзмененииТаблицыДопРеквизитов(Форма, ТаблицаСвойств) Экспорт
	
	Форма.ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования = Истина;
	
	Для каждого Элемент Из ТаблицаСвойств Цикл
		
		СвойствоНаименование = Строка(Элемент.Свойство);
		СвойствоЗначение     = Элемент.Значение;
		
		Если ТипЗнч(СвойствоЗначение) = Тип("Булево") Тогда
			СвойствоЗначение = ?(СвойствоЗначение = Истина, Элемент.Наименование, "");
		Иначе
			СвойствоЗначение = Формат(СвойствоЗначение, Элемент.ФорматСвойства);
		КонецЕсли;
		
		ЭлементСпискаСвойств = Форма.ШаблоныНаименованийНастройки.СписокСвойств.НайтиПоЗначению(СвойствоНаименование);
		Если ЭлементСпискаСвойств <> Неопределено Тогда
			ЭлементСпискаСвойств.Представление = СвойствоЗначение;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ОбновитьНаименование(Форма, Наименование, ВидНаименования) Экспорт
	
	Для каждого Шаблон Из Форма.ШаблоныНаименованийКэш Цикл
		
		Если Шаблон.ВидНаименования <> ВидНаименования Тогда
			Продолжить;
		КонецЕсли;
		
		Наименование = ШаблоныНаименованийКлиентСервер.СформироватьНаименованиеПоШаблону(
			Форма.ШаблоныНаименованийНастройки.СписокСвойств, 
			Шаблон.ШаблонДляФормирования
		);
		
		Прервать;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура НаименованиеПриИзменении(Форма, Наименование, ВидНаименования) Экспорт
	
	УдалитьИнформациюОШаблонеВНастройкиФормы(Форма, ВидНаименования);
	
КонецПроцедуры

Процедура ОбработкаОповещенияШаблоныНаименованийПослеЗаписи(Форма) Экспорт
	
	Форма.ШаблоныНаименованийНастройки.ТребуетсяОбновитьНаименования = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьИнформациюОШаблонеВНастройкиФормы(Форма, Шаблон, ШаблонДляФормирования, ВидНаименования)
	
	УдалитьИнформациюОШаблонеВНастройкиФормы(Форма, ВидНаименования);
	
	НоваяСтрока = Форма.ШаблоныНаименованийКэш.Добавить();
	НоваяСтрока.ШаблонСсылка          = Шаблон;
	НоваяСтрока.ШаблонДляФормирования = ШаблонДляФормирования;
	НоваяСтрока.ВидНаименования       = ВидНаименования;
	
КонецПроцедуры

Процедура УдалитьИнформациюОШаблонеВНастройкиФормы(Форма, ВидНаименования)
	
	Строки = Форма.ШаблоныНаименованийКэш.НайтиСтроки(Новый Структура("ВидНаименования", ВидНаименования));
	Если Строки.Количество() <> 0 Тогда
		Форма.ШаблоныНаименованийКэш.Удалить(Строки[0]);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти