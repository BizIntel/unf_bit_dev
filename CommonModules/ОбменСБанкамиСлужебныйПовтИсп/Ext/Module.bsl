////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиСлужебныйПовтИсп: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет массив актуальными видами электронных документов.
//
// Возвращаемое значение:
//  Массив - виды актуальных ЭД.
//
Функция АктуальныеВидыЭД() Экспорт
	
	МассивЭД = Новый Массив;
	ОбменСБанкамиПереопределяемый.ПолучитьАктуальныеВидыЭД(МассивЭД);
	
	МассивЭД.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросЗонд);
	МассивЭД.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросОСостоянииЭД);
	МассивЭД.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросНаОтзывЭД);
	МассивЭД.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросВыписки);

	МассивВозврата = ОбщегоНазначенияКлиентСервер.СвернутьМассив(МассивЭД);
	
	Возврат МассивВозврата;
	
КонецФункции

// Возвращает текст программы с указанием версии, используемой для обмена с банком.
//
// Параметры:
//  КоличествоСимволов - Число - ограничение на количество символов по версии программы, по умолчанию 100.
//
// Возвращаемое значение:
//  Строка - текст программы с указанием версии.
//
Функция ВерсияПрограммыКлиентаДляБанка(КоличествоСимволов = 100) Экспорт
	
	ВерсияПрограммы = СтрШаблон(НСтр("ru = '1С - БЭД: %1; %2: %3'"),
		ОбновлениеИнформационнойБазыБЭД.ВерсияБиблиотеки(),
		Метаданные.Имя,
		Метаданные.Версия);

	Если КоличествоСимволов > 0 Тогда
		ВерсияПрограммы = Лев(ВерсияПрограммы, КоличествоСимволов);
	КонецЕсли;
	
	Возврат СокрЛП(ВерсияПрограммы);
	
КонецФункции

// Возвращает пустую ссылку на справочник.
//
// Параметры:
//  ИмяСправочника - Строка - название справочника.
//
// Возвращаемое значение:
//  Ссылка - пустая ссылка на справочник.
//
Функция ПустаяСсылкаСправочника(ИмяСправочника) Экспорт
	
	Результат = Неопределено;
	
	ИмяПрикладногоСправочника = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника(ИмяСправочника);
	Если ЗначениеЗаполнено(ИмяПрикладногоСправочника) Тогда
		Результат = Справочники[ИмяПрикладногоСправочника].ПустаяСсылка();
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

// Получает данные внешней обработки.
// 
// Параметры:
//    ИмяМодуля - Строка - название модуля, данные которого нужно вернуть;
//
// Возвращаемое значение:
//    Структура - данные внешней обработки:
//      * Версия - строка - версия обработки;
//      * Наименование - Строка - представление внешней обработки;
//      * ДвоичныеДанныеМодуля - ДвоичныеДанные - двоичные данные внешней обработки.
//
Функция ДанныеВнешнейОбработки(ИмяМодуля) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеВнешнихМодулей = Константы.ВнешниеФайлыОбменСБанками.Получить().Получить();
	Если ДанныеВнешнихМодулей = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ДанныеВнешнихМодулей.Свойство("ВнешниеОбработки") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеМодуля = ДанныеВнешнихМодулей.ВнешниеОбработки.Получить(ИмяМодуля);
	
	Возврат ДанныеМодуля;
	
КонецФункции

// Возвращает данные по банкам, поддерживающих прямой обмен.
//
// Возвращаемое значение:
//   ТабличныйДокумент - содержит данные по банкам.
//
Функция СписокБанков() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ИспользуетсяТестовыйРежим = Ложь;
	ОбменСБанкамиПереопределяемый.ПроверитьИспользованиеТестовогоРежима(ИспользуетсяТестовыйРежим);
	
	Если ИспользуетсяТестовыйРежим Тогда
		Макет = Справочники.НастройкиОбменСБанками.ПолучитьМакет("СписокБанковВТестовомРежиме");
	Иначе
		ДанныеВнешнихФайлов = Константы.ОбщиеФайлыОбменСБанками.Получить().Получить();
		Если ДанныеВнешнихФайлов = Неопределено ИЛИ НЕ ДанныеВнешнихФайлов.Свойство("СписокБанков")
				ИЛИ ДанныеВнешнихФайлов.СписокБанков = Неопределено Тогда  // список еще не подкачивался из интернета
			Макет = Справочники.НастройкиОбменСБанками.ПолучитьМакет("СписокБанков");
		Иначе
			Попытка
				ВремФайл = ПолучитьИмяВременногоФайла("mxl");
				ДанныеВнешнихФайлов.СписокБанков.Записать(ВремФайл);
				Макет = Новый ТабличныйДокумент;
				Макет.Прочитать(ВремФайл);
				ЭлектронноеВзаимодействиеСлужебный.УдалитьВременныеФайлы(ВремФайл);
			Исключение
				// Если не удалось прочитать файл, то берем список из конфигурации
				ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ВидОперации = НСтр("ru = 'Чтение списка банков сервиса 1С:ДиректБанк из скачанного файла.'");
				ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ТекстОшибки, , 1);
				Макет = Справочники.НастройкиОбменСБанками.ПолучитьМакет("СписокБанков");
			КонецПопытки;
		КонецЕсли;
	КонецЕсли;

	Возврат Макет;
	
КонецФункции

// Возвращает список внешних компонент, сохраненных в информационной базе
// 
// Возвращаемое значение:
// Массив - информация о компонентах. В элементах структура со следующими полями:
//    * ИмяМодуля - Строка - уникальное имя модуля
//    * Версия - Строка - версия модуля
//    * Наименование - Строка - представление модуля для отображения пользователю.
//
Функция ВнешниеКомпонентыИнформационнойБазы() Экспорт
	
	МассивВозврата = Новый Массив;
	
	УстановитьПривилегированныйРежим(Истина);
	
	ОбщиеФайлыОбменСБанками = Константы.ОбщиеФайлыОбменСБанками.Получить().Получить();
	
	Если ОбщиеФайлыОбменСБанками = Неопределено ИЛИ НЕ ОбщиеФайлыОбменСБанками.Свойство("ВнешниеКомпоненты") Тогда
		Возврат МассивВозврата;
	КонецЕсли;

	ВнешниеКомпонентыИзКеша = ОбщиеФайлыОбменСБанками.ВнешниеКомпоненты;

	Для Каждого Элемент Из ВнешниеКомпонентыИзКеша Цикл
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ИмяМодуля", Элемент.Ключ);
		СтруктураЗаписи.Вставить("Версия", Элемент.Значение.Версия);
		СтруктураЗаписи.Вставить("Наименование", Элемент.Значение.Наименование);
		МассивВозврата.Добавить(СтруктураЗаписи);
	КонецЦикла;
	
	Возврат МассивВозврата;
	
КонецФункции

// Получает данные внешней компоненты.
// 
// Параметры:
//    ИмяМодуля - Строка - название модуля, данные которого нужно вернуть.
//
// Возвращаемое значение:
//    Структура - данные внешней обработки;
//      * Версия - строка - версия обработки;
//      * Наименование - Строка - представление внешней компоненты;
//      * ДвоичныеДанныеМодуля - ДвоичныеДанные - двоичные данные внешней компоненты.
//
Функция ДанныеВнешнейКомпоненты(ИмяМодуля) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	ДанныеВнешнихМодулей = Константы.ОбщиеФайлыОбменСБанками.Получить().Получить();
	Если ДанныеВнешнихМодулей = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если НЕ ДанныеВнешнихМодулей.Свойство("ВнешниеКомпоненты") Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ДанныеМодуля = ДанныеВнешнихМодулей.ВнешниеКомпоненты.Получить(ИмяМодуля);
	
	Возврат ДанныеМодуля;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет наличие механизма платформы, предупреждающего об опасных действиях.
//
// Возвращаемое значение:
//  Булево - если Истина, тогда работает механизм предупреждений безопасности.
//
Функция ЕстьЗащитаОтОпасныхДействий() Экспорт
	
	Свойства = Новый Структура("ЗащитаОтОпасныхДействий");
	ЗаполнитьЗначенияСвойств(Свойства, ПользователиИнформационнойБазы.ТекущийПользователь());
	Возврат Свойства.ЗащитаОтОпасныхДействий <> Неопределено;
	
КонецФункции

#КонецОбласти