 
#Область ПрограммныйИнтерфейс

// Функция возвращает возможность работы модуля в асинхронном режиме.
// Стандартные команды модуля:
// - ПодключитьУстройство
// - ОтключитьУстройство
// - ВыполнитьКоманду
// Команды модуля для работы асинхронном режиме (должны быть определены):
// - НачатьПодключениеУстройства
// - НачатьОтключениеУстройства
// - НачатьВыполнениеКоманды.
//
Функция ПоддержкаАсинхронногоРежима() Экспорт
	
	Возврат Ложь;
	
КонецФункции

// Функция осуществляет подключение устройства.
//
Функция ПодключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт
	
	Результат  = Истина;	
	ВыходныеПараметры = Новый Массив();
	
	// Проверка настроенных параметров.
	БитДанных = Неопределено;
	Порт      = Неопределено;
	Скорость  = Неопределено;
	СтопБит   = Неопределено;
	Префикс   = Неопределено;
	Суффикс   = Неопределено;
	ТаймаутCOM   = Неопределено;
	COMКодировка = Неопределено;
	
	Параметры.Свойство("БитДанных", БитДанных);
	Параметры.Свойство("Порт",      Порт);
	Параметры.Свойство("Скорость",  Скорость);
	Параметры.Свойство("СтопБит",   СтопБит);
	Параметры.Свойство("Префикс",   Префикс);
	Параметры.Свойство("Суффикс",   Суффикс);
	Параметры.Свойство("ТаймаутCOM",   ТаймаутCOM);
	Параметры.Свойство("COMКодировка", COMКодировка);
	
	Если БитДанных = Неопределено
	 ИЛИ Порт      = Неопределено
	 ИЛИ Скорость  = Неопределено
	 ИЛИ СтопБит   = Неопределено
	 ИЛИ Префикс   = Неопределено
	 ИЛИ Суффикс   = Неопределено Тогда
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Не настроены параметры устройства.
		|Для корректной работы устройства необходимо задать параметры его работы.
		|Сделать это можно при помощи формы ""Настройка параметров"" модели
		|подключаемого оборудования в форме ""Подключение и настройка оборудования"".'"));

		Результат = Ложь;
	КонецЕсли;
	// Конец: Проверка настроенных параметров.

	Если Результат Тогда
		ВыходныеПараметры.Добавить("СканерШтрихкода");
		ВыходныеПараметры.Добавить(Новый Массив());
		ВыходныеПараметры[1].Добавить("ПолученШтрихкод");
		
		Попытка
			Результат = (ОбъектДрайвера.Подсоединить(ВыходныеПараметры[0]) = 0);
		Исключение
			Результат = Ложь;
		КонецПопытки;
		
		Если НЕ Результат Тогда
			ВыходныеПараметры.Очистить();
			ВыходныеПараметры.Добавить(999);
			ВыходныеПараметры.Добавить(НСтр("ru='Ошибка при подключении устройства.
			|Проверьте настройки порта.'"));
		КонецЕсли;

		Если Результат = Истина Тогда
			ОбъектДрайвера.БитДанных  = Параметры.БитДанных;
			ОбъектДрайвера.Порт       = Параметры.Порт;
			ОбъектДрайвера.Скорость   = Параметры.Скорость;
			
			// Начиная с версии 8.0.12.2 произошли изменения в интерфейсе драйвера.
			Если ВерсияСтрокаВЧисло(ОбъектДрайвера.ПолучитьНомерВерсии()) >= 8001202 И Параметры.Порт=0 Тогда
				ОбъектДрайвера.ПрефиксСтрока = СПВСимволы(Параметры.Префикс, 0);
				ОбъектДрайвера.СуффиксСтрока = СПВСимволы(Параметры.Суффикс, 13);
				ОбъектДрайвера.Таймаут = Параметры.Таймаут;
			Иначе
				ОбъектДрайвера.СтопСимвол = КодСимвола(СПВСимволы(Параметры.Суффикс, 13));
				ОбъектДрайвера.Порт       = Параметры.Порт;
			КонецЕсли;
			
			// Начиная с версии 8.1.4.1 произошли изменения в интерфейсе драйвера.
			Если ВерсияСтрокаВЧисло(ОбъектДрайвера.ПолучитьНомерВерсии()) >= 8010401 И Параметры.Порт > 0 Тогда
				ОбъектДрайвера.ТаймаутCOM = ?(Параметры.ТаймаутCOM = Неопределено, 5, Параметры.ТаймаутCOM);
				ОбъектДрайвера.COMКодировка = ?(Параметры.COMКодировка = Неопределено, "UTF-8", Параметры.COMКодировка);
			КонецЕсли;
			
			ОбъектДрайвера.ИмяСобытия = ВыходныеПараметры[1][0];
			
			Попытка
				Результат = (ОбъектДрайвера.Занять(1) = 0);
			Исключение
				Результат = Ложь;
			КонецПопытки;
			
			Если Результат Тогда
				ОбъектДрайвера.УстройствоВключено = 1;
				ОбъектДрайвера.ПосылкаДанных      = 1;
				ОбъектДрайвера.ОчиститьВход();
				ОбъектДрайвера.ОчиститьВыход();
				
				Результат = (ОбъектДрайвера.УстройствоВключено = 1);
				Если НЕ Результат Тогда
					ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
					ВыходныеПараметры.Очистить();
					ВыходныеПараметры.Добавить(999);
					ВыходныеПараметры.Добавить(НСтр("ru='Ошибка при подключении устройства.
					|Проверьте настройки порта.'"));
				КонецЕсли;
			Иначе
				ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

				ВыходныеПараметры.Очистить();
				ВыходныеПараметры.Добавить(999);
				ВыходныеПараметры.Добавить(НСтр("ru='Не удалось занять устройство.
				|Проверьте настройки порта.'"));
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Возврат Результат;

КонецФункции

// Функция осуществляет отключение устройства.
//
Функция ОтключитьУстройство(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт

	Результат = Истина;

	ВыходныеПараметры = Новый Массив();

	ОбъектДрайвера.УстройствоВключено = 0;
	ОбъектДрайвера.Освободить();
	ОбъектДрайвера.Отсоединить();

	Возврат Результат;

КонецФункции

// Функция получает, обрабатывает и перенаправляет на исполнение команду к драйверу.
//
Функция ВыполнитьКоманду(Команда, ВходныеПараметры = Неопределено, ВыходныеПараметры = Неопределено,
                         ОбъектДрайвера, Параметры, ПараметрыПодключения) Экспорт

	Результат = Истина;

	ВыходныеПараметры = Новый Массив();

	// Обработка события от устройства.
	Если Команда = "ОбработатьСобытие" Тогда
		Событие = ВходныеПараметры[0];
		Данные  = ВходныеПараметры[1];
		Результат = ОбработатьСобытие(ОбъектДрайвера, Параметры, ПараметрыПодключения, Событие, Данные, ВыходныеПараметры);

	// Завершение обработки события от устройства.
	ИначеЕсли Команда = "ЗавершитьОбработкуСобытия" Тогда
		Результат = ЗавершитьОбработкуСобытия(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);
		
	// Тестирование устройства
	ИначеЕсли Команда = "CheckHealth" Тогда
		Результат = ТестУстройства(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Настройка параметров журналирования драйвера.
	ИначеЕсли Команда = "ПараметрыЖурналирования" Тогда
		Результат = ПараметрыЖурналирования(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Получение версии драйвера
	ИначеЕсли Команда = "ПолучитьВерсиюДрайвера" Тогда
		Результат = ПолучитьВерсиюДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры);

	// Указанная команда не поддерживается данным драйвером.
	Иначе
		ВыходныеПараметры.Добавить(999);
		ВыходныеПараметры.Добавить(НСтр("ru='Команда ""%Команда%"" не поддерживается данным драйвером.'"));
		ВыходныеПараметры[1] = СтрЗаменить(ВыходныеПараметры[1], "%Команда%", Команда);

		Результат = Ложь;
	КонецЕсли;

	Возврат Результат;

КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Возвращает версию компоненты расположенной в макете.
//
Функция ПолучитьВерсиюВнешнейКомпоненты() 
	
	Возврат "8.1.7.5";
	
КонецФункции

// Функция осуществляет обработку внешних событий торгового оборудования.
//
Функция ОбработатьСобытие(ОбъектДрайвера, Параметры, ПараметрыПодключения, Событие, Данные, ВыходныеПараметры) Экспорт
	
	Результат = Истина;
	ШК = СокрЛП(Данные);
	
	ОбъектДрайвера.ПосылкаДанных = 0;
	
	Если Параметры.Префикс <> 0 Тогда
		Если Параметры.Префикс = КодСимвола(Лев(ШК, 1)) Тогда
			ШК = Сред(ШК, 2);
		КонецЕсли;
	КонецЕсли;
	
	ВыходныеПараметры.Добавить("ScanData");
	ВыходныеПараметры.Добавить(Новый Массив());
	ВыходныеПараметры[1].Добавить(ШК);
	ВыходныеПараметры[1].Добавить(Новый Массив());
	ВыходныеПараметры[1][1].Добавить(Данные);
	ВыходныеПараметры[1][1].Добавить(ШК);
	ВыходныеПараметры[1][1].Добавить(0);
	
	Возврат Результат;
	
КонецФункции

// Процедура вызывается, когда система готова принять следующее событие от устройства.
//
Функция ЗавершитьОбработкуСобытия(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры) Экспорт
	
	Результат = Истина;
	ОбъектДрайвера.ПосылкаДанных = 1;
	
	Возврат Результат;
	
КонецФункции

// Осуществляется открытие формы проверки параметров драйвера.
//
Функция ТестУстройства(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)
	
	Результат = Истина;

	ОбъектДрайвера.Порт      = Параметры.Порт;
	ОбъектДрайвера.Скорость  = Параметры.Скорость;
	ОбъектДрайвера.БитДанных = Параметры.БитДанных;
		
	// Начиная с версии 8.0.12.2 произошли изменения в интерфейсе драйвера.
	Если ВерсияСтрокаВЧисло(ОбъектДрайвера.ПолучитьНомерВерсии()) >= 8001202 Тогда
		ОбъектДрайвера.Таймаут = Параметры.Таймаут;
		// Начиная с версии 8.1.4.1 произошли изменения в интерфейсе драйвера.
		Если ВерсияСтрокаВЧисло(ОбъектДрайвера.ПолучитьНомерВерсии()) >= 8010401 Тогда
			ОбъектДрайвера.ТаймаутCOM = ?(Параметры.ТаймаутCOM = Неопределено, 5, Параметры.ТаймаутCOM);
			ОбъектДрайвера.COMКодировка = ?(Параметры.COMКодировка = Неопределено, "UTF-8", Параметры.COMКодировка);
		КонецЕсли;
		ОбъектДрайвера.ТестУстройства();
		Параметры.Таймаут = ОбъектДрайвера.Таймаут;
		// Начиная с версии 8.1.4.1 произошли изменения в интерфейсе драйвера.
		Если ВерсияСтрокаВЧисло(ОбъектДрайвера.ПолучитьНомерВерсии()) >= 8010401 Тогда
			Параметры.ТаймаутCOM = ОбъектДрайвера.ТаймаутCOM;
		КонецЕсли;
	Иначе
		ОбъектДрайвера.ТестУстройства();
	КонецЕсли;
	
	Возврат Результат;

КонецФункции

// Осуществляется открытие формы настройки параметров журналирования драйвера.
//
Функция ПараметрыЖурналирования(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)

	Результат = Истина;

	ОбъектДрайвера.Скорость  = Параметры.Скорость;
	ОбъектДрайвера.БитДанных = Параметры.БитДанных;
	

	ОбъектДрайвера.ПараметрыЖурналирования();

	Возврат Результат;

КонецФункции

// Функция возвращает версию установленного драйвера.
//
Функция ПолучитьВерсиюДрайвера(ОбъектДрайвера, Параметры, ПараметрыПодключения, ВыходныеПараметры)
	
	Результат = Истина;
	
	ВыходныеПараметры.Добавить(НСтр("ru='Установлен'"));
	ВыходныеПараметры.Добавить(НСтр("ru='Не определена'"));
	
	Попытка
		Версия = ОбъектДрайвера.ПолучитьНомерВерсии();
		ВыходныеПараметры[1] = Версия;
		// Дополнительные параметры
		ВыходныеПараметры.Добавить(ПолучитьВерсиюВнешнейКомпоненты());
		ВыходныеПараметры.Добавить(ВерсияСтрокаВЧисло(Версия));
		ВыходныеПараметры.Добавить(ВерсияСтрокаВЧисло(ПолучитьВерсиюВнешнейКомпоненты()));
	Исключение
		Результат = Ложь;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Функция преобразует строковое представление версии (с тремя точками) к числовому (пригодному для сравнения).
//
Функция ВерсияСтрокаВЧисло(ВерсияСтрока) 
	
	Попытка
		Версия = ВерсияСтрока;
		Результат = Число(Лев(Версия, Найти(Версия, ".")-1));
		Версия = Сред(Версия, Найти(Версия, ".") + 1);
		Результат = Результат*100 + Число(Лев(Версия, Найти(Версия, ".")-1));
		Версия = Сред(Версия, Найти(Версия, ".") + 1);
		Результат = Результат*100 + Число(Лев(Версия, Найти(Версия, ".")-1));
		Версия = Сред(Версия, Найти(Версия, ".") + 1);
		Результат = Результат*100 + Число(Версия);
	Исключение
		Возврат 0;
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

// Функция преобразует суффикс/префикс в символьную строку для передачи в драйвер
// из формата #13#10 
// из формата 13(Строка) или  10(Число)
// из формата (13)CR.
//
Функция СПВСимволы(СуффиксНомер, ПоУмолчанию) 
	
	Номер           = СуффиксНомер;
	СуффиксДрайвера = "";
	Позиция         = Найти(Номер, "#");
	
	Если Позиция = 0 Тогда
		Попытка
			ПозицияЛев = Найти(Номер, "(");
			ПозицияПрав = Найти(Номер, ")");
			Если ПозицияПрав >= ПозицияЛев Тогда
				Номер = Сред(Номер, ПозицияЛев+1, ПозицияПрав-ПозицияЛев-1);
				Номер = Число(Номер);
			Иначе
				Номер = ПоУмолчанию;	
			КонецЕсли;			
		Исключение
			Номер = ПоУмолчанию;
		КонецПопытки;
		
		Возврат Символ(Номер);
	КонецЕсли;
	
	Номер = Сред(Номер, Найти(Номер, "#") + 1);
	
	Пока Истина Цикл
		ВремНомер = Номер;
		Позиция = Найти(Номер, "#");
		Если Позиция > 0 Тогда
			ВремНомер = Лев(Номер, Позиция-1);
		КонецЕсли;	
		СуффиксДрайвера = СуффиксДрайвера + Символ(Число(ВремНомер));
		Если Позиция = 0 Тогда 
			Прервать 
		КонецЕсли;
		Номер = Сред(Номер, Найти(Номер, "#") + 1);
	КонецЦикла;

	Возврат СуффиксДрайвера;
	
КонецФункции

#КонецОбласти
