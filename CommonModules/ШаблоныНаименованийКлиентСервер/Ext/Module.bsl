
#Область ПрограммныйИнтерфейс

Функция СформироватьНаименованиеПоШаблону(СписокЗначений, Шаблон) Экспорт
	
	Наименование = "";
	
	БлокиШаблона = РазделитьШаблонНаБлоки(Шаблон);
	Для каждого Блок Из БлокиШаблона Цикл
		
		Если НЕ Блок.ЭтоПараметр Тогда
			Наименование = Наименование + Блок.ТекстСлева;
			Продолжить;
		КонецЕсли;
		
		Если Блок.ЭтоПараметр Тогда
			
			ЗначениеПараметра = "";
			ЗначениеПараметраЭл = СписокЗначений.НайтиПоЗначению(Блок.Параметр);
			Если ЗначениеПараметраЭл <> Неопределено Тогда
				ЗначениеПараметра = ЗначениеПараметраЭл.Представление;
			КонецЕсли;
			
			Если ЗначениеЗаполнено(ЗначениеПараметра) Тогда
				Наименование = Наименование + Блок.ТекстСлева + ЗначениеПараметра + Блок.ТекстСправа;
			КонецЕсли;
			
			Продолжить;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ШаблоныНаименованийСокрЛП(Наименование);
	
	Возврат Наименование;
	
КонецФункции

Функция НаименованиеСформированоПоШаблону(Форма, ВидНаименования) Экспорт
	
	Для каждого ШаблонКэш Из Форма.ШаблоныНаименованийКэш Цикл
		
		Если ШаблонКэш.ВидНаименования = ВидНаименования Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазделитьШаблонНаБлоки(Знач Шаблон) Экспорт
	
	Блоки = Новый Массив;
	
	Пока Истина Цикл
		
		ПервыйСимвол    = Найти(Шаблон, "{");
		ПоследнийСимвол = Найти(Шаблон, "}");
		
		Если ПервыйСимвол = 0 ИЛИ ПоследнийСимвол = 0 Тогда
			Прервать;
		КонецЕсли;
		
		Текст = Лев(Шаблон, ПервыйСимвол - 1);
		Блоки.Добавить(НовыйБлок(Ложь, Текст));
		
		ТелоБлока = Сред(Шаблон, ПервыйСимвол + 1, ПоследнийСимвол - ПервыйСимвол - 1);
		Шаблон = Прав(Шаблон, СтрДлина(Шаблон) - ПоследнийСимвол);
		
		ПервыйСимвол    = Найти(ТелоБлока, "[");
		ПоследнийСимвол = Найти(ТелоБлока, "]");
		
		Если ПервыйСимвол <> 0 И ПоследнийСимвол <> 0 Тогда
			Параметр = Сред(ТелоБлока, ПервыйСимвол + 1, ПоследнийСимвол - ПервыйСимвол - 1);
			ЭтоПараметр = Истина;
		Иначе
			Параметр = "";
			ЭтоПараметр = Ложь;
		КонецЕсли;
		
		ТекстСлева = Лев(ТелоБлока, ПервыйСимвол - 1);
		ТекстСправа = Прав(ТелоБлока, СтрДлина(ТелоБлока) - ПоследнийСимвол);
		
		Блоки.Добавить(НовыйБлок(ЭтоПараметр, ТекстСлева, Параметр, ТекстСправа));
		
	КонецЦикла;
	
	Блоки.Добавить(НовыйБлок(Ложь, Шаблон));
	
	Возврат Блоки;
	
КонецФункции

Функция НовыйБлок(ЭтоПараметр, ТекстСлева = "", Параметр = "", ТекстСправа = "")
	
	Возврат Новый Структура(
		"ЭтоПараметр, ТекстСлева, Параметр, ТекстСправа",
		ЭтоПараметр,
		ТекстСлева,
		Параметр,
		ТекстСправа
	);
	
КонецФункции

Процедура ШаблоныНаименованийСокрЛП(Наименование)
	
	СимволыРазделители = Новый Массив;
	СимволыРазделители.Добавить(",");
	СимволыРазделители.Добавить(";");
	СимволыРазделители.Добавить("/");
	
	Наименование = СокрЛ(Наименование);
	
	ПервыйСимвол = Лев(Наименование, 1);
	Если СимволыРазделители.Найти(ПервыйСимвол) <> Неопределено Тогда
		Наименование = Прав(Наименование, СтрДлина(Наименование) - 1);
	КонецЕсли;
	
	ПоследнийСимвол = Прав(Наименование, 1);
	Если СимволыРазделители.Найти(ПоследнийСимвол) <> Неопределено Тогда
		Наименование = Лев(Наименование, СтрДлина(Наименование) - 1);
	КонецЕсли;
	
	Наименование = СокрЛП(Наименование);
	
КонецПроцедуры

#КонецОбласти