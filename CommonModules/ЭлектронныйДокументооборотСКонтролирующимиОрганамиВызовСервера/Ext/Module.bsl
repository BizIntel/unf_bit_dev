////////////////////////////////////////////////////////////////////////////////
// ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера
//  
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// 
Функция ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов) Экспорт

	Возврат ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, ИменаРеквизитов);
	
КонецФункции

// 
Функция ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита) Экспорт
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Ссылка, ИмяРеквизита);
	
КонецФункции 

Функция ВыгрузитьДокументСервер(СсылкаНаОтчет, ИдентификаторФормыВызова) Экспорт
	СвойстваФайлаВыгрузки = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ВыгрузитьДокумент(СсылкаНаОтчет, ИдентификаторФормыВызова);
	Если НЕ ЗначениеЗаполнено(СвойстваФайлаВыгрузки) Тогда
		Возврат Неопределено;
	Иначе
		Возврат Новый Структура("АдресФайлаВыгрузки, ИмяФайлаВыгрузки", СвойстваФайлаВыгрузки.АдресФайлаВыгрузки, СвойстваФайлаВыгрузки.ИмяФайлаВыгрузки);
	КонецЕсли;
КонецФункции

// Для переданной организции определяет, является ли она юридическим лицом
// 
// Параметры:
//  СправочникСсылка.Организации - организация, для которой определяется, является ли она юридическим лицом
//
// Результат:
//  Булево - Истина, если организация - юридическое лицо, Ложь - в противном случае
//
Функция ЭтоЮрЛицо(Организация) Экспорт
	Возврат РегламентированнаяОтчетностьПереопределяемый.ЭтоЮридическоеЛицо(Организация);
КонецФункции

Функция ПолучитьПутьВК() Экспорт
	
	Компоненты = Новый Соответствие;
	АктуальнаяВерсия = 0;
	
	ОбработатьМакетыСКомпонентойОбмена(Метаданные.ОбщиеМакеты, Компоненты, АктуальнаяВерсия);
		
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	Если КонтекстЭДОСервер <> Неопределено Тогда
		ОбработатьМакетыСКомпонентойОбмена(
			КонтекстЭДОСервер.Метаданные().Макеты, Компоненты, АктуальнаяВерсия, КонтекстЭДОСервер.ПутьКОбъекту);
	КонецЕсли;
	
	Возврат Компоненты.Получить(АктуальнаяВерсия);

КонецФункции

Функция ФИОФизЛица(ФизЛицо) Экспорт
	
	МассивПоказателей = Новый Массив;
		
	МассивПоказателей.Добавить("Фамилия");
	МассивПоказателей.Добавить("Имя");
	МассивПоказателей.Добавить("Отчество");
	
	ДатаЗначения = ТекущаяДатаСеанса();

	Возврат РегламентированнаяОтчетностьПереопределяемый.ПолучитьСведенияОФизЛице(ФизЛицо, МассивПоказателей, ДатаЗначения);
	
КонецФункции

// Функция возвращает вид отправляемого документа 
// Параметры:      
//  ОбъектСсылка - ссылка на отправляемый объект.
// Результат:
//	СправочникСсылка.ВидыОтправляемыхДокументов, в случае неудачи - пустая ссылка данного типа
//
Функция ПолучитьВидОтправляемогоДокументаПоСсылке(ОбъектСсылка) Экспорт
	
	Если ТипЗнч(ОбъектСсылка) = Тип("ДокументСсылка.УведомлениеОСпецрежимахНалогообложения") Тогда
		ВидУведомления = ОбъектСсылка.ВидУведомления;
		
		Если ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.Форма_1_6_Учет Тогда 
			Возврат Справочники.ВидыОтправляемыхДокументов.ВыборНалоговогоОрганаДляПостановкиНаУчет;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаС09_1 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ОткрытиеЗакрытиеСчета;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаС09_2 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.УчастиеВРоссийскихИностранныхОрганизациях;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаС09_3_1 Тогда 
			Возврат Справочники.ВидыОтправляемыхДокументов.СозданиеОбособленныхПодразделений;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаС09_3_2 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ЗакрытиеОбособленныхПодразделений;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаС09_4 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.РеорганизацияЛиквидацияОрганизации;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаЕНВД1 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ПостановкаНаУчетОрганизацииПлательщикаЕНВД;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаЕНВД2 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ПостановкаНаУчетПредпринимателяПлательщикаЕНВД;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаЕНВД3 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.СнятиеСУчетаОрганизацииПлательщикаЕНВД;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаЕНВД4 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.СнятиеСУчетаПредпринимателяПлательщикаЕНВД;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОбИзмененииОбъектаНалогообложенияПоУСН Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ИзменениеОбъектаУСН;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОбОтказеОтУСН Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ОтказОтУСН;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОбУтратеПраваНаУСН Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.УтратаПраваНаУСН;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОПереходеНаУСН Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ПереходНаУСН;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.УведомлениеОПрекращенииДеятельностиПоУСН Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ПрекращениеДеятельностиУСН;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаУ_ИО Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ФормаУ_ИО;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаТС1 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ФормаТС1;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаТС2 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ФормаТС2;
		ИначеЕсли ВидУведомления = Перечисления.ВидыУведомленийОСпецрежимахНалогообложения.ФормаС09_6 Тогда
			Возврат Справочники.ВидыОтправляемыхДокументов.ФормаС_09_6;
		Иначе
			Возврат Справочники.ВидыОтправляемыхДокументов.ПустаяСсылка();
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура СкрытьЭлементыФормыПриИспользованииОднойОрганизации(Форма, ИмяЭлемента) Экспорт
		
	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		
		Если ТипЗнч(ИмяЭлемента) = Тип("Массив") Тогда
			
			Для Каждого ИмяОдногоЭлемента Из ИмяЭлемента Цикл
				
				СкрываемыйЭлемент = Форма.Элементы.Найти(ИмяОдногоЭлемента);
				
				Если СкрываемыйЭлемент <> Неопределено Тогда
					СкрываемыйЭлемент.Видимость = Ложь;
				КонецЕсли;
				
			КонецЦикла; 
			
		ИначеЕсли ТипЗнч(ИмяЭлемента) = Тип("Строка") Тогда
			
			СкрываемыйЭлемент = Форма.Элементы.Найти(ИмяЭлемента);
			
			Если СкрываемыйЭлемент <> Неопределено Тогда
				
				СкрываемыйЭлемент.Видимость = Ложь;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

// Только для проверки факта отправки в ФНС, ПФР или Росстат
// Функция возвращает вид отправляемого документа 
// Параметры:      
//  Ссылка - ссылка на отправляемый объект.
// Результат:
//	Истина, если объект отправлялся в контролирующие органы
//	Ложь, если объект не отправлялся в контролирующие органы
//
Функция ОбъектОтправлялсяВКонтролирующиеОрганы(Ссылка) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ РАЗЛИЧНЫЕ
		|	СтатусыОтправки.Объект
		|ИЗ
		|	РегистрСведений.СтатусыОтправки КАК СтатусыОтправки
		|ГДЕ
		|	СтатусыОтправки.Статус в (&Статус)
		|	И СтатусыОтправки.Объект = &Объект";

	Статус = Новый Массив;
	Статус.Добавить(Перечисления.СтатусыОтправки.Отправлен);
	Статус.Добавить(Перечисления.СтатусыОтправки.Доставлен);
	Статус.Добавить(Перечисления.СтатусыОтправки.НеПринят);
	Статус.Добавить(Перечисления.СтатусыОтправки.Сдан);
	Статус.Добавить(Перечисления.СтатусыОтправки.ПринятЕстьОшибки);
	
	Запрос.УстановитьПараметр("Статус", Статус);
	Запрос.УстановитьПараметр("Объект", Ссылка);

	РезультатЗапроса = Запрос.Выполнить();
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Результат = Ложь;
	Если ВыборкаДетальныеЗаписи.Количество() > 0 Тогда
		Результат = Истина;
	Иначе
		Результат = Ложь;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат Результат;
	
КонецФункции

Функция ЗаголовокГиперссылкиЗаявления(Организация) Экспорт
	
	ЗаголовокПервичногоЗаявления = НСтр("ru = 'Заявление на подключение к 1С-Отчетности'");
	ЗаголовокВторичногоЗаявления = НСтр("ru = 'Заявление на изменение реквизитов или замену сертификата'");
	
	// Инициализируем контекст
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	// проверяем на наличие права к документообороту
	Если КонтекстЭДОСервер = Неопределено ИЛИ НЕ ЗначениеЗаполнено(Организация) Тогда
		Возврат ЗаголовокПервичногоЗаявления;
	КонецЕсли;
	
	ПоддерживаетсяВторичноеЗаявление = КонтекстЭДОСервер.ПоддерживаетсяВторичноеЗаявление(Организация);
	
	Если ПоддерживаетсяВторичноеЗаявление Тогда
		Возврат ЗаголовокВторичногоЗаявления;
	Иначе
		Возврат ЗаголовокПервичногоЗаявления;
	КонецЕсли;
	
КонецФункции

Функция ПараметрыОтображенияВЖурналеОтчетов() Экспорт
	
	ПараметрыОтчета = Новый Структура;
	
	ПараметрыОтчета.Вставить("ДокСсылка",					Неопределено);
	ПараметрыОтчета.Вставить("НаименованиеОтчета",			"");
	ПараметрыОтчета.Вставить("ВидКонтролирующегоОргана",	Неопределено);
	ПараметрыОтчета.Вставить("КодКонтролирующегоОргана",	"");
	ПараметрыОтчета.Вставить("ДатаНачалаОП",				Дата(1, 1, 1));
	ПараметрыОтчета.Вставить("ДатаОкончанияОП",				Дата(1, 1, 1));
	ПараметрыОтчета.Вставить("Организация",					Неопределено);
	ПараметрыОтчета.Вставить("СтатусОтправки",				"");
	ПараметрыОтчета.Вставить("ПредставлениеВида",			"");
	ПараметрыОтчета.Вставить("ДатаСоздания",				Дата(1, 1, 1));
	ПараметрыОтчета.Вставить("Комментарий",					"");
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Процедура ИзменитьЗначениеВФорме1СОтчетность(Ссылка, ИмяПоля, Знач НовоеЗначение) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Попытка
		
		КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
		СведенияПоОтправляемомуОбъекту = КонтекстЭДОСервер.СведенияПоОтправляемымОбъектам(Ссылка);
		
		СтраницаЖурнала = СведенияПоОтправляемомуОбъекту.СтраницаЖурнала;
		
		Если СтраницаЖурнала = Перечисления.СтраницыЖурналаОтчетность.Отчеты Тогда
			НаборЗаписей = РегистрыСведений.ЖурналОтчетовСтатусы.СоздатьНаборЗаписей();
		Иначе
			НаборЗаписей = РегистрыСведений.ЖурналОтправокВКонтролирующиеОрганы.СоздатьНаборЗаписей();
		КонецЕсли;
		
		НаборЗаписей.Отбор.Ссылка.Установить(Ссылка);
		НаборЗаписей.Прочитать();
		
		Для каждого ЗаписьНабора Из НаборЗаписей Цикл
			ЗаписьНабора[ИмяПоля] = НовоеЗначение;
		КонецЦикла;
		
		НаборЗаписей.Записать();
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Не удалось изменить свойство в таблице форме 1С-Отчетности'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;


КонецПроцедуры

Функция ВидыОтправляемыхУведомленийОСпецрежимахНалогообложения() Экспорт

	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ВидыОтправляемыхДокументов.Ссылка КАК Вид
		|ИЗ
		|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|ГДЕ
		|	ВидыОтправляемыхДокументов.Ссылка В ИЕРАРХИИ(&Уведомления)
		|	И ВидыОтправляемыхДокументов.Ссылка <> &Уведомления";

	Запрос.УстановитьПараметр("Уведомления", Справочники.ВидыОтправляемыхДокументов.Уведомления);

	Результат = Запрос.Выполнить().Выгрузить();
	
	МассивВидовУведомлений = Результат.ВыгрузитьКолонку("Вид");
	Возврат ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.МассивВидовПрочихУведомленийПоддерживающихДокументооборот(МассивВидовУведомлений);
	
КонецФункции

Функция ИмяПеречисления(ЗначениеПеречисления) Экспорт
	
	Если ЗначениеЗаполнено(ЗначениеПеречисления) Тогда
	
		Возврат ОбщегоНазначения.ИмяЗначенияПеречисления(ЗначениеПеречисления);
		
	Иначе
		
		Возврат Неопределено;
		
	КонецЕсли;
	
КонецФункции

Функция ПолноеИмяТипа(Тип) Экспорт

	Возврат Метаданные.НайтиПоТипу(Тип).ПолноеИмя();

КонецФункции

Функция ПолучитьСписокВложений(ВладелецВложения) Экспорт
	
	СписокИменФайлов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВложенияНеформализованныхДокументов.ИмяФайла,
	|	ВложенияНеформализованныхДокументов.Тип,
	|	ВложенияНеформализованныхДокументов.Размер
	|ИЗ
	|	РегистрСведений.ВложенияНеформализованныхДокументов КАК ВложенияНеформализованныхДокументов
	|ГДЕ
	|	ВложенияНеформализованныхДокументов.НеформализованныйДокумент = &НеформализованныйДокумент";
	Запрос.УстановитьПараметр("НеформализованныйДокумент", ВладелецВложения);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокИменФайлов.Добавить(Новый Структура("ИмяФайла,Размер", Выборка.ИмяФайла, Выборка.Размер));
	КонецЦикла;
	
	Возврат СписокИменФайлов;
	
КонецФункции

Функция ПолучитьВложенияДокументовРеализацииПолномочийНалоговыхОрганов(Владелец) Экспорт
	
	СписокИменФайлов = Новый Массив;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.ИмяФайла,
	|	ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.Размер
	|ИЗ
	|	РегистрСведений.ФайлыДокументовРеализацииПолномочийНалоговыхОрганов КАК ФайлыДокументовРеализацииПолномочийНалоговыхОрганов
	|ГДЕ
	|	ФайлыДокументовРеализацииПолномочийНалоговыхОрганов.Документ = &Владелец";
	Запрос.УстановитьПараметр("Владелец", Владелец);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		СписокИменФайлов.Добавить(Новый Структура("ИмяФайла,Размер", Выборка.ИмяФайла, Выборка.Размер));
	КонецЦикла;
	
	Возврат СписокИменФайлов;
	
КонецФункции

Процедура УдалитьВыборCSPИзВременныхНастроек() Экспорт

	Попытка
		
		ОбщегоНазначения.ХранилищеОбщихНастроекУдалить(
		"МастерФормированияЗаявкиНаПодключение",
		"ВыбранныйКриптопровайдер",
		ИмяПользователя()
		);
		
	Исключение
		
		ИнформацияОбОшибке = ИнформацияОбОшибке();
		
		ЗаписьЖурналаРегистрации(
			НСтр("ru = 'Удаление сохраненных настроек мастера подключения к 1С-Отчетности'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), 
			УровеньЖурналаРегистрации.Ошибка,,,
			ПодробноеПредставлениеОшибки(ИнформацияОбОшибке));
		
	КонецПопытки;

КонецПроцедуры

Функция УчетнаяЗаписьОрганизации(Организация) Экспорт
	
	ВидОбменаСКонтролирующимиОрганами = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Организация, "ВидОбменаСКонтролирующимиОрганами");
	Если ВидОбменаСКонтролирующимиОрганами = Перечисления.ВидыОбменаСКонтролирующимиОрганами.ОбменВУниверсальномФормате Тогда
		Возврат Организация.УчетнаяЗаписьОбмена;
	Иначе
		Возврат Справочники.УчетныеЗаписиДокументооборота.ПустаяСсылка();
	КонецЕсли;
	
КонецФункции

Функция ПолучитьИмяЗначенияПеречисленияСпецоператорыСвязиПоСинониму(Синоним) Экспорт
	
	НужныйИндекс = Перечисления.СпецоператорыСвязи.Индекс(Синоним);
	ИмяЗначения = Перечисления.СпецоператорыСвязи.Прочие.Метаданные().ЗначенияПеречисления[НужныйИндекс].Имя;
	Возврат ИмяЗначения;
	
КонецФункции

Функция ПолучитьПараметрСпецоператора(Знач Спецоператор, Параметр) Экспорт
	
	Если ЗначениеЗаполнено(Спецоператор) Тогда
		
		Имя 			= ПолучитьИмяЗначенияПеречисленияСпецоператорыСвязиПоСинониму(Спецоператор);
		КонтекстЭДО 	= ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
		Макет 			= КонтекстЭДО.ПолучитьМакет("ПараметрыСпецоператоровСвязи");
		НомерКолонки 	= Макет.Область(Параметр).Лево;
		
		Для НомерСтроки = 2 По Макет.ВысотаТаблицы Цикл
			ТекРегион = Макет.Область(НомерСтроки, 1).Текст;
			
			Если нРег(ТекРегион) = нРег(Имя) Тогда
				
				Возврат(Макет.Область(НомерСтроки,  НомерКолонки).Текст);
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Процедура ЗадатьКодОрганаФСГСВОрганизации(Организация, НовыйКодОрганаФСГС) Экспорт
	
	СтандартнаяОбработка = Истина;
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ЗадатьКодОрганаФСГСВОрганизации(Организация, НовыйКодОрганаФСГС, СтандартнаяОбработка);

	Если СтандартнаяОбработка Тогда
		ОрганизацияОбъект = Организация.ПолучитьОбъект();
		ОрганизацияОбъект.КодОрганаФСГС = НовыйКодОрганаФСГС;
		ОрганизацияОбъект.Записать();
	КонецЕсли;

КонецПроцедуры

Функция ЕстьДоступККонтекстуЭДО() Экспорт
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	Возврат КонтекстЭДОСервер <> Неопределено;
	
КонецФункции

Функция ЭтоВариантИнтерфейсаТакси() Экспорт
	
	Возврат ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси;
	
КонецФункции

Функция ВерсияВнешнегоМодуляДокументооборота() Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	Возврат Константы.ДокументооборотСКонтролирующимиОрганами_ВерсияВнешнегоМодуля.Получить();
	
КонецФункции

Функция АдресТекстаОтправленногоЗаявления(ЗаявлениеСсылка) Экспорт
	Возврат ПоместитьВоВременноеХранилище(ЗаявлениеСсылка.ТекстОтправленногоЗаявления.Получить(), Новый УникальныйИдентификатор);
КонецФункции

Функция ПредставлениеДекларацииПоНДС(ДекларацияПоНДС) Экспорт
	
	Представление = "";
	// Для регламентированного отчета формируем представление по отдельному алгоритму
	Если ТипЗнч(ДекларацияПоНДС) = Тип("ДокументСсылка.РегламентированныйОтчет") Тогда
		Представление = РегламентированнаяОтчетностьКлиентСервер.ПредставлениеДокументаРеглОтч(ДекларацияПоНДС);
	Иначе
		Представление = Строка(ДекларацияПоНДС);
	КонецЕсли;
	
	Возврат Представление;
	
КонецФункции

Функция ЭтоТребованиеФНС(ДокументСсылка) Экспорт
	
	Возврат ДокументСсылка.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииДокументов
		ИЛИ ДокументСсылка.ВидДокумента = Перечисления.ВидыНалоговыхДокументов.ТребованиеОПредставленииПоясненийКДекларацииНДС;
	
КонецФункции

Функция СведенияОЗаполненииПоясненияКДекларацииПоНДС(Пояснение) Экспорт
	
	СведенияОЗаполнении = Новый Структура("ЕстьНеОтвеченныеСтроки, ЕстьИзмененияПоНДС, ЕстьСтрокиДляОтправки", Ложь, Ложь, Ложь);
	
	Запрос = Новый Запрос();
	Запрос.Параметры.Вставить("Пояснение", Пояснение);
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЕСТЬNULL(СУММА(СведенияОЗаполнении.КоличествоНеОтвеченныхСтрок), 0) КАК КоличествоНеОтвеченныхСтрок,
	|	ЕСТЬNULL(СУММА(СведенияОЗаполнении.КоличествоСтрокДляОтправки), 0) КАК КоличествоСтрокДляОтправки,
	|	ЕСТЬNULL(СУММА(СведенияОЗаполнении.КоличествоСтрокСИзменениямиНДС), 0) КАК КоличествоСтрокСИзменениямиНДС
	|ИЗ
	|	(ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПокупок.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоНеОтвеченныхСтрок,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПокупок.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоСтрокДляОтправки,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПокупок.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|					И ПоясненияКДекларацииПоНДСКнигаПокупок.СуммаНДС <> ПоясненияКДекларацииПоНДСКнигаПокупок.СуммаНДСРасхождение
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ КАК КоличествоСтрокСИзменениямиНДС
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.КнигаПокупок КАК ПоясненияКДекларацииПоНДСКнигаПокупок
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСКнигаПокупок.Ссылка = &Пояснение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПокупокДл.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПокупокДл.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПокупокДл.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|					И ПоясненияКДекларацииПоНДСКнигаПокупокДл.СуммаНДС <> ПоясненияКДекларацииПоНДСКнигаПокупокДл.СуммаНДСРасхождение
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.КнигаПокупокДл КАК ПоясненияКДекларацииПоНДСКнигаПокупокДл
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСКнигаПокупокДл.Ссылка = &Пояснение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПродаж.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПродаж.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПродаж.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|					И (ПоясненияКДекларацииПоНДСКнигаПродаж.СуммаНДС18 <> ПоясненияКДекларацииПоНДСКнигаПродаж.СуммаНДС18Расхождение
	|						ИЛИ ПоясненияКДекларацииПоНДСКнигаПродаж.СуммаНДС10 <> ПоясненияКДекларацииПоНДСКнигаПродаж.СуммаНДС10Расхождение)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.КнигаПродаж КАК ПоясненияКДекларацииПоНДСКнигаПродаж
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСКнигаПродаж.Ссылка = &Пояснение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПродажДл.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПродажДл.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСКнигаПродажДл.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|					И (ПоясненияКДекларацииПоНДСКнигаПродажДл.СуммаНДС18 <> ПоясненияКДекларацииПоНДСКнигаПродажДл.СуммаНДС18Расхождение
	|						ИЛИ ПоясненияКДекларацииПоНДСКнигаПродажДл.СуммаНДС10 <> ПоясненияКДекларацииПоНДСКнигаПродажДл.СуммаНДС10Расхождение)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.КнигаПродажДл КАК ПоясненияКДекларацииПоНДСКнигаПродажДл
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСКнигаПродажДл.Ссылка = &Пояснение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСЖурналУчетаВыставленныхСчетовФактур.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСЖурналУчетаВыставленныхСчетовФактур.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.ЖурналУчетаВыставленныхСчетовФактур КАК ПоясненияКДекларацииПоНДСЖурналУчетаВыставленныхСчетовФактур
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСЖурналУчетаВыставленныхСчетовФактур.Ссылка = &Пояснение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСЖурналУчетаПолученныхСчетовФактур.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСЖурналУчетаПолученныхСчетовФактур.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.ЖурналУчетаПолученныхСчетовФактур КАК ПоясненияКДекларацииПоНДСЖурналУчетаПолученныхСчетовФактур
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСЖурналУчетаПолученныхСчетовФактур.Ссылка = &Пояснение
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС.РезультатПроверки = ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|					И ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС.СуммаНДС <> ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС.СуммаНДСРасхождение
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.ВыставленныеСчетаФактурыНеплательщиковНДС КАК ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСВыставленныеСчетаФактурыНеплательщиковНДС.Ссылка = &Пояснение
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДСНетКнигаПрод.РезультатПроверки <> ЗНАЧЕНИЕ(Перечисление.РезультатПроверкиСтрокиДекларации.ПустаяСсылка)
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.НетКнигаПрод КАК ПоясненияКДекларацииПоНДСНетКнигаПрод
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДСНетКнигаПрод.Ссылка = &Пояснение
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		1,
	|		0
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС.СведКС КАК ПоясненияКДекларацииПоНДССведКС
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДССведКС.Ссылка = &Пояснение	
	|
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		0,
	|		ВЫБОР
	|			КОГДА ПоясненияКДекларацииПоНДССведКС.ПояснИнОсн <> """"
	|				ТОГДА 1
	|			ИНАЧЕ 0
	|		КОНЕЦ,
	|		0
	|	ИЗ
	|		Документ.ПоясненияКДекларацииПоНДС КАК ПоясненияКДекларацииПоНДССведКС
	|	ГДЕ
	|		ПоясненияКДекларацииПоНДССведКС.Ссылка = &Пояснение) КАК СведенияОЗаполнении";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СведенияОЗаполнении.ЕстьНеОтвеченныеСтроки = Выборка.КоличествоНеОтвеченныхСтрок > 0;
		СведенияОЗаполнении.ЕстьСтрокиДляОтправки = (Выборка.КоличествоСтрокДляОтправки-Выборка.КоличествоСтрокСИзменениямиНДС) > 0;
		СведенияОЗаполнении.ЕстьИзмененияПоНДС = Выборка.КоличествоСтрокСИзменениямиНДС > 0;
	КонецЕсли;
	
	Возврат СведенияОЗаполнении;
	
КонецФункции

// По коду региона возвращает массив органов росстата.
//
// Параметры:
//  КодРегиона	 - Строка - Код региона, для которого надо получить список ТОГС.
//  Спецоператор - Строка - Если указан, ищутся только те органы, которые в указанном регионе поддерживаются
//		данным спецоператором. Если не указан, берутся все органы в данном регионе.
// 
// Возвращаемое значение:
//  Массив - пустой или содержит структуры с ключами КодТОГС и НаименованиеТОГС в качестве элементов.
//
Функция ОтделенияРосстатаРегиона(КодРегиона, Спецоператор = Неопределено) Экспорт
	
	ТаблицаТОГС = ТаблицаТОГС();
	ТаблицаТОГС.Индексы.Добавить("КодРегиона");
	
	Отбор = Новый Структура();
	Если ЗначениеЗаполнено(Спецоператор) Тогда
		
		ИмяКолонкиСпецоператора = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ПолучитьПараметрСпецоператора(
			Спецоператор, 
			"СпецоператорСвязи");
			
		ТаблицаТОГС = ТаблицаТОГС.Скопировать(Новый Структура(ИмяКолонкиСпецоператора, ""));
		
	КонецЕсли;
	
	ТаблицаТОГС.Сортировать("Код");
	
	Строки = ТаблицаТОГС.НайтиСтроки(Новый Структура("КодРегиона", КодРегиона));
	
	ОтделенияРосстата = Новый Массив();
	Для Каждого Строка ИЗ Строки Цикл
		Отделение = Новый Структура("КодТОГС, НаименованиеТОГС", Строка.Код, Строка.Наименование);
		ОтделенияРосстата.Добавить(Отделение);
	КонецЦикла;
	
	Возврат ОтделенияРосстата;
	
КонецФункции

// По коду органа Росстата возвращает его наименование.
//
// Параметры:
//  КодОрганаФСГС	 - Строка - код органа Росстата.
// 
// Возвращаемое значение:
//  Строка - наименование органа Росстата.
//
Функция НаименованиеТОГС(КодОрганаФСГС) Экспорт
	
	Если ЗначениеЗаполнено(КодОрганаФСГС) Тогда
		
		ТаблицаТОГС = ТаблицаТОГС();
		Строка = ТаблицаТОГС.Найти(КодОрганаФСГС, "Код");
		Если Строка <> Неопределено Тогда
			Возврат Строка.Наименование;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат НСтр("ru = '<Не выбран код территориального органа>'");
	
КонецФункции

Процедура ОтметитьКакПрочтенное(Ссылка) Экспорт

	ЭлектронныйДокументооборотСКонтролирующимиОрганами.ОтметитьКакПрочтенное(Ссылка);
	
КонецПроцедуры

Процедура СкрытьВосстановитьОбъект1СОтчетности(Ссылка, Скрыть) Экспорт
	
	ИзменитьЗначениеВФорме1СОтчетность(Ссылка, "Скрыт", Скрыть);
	
КонецПроцедуры

Функция ЗаполнитьПараметрыКриптографии() Экспорт
	
	СтруктураРезультата = Новый Структура;
	
	ПрисутствуетЗаполненныйПараметр = Ложь;
	
	ПараметрыКриптографии = Новый Соответствие;
	ПараметрыКриптографии.Вставить("ИмяКриптопровайдера");
	ПараметрыКриптографии.Вставить("ТипКриптопровайдера");
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	Константы.ДокументооборотСКонтролирующимиОрганами_ИмяКриптопровайдера КАК ИмяКриптопровайдера,
	                      |	Константы.ДокументооборотСКонтролирующимиОрганами_ТипКриптопровайдера КАК ТипКриптопровайдера
	                      |ИЗ
	                      |	Константы КАК Константы");
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Для Каждого ПараметрКриптографии Из ПараметрыКриптографии Цикл
			ПараметрыКриптографии.Вставить(ПараметрКриптографии.Ключ, Выборка[ПараметрКриптографии.Ключ]);
			Если ЗначениеЗаполнено(Выборка[ПараметрКриптографии.Ключ]) Тогда
				ПрисутствуетЗаполненныйПараметр = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	СтруктураРезультата.Вставить("ПрисутствуетЗаполненныйПараметр", ПрисутствуетЗаполненныйПараметр);
	СтруктураРезультата.Вставить("ПараметрыКриптографии", ПараметрыКриптографии);
	
	Возврат СтруктураРезультата;
	
КонецФункции

Функция РазделениеВключено() Экспорт
	
	Возврат ОбщегоНазначенияПовтИсп.РазделениеВключено();
	
КонецФункции

Функция ПолучитьДанныеСотрудника(ВидВладельцаЭЦП, ДанныеОрганизации, Сотрудник) Экспорт
	
	ФИО              = "";
	ВидДокумента     = "";
	Серия            = "";
	Номер            = "";
	ДатаВыдачи       = "";
	КемВыдан         = "";
	ВидДокумента     = "";
	Подразделение    = "";
	Должность        = "";
	СНИЛС            = "";
	ДатаРождения     = Неопределено;
	МестоРождения    = "";
	КодПодразделения = "";
	Пол              = Неопределено;
	Гражданство      = Неопределено;
	
	Попытка
		
		ЭтоЮрЛицо = Ложь;
		Если ДанныеОрганизации.Свойство("ТипОрганизации") Тогда
			ЭтоЮрЛицо = ДанныеОрганизации.ТипОрганизации;
		Иначе
			ЭтоЮрЛицо = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ЭтоЮрЛицо(ДанныеОрганизации.Организация);
		КонецЕсли;
		
		Если ВидВладельцаЭЦП = Перечисления.ТипыВладельцевЭЦП.Руководитель И ЭтоЮрЛицо Тогда //заполняем директора
			
			ДанныеОрганизации.Свойство("СтруктураФИОРук", 		ФИО);
			ДанныеОрганизации.Свойство("ВидУдЛичнРук", 			ВидДокумента);
			ДанныеОрганизации.Свойство("СерияУдЛичнРук", 		Серия);
			ДанныеОрганизации.Свойство("НомерУдЛичнРук", 		Номер);
			ДанныеОрганизации.Свойство("ДатаУдЛичнРук", 		ДатаВыдачи);
			ДанныеОрганизации.Свойство("ОрганВыданУдЛичнРук", 	КемВыдан);
			ДанныеОрганизации.Свойство("ДолжностьРук", 			Должность);
			ДанныеОрганизации.Свойство("СНИЛСРук", 				СНИЛС);
			ДанныеОрганизации.Свойство("ДатаРождРук", 			ДатаРождения);
			ДанныеОрганизации.Свойство("МестоРождРук", 			МестоРождения);
			ДанныеОрганизации.Свойство("КодПодрУдЛичнРук", 		КодПодразделения);
			ДанныеОрганизации.Свойство("ПолРук", 				Пол);
			ДанныеОрганизации.Свойство("ГраждРук", 				Гражданство);
			
			// Процедура ПолучитьСведенияОбОрганизации возвращает пол числом,
			// а нужно перечислением.
			Пол = ПолИзЧислаВПеречисление(Пол);
			
			// Процедура ПолучитьСведенияОбОрганизации возвращает код страны,
			// а нужен элемент справочника.
			Если ЗначениеЗаполнено(Гражданство) Тогда
				Гражданство = Справочники.СтраныМира.НайтиПоКоду(Гражданство);
			КонецЕсли;
				
		ИначеЕсли ВидВладельцаЭЦП = Перечисления.ТипыВладельцевЭЦП.ГлавныйБухгалтер И ЭтоЮрЛицо Тогда //заполняем бухгалтера
			
			ДанныеОрганизации.Свойство("СтруктураФИОБух", 		ФИО);
			ДанныеОрганизации.Свойство("ВидУдЛичнБух", 			ВидДокумента);
			ДанныеОрганизации.Свойство("СерияУдЛичнБух", 		Серия);
			ДанныеОрганизации.Свойство("НомерУдЛичнБух", 		Номер);
			ДанныеОрганизации.Свойство("ДатаУдЛичнБух", 		ДатаВыдачи);
			ДанныеОрганизации.Свойство("ОрганВыданУдЛичнБух", 	КемВыдан);
			ДанныеОрганизации.Свойство("ДолжностьБух", 			Должность);
			ДанныеОрганизации.Свойство("СНИЛСБух", 				СНИЛС);
			ДанныеОрганизации.Свойство("ДатаРождБух", 			ДатаРождения);
			ДанныеОрганизации.Свойство("МестоРождБух", 			МестоРождения);
			ДанныеОрганизации.Свойство("КодПодрУдЛичнБух", 		КодПодразделения);
			ДанныеОрганизации.Свойство("ПолБух", 				Пол);
			ДанныеОрганизации.Свойство("ГраждБух", 				Гражданство);
			
			// Процедура ПолучитьСведенияОбОрганизации возвращает пол числом,
			// а нужно перечислением.
			Пол = ПолИзЧислаВПеречисление(Пол);
			
			// Процедура ПолучитьСведенияОбОрганизации возвращает код страны,
			// а нужен элемент справочника.
			Если ЗначениеЗаполнено(Гражданство) Тогда
				Гражданство = Справочники.СтраныМира.НайтиПоКоду(Гражданство);
			КонецЕсли;
			
		ИначеЕсли ВидВладельцаЭЦП = Перечисления.ТипыВладельцевЭЦП.ДругойСотрудник ИЛИ ЗначениеЗаполнено(Сотрудник) Тогда //заполняем сотрудника
			
			ДанныеИсполнителя = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервераПереопределяемый.ПолучитьДанныеИсполнителя(Сотрудник, ДанныеОрганизации.Организация);
			
			Если ДанныеИсполнителя <> Неопределено Тогда
				ДанныеИсполнителя.Свойство("ФИО", 				ФИО);
				ДанныеИсполнителя.Свойство("ВидДокумента", 		ВидДокумента);
				ДанныеИсполнителя.Свойство("Серия", 			Серия);
				ДанныеИсполнителя.Свойство("Номер", 			Номер);
				ДанныеИсполнителя.Свойство("ДатаВыдачи", 		ДатаВыдачи);
				ДанныеИсполнителя.Свойство("КемВыдан", 			КемВыдан);
				ДанныеИсполнителя.Свойство("Должность", 		Должность);
				ДанныеИсполнителя.Свойство("СНИЛС", 			СНИЛС);
				ДанныеИсполнителя.Свойство("Подразделение", 	Подразделение);
				ДанныеИсполнителя.Свойство("ДатаРождения", 		ДатаРождения);
				ДанныеИсполнителя.Свойство("МестоРождения", 	МестоРождения);
				ДанныеИсполнителя.Свойство("КодПодразделения", 	КодПодразделения);
				ДанныеИсполнителя.Свойство("Пол", 				Пол);
				ДанныеИсполнителя.Свойство("Гражданство", 		Гражданство);
				
				// Процедура ПолучитьДанныеИсполнителя возвращает пол строкой,
				// а нужно перечислением.
				Пол = ПолИзСтрокиВПеречисление(Пол);
				
			КонецЕсли;
			
		КонецЕсли;
		
	Исключение
		
		ТекстСообщения = НСтр("ru = 'Электронный документооборот с контролирующими органами. Не удалось получить сведения о сотруднике'");
		ОписаниеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ЗаписьСобытияВЖурналРегистрации(ТекстСообщения, ОписаниеОшибки);
				
	КонецПопытки;
	
	// Дополнительная попытка определить ФИО
	Если ПустаяСтрока(ФИО) Тогда
		Если ЗначениеЗаполнено(Сотрудник) Тогда
			ФИО = ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.ФИОФизЛица(Сотрудник);
		Иначе
			ФИО = Новый Структура("Фамилия, Имя, Отчество");
		КонецЕсли;
	КонецЕсли;
	
	// Гражданство.
	Если НЕ ЗначениеЗаполнено(Гражданство) Тогда
		Гражданство = Справочники.СтраныМира.Россия;
	КонецЕсли;
	
	//Место рождения.
	МестоРождения = ПредставлениеМестаРождения(МестоРождения);
	
	Результат = Новый Структура();
	Результат.Вставить("ФИО",           ФИО);
	Результат.Вставить("ВидДокумента",  ВидДокумента);
	Результат.Вставить("Серия",         Серия);
	Результат.Вставить("Номер",         Номер);
	Результат.Вставить("ДатаВыдачи",    ДатаВыдачи);
	Результат.Вставить("КемВыдан",      КемВыдан);
	Результат.Вставить("Должность",     Должность);
	Результат.Вставить("ВидДокумента",  ВидДокумента);
	Результат.Вставить("Подразделение", Подразделение);
	Результат.Вставить("СНИЛС",         СНИЛС);
	Результат.Вставить("ДатаРождения",  ДатаРождения);
	Результат.Вставить("МестоРождения", МестоРождения);
	Результат.Вставить("КодПодразделения", КодПодразделения);
	Результат.Вставить("Пол",           Пол);
	Результат.Вставить("Гражданство",   Гражданство);
	
	Возврат Результат;
	
КонецФункции

Функция РеквизитыНеХранящиесяВБазе(Организация = Неопределено) Экспорт
	
	Реквизиты = Новый Соответствие();
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ТелефонОсновной, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ТелефонДополнительный, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ЭлектроннаяПочта, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДолжность, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППодразделение, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПСНИЛС, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ДополнительныйКодФСС, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПМестоРождения, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПДатаРождения, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПКодПодразделения, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦППол, Ложь);
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.ВладелецЭЦПГражданство, Ложь);
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиПереопределяемый.ОпределитьНаличиеДанныхДляЗаявленияНаСертификат(Реквизиты, Организация);
	
	// Этот реквизит всегда должен быть доступным для ввода, поскольку ниоткуда не заполняется.
	Реквизиты.Вставить(Перечисления.ПараметрыПодключенияК1СОтчетности.НомерОсновнойПоставки1С, Ложь);
	
	НеХранящиесяРеквизиты = Новый Массив;
	Для каждого РеквизитМастера Из Реквизиты Цикл
		
		РеквизитНеХранитсяВБазе = РеквизитМастера.Значение = Ложь;
		
		Если РеквизитНеХранитсяВБазе Тогда
			НеХранящиесяРеквизиты.Добавить(РеквизитМастера.Ключ);
		КонецЕсли;
	
	КонецЦикла; 
	
	Возврат НеХранящиесяРеквизиты; 
	
КонецФункции

Функция КодАльфа2Заполнен(Гражданство) Экспорт
	
	Возврат ЗначениеЗаполнено(Гражданство.КодАльфа2);
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция РазложитьМестоРождения(Знач СтрокаМестоРождения, ВерхнийРегистр = Истина)
	
	Особое = 0;НаселенныйПункт	= "";Район	= "";Область	= "";Страна	= "";
	
	МассивМестоРождения	= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(?(ВерхнийРегистр, Врег(СтрокаМестоРождения), СтрокаМестоРождения));
	
	ЭлементовВМассиве = МассивМестоРождения.Количество();   
	Если ЭлементовВМассиве > 0 Тогда
		Если СокрЛП(МассивМестоРождения[0]) = "1" Тогда
			Особое = 1;
		КонецЕсли;
	КонецЕсли;
	Если ЭлементовВМассиве > 1 Тогда
		НаселенныйПункт = СокрЛП(МассивМестоРождения[1]);
	КонецЕсли;
	Если ЭлементовВМассиве > 2 Тогда
		Район = СокрЛП(МассивМестоРождения[2]);
	КонецЕсли;
	Если ЭлементовВМассиве > 3 Тогда
		Область = СокрЛП(МассивМестоРождения[3]);
	КонецЕсли;
	Если ЭлементовВМассиве > 4 Тогда
		Страна = СокрЛП(МассивМестоРождения[4]);
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("Особое",Особое);
	СтруктураВозврата.Вставить("НаселенныйПункт",НаселенныйПункт);
	СтруктураВозврата.Вставить("Район",Район);
	СтруктураВозврата.Вставить("Область",Область);
	СтруктураВозврата.Вставить("Страна",Страна);
	Возврат СтруктураВозврата;
	
КонецФункции

// Возвращает строковое представление места рождения.
Функция ПредставлениеМестаРождения(Знач СтрокаМестоРождения)
	
	Если Лев(СтрокаМестоРождения, 2) = "0,"
		ИЛИ Лев(СтрокаМестоРождения, 2) = "1," Тогда
	
		СтруктураМестоРождения = РазложитьМестоРождения(СтрокаМестоРождения, Ложь);
		
		Если СтруктураМестоРождения.Особое = 1 Тогда
			
			Представление	=	"особое" +
			?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.НаселенныйПункт),		"",	"  "	+	СокрЛП(СтруктураМестоРождения.НаселенныйПункт))
			+?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.Район),	"",	"  "	+	СокрЛП(СтруктураМестоРождения.Район))
			+?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.Область),	"",	"  "	+	СокрЛП(СтруктураМестоРождения.Область))
			+?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.Страна),	"",	"  "	+	СокрЛП(СтруктураМестоРождения.Страна));
			
		Иначе
			
			Представление	= "" + ?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.НаселенныйПункт),		"",	СокрЛП(СтруктураМестоРождения.НаселенныйПункт))
			+?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.Район),	"",	", " + СокрЛП(СтруктураМестоРождения.Район))
			+?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.Область),	"",	", "	+	СокрЛП(СтруктураМестоРождения.Область))
			+?(НЕ ЗначениеЗаполнено(СтруктураМестоРождения.Страна),	"",	", "	+	СокрЛП(СтруктураМестоРождения.Страна));
			
			Если Лев(Представление, 1) = ","  Тогда
				Представление = Сред(Представление, 2);
			КонецЕсли;
			
		КонецЕсли;
		
		Возврат Представление;
		
	Иначе
		
		Возврат СтрокаМестоРождения;
		
	КонецЕсли;
	
КонецФункции

Процедура ОбработатьМакетыСКомпонентойОбмена(КоллекцияМакетов, Компоненты, АктуальнаяВерсия, ПутьКОбъекту = "")
	
	ЭтоВнешняяОбработка = ЗначениеЗаполнено(ПутьКОбъекту);
	Для Каждого Макет Из КоллекцияМакетов Цикл
		Если СтрНайти(Макет.Имя, "КомпонентаОбмена") Тогда
			Если Макет.Имя = "КомпонентаОбмена" Тогда
				Компоненты.Вставить(?(ЭтоВнешняяОбработка, 1000000, 0), Макет.Имя);
			Иначе
				ВерсияИзМакета = Число(СтрЗаменить(Макет.Имя, "КомпонентаОбмена", ""));
				Версия = ?(ЭтоВнешняяОбработка, 1000000 + ВерсияИзМакета, ВерсияИзМакета);
				
				ПолноеИмяМакета = Макет.ПолноеИмя();
				Если ЗначениеЗаполнено(ПутьКОбъекту) Тогда
					ДлинаПутиКОбъекту = СтрДлина(ПутьКОбъекту);
					НачалоПолногоИмениМакета = Лев(ПолноеИмяМакета, ДлинаПутиКОбъекту + 1);
					Если ВРег(НачалоПолногоИмениМакета) <> ВРег(ПутьКОбъекту + ".") Тогда
						ПолноеИмяМакета = ПутьКОбъекту + "." + Макет.ПолноеИмя();
					КонецЕсли;
				КонецЕсли;
				
				Компоненты.Вставить(Версия, ПолноеИмяМакета);
				
				АктуальнаяВерсия = Макс(АктуальнаяВерсия, Версия);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
КонецПроцедуры

// Возвращает цикл обмена или отправку по их идентификатору.
Функция ОтправкаПоИдентификатору(ИдентификаторОтправки) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	               |	ЦиклыОбмена.Ссылка
	               |ИЗ
	               |	Справочник.ЦиклыОбмена КАК ЦиклыОбмена
	               |ГДЕ
	               |	ЦиклыОбмена.Идентификатор = &ИдентификаторОтправки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОтправкиФСС.Ссылка
	               |ИЗ
	               |	Справочник.ОтправкиФСС КАК ОтправкиФСС
	               |ГДЕ
	               |	ОтправкиФСС.ИдентификаторОтправкиНаСервере = &ИдентификаторОтправки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОтправкиФСРАР.Ссылка
	               |ИЗ
	               |	Справочник.ОтправкиФСРАР КАК ОтправкиФСРАР
	               |ГДЕ
	               |	ОтправкиФСРАР.ИдентификаторОтправкиНаСервере = &ИдентификаторОтправки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОтправкиРПН.Ссылка
	               |ИЗ
	               |	Справочник.ОтправкиРПН КАК ОтправкиРПН
	               |ГДЕ
	               |	ОтправкиРПН.ИдентификаторОтправкиНаСервере = &ИдентификаторОтправки
	               |
	               |ОБЪЕДИНИТЬ ВСЕ
	               |
	               |ВЫБРАТЬ
	               |	ОтправкиФТС.Ссылка
	               |ИЗ
	               |	Справочник.ОтправкиФТС КАК ОтправкиФТС
	               |ГДЕ
	               |	ОтправкиФТС.ИдентификаторОтправкиНаСервере = &ИдентификаторОтправки";
				   
	Запрос.УстановитьПараметр("ИдентификаторОтправки", ИдентификаторОтправки);
	
	ВсеОтправки = Запрос.Выполнить().Выбрать();
	
	Если ВсеОтправки.Следующий() Тогда
		Возврат ВсеОтправки.Ссылка;
	Иначе
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция НужноПоказатьРекламу1СОтчетности(Организация) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		УчетнаяЗаписьОрганизации 	= КонтекстЭДОСервер.УчетнаяЗаписьОрганизации(Организация);
		ПоказатьПредложение 		= НЕ ЗначениеЗаполнено(УчетнаяЗаписьОрганизации);
		
	Иначе
		
		// Проверка по всем организациям.
		ПоказатьПредложение = НЕ КонтекстЭДОСервер.ЭлектронныйДокументооборотИспользуется();
		
	КонецЕсли;
	
	Возврат ПоказатьПредложение;
	
КонецФункции

Функция ТаблицаТОГС()
	
	УстановитьПривилегированныйРежим(Истина);
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КлассификаторXML = КонтекстЭДОСервер.ПолучитьМакет("КодыТОГС").ПолучитьТекст();
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат ОбщегоНазначения.ПрочитатьXMLВТаблицу(КлассификаторXML).Данные;
	
КонецФункции

Функция ПолИзЧислаВПеречисление(ПолЧислом)
	
	Если ТипЗнч(ПолЧислом) <> Тип("Число") Тогда
		Возврат ПолЧислом;
	Конецесли;

	ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.ПустаяСсылка");
	Если ПолЧислом = 0 Тогда
		ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.ПустаяСсылка");
	ИначеЕсли ПолЧислом = 1 Тогда
		ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской");
	ИначеЕсли ПолЧислом = 2 Тогда
		ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский");
	КонецЕсли;

	Возврат ПолПеречислением;
	
КонецФункции

Функция ПолИзСтрокиВПеречисление(ПолСтрокой)
	
	Если ТипЗнч(ПолСтрокой) <> Тип("Строка") Тогда
		Возврат ПолСтрокой;
	Конецесли;
	
	ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.ПустаяСсылка");
	Если ПолСтрокой = "Мужской" Тогда
		ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Мужской");
	ИначеЕсли ПолСтрокой = "Женский" Тогда
		ПолПеречислением = ПредопределенноеЗначение("Перечисление.ПолФизическогоЛица.Женский");
	КонецЕсли;

	Возврат ПолПеречислением;
	
КонецФункции

#КонецОбласти
