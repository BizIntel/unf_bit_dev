
#Область СобытияФормы

Процедура ПриСозданииНаСервере(Форма, ИмяГруппыРазмещения = "") Экспорт
	
	ПроверитьСоздатьРеквизитыПанели(Форма, ИмяГруппыРазмещения);
	УстановитьУсловноеОформление(Форма);
	
	Если Форма.ДанныеПанелиКонтактнойИнформации.Количество() = 0 Тогда
		ДобавитьСообщениеОбОтсутствииДанных(Форма.ДанныеПанелиКонтактнойИнформации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЧтенииНаСервере(Форма, Контрагент, ИмяГруппыРазмещения = "") Экспорт
	
	ПроверитьСоздатьРеквизитыПанели(Форма, ИмяГруппыРазмещения);
	ОбновитьДанныеПанели(Форма, Контрагент);
	
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ОбновитьДанныеПанели(Форма, Контрагент) Экспорт
	
	Форма.ДанныеПанелиКонтактнойИнформации.Очистить();
	
	Если Контрагент = Неопределено Тогда
		ДобавитьСообщениеОбОтсутствииДанных(Форма.ДанныеПанелиКонтактнойИнформации);
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.*
		|ИЗ
		|	Справочник.Контрагенты.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Ссылка = &Контрагент";
	
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	ДанныеКИ = Запрос.Выполнить().Выбрать();
	
	Пока ДанныеКИ.Следующий() Цикл
		НоваяСтрока = Форма.ДанныеПанелиКонтактнойИнформации.Добавить();
		Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ДанныеКИ.ЗначенияПолей);
		НоваяСтрока.Отображение = Строка(ДанныеКИ.Вид) + ": " + ДанныеКИ.Представление + ?(ПустаяСтрока(Комментарий), "", ", " + Комментарий);
		НоваяСтрока.ИндексПиктограммы = ИндексПиктограммыПоТипу(ДанныеКИ.Тип);
		НоваяСтрока.ТипОтображаемыхДанных = "ЗначениеКИ";
		НоваяСтрока.ВладелецКИ = Контрагент;
		НоваяСтрока.ПредставлениеКИ = ДанныеКИ.Представление;
	КонецЦикла;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПорядокТиповКИ.Тип,
		|	ПорядокТиповКИ.Порядок
		|ПОМЕСТИТЬ втПорядокТиповКИ
		|ИЗ
		|	&ПорядокТиповКИ КАК ПорядокТиповКИ
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонтактныеЛица.Ссылка КАК КонтактноеЛицо,
		|	КонтактныеЛица.Наименование КАК Наименование,
		|	КонтактныеЛица.Должность КАК Должность,
		|	ВЫБОР
		|		КОГДА КонтактныеЛица.Ссылка = &ОсновноеКонтактноеЛицо
		|			ТОГДА 0
		|		ИНАЧЕ 1
		|	КОНЕЦ КАК ПорядокКонтактов
		|ПОМЕСТИТЬ втКонтакты
		|ИЗ
		|	Справочник.КонтактныеЛица КАК КонтактныеЛица
		|ГДЕ
		|	КонтактныеЛица.ПометкаУдаления = ЛОЖЬ
		|	И КонтактныеЛица.Недействителен = ЛОЖЬ
		|	И КонтактныеЛица.Владелец = &Владелец
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втКонтакты.КонтактноеЛицо,
		|	втКонтакты.Наименование КАК Наименование,
		|	втКонтакты.Должность
		|ИЗ
		|	втКонтакты КАК втКонтакты
		|
		|УПОРЯДОЧИТЬ ПО
		|	втКонтакты.ПорядокКонтактов,
		|	Наименование
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка КАК КонтактноеЛицо,
		|	КонтактныеЛицаКонтактнаяИнформация.Тип КАК Тип,
		|	КонтактныеЛицаКонтактнаяИнформация.Вид КАК Вид,
		|	КонтактныеЛицаКонтактнаяИнформация.Представление КАК Представление,
		|	КонтактныеЛицаКонтактнаяИнформация.ЗначенияПолей КАК ЗначенияПолей,
		|	ПРЕДСТАВЛЕНИЕ(КонтактныеЛицаКонтактнаяИнформация.Вид) КАК ПредставлениеВидаКИ
		|ИЗ
		|	Справочник.КонтактныеЛица.КонтактнаяИнформация КАК КонтактныеЛицаКонтактнаяИнформация
		|		ЛЕВОЕ СОЕДИНЕНИЕ втПорядокТиповКИ КАК втПорядокТиповКИ
		|		ПО КонтактныеЛицаКонтактнаяИнформация.Тип = втПорядокТиповКИ.Тип
		|ГДЕ
		|	КонтактныеЛицаКонтактнаяИнформация.Ссылка В
		|			(ВЫБРАТЬ
		|				втКонтакты.КонтактноеЛицо
		|			ИЗ
		|				втКонтакты)
		|
		|УПОРЯДОЧИТЬ ПО
		|	втПорядокТиповКИ.Порядок,
		|	КонтактныеЛицаКонтактнаяИнформация.Вид.Наименование";
	
	Запрос.УстановитьПараметр("Владелец", Контрагент);
	Запрос.УстановитьПараметр("ОсновноеКонтактноеЛицо", ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Контрагент, "КонтактноеЛицо"));
	Запрос.УстановитьПараметр("ПорядокТиповКИ", КонтактнаяИнформацияУНФ.ПорядокТиповКИ());
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ВыборкаКонтакты = МассивРезультатов[2].Выбрать();
	ВыборкаКонтактнаяИнформация = МассивРезультатов[3].Выбрать();
	ОтборКИ = Новый Структура("КонтактноеЛицо");
	
	Пока ВыборкаКонтакты.Следующий() Цикл
		
		НоваяСтрока = Форма.ДанныеПанелиКонтактнойИнформации.Добавить();
		НоваяСтрока.Отображение = Строка(ВыборкаКонтакты.КонтактноеЛицо);
		НоваяСтрока.ИндексПиктограммы = -1;
		НоваяСтрока.ТипОтображаемыхДанных = "КонтактноеЛицо";
		НоваяСтрока.ВладелецКИ = ВыборкаКонтакты.КонтактноеЛицо;
		
		ВыборкаКонтактнаяИнформация.Сбросить();
		ОтборКИ.КонтактноеЛицо = ВыборкаКонтакты.КонтактноеЛицо;
		
		Пока ВыборкаКонтактнаяИнформация.НайтиСледующий(ОтборКИ) Цикл
			
			НоваяСтрока = Форма.ДанныеПанелиКонтактнойИнформации.Добавить();
			Комментарий = УправлениеКонтактнойИнформацией.КомментарийКонтактнойИнформации(ВыборкаКонтактнаяИнформация.ЗначенияПолей);
			НоваяСтрока.Отображение = Строка(ВыборкаКонтактнаяИнформация.Вид) + ": " +ВыборкаКонтактнаяИнформация.Представление + ?(ПустаяСтрока(Комментарий), "", ", " + Комментарий);
			НоваяСтрока.ИндексПиктограммы = ИндексПиктограммыПоТипу(ВыборкаКонтактнаяИнформация.Тип);
			НоваяСтрока.ТипОтображаемыхДанных = "ЗначениеКИ";
			НоваяСтрока.ВладелецКИ = ВыборкаКонтакты.КонтактноеЛицо;
			НоваяСтрока.ПредставлениеКИ = ВыборкаКонтактнаяИнформация.Представление;
			
		КонецЦикла;
		
	КонецЦикла;
	
	Если Форма.ДанныеПанелиКонтактнойИнформации.Количество() = 0 Тогда
		ДобавитьСообщениеОбОтсутствииДанных(Форма.ДанныеПанелиКонтактнойИнформации);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьСоздатьРеквизитыПанели(Форма, ИмяГруппыРазмещения)
	
	СписокРеквизитовФормы = Форма.ПолучитьРеквизиты();
	
	ПервыйЗапуск = Истина;
	Для Каждого Реквизит Из СписокРеквизитовФормы Цикл
		Если Реквизит.Имя = "ДанныеПанелиКонтактнойИнформации" Тогда
			ПервыйЗапуск = Ложь;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ПервыйЗапуск Тогда
		Возврат;
	КонецЕсли;
	
	ДобавляемыеРеквизиты = Новый Массив;
	
	// Создадим таблицу значений
	ИмяТаблицы = "ДанныеПанелиКонтактнойИнформации";
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы(ИмяТаблицы, Новый ОписаниеТипов("ТаблицаЗначений")));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("Отображение", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(150)), ИмяТаблицы));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ИндексПиктограммы", Новый ОписаниеТипов("Число"), ИмяТаблицы));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ТипОтображаемыхДанных", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(20)), ИмяТаблицы));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ВладелецКИ", Новый ОписаниеТипов("СправочникСсылка.Контрагенты, СправочникСсылка.КонтактныеЛица"), ИмяТаблицы));
	ДобавляемыеРеквизиты.Добавить(Новый РеквизитФормы("ПредставлениеКИ", Новый ОписаниеТипов("Строка", , Новый КвалификаторыСтроки(500)), ИмяТаблицы));
	
	Форма.ИзменитьРеквизиты(ДобавляемыеРеквизиты);
	
	// Создадим элементы
	ТаблицаФормы = Форма.Элементы.Добавить(ИмяТаблицы, Тип("ТаблицаФормы"), Форма.Элементы[ИмяГруппыРазмещения]);
	ТаблицаФормы.ПутьКДанным = ИмяТаблицы;
	ТаблицаФормы.ПоложениеКоманднойПанели = ПоложениеКоманднойПанелиЭлементаФормы.Нет;
	ТаблицаФормы.ИзменятьСоставСтрок = Ложь;
	ТаблицаФормы.ИзменятьПорядокСтрок = Ложь;
	ТаблицаФормы.РежимВыделенияСтроки = РежимВыделенияСтрокиТаблицы.Строка;
	ТаблицаФормы.Шапка = Ложь;
	ТаблицаФормы.АвтоВводНовойСтроки = Ложь;
	ТаблицаФормы.РазрешитьНачалоПеретаскивания = Ложь;
	ТаблицаФормы.РазрешитьПеретаскивание = Ложь;
	ТаблицаФормы.ГоризонтальнаяПолосаПрокрутки = ИспользованиеПолосыПрокрутки.НеИспользовать;
	ТаблицаФормы.ВертикальнаяПолосаПрокрутки = ИспользованиеПолосыПрокрутки.НеИспользовать;
	ТаблицаФормы.ГоризонтальныеЛинии = Ложь;
	ТаблицаФормы.ВертикальныеЛинии = Ложь;
	ТаблицаФормы.ЦветРамки = ЦветаСтиля.ЦветФонаФормы;
	ТаблицаФормы.Ширина = 25;
	ТаблицаФормы.Высота = 10;
	ТаблицаФормы.РастягиватьПоГоризонтали = Ложь;
	ТаблицаФормы.УстановитьДействие("Выбор",				"Подключаемый_ДанныеПанелиКонтактнойИнформацииВыбор");
	ТаблицаФормы.УстановитьДействие("ПриАктивизацииСтроки",	"Подключаемый_ДанныеПанелиКонтактнойИнформацииПриАктивизацииСтроки");
	
	Отображение = Форма.Элементы.Добавить(ИмяТаблицы + "Отображение", Тип("ПолеФормы"), ТаблицаФормы);
	Отображение.ПутьКДанным = ИмяТаблицы + ".Отображение";
	Отображение.Вид = ВидПоляФормы.ПолеНадписи;
	Отображение.РежимРедактирования = РежимРедактированияКолонки.Вход;
	Отображение.АвтоВысотаЯчейки = Истина;
	Отображение.Ширина = 23;
	
	Пиктограмма = Форма.Элементы.Добавить(ИмяТаблицы + "Пиктограмма", Тип("ПолеФормы"), ТаблицаФормы);
	Пиктограмма.ПутьКДанным = ИмяТаблицы + ".ИндексПиктограммы";
	Пиктограмма.Вид = ВидПоляФормы.ПолеКартинки;
	Пиктограмма.КартинкаЗначений = БиблиотекаКартинок.ВидыКонтактнойИнформации;
	Пиктограмма.АвтоВысотаЯчейки = Истина;
	Пиктограмма.ГиперссылкаЯчейки = Истина;
	Пиктограмма.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.БезРамки, -1);
	Пиктограмма.Ширина = 1;
	Пиктограмма.РастягиватьПоГоризонтали = Ложь;
	Пиктограмма.РастягиватьПоВертикали = Ложь;
	
	ДобавитьКомандуКонтекстногоМеню(Форма,
		"КонтекстноеМенюПанелиКартаЯндекс",
		БиблиотекаКартинок.ЯндексКарты,
		НСтр("ru = 'Адрес на Яндекс.Картах'"),
		НСтр("ru = 'Показывает адрес на картах Яндекс.Карты'"),
		ТаблицаФормы
	);
	ДобавитьКомандуКонтекстногоМеню(Форма,
		"КонтекстноеМенюПанелиКартаGoogle",
		БиблиотекаКартинок.GoogleMaps,
		НСтр("ru = 'Адрес на Google Maps'"),
		НСтр("ru = 'Показывает адрес на карте Google Maps'"),
		ТаблицаФормы
	);
	
КонецПроцедуры

Функция ИндексПиктограммыПоТипу(ТипКИ)
	
	Если ТипКИ = Перечисления.ТипыКонтактнойИнформации.Адрес Тогда
		ИндексКартинки = 12;
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.АдресЭлектроннойПочты Тогда
		ИндексКартинки = 8;
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.ВебСтраница Тогда
		ИндексКартинки = 9;
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.Skype Тогда
		ИндексКартинки = 20;
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.Другое Тогда
		ИндексКартинки = 11;
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.Телефон Тогда
		ИндексКартинки = 7;
	ИначеЕсли ТипКИ = Перечисления.ТипыКонтактнойИнформации.Факс Тогда
		ИндексКартинки = 10;
	Иначе
		ИндексКартинки = 0;
	КонецЕсли;
	
	Возврат ИндексКартинки;
	
КонецФункции

Процедура УстановитьУсловноеОформление(Форма)
	
	// 1. Значение контактной информации - обычный стиль, контактные лица - выделяем
	НовоеУсловноеОформление = Форма.УсловноеОформление.Элементы.Добавить();
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("ЦветТекста");
	Оформление.Значение 		= ЦветаСтиля.ЦветТекстаПравойПанелиОтборов;
	Оформление.Использование 	= Истина;
	
	Оформление = НовоеУсловноеОформление.Оформление.Элементы.Найти("Шрифт");
	Оформление.Значение 		= ШрифтыСтиля.ШрифтПравойПанелиОтборов;
	Оформление.Использование 	= Истина;
	
	Отбор = НовоеУсловноеОформление.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	Отбор.ВидСравнения 		= ВидСравненияКомпоновкиДанных.Равно;
	Отбор.Использование 	= Истина;
	Отбор.ЛевоеЗначение 	= Новый ПолеКомпоновкиДанных("ДанныеПанелиКонтактнойИнформации.ТипОтображаемыхДанных");
	Отбор.ПравоеЗначение 	= "КонтактноеЛицо";
	
	ОформляемоеПоле = НовоеУсловноеОформление.Поля.Элементы.Добавить();
	ОформляемоеПоле.Поле			= Новый ПолеКомпоновкиДанных("ДанныеПанелиКонтактнойИнформацииОтображение");
	ОформляемоеПоле.Использование	= Истина;
	
КонецПроцедуры

Процедура ДобавитьСообщениеОбОтсутствииДанных(ДанныеПанелиКонтактнойИнформации)
	
	НоваяСтрока = ДанныеПанелиКонтактнойИнформации.Добавить();
	НоваяСтрока.Отображение = НСтр("ru='<Нет контактных данных>'");
	НоваяСтрока.ИндексПиктограммы = -1;
	НоваяСтрока.ТипОтображаемыхДанных = "НетДанных";
	НоваяСтрока.ВладелецКИ = Неопределено;
	
КонецПроцедуры

Процедура ДобавитьКомандуКонтекстногоМеню(Форма, ИмяКоманды, Картинка, Заголовок, Подсказка, ПолеВладелец)
	
	Если Форма.Команды.Найти(ИмяКоманды) = Неопределено Тогда
		Команда = Форма.Команды.Добавить(ИмяКоманды);
		Команда.Картинка = Картинка;
		Команда.Заголовок = Заголовок;
		Команда.Подсказка = Подсказка;
		Команда.Действие = "Подключаемый_ДанныеПанелиКонтактнойИнформацииВыполнитьКоманду";
	КонецЕсли;
	
	Кнопка = Форма.Элементы.Добавить(ИмяКоманды, Тип("КнопкаФормы"), ПолеВладелец.КонтекстноеМеню);
	Кнопка.ИмяКоманды = ИмяКоманды;
	
КонецПроцедуры

#КонецОбласти
