////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиСлужебныйКлиент: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область СлужебныйПрограммныйИнтерфейс

// Открывает форму разбора банковской выписки.
//
// Параметры:
//    * СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение с выпиской банка.
//
Процедура РазобратьВыпискуБанка(СообщениеОбмена) Экспорт
	
	Перем СсылкаНаХранилище, Организация, НастройкаОбмена, МассивСчетов;
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьДанныеВыписки(
		СообщениеОбмена, СсылкаНаХранилище, МассивСчетов, Организация, НастройкаОбмена);
	Если НЕ ЗначениеЗаполнено(СсылкаНаХранилище) Тогда
		Возврат;
	КонецЕсли;
	ОбменСБанкамиКлиентПереопределяемый.РазобратьФайлВыписки(
		СообщениеОбмена, СсылкаНаХранилище, Организация, МассивСчетов, НастройкаОбмена);
	
КонецПроцедуры

// Обработчик асинхронного вызова
Процедура ПриОткрытииЭлектронногоДокумента(Результат, Параметры) Экспорт
	
	ОткрытьЭДДляПросмотра(Параметры, , , Истина);
	
КонецПроцедуры

// Возвращает расширение файла.
//
// Параметры:
//  ПутьКФайлу - Строка - полный путь к файлу.
// 
// Возвращаемое значение:
// Строка - расширение файла.
//
Функция РасширениеФайла(ПутьКФайлу) Экспорт
	
	Позиция = СтрНайти(ПутьКФайлу, ".", НаправлениеПоиска.СКонца);
	
	Если Позиция > 0 Тогда
		Возврат Сред(ПутьКФайлу, Позиция + 1);
	КонецЕсли;
	
	Возврат "";
	
КонецФункции

// Удаляет пароль, сохраненный на время сеанса.
//
// Параметры:
//  ОбъектПривязки - Ссылка - ссылка на объект, к которому относится пароль.
//
Процедура УдалитьПарольИзСеанса(ОбъектПривязки) Экспорт
	
	СоответствиеСертификатаИПароля = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(СоответствиеСертификатаИПароля) = Тип("ФиксированноеСоответствие")
		И СоответствиеСертификатаИПароля.Получить(ОбъектПривязки) <> Неопределено Тогда
		Соответствие = Новый Соответствие;
		Для Каждого Элемент Из СоответствиеСертификатаИПароля Цикл
			Если Элемент.Ключ <> ОбъектПривязки Тогда
				Соответствие.Вставить(Элемент.Ключ, Элемент.Значение);
			КонецЕсли;
		КонецЦикла;
		СоответствиеПаролей = Новый ФиксированноеСоответствие(Соответствие);
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса", СоответствиеПаролей);
	КонецЕсли;
	
КонецПроцедуры

#Область РаботаСВнешнимиКомпонентами

// Удаляет внешнюю компоненту из КЭШ.
//
// Параметры:
//  ИмяМодуля - Строка - название удаляемого модуля.
//
Процедура УдалитьВнешнююКомпонентуИзКэш(ИмяМодуля) Экспорт
	
	ПараметрыОбменаСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ПараметрыОбменаСБанками <> Неопределено Тогда
		ВнешниеКомпонентыИзКеша = ПараметрыОбменаСБанками.Получить("ВнешниеКомпоненты");
		Если ВнешниеКомпонентыИзКеша <> Неопределено Тогда
			ВнешниеКомпонентыИзКеша.Удалить(ИмяМодуля);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Возвращает подключенный экземпляр внешней компоненты по имени внешнего модуля.
//
// Параметры:
//   ИмяВК - Строка - название внешнего модуля.
//
// Возвращаемое значение:
//   COMОбъект - подключенный внешний модуль.
//
Функция ПодключеннаяВнешняяКомпонентаБанка(ИмяВК) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];

	Если ПараметрыПодсистемыОбменСБанками = Неопределено Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		ПараметрыПодсистемыОбменСБанками.Вставить("ВнешниеКомпоненты");
	Иначе
		ВнешниеКомпонентыИзКеша = ПараметрыПодсистемыОбменСБанками.Получить("ВнешниеКомпоненты");
		Если ЗначениеЗаполнено(ВнешниеКомпонентыИзКеша) Тогда
			Возврат ВнешниеКомпонентыИзКеша.Получить(ИмяВК);
		КонецЕсли;
	КонецЕсли;

КонецФункции

// Сохраняет подключенный внешний модуль в кэше на клиенте.
//
// Параметры:
//    ИмяВК - Строка - название внешнего модуля;
//    ПодключаемыйМодуль - ComОбъект или УправляемаяФорма - подключенный модуль.
//
Процедура ЗакэшироватьПодключаемыйМодуль(ИмяВК, ПодключаемыйМодуль) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	ВнешниеКомпонентыИзКеша = ПараметрыПодсистемыОбменСБанками.Получить("ВнешниеКомпоненты");
	Если ВнешниеКомпонентыИзКеша = Неопределено Тогда
		ВнешниеКомпонентыИзКеша = Новый Соответствие;
	КонецЕсли;
	ВнешниеКомпонентыИзКеша.Вставить(ИмяВК, ПодключаемыйМодуль);
	ПараметрыПодсистемыОбменСБанками.Вставить("ВнешниеКомпоненты", ВнешниеКомпонентыИзКеша);
	
КонецПроцедуры

// Осуществляет подключение внешней компоненты.
//
// Параметры:
//    ОписаниеОповещения - ОписаниеОповещения - вызывается после выполнения процедуры
//       * Результат - AddIn, Неопределено - подключенная внешняя компонента;
//    Источник - АдресВременногоХранилища, СправочникСсылка.НастройкиОбменСБанками - источник получения данных ВК;
//    НазваниеВК - Строка - название внешней компоненты.
//
Процедура ПодключитьВнешнююКомпонентуБанка(ОписаниеОповещения, Источник, НазваниеВК) Экспорт
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка(НазваниеВК);
		
	Если ПодключаемыйМодуль = Неопределено Тогда
		Если ТипЗнч(Источник) = Тип("СправочникСсылка.НастройкиОбменСБанками") Тогда
			АдресВнешнейКомпоненты = ОбменСБанкамиСлужебныйВызовСервера.АдресВнешнейКомпонентыБанка(Источник, НазваниеВК);
		Иначе
			АдресВнешнейКомпоненты = Источник;
		КонецЕсли;
		ИдентификаторВК = "С" + СтрЗаменить(Строка(Новый УникальныйИдентификатор()), "-", ""); //генерация уникального имени
		Параметры = Новый Структура();
		Параметры.Вставить("ОповещениеПослеПодключенияКомпонентыБанка", ОписаниеОповещения);
		Параметры.Вставить("НазваниеВК", НазваниеВК);
		Параметры.Вставить("ИдентификаторВК", ИдентификаторВК);
		Параметры.Вставить("АдресВнешнейКомпоненты", АдресВнешнейКомпоненты);
		ОповещениеПослеПодключенияКомпоненты = Новый ОписаниеОповещения(
			"ПослеПопыткиПодключенияВнешнейКомпоненты", ЭтотОбъект, Параметры);
		НачатьПодключениеВнешнейКомпоненты(
			ОповещениеПослеПодключенияКомпоненты, АдресВнешнейКомпоненты, ИдентификаторВК, ТипВнешнейКомпоненты.Native);
		Возврат;
	КонецЕсли;
		
	ВыполнитьОбработкуОповещения(ОписаниеОповещения, ПодключаемыйМодуль);
	
КонецПроцедуры

#КонецОбласти

#Область Сбербанк

// Обработчик асинхронного вызова из ОбменСБанкамиКлиент.ПолучитьВыпискуБанка
//
Процедура ПослеУточненияПериодаЗапросаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена ИЛИ Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбменСБанкамиКлиент.ПолучитьВыпискуБанка(ДополнительныеПараметры.НастройкаОбмена, Результат.ДатаНачала,
		Результат.ДатаОкончания, ДополнительныеПараметры.Владелец, ДополнительныеПараметры.НомерСчета);
	
КонецПроцедуры

// Подключает внешнюю компоненту банка, проверяет на минимально допустимую версию и на соответствие ожидаемой версии.
//
// Параметры:
//  Оповещение - ОписаниеОповещения -Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//   * Результат - Булево - Истина - проверка прошла успешно, Ложь - текст проблемы выведен в виде сообщения.
//   * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  АдресВК - Строка - адрес временного хранилища, содержащего данные внешней компоненты;
//  ОжидаемаяВерсия - Строка - версия компоненты, которая должна быть подключена.
//  ЭтоТест - Булево - признак теста настройки.
//
Процедура ПроверитьВерсиюВКСбербанк(Оповещение, АдресВК) Экспорт
	
	ИнформацияОВК = ОбменСБанкамиСлужебныйВызовСервера.ИнформацияОВКБанка(АдресВК);
	
	Если ИнформацияОВК = Неопределено
		ИЛИ НЕ ОбменСБанкамиКлиентСервер.ПоддерживаетсяВерсияКомпонентыСбербанк(ИнформацияОВК.Версия) Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиВерсии", Оповещение);
	ДополнительныеПараметры.Вставить("ОжидаемаяВерсия", ИнформацияОВК.Версия);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПолучитьВерсиюВКПослеПодключенияСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОбменСБанкамиСлужебныйКлиент.ПодключитьВнешнююКомпонентуБанка(ОповещениеПослеПодключенияВК, АдресВК, "VPNKeyTLS");
	
КонецПроцедуры

// Получить версию внешней компоненты Сбербанка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//     * Результат - Строка - версия внешней компоненты;
//                 - Булево - содержит Ложь при возникновении ошибки
//  ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка.
//
Процедура ПолучитьВерсиюВнешнейКомпонентыСбербанк(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияВерсии", Оповещение);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВерсииКомпонентыСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияВерсииКомпонентыСбербанк", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовВерсия(Оповещение);
	
КонецПроцедуры

// Отправляет запрос Request в Сбербанк (не выводит сообщения об ошибках, но пишет в ЖР).
//
// Параметры:
//  Оповещение			 - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//    * Результат - Структура - содержит следующие поля:
//         ** Успех - Булево - признак успешной отправки запроса в банк.
//         ** ТекстОшибки - Строка - текст ошибки для вывода пользователю. Поле присутствует когда Успех = Ложь.
//         ** СетеваяОшибка - Булево - наличие указанного элемента означает, что сервер банка не ответил (сетевая ошибка).
//         ** Тикет - УникальныйИдентификатор - тикет, который вернул банк.
//  ПодключаемыйМодуль1С - ComОбъект - внешняя компонента для отправки данных в Сбербанк.
//  ОтправляемыеДанные	 - Строка - текст запроса, отправляемого в банк.
//  НастройкаОбмена		 - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//
Процедура ОтправитьЗапросВСбербанк(Оповещение, ПодключаемыйМодуль1С, ОтправляемыеДанные, НастройкаОбмена) Экспорт
	
	Параметры = Новый Структура();
	Параметры.Вставить("Оповещение", Оповещение);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("ОтправляемыеДанные", ОтправляемыеДанные);
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросаВСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуОтправкиЗапросаВСбербанк", ЭтотОбъект);
	
	КодОшибки = ""; ТекстОшибки = "";

	ПодключаемыйМодуль1С.BeginCallingsendRequests(Оповещение, ОтправляемыеДанные, КодОшибки, ТекстОшибки);
	
КонецПроцедуры

// Обработчик немодального диалога.
Процедура ПолучитьВыпискуСбербанкаПослеВопросаОбИхНаличии(РезультатВопроса, Параметры) Экспорт
	
	Если НЕ РезультатВопроса Тогда
		ЗапросВыписки = ОбменСБанкамиСлужебныйВызовСервера.ЗапросВыпискиСбербанк(
			Параметры.НастройкаОбмена, Параметры.ДатаНачала, Параметры.ДатаОкончания, Параметры.НомерСчета, Истина);
		Параметры.Вставить("СообщениеОбмена", ЗапросВыписки);
		Параметры.Вставить("ПринудительноеПолучениеВыписки", Истина);
		Параметры.ГотовыеВыписки.Очистить();
		
		Если НЕ ЗначениеЗаполнено(ЗапросВыписки) Тогда
			Возврат;
		КонецЕсли;
		
		ОбработчикПослеПодписания = Новый ОписаниеОповещения("ОтправитьЗапросВыпискиПослеПодписания", ЭтотОбъект, Параметры);
		МассивСообщенийОбмена = Новый Массив;
		МассивСообщенийОбмена.Добавить(ЗапросВыписки);
		
		ПодписатьЭДСбербанк(ОбработчикПослеПодписания, Параметры.НастройкаОбмена, МассивСообщенийОбмена);

	Иначе
		ОтправитьЗапросВыпискиПослеУстановленияКаналаСбербанк(Истина, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Выполняет синхронизацию данных со сбербанком.
//
// Параметры:
//  НастройкаОбмена	 - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//  Параметры		 - Структура - параметры выполнения синхронизации.
//
Процедура ВыполнитьСинхронизациюСбербанк(НастройкаОбмена, Параметры) Экспорт
	
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОбработчикПослеОпределенияСертификата = Новый ОписаниеОповещения(
		"ОпределитьСертификатПодписиПослеУстановкиВиртуальногоКаналаСбербанк", ЭтотОбъект, Параметры);

	УстановитьВиртуальныйКаналСоСбербанком(ОбработчикПослеОпределенияСертификата, НастройкаОбмена);
	
КонецПроцедуры

// Добавляет подпись для электронных документов.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Оповещение, вызываемое после подписания:
//      * Результат - Структура - результат выполнения процедуры:
//           * Успех - Булево - если Истина, то все электронные документы были подписаны;
//           * Количество - Число - количество подписанных электронных документов;
//      * ДополнительныеПараметры - Структура - контекст выполнения процедуры;
//  НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  МассивСообщенийОбмена  - Массив - подписываемые электронные документы:
//     * ДокументСсылка.СообщениеОбменСБанками - сообщения обмена с банками.
//
Процедура ПодписатьЭДСбербанк(Оповещение, НастройкаОбмена, МассивСообщенийОбмена) Экспорт
	
	Параметры = Новый Структура;
	
	СообщенияКПодписи = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивСообщенийОбмена);
	
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("СообщенияОбменаСбербанк", СообщенияКПодписи);
	Параметры.Вставить("ОповещениеПослеПодписания", Оповещение);
	
	ОповещениеПослеАутентификацииНаТокене = Новый ОписаниеОповещения(
		"ОпределитьСертификатПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект, Параметры);
	
	АутентифицироватьсяНаТокенеСбербанка(ОповещениеПослеАутентификацииНаТокене, НастройкаОбмена);
	
КонецПроцедуры

// Определяет валидность установленных подписей и сохраняет результат в ЭД.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое после проверки подписи электронных документов;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена со Сбербанком;
//  Параметры - Структура - содержит данные для обработки.
//
Процедура ОпределитьСтатусыПодписейСбербанк(Оповещение, НастройкаОбмена, МассивСообщенийОбмена) Экспорт

	Если МассивСообщенийОбмена.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
		Возврат;
	КонецЕсли;
	
	КопияМассиваСообщенийОбмена = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивСообщенийОбмена);
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПроверкиПодписи", Оповещение);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("МассивСообщенийОбмена", КопияМассиваСообщенийОбмена);
	
	ОписаниеОбработчика = Новый ОписаниеОповещения(
		"ПослеАутентификацииНаТокенеОпределитьСтатусыПодписейСбербанк", ЭтотОбъект, Параметры);

	АутентифицироватьсяНаТокенеСбербанка(ОписаниеОбработчика, НастройкаОбмена);
	
КонецПроцедуры

// Устанавливает зашифрованный канал с банком через токен
//
// Параметры:
//    Обработчик - ОписаниеОповещения - обработчик после выполнения процедуры
//       * КаналУстановлен - Булево - канал успешно установлен,
//       * Параметры - Структура - контекст выполнения
//    НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - настройка обмена.
//
Процедура УстановитьВиртуальныйКаналСоСбербанком(Обработчик, НастройкаОбмена) Экспорт
	
	ЗакэшироватьПараметрСбербанка("КаналУстановлен", Ложь);
	
	Параметры = Новый Структура;
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("ОповещениеПослеУстановкиВиртуальногоКанала", Обработчик);
	
	ОписаниеОбработчика = Новый ОписаниеОповещения(
		"ПослеПодключенияКомпонентыУстановитьВиртуальныйКаналСбербанк", ЭтотОбъект, Параметры);

	ПодключитьВнешнююКомпонентуБанка(ОписаниеОбработчика, НастройкаОбмена, "VPNKeyTLS");

КонецПроцедуры

// Возвращает закэшированный параметр обмена со сбербанком.
//
// Параметры:
//  НазваниеПараметра  - Строка - название параметра.
//
// Возвращаемое значение:
// Произвольный - значение параметра.
//
Функция ЗначениеИзКэшаСбербанк(НазваниеПараметра) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		ПараметрыОбменаСбербанк = ПараметрыПодсистемыОбменСБанками.Получить("Сбербанк");
		Если ПараметрыОбменаСбербанк <> Неопределено И ПараметрыОбменаСбербанк.Свойство(НазваниеПараметра) Тогда
			Возврат ПараметрыОбменаСбербанк[НазваниеПараметра];
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

// Выполняет очистку закэшированных параметров обмена со Сбербанком
//
Процедура ОчиститьДанныеАвторизацииСбербанк() Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие") Тогда
		ПараметрыПодсистемыОбменСБанками.Удалить("Сбербанк");
	КонецЕсли;
	
КонецПроцедуры

// обработчик асинхронного вызова из ОбменСБанкамиКлиент.ПроверитьПодписьСбербанк
Процедура ЗавершитьТестПодписиСертификатаСбербанк(ПодписьВерна, Параметры) Экспорт
	
	Если ПодписьВерна = Неопределено Тогда
		Параметры.Контекст.ОписаниеОшибки = НСтр("ru = 'При проверке подписи произошла ошибка'");
	ИначеЕсли НЕ ПодписьВерна Тогда
		Параметры.Контекст.ОписаниеОшибки = НСтр("ru = 'Установленная подпись неверна'");
	КонецЕсли;

	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаПодписиСертификата, Параметры.Контекст);
	
КонецПроцедуры

// Осуществляет проверку подписи на токене сбербанка.
//
// Параметры:
//  Оповещение			 - 	 - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//       * ПодписьВерна - Булево, Неопределено - признак валидности подписи, Неопределено в случае возникновения ошибки.
//       * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль	 - ComОбъект - внешняя компонента банка.
//  ПодписанныеДанные	 - Строка - подписанные данные в формате Base64.
//  Подпись				 - Строка - двоичные данные подписи в формате Base64.
//  Сертификат			 - Строка - сертификат в формате Base64.
//
Процедура ПроверитьПодписьНаТокенеСбербанк(Оповещение, ПодключаемыйМодуль, ПодписанныеДанные, Подпись, Сертификат) Экспорт
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПроверкиПодписи", Оповещение);
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатПроверкиПодписиНаТокенеСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПослеПроверкиПодписиНаТокенеСбербанк", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовПроверитьПодписьДанныхЧерезVPNKeyTLS(
		Оповещение, ПодписанныеДанные, Подпись, Сертификат);
	
КонецПроцедуры

// Осуществляет подпись данных на токене Сбербанка.
//
// Параметры:
//    ОписаниеОповещения - ОписаниеОповещения - оповещение, вызываемое по результату выполнения:
//       * Результат - Структура - данные подписи:
//            ** Успех - Булево - признак успешности проведенной операции, если Истина - операция выполнена, иначе Ложь;
//            ** ЭП - Строка - данные подписи в Base64;
//            ** ТекстОшибки - Строка - текст ошибки. Элемент присутствует только если возникла ошибка;
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка;
//    СтрокаПодписи - Строка - подписываемые данные;
//    ИдентификаторСертификата - Строка - идентификатор сертификата на токене банка;
//    Представление - Строка, Ссылка - отображается при подписании на сенсорном или экранном токене.
//
Процедура ПодписатьДанныеСбербанк(ОписаниеОповещения, ПодключаемыйМодуль, СтрокаПодписи, ИдентификаторСертификата, Представление) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписи", ОписаниеОповещения);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СтрокаПодписи", СтрокаПодписи);
	ДополнительныеПараметры.Вставить("ИдентификаторСертификата", ИдентификаторСертификата);
	ДополнительныеПараметры.Вставить("Представление", Представление);
	Оповещение = Новый ОписаниеОповещения(
		"ПодписатьДанныеПослеПолученияИнформацииОТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИнформациюОТокенеСбербанк(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

// Осуществляет подбор сертификата подписи на основе установленного соединения.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - возвращаемое значение, возможные значения:
//           - Структура - данные сертификата;
//                ** ИдентификаторСертификата - Строка - идентификатор сертификата на токене;
//                ** СертификатСсылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - найденный сертификат;
//           - Неопределено - произошла ошибка ошибки;
//           - КодВозвратаДиалога.Отмена - пользователь отказался от выбора сертификата;
//       * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//
Процедура ОпределитьСертификатПодписиСбербанк(Оповещение, НастройкаОбмена) Экспорт
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОпределенияСертификата", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПолученияСертификатов = Новый ОписаниеОповещения(
		"ПодобратьСертификатПослеПолученияСпискаСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеСертификатовСТокенаСбербанк(ОповещениеПослеПолученияСертификатов, ПодключаемыйМодуль);
		
КонецПроцедуры

// Получает идентификаторы сертификатов с токена Сбербанка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры,
//   которая будет вызвана после завершения вызова метода со следующими параметрами:
//      * Результат - Структура - результат выполнения метода:
//           ** Успех - Булево - признак успешности выполнения процедуры;
//           ** ТекстОшибки - Строка - содержит текст ошибки, если Успех = Ложь;
//           ** СоответствиеСертификатов - Соответствие - содержит идентификаторы сертификатов с токена:
//                 *** Ключ - Строка - идентификатор сертификата токена;
//                 *** Значение - ДвоичныеДанные - двоичные данные сертификата;
//  ПодключаемыйМодуль - COMОбъект - внешняя компонента банка.
//
Процедура ПолучитьДанныеСертификатовСТокенаСбербанк(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСпискаСертификатов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияСпискаИдентификаторов = Новый ОписаниеОповещения(
		"ПослеПолученияСпискаСертификатовСТокенаСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияСпискаИдентификаторовСертификатов", ЭтотОбъект);
		
	ИдентификаторыСертификатов = "";

	ПодключаемыйМодуль.НачатьВызовПолучитьСписокИдентСертификатовVPNKeyTLS(
		ОповещениеПослеПолученияСпискаИдентификаторов, "0", ИдентификаторыСертификатов);
	
КонецПроцедуры

Процедура ПослеПолученияСпискаБизнесСистемСТокенаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении списка бизнес систем.'");
		ТекстОшибки = НСтр("ru = 'Внешняя компонента VpnKey-TLS при получении списка бизнес систем вернула код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Получение списка бизнес систем.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		ОчиститьДанныеАвторизацииСбербанк();
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	Иначе
		Результат = Новый Структура("Успех, БизнесСистемы", Истина, ПараметрыВызова.Получить(0));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаБизнесСистем, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаБизнесСистемНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение списка бизнес систем на аппаратном устройстве.'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить список бизнес систем с аппаратного устройства'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаБизнесСистем, Результат);

КонецПроцедуры

// Обработчик асинхронного метода ОбменСБанкамиКлиент.ПроверитьУстановкуПодписиСбербанк.
//
// Параметры:
//    Успех - Булево - признак аутентификации, если Истина, то аутентификация была успешно выполнена.
//    Параметры - Структура - данные для проверки подписи:
//       * Контекст - Структура - контекст вызова;
//       * ОповещениеПослеПодписания - ОписаниеОповещения - оповещение после проверки подписи;
//       * Сертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат установленной подписи.
//
Процедура ПроверитьУстановкуПодписиПослеАутентификацииНаТокенеСбербанк(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		ОписаниеОшибки = НСтр("ru = 'Не удалось аутентифицироваться на банковском ключе'");
		Параметры.Контекст.ОписаниеОшибки = ОписаниеОшибки;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
		Возврат;
	КонецЕсли;

	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");

	ОповещениеПослеПолученияСпискаСертификатов = Новый ОписаниеОповещения(
		"ПроверитьУстановкуПодписиПолученияСертификатовСТокенаСбербанк", ЭтотОбъект, Параметры);
		
	ПолучитьДанныеСертификатовСТокенаСбербанк(ОповещениеПослеПолученияСпискаСертификатов, ПодключаемыйМодуль);
	
КонецПроцедуры

// Обработчик оповещения из ОбменСБанкамиКлиент
Процедура ОтправитьЗапросВыпискиПослеПодписания(Результат, Параметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		Возврат;
	КонецЕсли;
	
	ОписаниеОбработчика = Новый ОписаниеОповещения(
		"ОтправитьЗапросВыпискиПослеУстановленияКаналаСбербанк", ЭтотОбъект, Параметры);
	УстановитьВиртуальныйКаналСоСбербанком(ОписаниеОбработчика, Параметры.НастройкаОбмена);
	
КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезДополнительнуюОбработку


// Получает данные сертификата через дополнительную обработку
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//      * Результат - Неопределено, если произошла ошибка или пользователь отказался от ввода пароля;
//                  - Структура - данные сертификата. Содержит следующие поля:
//                     * Отпечаток - Строка - отпечаток сертификата
//                     * СертификатBase64 - Строка - данные сертификата в Base64
//                     * Пароль - Строка - пароль к закрытой части сертификата.
//      * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - УправляемаяФорма - внешний модуль обмена с банком;
//  ИдентификаторХранилища - Строка - серийный номер аппаратного устройства.
//
Процедура ПолучитьСертификатЧерезДополнительнуюОбработку(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища) Экспорт
	
	Попытка
		СертификатыНаУстройстве = ПодключаемыйМодуль.СертификатыВХранилище(ИдентификаторХранилища);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения банковских сертификатов.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Получение банковских сертификатов'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецПопытки;
	
	Если СертификатыНаУстройстве.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'На банковском ключе отсутствуют сертификаты.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецЕсли;

	ДанныеВыбора = Новый Соответствие;
		
	Для Каждого СертификатXML Из СертификатыНаУстройстве Цикл
		ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ПодключаемыйМодуль, СертификатXML);
		Если ДанныеСертификата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СтруктураСертификата = Новый Структура;
		СтруктураСертификата.Вставить("СертификатBase64", СертификатXML);
		СтруктураСертификата.Вставить("Псевдоним", ДанныеСертификата.Псевдоним);
		ДанныеВыбора.Вставить(ДанныеСертификата.Отпечаток, СтруктураСертификата);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("ДанныеВыбора", ДанныеВыбора);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСертификата", Оповещение);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВыбораСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборСертификатаДляДобавления", ПараметрыФормы, , , , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПослеВыбораСертификатаЧерезДополнительнуюОбработку(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	
	СертификатXML = ДанныеВыбора.СертификатBase64;
	Отпечаток = ДанныеВыбора.Отпечаток;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ДополнительныеПараметры.ПодключаемыйМодуль, СертификатXML, ДанныеВыбора.Пароль);
		
	Если НЕ ПарольУстановлен Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СертификатXML", СертификатXML);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеУстановкиСоединенияЧерезДополнительнуюОбработку", ЭтотОбъект, ДополнительныеПараметры);
	УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, СертификатXML);
	
КонецПроцедуры

// Выводит сообщение пользователю с привязкой к форме.
//
// Параметры:
//   ТекстСообщенияПользователю - Строка - выводимый текст сообщения;
//   ИдентификаторНазначения - УникальныйИдентификатор - идентификатор формы.
//
Процедура СообщитьПользователю(ТекстСообщенияПользователю, ИдентификаторНазначения) Экспорт
	
	Сообщение = Новый СообщениеПользователю;
	Сообщение.Текст = ТекстСообщенияПользователю;
	Если ЗначениеЗаполнено(ИдентификаторНазначения) Тогда
		Сообщение.ИдентификаторНазначения = ИдентификаторНазначения;
	КонецЕсли;
	Сообщение.Сообщить();
	
КонецПроцедуры

// Осуществляет проверку валидности подписей
//
// Параметры
//    ВнешнийПодключаемыйМодуль - внешний подключаемый модуль.
//    Параметры                 - Структура:
//       НастройкаОбмена                        - СправочникСсылка.НастройкиОбменСБанками - настройка обмена, по которой
//                                           выполняется подписание.
//
Процедура НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		ОО = Новый ОписаниеОповещения("ПолучитьДоступноеХранилищеЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(Параметры.НастройкаОбмена);
		ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(ОО, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	Иначе
		ПолучитьДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Получает внешний интерфейс дополнительной обработки.
//
// Параметры:
//  ИмяВнешнегоМодуля  - Строка - имя подключаемой обработки;
//  АдресКомпоненты - АдресВременногоХранилища - адрес внешней компоненты, которую требуется установить.
//
// Возвращаемое значение:
//  Форма - форма внешней обработки или Неопределено, если не удалось получить внешний интерфейс.
//
Функция ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(ИмяВнешнегоМодуля, АдресКомпоненты = Неопределено) Экспорт

	Перем АдресФайлаВнешнегоМодуля;
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка(ИмяВнешнегоМодуля);

	Если ПодключаемыйМодуль <> Неопределено Тогда
		Возврат ПодключаемыйМодуль;
	КонецЕсли;
	
	ВнешняяОбработкаПодключена = ОбменСБанкамиСлужебныйВызовСервера.ПодключитьАктуальнуюВнешнююОбработку(
		ИмяВнешнегоМодуля, АдресФайлаВнешнегоМодуля);
	
	Если Не ВнешняяОбработкаПодключена Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	ОбработкаИнициализирована = ИнициализироватьИнтерфейсДополнительнойОбработки(
		АдресФайлаВнешнегоМодуля, ИмяВнешнегоМодуля, АдресКомпоненты);
	
	Если Не ОбработкаИнициализирована Тогда  // Требуется установка внешней компоненты
		Возврат Неопределено;
	КонецЕсли;
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка(ИмяВнешнегоМодуля);

	Возврат ПодключаемыйМодуль;
	
КонецФункции

// Подключает внешнюю обработку.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - обработчик, который вызывается после получения внешнего модуля:
//        Результат - УправляемаяФорма или Неопределено - подключенный модуль и Неопределено в случае ошибки;
//    ИмяВнешнегоМодуля - Строка - имя модуля, который нужно подключить.
//
Процедура ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(Оповещение, ИмяВнешнегоМодуля) Экспорт
	
	АдресВК = Неопределено;
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(ИмяВнешнегоМодуля, АдресВК);
	Если ВнешнийПодключаемыйМодуль = Неопределено И ЗначениеЗаполнено(АдресВК) Тогда
		ДополнительныеПараметры = Новый Структура();
		ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияВнешнегоМодуля", Оповещение);
		ДополнительныеПараметры.Вставить("ИмяВнешнегоМодуля", ИмяВнешнегоМодуля);
		ОповещениеПослеУстановкиКомпоненты = Новый ОписаниеОповещения(
			"ПослеУстановкиВнешнейКомпонентыДополнительнойОбработки", ЭтотОбъект, ДополнительныеПараметры);
		НачатьУстановкуВнешнейКомпоненты(ОповещениеПослеУстановкиКомпоненты, АдресВК);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, ВнешнийПодключаемыйМодуль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеУстановкиВнешнейКомпонентыДополнительнойОбработки(Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(Параметры.ИмяВнешнегоМодуля);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияВнешнегоМодуля, ВнешнийПодключаемыйМодуль);
	
КонецПроцедуры

// Проверяет необходимость установки пин-кода.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища.
//
// Возвращаемое значение:
//   Булево, Неопределено - Истина - пин-код уже установлен или не требуется, Ложь - требуется установка пин кода,
//                          Неопределено - произошла ошибка при определении необходимости пин-кода.
//
Функция НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища) Экспорт
	
	Ошибка = Ложь;
	ТребуетсяПИН = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, Ошибка);
	
	Если Ошибка Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Если ТребуетсяПИН Тогда
		
		УстановленPINКодХранилища = УстановленPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, Ошибка);
	
		Если Ошибка Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Возврат НЕ УстановленPINКодХранилища;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Устанавливает PIN код для доступа к банковскому ключу.
//
// Параметры
//  ВнешнийПодключаемыйМодуль - УправляемаяФорма - внешний программный интерфейс;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища;
//  ПинКод  - Строка - Пин код.
//
// Возвращаемое значение:
//  Булево -  пин код установлен успешно или нет.
//
Функция УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ПинКод) Экспорт
	
	ПинКодУстановлен = Истина;
	
	Попытка
		ВнешнийПодключаемыйМодуль.УстановитьPINКодХранилища(ИдентификаторХранилища, ПинКод);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки PIN-кода.
		                          |Код ошибки: ДО-%1
		                          |%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка PIN-кода'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		ПинКодУстановлен = Ложь;
	КонецПопытки;
	
	Возврат ПинКодУстановлен;
	
КонецФункции

// Получает внешний модуль для подписания ЭД через доп.обработку и переходит к получению выписки банка.
//
// Параметры:
//    ВнешнийПодключаемыйМодуль - Внешний подключаемый модуль.
//    Параметры                 - Структура:
//       НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком
//                    по которой выполняется получение выписки.
//       НомерСчета   - Строка - номер банковского счета организации. Если не указан, то запрос по всем счетам.
//       ДатаНачала    - Дата.
//       ДатаОкончания - Дата.
//       Владелец      - Форма или элемент формы - получатель оповещения о выборе элемента - выписки банка.
//
Процедура ПолучитьВыпискуЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		НастройкаОбмена = Параметры.НастройкаОбмена;
		ДанныеСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовБанка(НастройкаОбмена);
		
		Устройства = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль);
		
		СписокВыбора = Новый Массив;
		Соответствие = Новый Соответствие;

		Если Устройства <> Неопределено И Устройства.Количество() > 0 Тогда
			Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
				ПроверитьСрокДействияСертификатаБанка(ДанныеСертификата.Сертификат, ДанныеСертификата.ДействителенДО,
					ДанныеСертификата.ПользовательОповещенОСрокеДействия);
				ДанныеСертификатаЧерезДополнительнуюОбработку = ДанныеСертификатаЧерезДополнительнуюОбработку(
													ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если Устройства.Найти(ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища) <> Неопределено Тогда
					Соответствие.Вставить(ДанныеСертификата.Сертификат, ДанныеСертификата);
				КонецЕсли;
			КонецЦикла
		Иначе
			ТекстСообщения = НСтр("ru = 'Для выполнения операции необходимо подключить банковский ключ к компьютеру'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат
		КонецЕсли;
		
		Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
		Параметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		
		ВидОперации = НСтр("ru = 'Аутентификация на ресурсе банка'");
		Если Соответствие.Количество() > 0 Тогда
			Если ЕстьСертификатССохраненнымПаролем(Соответствие, ВнешнийПодключаемыйМодуль) Тогда
				ВыбранныйСертификат = Неопределено;
				Если НЕ (ДанныеСертификата.Свойство("ВыбранныйСертификат")
					И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования")) Тогда
				
					ДанныеСертификата.Вставить("ВыбранныйСертификат", ДанныеСертификата.Сертификат);
				КонецЕсли;
				ПродолжитьПолучениеВыпискиПослеВводаПароляСертификатаЧерезДополнительнуюОбработку(ДанныеСертификата, Параметры)
			Иначе
				Оповещение = Новый ОписаниеОповещения(
					"ПродолжитьПолучениеВыпискиПослеВводаПароляСертификатаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
				ПолучитьПарольКСертификату(Оповещение, Соответствие, ВидОперации);
				Возврат;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'К компьютеру подключен не подходящий банковский ключ'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Получает внешний модуль для подписания ЭД через доп.обработку и переходит к получению выписки банка.
//
// Параметры:
//    Результат - Структура, Неопределено - если структура,
//              то процедура была вызвана после получения пароля к сертификату:
//       ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования.
//       Комментарий         - Строка.
//       ПарольПользователя  - Строка.
//       Пользователь        - СправочникСсылка.Пользователи, Неопределено.
//    Параметры - Структура:
//       СоотвСертификатовИИхСтруктур - Соответствие.
//       ДанныеСертификатов           - Структура.
//       ВнешнийПодключаемыйМодуль    - Внешний подключаемый модуль.
//       НастройкаОбмена                 - СправочникСсылка.НастройкиОбменСБанками - настройка обмена
//                                    по которой выполняется получение выписки.
//       НомерСчета                   - Строка - номер банковского счета организации. Если не указан, то запрос по всем счетам.
//       Владелец                     - Форма или элемент формы - получатель оповещения о выборе элемента - выписки банка.
//       ДатаНачала                   - Дата.
//       ДатаОкончания                - Дата.
//
Процедура ПродолжитьПолучениеВыпискиПослеВводаПароляСертификатаЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		СоотвСертификатовИИхСтруктур = Параметры.СоотвСертификатовИИхСтруктур;
		НастройкаОбмена = Параметры.НастройкаОбмена;
		
		Если СоотвСертификатовИИхСтруктур.Количество() > 0 Тогда
			ДанныеСертификатов = Параметры.ДанныеСертификатов;
			ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
			
			Для Каждого ДанныеСертификата Из ДанныеСертификатов Цикл
				Если ДанныеСертификата.Сертификат = ВыбранныйСертификат Тогда
					Прервать;
				КонецЕсли;
			КонецЦикла;
			
			ДанныеСертификатаЧерезДополнительнуюОбработку = ДанныеСертификатаЧерезДополнительнуюОбработку(
											ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
			Если ДанныеСертификатаЧерезДополнительнуюОбработку <> Неопределено Тогда
				Параметры.Вставить("ИмяПроцедуры", "ПродолжитьПолучениеВыписки");
				Параметры.Вставить("Модуль", ЭтотОбъект);
				Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
				Параметры.Вставить("ПарольПользователя", Результат.ПарольПользователя);
				Параметры.Вставить("ИдентификаторХранилища", ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища);
				
				ТребуетсяУстановкаPINКода = НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
					ВнешнийПодключаемыйМодуль, ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища);
					
				Если ТребуетсяУстановкаPINКода = Ложь Тогда
					ПродолжитьПолучениеВыпискиЧерезДополнительнуюОбработку(Параметры);
				ИначеЕсли ТребуетсяУстановкаPINКода = Истина Тогда
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
						"ПродолжитьПолучениеВыпискиПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					НачатьУстановкуPINКодаХранилища(НастройкаОбмена,
						ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из ВыполнитьДействияПослеОтправкиПЭДЗавершить.
// Рекурсивная обработка (отправка) банковских документов.
//
// Параметры:
//    Результат - Число, Неопределено.
//    Параметры - Структура:
//       ОбработчикПослеОтправкиПЭД - ОписаниеОповещения - описание, которое надо выполнить когда
//                                  будут обработаны все данные для отправки (ДанныеДляОтправки).
//       ДанныеДляОтправки          - Соответствие
//          Ключ     - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком,
//                   по которой требуется отправить документы.
//          Значение - Структура:
//             ДанныеПакетов - Соответствие.
//             Сертификаты   - Массив - структуры данных сертификатов.
//       КолПодготовленных - Число.
//       КолОтправленных   - Число.
//
Процедура ОтправитьЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Число") Тогда
		Параметры.ИтогКолОтправленных = Параметры.ИтогКолОтправленных + Результат;
	КонецЕсли;
	
	ДанныеДляОтправки = Неопределено;
	Если НЕ Параметры.Свойство("ДанныеДляОтправки", ДанныеДляОтправки)
		ИЛИ ДанныеДляОтправки.Количество() = 0 Тогда
		
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеОтправкиПЭД, Параметры);
	Иначе
		Для Каждого КлючИЗначение Из ДанныеДляОтправки Цикл
			// СтруктураОтправки будет передана в процедуру
			// НачатьОтправкуПакетовЧерезДополнительнуюОбработку, после получения внешнего модуля.
			СтруктураОтправки = Новый Структура;
			СтруктураОтправки.Вставить("НастройкаОбмена", КлючИЗначение.Ключ);
			СтруктураОтправки.Вставить("ДанныеДляОтправки", КлючИЗначение.Значение);
			СтруктураОтправки.Вставить("ИтогКолОтправленных", 0);
			СтруктураОтправки.Вставить("ИтогКолПодготовленных", 0);
			
			Параметры.ДанныеДляОтправки.Удалить(КлючИЗначение.Ключ);
			// ОбработчикПродолжения - рекурсивный вызов текущей процедуры, для продолжения
			// отправки следующего элемента ДанныеДляОтправки.
			ОбработчикПродолжения = Новый ОписаниеОповещения("ОтправитьЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			СтруктураОтправки.Вставить("ОбработчикПродолжения", ОбработчикПродолжения);
			РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(КлючИЗначение.Ключ);
			ОбработчикПослеПодключения = Новый ОписаниеОповещения(
				"НачатьОтправкуПакетовЧерезДополнительнуюОбработку", ЭтотОбъект, СтруктураОтправки);
			ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(
				ОбработчикПослеПодключения, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
			Прервать;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьОтправкуПакетовЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	ПрерватьОбработку = Истина;
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		ДанныеОтправки = Параметры.ДанныеДляОтправки;
		
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		Устройства = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль);
		
		СписокВыбораСертификата = Новый Массив;
		
		Если Устройства <> Неопределено И Устройства.Количество() > 0 Тогда
			Для Каждого ДанныеСертификата Из ДанныеОтправки.Сертификаты Цикл
				ПарольУстановлен = УстановленПарольСертификатаЧерезДополнительнуюОбработку(
								ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если ПарольУстановлен Тогда
					Соответствие = Новый Соответствие;
					СтруктураСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
																		ДанныеСертификата.СертификатСсылка);
					Соответствие.Вставить(ДанныеСертификата.СертификатСсылка, СтруктураСертификата);
					Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
					Прервать;
				КонецЕсли;
			
				СодержимоеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(
						ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
				Если Устройства.Найти(СодержимоеСертификата.ИдентификаторХранилища) <> Неопределено Тогда
					СписокВыбораСертификата.Добавить(ДанныеСертификата.СертификатСсылка);
				КонецЕсли;
			КонецЦикла;
			
			Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
			
			Если НЕ ПарольУстановлен Тогда
				Если СписокВыбораСертификата.Количество() = 0 Тогда
					Для Каждого ДанныеСертификата Из ДанныеОтправки.Сертификаты Цикл
						СписокВыбораСертификата.Вставить(ДанныеСертификата.СертификатСсылка);
					КонецЦикла;
				КонецЕсли;
				Соответствие = Новый Соответствие;
				Для Каждого Сертификат Из СписокВыбораСертификата Цикл
					СтруктураСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(Сертификат);
					Соответствие.Вставить(Сертификат, СтруктураСертификата);
				КонецЦикла;
				
				Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
				
				Если ЕстьСертификатССохраненнымПаролем(Соответствие, ВнешнийПодключаемыйМодуль) Тогда
					Для Каждого Элемент Из Соответствие Цикл
						ДанныеСертификата = Элемент.Значение;
						Прервать;
					КонецЦикла;
					ПарольУстановлен = Истина;
				Иначе
					ОООЗ = Новый ОписаниеОповещения(
						"ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					ПолучитьПарольКСертификату(ОООЗ, Соответствие, НСтр("ru = 'Аутентификация на ресурсе банка'"));
					ПрерватьОбработку = Ложь;
				КонецЕсли;
			КонецЕсли;
			Если ПарольУстановлен Тогда
				ВыбранныйСертификат = Неопределено;
				Если Не ДанныеСертификата.Свойство("ВыбранныйСертификат")
					Или Не ЗначениеЗаполнено(ДанныеСертификата.ВыбранныйСертификат) Тогда
					
					ДанныеСертификата.Вставить("ВыбранныйСертификат", ДанныеСертификата.СертификатСсылка);
				КонецЕсли;
				ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(ДанныеСертификата, Параметры);
				ПрерватьОбработку = Ложь;
			КонецЕсли;
		Иначе
			ТекстСообщения = НСтр("ru = 'К компьютеру не подключен банковский ключ для отправки данных по настройке обмена: %1'");
			ТекстСообщения = СтрШаблон(ТекстСообщения, Параметры.НастройкаОбмена);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
	ОбработчикПродолжения = Неопределено;
	Если ПрерватьОбработку И Параметры.Свойство("ОбработчикПродолжения", ОбработчикПродолжения)
		И ТипЗнч(ОбработчикПродолжения) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения, Параметры.ИтогКолОтправленных);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из НачатьОтправкуПакетовЧерезДополнительнуюОбработку и,
// по описанию оповещения, из ПолучитьПарольКСертификату.
//
// Параметры:
//    Результат - Неопределено, Структура - если Неопределено - то пароль к сертификату не был получен.
//    Параметры - Структура:
//       ВнешнийПодключаемыйМодуль - внешний подключаемый модуль.
//       СоотвСертификатовИИхСтруктур - Структура.
//       ОбработчикПродолжения - ОписаниеОповещения.
//       ДанныеДляОтправки     - Структура:
//          ДанныеПакетов - Соответствие.
//          Сертификаты   - Массив - массив структур сертификатов.
//       НастройкаОбмена          - СправочникСсылка.НастройкиОбменСБанками.
//
//       ИтогКолПодготовленных - Число.
//       ИтогКолОтправленных   - Число.
//       ПараметрыАвторизации  - Соответствие.
//       ТребуетсяАвторизация  - Булево.
//
Процедура ПродолжитьОтправкуПакетовПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(Результат, Параметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	ПрерватьОбработку = Истина;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		ПараметрыСертификата = Параметры.СоотвСертификатовИИхСтруктур[ВыбранныйСертификат];
		ПараметрыСертификата.Вставить("ПарольПолучен", Истина);
		ПараметрыСертификата.Вставить("ПарольПользователя", Результат.ПарольПользователя);
		ПараметрыСертификата.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
		ПараметрыСертификата.Вставить("Комментарий", "");
		Результат.Свойство("Комментарий", ПараметрыСертификата.Комментарий);
		Параметры.Вставить("СтруктураСертификата", ПараметрыСертификата);
		
		ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
		Сертификаты = Параметры.ДанныеДляОтправки.Сертификаты;
		ДанныеСертификата = Неопределено;
		Для Каждого СтруктураСертификата Из Сертификаты Цикл
			Если СтруктураСертификата.СертификатСсылка = ВыбранныйСертификат Тогда
				ДанныеСертификата = СтруктураСертификата;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ДанныеСертификата <> Неопределено Тогда
			Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
			ДанныеСертификатаЧерезДополнительнуюОбработку = ДанныеСертификатаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата);
			ИдентификаторХранилища = ДанныеСертификатаЧерезДополнительнуюОбработку.ИдентификаторХранилища;
			
			Если ДанныеСертификатаЧерезДополнительнуюОбработку <> Неопределено Тогда
				Параметры.Вставить("ОтправляемыеПакетыЧерезДопОбработку", Параметры.ДанныеДляОтправки.ДанныеПакетов);
				Параметры.Вставить("ИмяПроцедуры", "ОтправитьПакетыЧерезДополнительнуюОбработку");
				Параметры.Вставить("Модуль", ЭтотОбъект);
				Параметры.Вставить("ПарольУстановлен", Истина);
				Параметры.Вставить("ПарольПользователя", Результат.ПарольПользователя);
				Параметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);

				НеобходимаУстановкаPINКодаХранилища = НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
																	ВнешнийПодключаемыйМодуль, ИдентификаторХранилища);
				Если НеобходимаУстановкаPINКодаХранилища = Истина Тогда
					ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
						"ПродолжитьОтправкуПакетаПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
					НачатьУстановкуPINКодаХранилища(Параметры.НастройкаОбмена, ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии);
					ПрерватьОбработку = Ложь;
				ИначеЕсли НеобходимаУстановкаPINКодаХранилища = Ложь Тогда
					ПродолжитьОтправкуПакетаЧерезДополнительнуюОбработку(Параметры);
					ПрерватьОбработку = Ложь;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбработчикПродолжения = Неопределено;
	Если ПрерватьОбработку И Параметры.Свойство("ОбработчикПродолжения", ОбработчикПродолжения)
		И ТипЗнч(ОбработчикПродолжения) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения, Параметры.ИтогКолОтправленных);
	КонецЕсли;
	
КонецПроцедуры

// Дозаполняет недостающие данные, необходимые для подписания ЭД и переходит к подписанию ЭД.
//
// Параметры:
//    ВнешнийПодключаемыйМодуль - внешний подключаемый модуль.
//    Параметры                 - Структура:
//       НастройкаОбмена - СправочникСсылка.НастройкиОбменаСБанками - настройка обмена с банком.
//       МассивСообщенийОбменаКПодписи - Массив - подписываемые сообщения обмена.
//       СтруктураСертификата  - Структура - параметры сертификата, для которого "выше"
//                             выполнялся запрос пароля.
//       ОбработчикПродолжения - ОписаниеОповещения - описание, которое надо выполнить после завершения
//                             отработки текущих ЭД (МассивСообщенийОбменаКПодписи).
//
Процедура ПроверитьНеобходимостьУстановкиПинКода(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	ПрерватьПодписание = Истина;
	Если ВнешнийПодключаемыйМодуль <> Неопределено Тогда
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		СертификатСсылка = Параметры.СтруктураСертификата.СертификатПодписи;
		РеквизитыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(СертификатСсылка);
		
		СертификатXML = РеквизитыСертификата.ДвоичныеДанныеСертификата;
		Параметры.Вставить("СертификатXMLЧерезДопОбработку", СертификатXML);
		
		ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML);
		
		Если ДанныеСертификата <> Неопределено Тогда
			Параметры.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
			
			ТребуетсяУстановкаPINКода = НеобходимаУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
				ВнешнийПодключаемыйМодуль, ДанныеСертификата.ИдентификаторХранилища);
			
			Если ТребуетсяУстановкаPINКода = Истина Тогда
				ОО = Новый ОписаниеОповещения(
					"ПродолжитьПодписаниеПослеВводаPINКодаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
				НачатьУстановкуPINКодаХранилища(Параметры.НастройкаОбмена, ДанныеСертификата.ИдентификаторХранилища, ОО);
				ПрерватьПодписание = Ложь;
			ИначеЕсли ТребуетсяУстановкаPINКода = Ложь Тогда
				ПродолжитьПодписаниеЧерезДополнительнуюОбработку(Параметры);
				ПрерватьПодписание = Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если ПрерватьПодписание И Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД)
		И ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

// Предлагает пользователю выбрать хранилище и возвращает результат выбора.
//
// Параметры:
//  НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//  ОО - ОписаниеОповещения - описание вызова процедуры программного модуля, который будет осуществлен после выбора хранилища;
//  Параметры  - структура - параметры обработки ЭД.
//
Процедура ВыбратьХранилищеЧерезДополнительнуюОбработку(НастройкаОбмена, ОО, Параметры) Экспорт
	
	ПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	Хранилища = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ПодключаемыйМодуль);
	
	Если Хранилища = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ОО);
	ИначеЕсли Хранилища.Количество() = 1
		И НЕ ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(ПодключаемыйМодуль, Хранилища[0]) Тогда
		ВыполнитьОбработкуОповещения(ОО, Хранилища[0]);
	ИначеЕсли Хранилища.Количество() > 0 Тогда
		СтруктураПараметров = Новый Структура("Хранилища, НастройкаОбмена", Хранилища, НастройкаОбмена);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборХранилища", СтруктураПараметров, , , , , ОО);
	Иначе
		ТекстСообщения = НСтр("ru = 'Для выполнения операции необходимо подключить банковский ключ к компьютеру'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ОО);
	КонецЕсли
	
КонецПроцедуры

// Вызывается из НачатьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку и,
// по обработке оповещения, из ВыбратьХранилищеЧерезДополнительнуюОбработку.
// Осуществляет проверку валидности подписей.
//
// Параметры
//    ДоступноеХранилище - хранилище сертификатов.
//    Параметры                 - Структура:
//       ВнешнийПодключаемыйМодуль           - внешний подключаемый модуль.
//       МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку - Массив - ЭД в которых надо проверить статусы подписей.
//       ОбработчикПродолжения     - ОписаниеОповещения - (необязательный) описание, которое надо выполнить
//                                           после завершения отработки текущих ЭД (МассивСообщенийОбменаКПодписи).
//       ОповеститьОПроверкеЭП               - Строка - (необязательный) признак необходимости выполнить "Оповестить()".
//
Процедура ВыполнитьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(ДоступноеХранилище, Параметры) Экспорт
	
	Если ДоступноеХранилище <> Неопределено Тогда
		ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
		МассивСообщенийОбмена = Параметры.МассивСообщенийОбменаДляПроверкиЧерезДополнительнуюОбработку;
		Для Каждого СообщениеОбмена Из МассивСообщенийОбмена Цикл
			СтруктураСодержимогоСообщенияОбмена = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(
				СообщениеОбмена);
				
			Если СтруктураСодержимогоСообщенияОбмена = Неопределено Тогда
				Продолжить;
			КонецЕсли;
				
			РезультатПроверки = Новый Массив;
			Для Каждого СтрокаЭП Из СтруктураСодержимогоСообщенияОбмена.Подписи Цикл
				СтруктураЗаписи = Новый Структура();
				Попытка
					ДвоичныеДанныеЭП = СтрокаЭП.Подпись;
					СертификатXML = СтрокаЭП.Сертификат;
					ДопПараметры = Новый Структура("ИдентификаторХранилища", ДоступноеХранилище);
					ПодписьВалидна = ВнешнийПодключаемыйМодуль.ПроверитьПодпись(
						СертификатXML, СтруктураСодержимогоСообщенияОбмена.ДанныеЭД, ДвоичныеДанныеЭП, ДопПараметры);
					СтруктураЗаписи.Вставить("ПодписьВерна", ПодписьВалидна);
					СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
					РезультатПроверки.Добавить(СтруктураЗаписи);
				Исключение
					ШаблонОшибки = НСтр("ru = 'Ошибка проверки подписи.
											|Код ошибки: ДО-%1
											|%2'");
					ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
					ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
					Операция = НСтр("ru = 'Проверка подписи'");
					ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
					ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
						Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, СообщениеОбмена);
				КонецПопытки;
			КонецЦикла;
			Если РезультатПроверки.Количество() Тогда
				ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(СообщениеОбмена, РезультатПроверки);
			КонецЕсли;
		КонецЦикла;
		
		Если Параметры.Свойство("ОповеститьОПроверкеЭП") Тогда
			Оповестить("ПроведенаПроверкаЭП", МассивСообщенийОбмена);
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеОповещения = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеОповещения)
		И ТипЗнч(ОписаниеОповещения) = Тип("ОписаниеОповещения") Тогда
		
		Результат = Неопределено;
		Параметры.Свойство("РезультатПодписания", Результат);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает пароль для установки соединения с банком.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  СертификатXML  - Строка - Содержит данные сертификата;
//  Пароль  - Строка - пароль сертификата.
//
// Возвращаемое значение:
//   Булево   - пароль верный или нет.
//
Функция УстановитьПарольСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML, Пароль, Идентификатор = Неопределено) Экспорт
	
	Попытка
		ВнешнийПодключаемыйМодуль.УстановитьПарольСертификата(СертификатXML, Пароль);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки пароля сертификата.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка пароля сертификата'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ПодробноеПредставлениеОшибки, , 1);
		СообщитьПользователю(ТекстСообщения, Идентификатор);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат Истина;
	
КонецФункции

// Проверяет наличие уже установленного PIN код для хранилища.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища.
//
// Возвращаемое значение:
//   Булево, Неопределено - Истина - пин-код установлен установлен ранее, Ложь - пин код не установлен,
//                          Неопределено - произошла ошибка.
//
Функция УстановленPINКодХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ЕстьОшибка = Ложь) Экспорт
	
	Попытка
		УстановленPINКодХранилища = ВнешнийПодключаемыйМодуль.УстановленPINКодХранилища(ИдентификаторХранилища);
	Исключение
		ЕстьОшибка = Истина;
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки установленного PIN-кода.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка наличия установленного PIN-кода'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат Ложь;
	КонецПопытки;
	
	Возврат УстановленPINКодХранилища;
	
КонецФункции

// Открывает форму ввода пин-кода.
//
// Параметры:
//  НастройкаОбмена  - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//  ИдентификаторХранилища  - Строка - идентификатор хранилища;
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения - возврат после ввода PIN-кода.
//
Процедура НачатьУстановкуPINКодаХранилища(НастройкаОбмена, ИдентификаторХранилища, ОписаниеОповещенияОЗакрытии) Экспорт
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
	ПараметрыФормы.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросPINКода", ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии);
	
КонецПроцедуры

// Отправляет запрос в банк.
//
// Параметры
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  СертификатXML  - строка - данные сертификата;
//  ТипЗапроса  - Число - тип запроса;
//  ДанныеОтправки  - Структура - данные для отправки.
//
// Возвращаемое значение:
//  Соответствие или Неопределено -  результат выполнения.
//
Функция ОтправитьЗапросЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML, ТипЗапроса, ДанныеОтправки) Экспорт

	Попытка
		Результат = ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, ТипЗапроса, ДанныеОтправки);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки данных.
		                          |Код ошибки: ДО-%1
		                          |%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка данных'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
	КонецПопытки;
	
	Возврат Результат

КонецФункции 

// Только для внутреннего использования
Процедура НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры) Экспорт
	
	ДоступныеСертификаты = Параметры.ДоступныеСертификаты;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	ИдентификаторНазначения = Параметры.ИдентификаторНазначения;
	РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(НастройкаОбмена);
	Параметры.Вставить("ИмяВнешнегоМодуля", РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Тест не пройден.'");
		СообщитьПользователю(ТекстСообщения, ИдентификаторНазначения);
		Возврат;
	КонецЕсли;
	
	Если Параметры.Свойство("УстановленаВнешняяКомпонента") Тогда
		СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
		Параметры.Удалить("УстановленаВнешняяКомпонента");
	КонецЕсли;
	
	Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	
	ТекущийИндекс = 0;
	Для Каждого Элемент Из ДоступныеСертификаты Цикл
		
		ТекущийИндекс = ТекущийИндекс + 1;
		Если ТекущийИндекс <= Параметры.ИндексТестаСертификата Тогда
			Продолжить;
		КонецЕсли;
		Параметры.ИндексТестаСертификата = ТекущийИндекс;
		
		ПараметрыСертификата = Элемент.Значение;
		
		ТекстСообщения = НСтр("ru = 'Проверка сертификата: %1'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Элемент.Ключ);
		СообщитьПользователю(ТекстСообщения, ИдентификаторНазначения);
		
		Соответствие = Новый Соответствие;
		Соответствие.Вставить(Элемент.Ключ, ПараметрыСертификата);
		
		Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
		
		Оповещение = Новый ОписаниеОповещения(
			"ПродолжитьТестНастройкиОбменаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		ПолучитьПарольКСертификату(Оповещение, Соответствие, НСтр("ru = 'Аутентификация на ресурсе банка'"));
		Возврат;
	КонецЦикла;
	
	Если ДоступныеСертификаты.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Проверка проведена не полностью, т.к. в настройке обмена отсутствуют сертификаты подписи'");
		СообщитьПользователю(ТекстСообщения, ИдентификаторНазначения);
		Возврат;
	КонецЕсли;
	
	// Блок проверки отправки тестового запроса.
	ОписаниеТеста = НСтр("ru = 'Тест. Отправка тестового запроса.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	
	СертификатXML = Параметры.СертификатXML;
	
	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьЗапрос(СертификатXML, 1);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка отправки тестового запроса.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Отправка тестового запроса'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, НастройкаОбмена);
		Возврат;
	КонецПопытки;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	
КонецПроцедуры

// Получает массив идентификаторов хранилищ, подключенных к компьютеру.
//
// Параметры:
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма.
//  ВыводитьСообщение - Булево - если Ложь, то при отсутствии подключенного аппаратного устройства будет выведено сообщение.
//
// Возвращаемое значение:
//  Массив - идентификаторы хранилищ или Неопределено в случае ошибки.
//
Функция ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ВыводитьСообщение = Истина) Экспорт
	
	Попытка
		Устройства = ВнешнийПодключаемыйМодуль.ХранилищаСертификатов();
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка при поиске подключенных аппаратных устройств.
		                          |Код ошибки: ДО-%1
		                          |%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Поиск аппаратных устройств.'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
							Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат Неопределено;
	КонецПопытки;
	
	Если Устройства.Количество() = 0 И ВыводитьСообщение Тогда
		ТекстСообщения = НСтр("ru = 'Не найдено ни одного аппаратного устройства.
									|Убедитесь, что устройство подключено к компьютеру и повторите операцию'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Возврат Устройства;
	
КонецФункции

// Устанавливает соединение с банком.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//                  * Результат - результат вызова метода.
//                  * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  СертификатXML  - Строка - Содержит данные сертификата;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка с банком;
//
Процедура УстановитьСоединениеЧерезДополнительнуюОбработку(Оповещение, ПодключаемыйМодуль, СертификатXML, НастройкаОбмена = Неопределено) Экспорт
	
	Попытка
		
		Если НЕ ПодключаемыйМодуль.УстановитьСоединение(СертификатXML) Тогда
			ПараметрыРасширеннойАутентификации = Неопределено;
			ТребуетсяРасширеннаяАутентификация = ПодключаемыйМодуль.ТребуетсяРасширеннаяАутентификация(
															СертификатXML, ПараметрыРасширеннойАутентификации);
			Если ТребуетсяРасширеннаяАутентификация Тогда
				Если ПараметрыРасширеннойАутентификации.Способы.Количество() = 0 Тогда
					ВызватьИсключение НСтр("ru = 'Не определены способы расширенной аутентификации.'");
				КонецЕсли;
				Если НЕ ПараметрыРасширеннойАутентификации.Способы.Свойство("SMS") Тогда
					ВызватьИсключение НСтр("ru = 'Расширенная аутентификация по SMS не поддерживается.'");
				КонецЕсли;
				ОдноразовыйПароль = Неопределено;
				
				РасширеннаяАутентификацияЧерезДопОбработку(
					ПодключаемыйМодуль, СертификатXML, ПараметрыРасширеннойАутентификации.Сессия, Оповещение);
			Иначе
				ВызватьИсключение НСтр("ru = 'Ошибка установки соединения.'");
			КонецЕсли;
		Иначе
			ВыполнитьОбработкуОповещения(Оповещение, Истина);
		КонецЕсли;
		
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки соединения.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка соединения'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, НастройкаОбмена);
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
	КонецПопытки;
	
КонецПроцедуры

// Проверяет, нужно ли устанавливать пин-код.
//
// Параметры
//  ВнешнийПодключаемыйМодуль  - Управляемая Форма - внешняя управляемая форма;
//  ИдентификаторХранилища - Строка - идентификатор хранилища;
//  Ошибка - Булево - признак возникновения ошибки при выполнении функции;
//
// Возвращаемое значение:
//  Булево или Неопределено - нужно установить PIN или Неопределено при возникновении ошибки.
//
Функция ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, Ошибка = Ложь) Экспорт
	
	Ошибка = Ложь;
	Попытка
		ТребуетсяПИН = ВнешнийПодключаемыйМодуль.ТребуетсяУстановкаPINКодаХранилища(ИдентификаторХранилища);
	Исключение
		ОчиститьСообщения();
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки необходимости ввода PIN-кода.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка необходимости ввода PIN-кода'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Ошибка = Истина;
		ТребуетсяПИН = Ложь;
	КонецПопытки;
	
	Возврат ТребуетсяПИН;
	
КонецФункции

#КонецОбласти

#Область Криптография

// Получает пароль к сертификату для нестандартного подписания.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//   * Результат - Структура - результат вызова метода. Содержит следующие поля:
//                  ** ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - выбранный пользователем сертификат подписи;
//                  ** ПарольПользователя - Строка - пароль к закрытой части сертификата;
//               - Неопределено - Пользователь отказался от ввода и закрыл форму.
//    * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  СоотвСертификатовИИхСтруктур - Соответствие - набор сертификатов для выбора:
//        * Ключ - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат подписи;
//        * Значение - Структура - параметры сертификат подписи. Содержит следующие поля:
//             * ПарольПолучен - Булево - признак, что пароль сохранен в информационной базе
//  ВидОперации - Строка - выполняемая операция;
//  ОбъектыДляОбработки - Ссылка или Массив - объект, с которым связано событие;
//  ПрограммаБанка - ПеречислениеСсылка.ПрограммыБанков - программа обмена с банком.
//
Процедура ПолучитьПарольКСертификату(Оповещение, СоотвСертификатовИИхСтруктур, ВидОперации, ОбъектыДляОбработки = Неопределено, ПрограммаБанка = Неопределено) Экспорт
	
	Если ТипЗнч(СоотвСертификатовИИхСтруктур) = Тип("Соответствие")
		И СоотвСертификатовИИхСтруктур.Количество() > 0 Тогда
		
		СбербанкОнлайн = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
		
		Если СоотвСертификатовИИхСтруктур.Количество() = 1 ИЛИ ПрограммаБанка = СбербанкОнлайн Тогда
			Для Каждого Элемент Из СоотвСертификатовИИхСтруктур Цикл
				Если Элемент.Значение.ПарольПолучен = Истина ИЛИ ПрограммаБанка = СбербанкОнлайн Тогда
					Элемент.Значение.Вставить("ВыбранныйСертификат", Элемент.Ключ);
					ВыполнитьОбработкуОповещения(Оповещение, Элемент.Значение);
					Возврат;
				КонецЕсли;
			КонецЦикла;
			Если СоотвСертификатовИИхСтруктур.Количество() > 1 И ПрограммаБанка = СбербанкОнлайн Тогда
				ПустойОбработчик = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
				ЗавершитьСессиюНаТокенеСбербанк(ПустойОбработчик);
			КонецЕсли;
		КонецЕсли;
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОперации", ВидОперации);
		ПараметрыФормы.Вставить("ДляЗаписиВИБ", Ложь);
		МассивСертификатов = Новый Массив;
		Для Каждого КлючЗначение Из СоотвСертификатовИИхСтруктур Цикл
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		ПараметрыФормы.Вставить("МассивСертификатов",  МассивСертификатов);
		Если ОбъектыДляОбработки <> Неопределено Тогда
			Если ТипЗнч(ОбъектыДляОбработки) <> Тип("Массив") Тогда
				МассивОбъектов = Новый Массив;
				МассивОбъектов.Добавить(ОбъектыДляОбработки);
			Иначе
				МассивОбъектов = ОбъектыДляОбработки;
			КонецЕсли;
			ПараметрыФормы.Вставить("ОбъектыДляОбработки", МассивОбъектов);
		КонецЕсли;
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату", ПараметрыФормы, , , , , Оповещение,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
	КонецЕсли;
	
КонецПроцедуры

// Проверяет подписи электронного документа.
//
// Параметры:
// Оповещение - ОписаниеОповещения - оповещение после проверки подписей:
//    * Результат - Булево - Истина, если все не было ошибки и все подписи валидны;
//    * ДополнительныеПараметры - Структура - Содержит дополнительные параметры;
// СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение обмена электронного документа.
//
Процедура ПроверитьПодписи(Оповещение, СообщениеОбмена) Экспорт
	
	СтруктураСодержимого = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(СообщениеОбмена);
	
	МассивПодписей = СтруктураСодержимого.Подписи;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписей", Оповещение);
	ДополнительныеПараметры.Вставить("МассивПодписей", МассивПодписей);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеЭД", СтруктураСодержимого.ДанныеЭД);
	ДополнительныеПараметры.Вставить("ИндексТекущейПодписи", -1);
	ДополнительныеПараметры.Вставить("РезультатыПроверкиПодписей", Новый Массив);
	
	ПроверитьОчереднуюПодпись(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ЗаписатьРезультатПослеПроверкиПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	СтруктураРезультата = Новый Структура;
	СтруктураРезультата.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
	Если ТипЗнч(Результат) = Тип("Булево") Тогда
		СтруктураРезультата.Вставить("ПодписьВерна", Результат);
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		ТекстОшибки = НСтр("ru = 'Электронная подпись неверна.
								|Причина: %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Результат);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки);
		СтруктураРезультата.Вставить("ПодписьВерна", Ложь);
	Иначе
		ТекстСообщения = НСтр("ru = 'Не удалось проверить подписи электронного документа.
									|Проверьте настройки криптографии или обратитесь к администратору.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.РезультатыПроверкиПодписей.Добавить(СтруктураРезультата);
	
	ПроверитьОчереднуюПодпись(ДополнительныеПараметры)
	
КонецПроцедуры

#КонецОбласти

#Область ПодменюКомандЭДО

////////////////////////////////////////////////////////////////////////////////
// Обработчики команд ЭДО

// Команда открытия электронного документа.
Процедура ОткрытьАктуальныйЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбменСБанкамиКлиент.ОткрытьАктуальныйЭД(
		ПараметрКоманды, ПараметрыВыполненияКоманды.Источник, ПараметрыВыполненияКоманды);
	
КонецПроцедуры

// Команда открытия формы списка электронных документов.
Процедура СписокЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОбменСБанкамиКлиент.ОткрытьСписокЭД(ПараметрКоманды, ПараметрыВыполненияКоманды);
	
КонецПроцедуры

// Команда создания электронного документа.
Процедура СформироватьЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОчиститьСообщения();
	Обработчик = Новый ОписаниеОповещения("СформироватьНовыйЭД", ОбменСБанкамиКлиент, Истина);
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ВыполнитьПроверкуПроведенияДокументов(
		ПараметрКоманды, Обработчик, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

// Команда создания, подписания и отправки электронного документа.
Процедура СформироватьПодписатьОтправитьЭД(ПараметрКоманды, ПараметрыВыполненияКоманды) Экспорт
	
	ОчиститьСообщения();
	Обработчик = Новый ОписаниеОповещения("СформироватьПодписатьОтправитьЭД", ОбменСБанкамиКлиент);
	ЭлектронноеВзаимодействиеКлиентПереопределяемый.ВыполнитьПроверкуПроведенияДокументов(
		ПараметрКоманды, Обработчик, ПараметрыВыполненияКоманды.Источник);
	
КонецПроцедуры

#КонецОбласти

#Область НастройкиОбмена

// Проверяет корректность URL.
// 
// Параметры:
//  АдресСервера - Строка - URL сервера банка.
//
// Возвращаемое значение:
//  Булево - Истина, если адрес корректный, иначе Ложь.
//
Функция ПравильныйФорматАдреса(АдресСервера) Экспорт
	
	Если НРег(Лев(АдресСервера, 7)) = "http://"
			ИЛИ НРег(Лев(АдресСервера, 8)) = "https://" Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

// Производит тестирование настройки обмена с банком.
//
// Параметры:
//  Обработчик - ОписаниеОповещения - обработчик, вызываемый после тестирования настройки:
//           * Результат - Структура - с полями:
//              ** Успех - Булево - результат тестирования настройки;
//              ** МассивСообщений - Массив - в элементах строки, содержащие текст сообщений об ошибках;
//  Параметры - Структура - параметры обработки:
//           * НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - тестируемая настройка обмена с банком;
//           * ИдентификаторНазначения - УникальныйИдентификатор - идентификатор формы для вывода сообщений.
//
Процедура ПровестиТестНастройки(Обработчик, Параметры) Экспорт
	
	ПараметрыОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаПоВидуЭД(
		Параметры.НастройкаОбмена, ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросЗонд"));
	ПрограммаБанка = ПараметрыОбмена.ПрограммаБанка;
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен") Тогда
		Параметры.Вставить("РеквизитыНастройкиОбмена", ПараметрыОбмена);
		Параметры.Вставить("ОбработчикПослеТестаНастройки", Обработчик);
		Если ПараметрыОбмена.АутентификацияПоСертификату ИЛИ ПараметрыОбмена.ТребуетсяПодпись Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковПроверитьСвязь", ЭтотОбъект, Параметры);
			ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Истина);
		Иначе
			ПослеПолученияОтпечатковПроверитьСвязь( , Параметры);
		КонецЕсли;
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
		ПровестиТестНастройкиСбербанк(Обработчик, Параметры.НастройкаОбмена);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
		ТестНастройкиОбменаВК(Обработчик, Параметры.НастройкаОбмена);
	ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
		ТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры.НастройкаОбмена, Параметры.ИдентификаторНазначения);
	КонецЕсли;
	
КонецПроцедуры

Функция ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML) Экспорт
	
	Попытка
		ДанныеСертификата = ВнешнийПодключаемыйМодуль.ДанныеСертификата(СертификатXML);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка получения данных сертификата.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Получение данных сертификата'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат ДанныеСертификата;
	
КонецФункции

Функция УстановленПарольСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML) Экспорт
	
	Попытка
		УстановленПарольСертификата = ВнешнийПодключаемыйМодуль.УстановленПарольСертификата(СертификатXML);
	Исключение
		УстановленПарольСертификата = Ложь;
	КонецПопытки;

	Возврат УстановленПарольСертификата;
	
КонецФункции

Процедура ПродолжитьПолучениеВыпискиПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	
	Если PINКод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	УстановленPIN = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
		
	Если НЕ УстановленPIN Тогда
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ДанныеСертификата = Параметры.ДанныеСертификата;
	ПарольПользователя = Параметры.ПарольПользователя;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, ПарольПользователя);
	Если НЕ ПарольУстановлен Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьПолучениеВыписки", ЭтотОбъект, Параметры);
	
	УстановитьСоединениеЧерезДополнительнуюОбработку(Оповещение, ВнешнийПодключаемыйМодуль,
		ДанныеСертификата.ДвоичныеДанныеСертификата, НастройкаОбмена);
		
КонецПроцедуры

Процедура ПродолжитьПолучениеВыписки(АутентификацияВыполнена, Параметры) Экспорт
	
	Если Не АутентификацияВыполнена Тогда
		Возврат
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ПараметрыФормы.Вставить("ДанныеСертификата", Параметры.ДанныеСертификата);
	ПараметрыФормы.Вставить("ДатаНачала", Параметры.ДатаНачала);
	ПараметрыФормы.Вставить("ДатаОкончания", Параметры.ДатаОкончания);
	ПараметрыФормы.Вставить("НомерСчета", Параметры.НомерСчета);

	ОткрытьФорму(
		"Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, Параметры.Владелец);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетаПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	ПрерватьОбработку = Истина;
	Если PINКод <> Неопределено Тогда
		УстановленPIN = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
		Если УстановленPIN Тогда
			ПродолжитьОтправкуПакетаЧерезДополнительнуюОбработку(Параметры)
		КонецЕсли;
	КонецЕсли;
	
	ОбработчикПродолжения = Неопределено;
	Если ПрерватьОбработку И Параметры.Свойство("ОбработчикПродолжения", ОбработчикПродолжения)
		И ТипЗнч(ОбработчикПродолжения) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения, Параметры.ИтогКолОтправленных);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьПакетыЧерезДополнительнуюОбработку(АутентификацияВыполнена, Параметры) Экспорт
	
	Если АутентификацияВыполнена = Истина Тогда
		ДокументыКОтправке = Новый Массив;
		МассивОтправленныхЭД = Новый Массив;
		
		Для Каждого Документ Из Параметры.ОтправляемыеПакетыЧерезДопОбработку Цикл
			СтруктураОтправки = Новый Структура;
			СтруктураОтправки.Вставить("Ключ",                Документ.Значение.Ключ);
			СтруктураОтправки.Вставить("ЭлектронныйДокумент", Документ.Значение.ПлатежноеПоручение);
			СтруктураОтправки.Вставить("СхемаДанных",         ПолучитьИзВременногоХранилища(Документ.Значение.СлужебныеДанные));
			СтруктураОтправки.Вставить("Подписи",             Новый Массив);
		
			Для Каждого СтрокаДанныеПодписи Из Документ.Значение.Подписи Цикл
				Подпись = ПолучитьИзВременногоХранилища(СтрокаДанныеПодписи.АдресПодписи);
				ДанныеПодписи = Новый Структура("Сертификат, Подпись", СтрокаДанныеПодписи.Сертификат, Подпись);
				СтруктураОтправки.Подписи.Добавить(ДанныеПодписи);
			КонецЦикла;
			ДокументыКОтправке.Добавить(СтруктураОтправки);
		КонецЦикла;
		
		СтруктураОтправки = Новый Структура();
		СтруктураОтправки.Вставить("Документы",         ДокументыКОтправке);
		СтруктураОтправки.Вставить("ВерсияСхемыДанных", ОбменСБанкамиКлиентСервер.ВерсияФорматаСинхронногоОбмена());

		Результат = ОтправитьЗапросЧерезДополнительнуюОбработку(
			Параметры.ВнешнийПодключаемыйМодуль, Параметры.ДанныеСертификата.ДвоичныеДанныеСертификата, 3, СтруктураОтправки);
		
		Если НЕ Результат = Неопределено Тогда
			КоличествоОтправленных = Результат.Количество();
			ОтправленныеДокументы = Новый Массив;
			ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветБанкаПослеОтправкиДокументовЧерезВнешнююОбработку(
					Параметры.ОтправляемыеПакетыЧерезДопОбработку, Результат, ОтправленныеДокументы);
			Если ОтправленныеДокументы.Количество() Тогда
				Оповестить("ОтправленоDirectBank", ОтправленныеДокументы);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОбработчикПродолжения = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОбработчикПродолжения)
		И ТипЗнч(ОбработчикПродолжения) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОбработчикПродолжения, КоличествоОтправленных);
		Параметры.Удалить("ОбработчикПродолжения");
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьПодписаниеПослеВводаPINКодаЧерезДополнительнуюОбработку(PINКод, Параметры) Экспорт
	
	ПрерватьПодписание = Истина;
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	Если PINКод <> Неопределено Тогда
		УстановленPIN = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
			
		Если УстановленPIN Тогда
			ПрерватьПодписание = Ложь;
			ПродолжитьПодписаниеЧерезДополнительнуюОбработку(Параметры)
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если ПрерватьПодписание И Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД)
		И ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПодписаниеЭДЧерезДополнительнуюОбработку(АутентификацияВыполнена, Параметры) Экспорт
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	МассивСообщенийОбменаДляПроверки = Новый Массив;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	СтруктураСертификата = Параметры.СтруктураСертификата;
	МассивСообщенийОбменаКПодписи = Параметры.МассивСообщенийОбменаКПодписи;
	ПрерватьПодписание = Ложь;
	Если АутентификацияВыполнена = Истина Тогда
		СертификатXML = Параметры.СертификатXMLЧерезДопОбработку;
		ДанныеОбработки = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляФормированияЭПЧерезДопОбработку(
			Параметры.МассивСообщенийОбменаКПодписи);

		Если ДанныеОбработки.МассивТекстовыхДанныхЭД.Количество() > 0 Тогда
			Попытка
				МассивНовыхСхемДанных = ВнешнийПодключаемыйМодуль.СхемаДанных(
						СертификатXML, ДанныеОбработки.МассивТекстовыхДанныхЭД);
			Исключение
				ШаблонОшибки = НСтр("ru = 'Ошибка получения схемы данных.
											|Код ошибки: ДО-%1
											|%2'");
				ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
				ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
				Операция = НСтр("ru = 'Получение схемы данных'");
				ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
					Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, НастройкаОбмена);
				ПрерватьПодписание = Истина;
			КонецПопытки;
			Если НЕ ПрерватьПодписание Тогда
				ОбменСБанкамиСлужебныйВызовСервера.СохранитьСхемыДанных(
					НастройкаОбмена, ДанныеОбработки.МассивСообщенийБезСхем, МассивНовыхСхемДанных);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(ДанныеОбработки.МассивДанныхСхем,  МассивНовыхСхемДанных);
				ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
					ДанныеОбработки.МассивСообщенийСоСхемами, ДанныеОбработки.МассивСообщенийБезСхем);
			КонецЕсли;
		КонецЕсли;
		
		Если НЕ ПрерватьПодписание Тогда
			ДанныеПодписей = Новый Соответствие;
			КоличествоЭД = ДанныеОбработки.МассивСообщенийСоСхемами.Количество();
			Попытка
				МассивПодписей = ВнешнийПодключаемыйМодуль.Подписать(СертификатXML, ДанныеОбработки.МассивДанныхСхем);
			Исключение
				ШаблонОшибки = НСтр("ru = 'Ошибка подписания документов.
											|Код ошибки: ДО-%1
											|%2'");
				ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
				ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
				Операция = НСтр("ru = 'Подписание документов'");
				ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
									Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, НастройкаОбмена);
				ПрерватьПодписание = Истина;
			КонецПопытки;
			Если НЕ ПрерватьПодписание Тогда
				ОбменСБанкамиСлужебныйВызовСервера.СохранитьДанныеПодписей(
					МассивСообщенийОбменаКПодписи, МассивПодписей, СтруктураСертификата.СертификатПодписи);
				Параметры.ИтогКолПодписанных = Параметры.ИтогКолПодписанных + МассивПодписей.Количество();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД);
	Если ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Параметры) Экспорт
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		ОписаниеОповещения = Неопределено;
		Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеОповещения)
			И ТипЗнч(ОписаниеОповещения) = Тип("ОписаниеОповещения") Тогда
			
			Результат = Неопределено;
			Параметры.Свойство("РезультатПодписания", Результат);
			ВыполнитьОбработкуОповещения(ОписаниеОповещения, Результат);
		КонецЕсли;
	Иначе
		Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
		ДоступноеХранилище = ДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль);
		Если ЗначениеЗаполнено(ДоступноеХранилище) Тогда
			ВыполнитьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку(ДоступноеХранилище, Параметры);
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"ВыполнитьПроверкуСтатусовПодписейЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			ВыбратьХранилищеЧерезДополнительнуюОбработку(Параметры.НастройкаОбмена, Оповещение, Параметры);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаПослеВводаПароляКСертификатуЧерезДополнительнуюОбработку(ПараметрВозврата, Параметры) Экспорт
	
	ИдентификаторНазначения = Параметры.ИдентификаторНазначения;
	
	Если ПараметрВозврата = Неопределено ИЛИ Параметры.СоотвСертификатовИИхСтруктур.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не введен пароль для сертификата.
									|Тест прерван.'");
		СообщитьПользователю(ТекстСообщения, ИдентификаторНазначения);
		Возврат;
	КонецЕсли;
	
	ПарольПользователя = Неопределено;
	ВыбранныйСертификат = Неопределено;
	ПараметрВозврата.Свойство("ПарольПользователя", ПарольПользователя);
	Для Каждого КлючИЗначение Из Параметры.СоотвСертификатовИИхСтруктур Цикл
		ПараметрыСертификата = КлючИЗначение.Значение;
		ПараметрыСертификата.Вставить("ПарольПользователя", ПарольПользователя);
		Прервать;
	КонецЦикла;
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	
	СертификатXML = ПараметрыСертификата.ДанныеСертификата;
		
	// Блок чтения данных сертификата.
	ОписаниеТеста = НСтр("ru = 'Тест. Чтение данных сертификата.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, СертификатXML);
	Если ДанныеСертификата = Неопределено Тогда
		Возврат;
	КонецЕсли;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	
	// Блок проверка наличия установленного PIN-кода.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка наличия PIN-кода на аппаратном устройстве.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	ЕстьОшибка = Ложь;
	ТребуетсяПИН = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ДанныеСертификата.ИдентификаторХранилища, ЕстьОшибка);
	Если ЕстьОшибка Тогда
		Возврат;
	КонецЕсли;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	
	Параметры.Вставить("СертификатXML", СертификатXML);
	Параметры.Вставить("ПарольПользователя", ПарольПользователя);
	Параметры.Вставить("ДанныеСертификата", ДанныеСертификата);
	Параметры.Вставить("ИмяПроцедуры", "ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеАутентификации");
	Параметры.Вставить("Модуль", ЭтотОбъект);
	Параметры.Вставить("ПараметрыСертификата", ПараметрыСертификата);
	Параметры.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
	
	// Блок проверка установки PIN-кода.
	Если ТребуетсяПИН Тогда
		ОписаниеТеста = НСтр("ru = 'Тест. Установка PIN-кода на аппаратном устройстве.'");
		СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
		ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
		Параметры.Вставить("УстановленPINКод");
		ОООЗ = Новый ОписаниеОповещения(
			"ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеВводаPINКода", ЭтотОбъект, Параметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросPINКода", ПараметрыФормы, , , , , ОООЗ);
		Возврат;
	КонецЕсли;
	ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры);
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеАутентификации(АутентификацияВыполнена, Параметры) Экспорт
	
	Если Не АутентификацияВыполнена = Истина Тогда
		Возврат;
	КонецЕсли;

	ИдентификаторНазначения = Параметры.ИдентификаторНазначения;
	ПараметрыСертификата = Параметры.ПараметрыСертификата;
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = Параметры.СертификатXML;
	ДанныеСертификата = Параметры.ДанныеСертификата;
	
	РезультатТеста = НСтр("ru = 'Пройден успешно.'");

	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
		
	// Блок проверка установки подписи для данных.
	ОписаниеТеста = НСтр("ru = 'Тест. Установка подписи.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	ОтпечатокBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(ПараметрыСертификата.Отпечаток);
	ДвоичныеДанные = Base64Значение(ОтпечатокBase64);
	МассивПодписи = Новый Массив;
	МассивПодписи.Добавить(ДвоичныеДанные);
	Попытка
		МассивПодписей = ВнешнийПодключаемыйМодуль.Подписать(СертификатXML, МассивПодписи);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка установки подписи.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат;
	КонецПопытки;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	
	// Блок проверка подписи.
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка подписи.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	Попытка
		ДопПараметры = Новый Структура("ИдентификаторХранилища", ДанныеСертификата.ИдентификаторХранилища);
		ПодписьВалидна = ВнешнийПодключаемыйМодуль.ПроверитьПодпись(
			СертификатXML, ДвоичныеДанные, МассивПодписей[0], ДопПараметры);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка проверки подписи.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Проверка подписи'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		Возврат;
	КонецПопытки;
	Если НЕ ПодписьВалидна Тогда
		СообщитьПользователю(НСтр("ru = 'Подпись не валидна'"), ИдентификаторНазначения);
		Возврат;
	КонецЕсли;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);

	НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры);
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеВводаPINКода(PINКод, Параметры) Экспорт
	
	Если PINКод = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(Параметры.ИмяВнешнегоМодуля);
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПинКодУстановлен = УстановитьPINКодХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, Параметры.ИдентификаторХранилища, PINКод);
	
	Если Не ПинКодУстановлен Тогда
		Возврат;
	КонецЕсли;
	
	ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры)
	
КонецПроцедуры

// Производит сериализацию данных.
//
// Параметры:
//  Значение - Произвольный - данные для сериализации;
//
// Возвращаемое значение:
//  Строка - сериализованные данные.
//
Функция СериализованныеДанные(Знач Значение) Экспорт

	#Если НЕ ВебКлиент Тогда

		Если Значение = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;

		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		ОбъектXDTO = Сериализатор.ЗаписатьXDTO(Значение);
		ЗаписьXML = Новый ЗаписьXML;
		ЗаписьXML.УстановитьСтроку();
		ФабрикаXDTO.ЗаписатьXML(ЗаписьXML, ОбъектXDTO);

		Возврат ЗаписьXML.Закрыть();
		
	#Иначе
		
		Возврат ОбменСБанкамиСлужебныйВызовСервера.СериализованныеДанные(Значение);

	#КонецЕсли

КонецФункции

// Функция получает пароль пользователя к сертификатам ЭП. Если в СоотвСертификатовИИхСтруктур передано несколько
// сертификатов, то в этот параметр вместо списка помещается один найденный сертификат с паролем и его параметры.
//
// Параметры:
//  СоотвСертификатовИИхСтруктур - Соответствие - содержит соответствие сертификатов и их параметров:
//    * Ключ     - СправочникСсылка.СертификатыЭП - сертификат ЭП.
//    * Значение - Структура - содержит параметры сертификата.
//  ВнешнийПодключаемыйМодуль - Форма - внешний модуль.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен, иначе - Ложь.
//
Функция ЕстьСертификатССохраненнымПаролем(СоотвСертификатовИИхСтруктур, ВнешнийПодключаемыйМодуль = Неопределено) Экспорт
	
	ПарольПолучен = Ложь;
	
	Если ТипЗнч(СоотвСертификатовИИхСтруктур) = Тип("Соответствие") Тогда
		КоличествоСертификатов = СоотвСертификатовИИхСтруктур.Количество();
		Для Каждого КлючИЗначение Из СоотвСертификатовИИхСтруктур Цикл
			Сертификат = КлючИЗначение.Ключ;
			ПараметрыСертификата = КлючИЗначение.Значение;
			ПрограммаБанка = Неопределено;
			Если НЕ ЗначениеЗаполнено(ПараметрыСертификата) Тогда
				ПараметрыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(Сертификат);
			КонецЕсли;
			Если ПараметрыСертификата.Свойство("ПрограммаБанка")
				И ПараметрыСертификата.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
				ПарольПолучен = Истина;
				Прервать;
			КонецЕсли;
			ПарольПользователя = Неопределено;
			Если (КоличествоСертификатов = 1) И (ПараметрыСертификата.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен
													ИЛИ ПарольКСертификатуПолучен(Сертификат, ПарольПользователя)) Тогда
				Если НЕ ПарольПолучен Тогда
					ПарольПолучен = Истина;
					ПараметрыСертификата.Вставить("ПарольПолучен", ПарольПолучен);
					ПараметрыСертификата.Вставить("ПарольПользователя", ПарольПользователя);
					ПараметрыСертификата.Вставить("ВыбранныйСертификат", Сертификат);
				КонецЕсли;
				Прервать;
			ИначеЕсли ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
					И НЕ ВнешнийПодключаемыйМодуль = Неопределено
					И АктуаленКэшПароляСертификатаЧерезДополнительнуюОбработку(ПараметрыСертификата, ВнешнийПодключаемыйМодуль) Тогда
				ПараметрыСертификата.Вставить("ВыбранныйСертификат", Сертификат);
				ПарольПолучен = Истина;
				Прервать;
			КонецЕсли;
		КонецЦикла;
		Если ПарольПолучен Тогда
			СоотвСертификатовИИхСтруктур = Новый Соответствие;
			СоотвСертификатовИИхСтруктур.Вставить(Сертификат, ПараметрыСертификата);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ПарольПолучен;

КонецФункции

// Прерывает процесс получения документов через ВК.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком.
//
Процедура ПрерватьПроцессыНаКлиенте(НастройкаОбмена) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	КонецЕсли;

	Если ПараметрыПодсистемыОбменСБанками.Получить("ПрерватьПроцессПоНастройкамОбмена") <> Неопределено Тогда
		МассивНастроекОбмена = ПараметрыПодсистемыОбменСБанками.Получить("ПрерватьПроцессПоНастройкамОбмена");
	Иначе
		МассивНастроекОбмена = Новый Массив;
	КонецЕсли;
	
	МассивНастроекОбмена.Добавить(НастройкаОбмена);
	
	ПараметрыПодсистемыОбменСБанками.Вставить("ПрерватьПроцессПоНастройкамОбмена", МассивНастроекОбмена);
	
КонецПроцедуры

// Определяет, отменил ли пользователь процесс запроса данных. При этом признак прерывания сбрасывается.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка, по который идет процесс получения документов.
// 
// Возвращаемое значение:
// Булево - если Истина, значит пользователь нажал на кнопку Отмена.
//
Функция ПроцессПрерван(НастройкаОбмена) Экспорт
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) = Тип("Соответствие")
		И НЕ ПараметрыПодсистемыОбменСБанками.Получить("ПрерватьПроцессПоНастройкамОбмена") = Неопределено Тогда
		НастройкиОбменаДляПрерыванияПроцесса = ПараметрыПодсистемыОбменСБанками.Получить(
			"ПрерватьПроцессПоНастройкамОбмена");
		Если НастройкиОбменаДляПрерыванияПроцесса.Найти(НастройкаОбмена) <> Неопределено Тогда
			ОбщегоНазначенияКлиентСервер.УдалитьЗначениеИзМассива(НастройкиОбменаДляПрерыванияПроцесса, НастройкаОбмена);
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;

	Возврат Ложь;
	
КонецФункции

// Проверяет, соответствует ли версия подключенной компоненты ожидаемой версии.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//    РезультатВызова - Строка - текст сообщения об ошибке
//                    - Булево - Истина, если ожидаемая версия соответствует реальной.
//    ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - AddIn - внешний модуль;
//  ОжидаемаяВерсия - Строка - ожидаемая версия подключенной внешней компоненты.
//
Процедура ПроверитьВерсиюВК(Оповещение, ПодключаемыйМодуль, ОжидаемаяВерсия) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОжидаемаяВерсия", ОжидаемаяВерсия);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияНомераВерсии", Оповещение);
	
	ОповещениеПослеПолученияВерсии = Новый ОписаниеОповещения(
		"ПослеПолученияВерсииВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияВерсииВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовВерсия(ОповещениеПослеПолученияВерсии);
	
КонецПроцедуры

// Производит инициализацию внешней компоненты банка (асинхронно).
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - Булево - Истина при успешной инициализации, в противном случае возвращается строка;
//                   - Строка - Содержит текст ошибки, если операцию не удалось выполнить.
//       * ДополнительныеПараметры - Произвольный - контекст выполнения.
//    ПодключаемыйМодуль - AddIn - внешняя компонента банка;
//
Процедура ИнициализироватьВК(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеИнициализацииВК", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеИнициализацииВК = Новый ОписаниеОповещения(
		"ПослеИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуИнициализацииВК", ЭтотОбъект);

	ИнформацияОПрограмме = ОбменСБанкамиСлужебныйВызовСервера.ИнформацияОПрограммеДляВК();
	
	ВерсияФормата = ОбменСБанкамиКлиентСервер.АктуальнаяВерсияФорматаАсинхронногоОбмена();
	
	ПодключаемыйМодуль.НачатьВызовИнициализировать(ОповещениеПослеИнициализацииВК, ИнформацияОПрограмме, ВерсияФормата);

КонецПроцедуры

// Формирует пакеты и отправляет в банк через внешнюю компоненту
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//    * РезультатВызова - Структура - результат выполнения процедуры. Содержит следующие поля:
//                      - ИтогКолПодготовленных - Число - количество подготовленных пакетов;
//                      - ИтогКолОтправленных - Число - количество отправленных пакетов.
//    * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения
//  ДанныеОтправки - Соответствие - отправляемые данные:
//     * Ключ - СправочникСсылка.НастройкиОбменСБанками - настройка обмена;
//     * Значение - Массив - электронные документы для отправки в банк:
//         ** ДокументСсылка.СообщениеОбменСБанками - электронный документ.
//  ПолучитьНовыеДокументы - Булево - после отправки получить новые документы из банка.
//
Процедура ОтправитьДокументыЧерезВК(Оповещение, ДанныеОтправки, ПолучитьНовыеДокументы = Истина) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиСообщенийОбменаПоНастройкам", Оповещение);
	ДополнительныеПараметры.Вставить("ДанныеОтправки", ДанныеОтправки);
	ДополнительныеПараметры.Вставить("ИтогКолПодготовленных", 0);
	ДополнительныеПараметры.Вставить("ИтогКолОтправленных", 0);
	ДополнительныеПараметры.Вставить("ПолучитьНовыеДокументы", ПолучитьНовыеДокументы);
	
	ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

// Подписывает электронный документ с использованием внешней компоненты.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//    * РезультатВызова - Булево - Истина, если документ успешно подписан, Ложь - операция не выполнена.
//                      - Строка - текст ошибки, которая произошла при подписи.
//                      - Неопределено - пользователь отказался от продолжения операции.
//    * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения
//  СообщениеОбмена - ДокументСсылка.СообщенияОбменСБанками - подписываемый электронный документ;
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена;
//  СертификатСсылка - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования - сертификат подписи
//  Пароль - Строка - пароль к закрытой части сертификата;
//  ПодключаемыйМодуль - AddIn - подключенная внешняя компонента банка.
//
Процедура ПодписатьЭДПоСертификатуЧерезВК(Оповещение, СообщениеОбмена, НастройкаОбмена, СертификатСсылка, Пароль, ПодключаемыйМодуль = Неопределено, ЭтоТест = Ложь) Экспорт

	ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(СообщениеОбмена);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписанияЧерезВК", Оповещение);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДанныеЭДBase64", Base64Строка(ДвоичныеДанныеЭД));
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("СертификатСсылка", СертификатСсылка);
	ДополнительныеПараметры.Вставить("Пароль", Пароль);
	ДополнительныеПараметры.Вставить("ЭтоТест", ЭтоТест);
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(СертификатСсылка, РеквизитыСертификата);
	ДополнительныеПараметры.Вставить("СертификатBase64", РеквизитыСертификата.ДанныеСертификата);
	
	Если ПодключаемыйМодуль = Неопределено Тогда
		ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
			"АутентифицироватьсяНаТокенеПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
		
		ПодключитьИИнициализироватьВК(ОповещениеПослеПодключенияВК, НастройкаОбмена);
	Иначе // используется при тесте настроек, когда модуль выбран из файла
		АутентифицироватьсяНаТокенеПослеПодключенияВК(ПодключаемыйМодуль, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

// Устанавливает соединение с банком на доступном сертификате.
// Если соединение уже установлено, то повторную установку соединения не инициирует.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//   * Результат - Булево - истина, если соединение успешно установлено.
//               - Строка - текст ошибки выполнения операции. Ее необходимо вывести пользователю.
//               - Неопределено - пользователь отказался от установки соединения.
//   * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//
Процедура УстановитьСоединениеЧерезВК(Оповещение, НастройкаОбмена) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиСоединенияВК", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПолученияПодключаемогоМодуля = Новый ОписаниеОповещения(
		"ПослеПолученияПодключаемогоМодуляВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьИИнициализироватьВК(ОповещениеПослеПолученияПодключаемогоМодуля, НастройкаОбмена);
	
КонецПроцедуры

// Запускает процесс получения выписки через ВК
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  ДатаНачала - Дата - начало периода запроса выписки;
//  ДатаОкончания - Дата - дата окончания периода запроса выписки;
//  НомерСчета - Строка - номер банковского счета;
//  Владелец - Форма или элемент формы - получатель оповещения о выборе элемента - массива выписок банка.
//
Процедура НачатьПроцессПолученияВыпискиЧерезВК(НастройкаОбмена, ДатаНачала, ДатаОкончания, НомерСчета, Владелец) Экспорт

	НастройкиОбмена = Неопределено;
	МассивЗапросов = ОбменСБанкамиСлужебныйВызовСервера.ЗапросыВыписок(
		НастройкаОбмена, ДатаНачала, ДатаОкончания, НомерСчета, Новый Массив, НастройкиОбмена);
	
	Если НЕ МассивЗапросов.Количество() ИЛИ НастройкиОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("Владелец", Владелец);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбменаКОтправке", МассивЗапросов);
	
	Если НастройкиОбмена.Подписывать Тогда
		Если НЕ ЗначениеЗаполнено(НастройкиОбмена.СертификатОрганизацииДляПодписи)
				ИЛИ НЕ НастройкиОбмена.СертификатДоступен Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат для подписи документа Запрос выписки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		СоответствиеСертификатов = Новый Соответствие;
		
		Для Каждого СертификатСсылка Из НастройкиОбмена.ДоступныеСертификаты Цикл
			СоответствиеСертификатов.Вставить(СертификатСсылка, Новый Структура("ПарольПолучен", Ложь));
		КонецЦикла;
		Если ЕстьСертификатССохраненнымПаролем(СоответствиеСертификатов) Тогда
			Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
				ДанныеАутентификации = Новый Структура;
				ДанныеАутентификации.Вставить("ВыбранныйСертификат", КлючЗначение.Ключ);
				ДанныеАутентификации.Вставить("ПарольПользователя", КлючЗначение.Значение.ПарольПользователя);
				ПодписатьЗапросыВыписокПослеВводаПароляЧерезВК(ДанныеАутентификации, ДополнительныеПараметры);
			КонецЦикла;
		Иначе
			Оповещение = Новый ОписаниеОповещения(
				"ПодписатьЗапросыВыписокПослеВводаПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
			ВидОперации = НСтр("ru = 'Подписание электронных документов'");
			ПолучитьПарольКСертификату(Оповещение, СоответствиеСертификатов, ВидОперации, МассивЗапросов);
		КонецЕсли;
	Иначе
		Оповещение =Новый ОписаниеОповещения("ЗапуститьПроцессПолученияВыпискиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ДанныеОтправки = Новый Соответствие;
		ДанныеОтправки.Вставить(НастройкаОбмена, МассивЗапросов);
		ОтправитьДокументыЧерезВК(Оповещение, ДанныеОтправки, Ложь);
	КонецЕсли;
	
КонецПроцедуры

// Осуществляет подключение и инициализацию внешней компоненты.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//        * РезультатВызова - AddIn - внешняя компонента банка;
//                          - Строка - текст ошибки;
//                          - Неопределено - внешняя компонента не подключена, текст ошибки выведен пользователю.
//        * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена.
//
Процедура ПодключитьИИнициализироватьВК(Оповещение, НастройкаОбмена) Экспорт
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля, ИспользоватьЖурналирование, КаталогДляЖурналирования");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка(РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	
	Если ПодключаемыйМодуль = Неопределено Тогда
		
		ОжидаемаяВерсияВК = ОбменСБанкамиСлужебныйВызовСервера.ВерсияАктуальнойВК(
			РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
			
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("ОповещенияПослеПодключенияИИнициализацииВК", Оповещение);
		ДополнительныеПараметры.Вставить("ОжидаемаяВерсияВК", ОжидаемаяВерсияВК);
		ДополнительныеПараметры.Вставить("ИспользоватьЖурналирование", РеквизитыНастройкиОбмена.ИспользоватьЖурналирование);
		ДополнительныеПараметры.Вставить("КаталогДляЖурналирования", РеквизитыНастройкиОбмена.КаталогДляЖурналирования);
		
		ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
			"ПроверитьВерсиюПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	
		ПодключитьВнешнююКомпонентуБанка(
			ОповещениеПослеПодключенияВК, НастройкаОбмена, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, ПодключаемыйМодуль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВключенияЖурналированияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, Результат);
	Иначе
		ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, ДополнительныеПараметры.ПодключаемыйМодуль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВключенияЖурналирования(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеВключенияЖурналирования, Истина);
	
КонецПроцедуры

Процедура ПослеОшибкиВключенияЖурналированияВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При включении журналирования произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Включение журналирования.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеВключенияЖурналирования,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

// Получает новые документы из банка.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * РезультатВызова - Структура - результат работы процедуры. Имеет следующие поля
//                            * Результат - Булево - Истина, если метод отработал корректно и новые документы были получены и загружены в базу;
//                                        - Строка - текст ошибки, которую необходимо вывести пользователю.
//                            * ИтогКолПолученных - Число - количество полученных пакетов.
//     * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена.
//
Процедура ПолучитьНовыеДокументыВК(Оповещение, ПодключаемыйМодуль, НастройкаОбмена) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияНовыхДокументов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ИтогКолПолученных", 0);
	
	ОповещениеПослеПолученияИдентификаторовПакетовЧерезВК = Новый ОписаниеОповещения(
		"ПолучитьПакетыПослеПолученияИдентификаторовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьПолучениеИдентификаторовНовыхПакетовЧерезВК(
		ОповещениеПослеПолученияИдентификаторовПакетовЧерезВК, НастройкаОбмена, ПодключаемыйМодуль);
	
КонецПроцедуры

// Получает идентификаторы подключенных ключей через ВК.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//       * РезультатВызова - Массив - содержит идентификаторы подключенных электронных ключей.
//                         - Строка - текст ошибки при исключении
//       * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка.
//
Процедура ПолучитьИдентификаторыПодключенныхКлючейЧерезВК(Оповещение, ПодключаемыйМодуль) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияИдентификаторовКлючей", Оповещение);
	ОповещениеПослеПолученияХранилищКлючей = Новый ОписаниеОповещения("ПослеПолученияХранилищКлючейЭПВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПолученияХранилищКлючейЭП", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовХранилищаКлючейЭП(ОповещениеПослеПолученияХранилищКлючей);
	
КонецПроцедуры

// Выполняет тестирование настройки обмена.
//
// Параметры:
//  Оповещение -  ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//                  * Результат - равен Истина, если тест пройден успешно, Ложь - тест не выполнен с выводом сообщения об ошибке.
//                              - Строка - текст ошибки;
//                              - Неопределено - пользователь нажал Отмена.
//                  * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  ПараметрыУстановленногоСоединения - Структура - если соединение устанавливалось при получении настроек, то оно и будет использоваться для теста:
//     * ИдентификаторХранилища - Строка - идентификатор электронного ключа, на котором уже установлено соединение;
//     * Отпечаток - Строка - отпечаток сертификата, по которому уже установлено соединение.
//     * СертификатBase64 - Строка - сертификат, на котором установлено соединение в формате Base64.
//
Процедура ТестНастройкиОбменаВК(Оповещение, НастройкаОбмена, ПараметрыУстановленногоСоединения = Неопределено) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеТестаНастройки", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ПараметрыУстановленногоСоединения", ПараметрыУстановленногоСоединения);
	
	Если ПараметрыУстановленногоСоединения <> Неопределено Тогда
		ПолучитьСертификатыСКлючаПослеПодключенияВК(
			ПараметрыУстановленногоСоединения.ПодключаемыйМодуль, ДополнительныеПараметры);
	Иначе
		ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
			"ПолучитьСертификатыСКлючаПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
		
		ПодключитьИИнициализироватьВК(ОповещениеПослеПодключенияВК, НастройкаОбмена);
	КонецЕсли;
		
КонецПроцедуры

// Предлагает пользователю аутентифицироваться на токене и получить дополненный сертификат (асинхронно).
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое в результате выполнения процедуры:
//     * Результат - Структура - данные сертификата;
//                 - Неопределено - пользователь отказался от продолжения операции;
//                 - Строка - текст сообщения о возникшей ошибке
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка.
//  ПараметрыСоединения - Структура - параметры соединения. Содержит следующие элементы:
//     * ИдентификаторОрганизации - Строка - идентификатор организации из настройки обмена;
//     * БИК - Строка - БИК банка;
//     * КлючУникальности - идентифицирующая пользователя строка, например “<инн>_<бик>”. Используется для сбора статистики подключений.
//
Процедура ПолучитьДанныеСертификатаСТокенаВК(Оповещение, ПодключаемыйМодуль, ПараметрыСоединения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещенияПослеПолученияДанныхСертификата", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ПараметрыСоединения", ПараметрыСоединения);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПредъявитьДанныеАутентификацииПослеПолучения", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеАутентификацииНаТокенеВК(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

// Получает подробную информацию об ошибке, возникшей при работе с внешней компонентой (асинхронно).
// Также производит запись в журнал регистрации.
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//     * ТекстОшибки - Строка - текст ошибки для вывода пользователю в виде сообщения;
//     * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    ПодключаемыйМодуль - AddIn - внешняя компонента банка;
//    ВидОперации - Строка - выполняемая операция;
//    ТекстСообщения - Строка - начальный текст сообщения пользователю.
//
Процедура ПолучитьИнформациюОбОшибкеВК(Оповещение, ПодключаемыйМодуль, ВидОперации, ТекстСообщения) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ВидОперации", ВидОперации);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("ТекстСообщения", ТекстСообщения);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияИнформацииОбОшибкеВК", ЭтотОбъект, ДополнительныеПараметры);
	ПодключаемыйМодуль.НачатьВызовДеталиОшибки(Оповещение);
	
КонецПроцедуры

Процедура ПослеПолученияИнформацииОбОшибкеВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДеталиОшибки = ДеСериализованныеДанные(РезультатВызова);

	Если ДеталиОшибки.Код = 0 Тогда
		ПодробныйТекстОшибки = ДеталиОшибки.Сообщение;
	Иначе
		ШаблонОшибки = ДополнительныеПараметры.ТекстСообщения + Символы.ПС + НСтр("ru = 'Код ошибки: ВК-%1
																					|%2'");
		ПодробныйТекстОшибки = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧГ="), ДеталиОшибки.Сообщение);
	КонецЕсли;
	
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
		ДополнительныеПараметры.ВидОперации, ПодробныйТекстОшибки, , 1);
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, ПодробныйТекстОшибки);
	
КонецПроцедуры

// Получает данные ключа через внешнюю компоненту (асинхронно).
//
// Параметры:
//  Обработчик - ОписаниеОповещения - Обработчик, вызываемый после получения данных ключа:
//     * Результат - Структура - данные сертификата банка с элементами:
//                     ** ИдентификаторХранилища - Строка - идентификатор хранилища (для данных банковского ключа может не заполняться)
//                     ** Псевдоним - Строка - псевдоним ключа ЭП 
//                     ** Отпечаток - Строка - уникальный идентификатор ключа ЭП, уникальность поддерживается в рамках всех сертификатов данного вида (для сертификатов x.509 вернуть отпечаток сертификата)
//                     ** СерийныйНомер - Строка серийный номер ключа в hex (для сертификатов x.509 серийный номер сертификата)
//                     ** ИмяИздателя - Строка - имя издателя сертификата (для сертификатов x.509 поле CN)
//                     ** ВладелецФИО* - Строка - ФИО владельца ключа ЭП 
//                     ** ВладелецДолжность* - Строка, должность владельца ключа ЭП
//                     ** ДатаОкончания* - Дата - указывает последний день, когда можно использовать ключ ЭП
//                     Поля отмеченные символом * заполнены после дополнения данных ключа ЭП
//                - Строка - текст ошибки.
//     * ДополнительныеПараметры - Произвольный - контекст выполнения.
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка;
//  СертификатBase64 - Строка - сертификат в формате Base64.
//
Процедура ПолучитьДанныеКлючаЧерезВК(Обработчик, ПодключаемыйМодуль, СертификатBase64) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификатаВК", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатаВК", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияДанныхСертификатаВК", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовДанныеКлючаЭП(Оповещение, СертификатBase64);

КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезВК

// Проверяет подписи сообщения обмена
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * РезультатВызова - результат вызова метода.
//             - Строка - текст ошибки.
//             - Булево - если Истина, то операция выполнена успешно.
//     * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком.
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение обмена, у которого необходимо проверить подписи.
//
Процедура ПроверитьПодписиСообщенияОбменаЧерезВК(Оповещение, НастройкаОбмена, СообщениеОбмена) Экспорт
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписей", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ОповещениеПослеИнициализацииВК = Новый ОписаниеОповещения(
		"ОбеспечитьПодключениеАппаратногоУстройстваПослеИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьИИнициализироватьВК(ОповещениеПослеИнициализацииВК, НастройкаОбмена);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

#Область ВнешниеКомпоненты

Процедура ПослеПопыткиПодключенияВнешнейКомпоненты(Результат, Параметры) Экспорт
	
	Если Результат Тогда
		Попытка
			ПодключаемыйМодуль = Новый("AddIn." + Параметры.ИдентификаторВК + "." + Параметры.НазваниеВК);
			ЗакэшироватьПодключаемыйМодуль(Параметры.НазваниеВК, ПодключаемыйМодуль);
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка подключения внешнего модуля банка.'");
			Операция = НСтр("ru = 'Подключение внешней компоненты банка'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		КонецПопытки;
	Иначе
		ОО = Новый ОписаниеОповещения("ПодключитьВнешнююКомпонентуБанкаПослеУстановки", ЭтотОбъект, Параметры);
		НачатьУстановкуВнешнейКомпоненты(ОО, Параметры.АдресВнешнейКомпоненты);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодключенияКомпонентыБанка, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПодключитьВнешнююКомпонентуБанкаПослеУстановки(ДополнительныеПараметры) Экспорт
	
	ОповещениеПослеПодключенияКомпоненты = Новый ОписаниеОповещения(
		"ПослеПодключенияВнешнейКомпоненты", ЭтотОбъект, ДополнительныеПараметры);

	НачатьПодключениеВнешнейКомпоненты(ОповещениеПослеПодключенияКомпоненты,
		ДополнительныеПараметры.АдресВнешнейКомпоненты, ДополнительныеПараметры.ИдентификаторВК,
		ТипВнешнейКомпоненты.Native);
	
КонецПроцедуры

Процедура ПослеПодключенияВнешнейКомпоненты(Подключено, Параметры) Экспорт

	Если Подключено Тогда
		Попытка
			ПодключаемыйМодуль = Новый("AddIn." + Параметры.ИдентификаторВК + "." + Параметры.НазваниеВК);
			ЗакэшироватьПодключаемыйМодуль(Параметры.НазваниеВК, ПодключаемыйМодуль);
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка подключения внешней компоненты банка.'");
			Операция = НСтр("ru = 'Подключение внешнего модуля банка'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		КонецПопытки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодключенияКомпонентыБанка, ПодключаемыйМодуль);
	
КонецПроцедуры

#КонецОбласти

#Область Сбербанк

// Определяет номер бизнес системы на банковском токене
// 
// Параметры:
//    Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//        * НомерБизнесСистемы - Строка, Неопределено - номер бизнес системы на токене, или Неопределено в случае ошибки
//        * ДополнительныеПараметры - Произвольный - контекст выполнения
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка
//    НазваниеБизнесСистемы - Строка - название бизнес-системы.
//
Процедура ОпределитьНомерБизнесСистемыСбербанк(Оповещение, ПодключаемыйМодуль, НазваниеБизнесСистемы)
	
	Параметры = Новый Структура();
	Параметры.Вставить("ОповещенияПослеОпределенияНомераБизнесСистемы", Оповещение);
	Параметры.Вставить("НазваниеБизнесСистемы", НазваниеБизнесСистемы);
	ОповещениеПослеПолученияНомераБизнесСистемы = Новый ОписаниеОповещения(
		"НайтиБизнесСистемуПослеПолученияСпискаСТокенаСбербанк", ЭтотОбъект, Параметры);
	
	ПолучитьБизнесСистемыНаТокенеСбербанк(ОповещениеПослеПолученияНомераБизнесСистемы, ПодключаемыйМодуль);
	
КонецПроцедуры

// Выполняет кэширование данных авторизации на сервере сбербанка.
//
// Параметры:
//  Название  - Строка - название кэшируемого параметра;
//  Значение  - Произвольный - значение кэшируемого параметра.
//
Процедура ЗакэшироватьПараметрСбербанка(Название, Значение)
	
	ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
		ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
	КонецЕсли;
	
	ПараметрыОбменаСбербанк = ПараметрыПодсистемыОбменСБанками.Получить("Сбербанк");
	
	Если ПараметрыОбменаСбербанк = Неопределено Тогда
		ПараметрыОбменаСбербанк = Новый Структура;
	КонецЕсли;
	
	ПараметрыОбменаСбербанк.Вставить(Название, Значение);
	ПараметрыПодсистемыОбменСБанками.Вставить("Сбербанк", ПараметрыОбменаСбербанк);
	
КонецПроцедуры

Процедура ОпределитьНеобходимостьПовторнойАутентификацииНаТокенеПослеПолученияСпискаБизнесСистемСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		АутентифицироватьсяНаТокенеСбербанка(
			ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, ДополнительныеПараметры.НастройкаОбмена, Истина);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Истина);
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстСообщенияСбербанк(Тикет)
	
	Если Тикет = "00000000-0000-0000-0000-000000000000" Тогда
		ТекстСообщения = НСтр("ru = 'Сервер банка временно недоступен. Повторите попытку или обратитесь в свой банк.'");
	ИначеЕсли Тикет = "00000000-0000-0000-0000-000000000007" Тогда
		ТекстСообщения = НСтр("ru = 'Неверный идентификатор организации.
									|Проверьте настройки обмена с сервисом 1С:ДиректБанк или обратитесь в свой банк.'");
	Иначе
		ТекстСообщения = НСтр("ru = 'Сервер банка вернул неизвестную ошибку.
									|Код: %1
									|Повторите сеанс связи или обратитесь в свой банк.'");
		ТекстСообщения = СтрШаблон(ТекстСообщения, Тикет);
	КонецЕсли;
	
	Возврат ТекстСообщения;
	
КонецФункции

Процедура НайтиБизнесСистемуПослеПолученияСпискаСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, Неопределено);
		Возврат;
	КонецЕсли;
	
	БизнесСистемы = Результат.БизнесСистемы;
	НазваниеБизнесСистемы = ДополнительныеПараметры.НазваниеБизнесСистемы;
	
	МассивСтрок = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(БизнесСистемы, Символы.ПС);
	Для Каждого Строка Из МассивСтрок Цикл
		Если СтрНайти(ВРег(Строка), ВРег(НазваниеБизнесСистемы)) > 0 Тогда
			ПозПервойКавычки = СтрНайти(Строка, """");
			БизнесСистемаСтрокой = Сред(Строка, ПозПервойКавычки + 1);
			ПозВторойКавычки = СтрНайти(БизнесСистемаСтрокой, """");
			БизнесСистемаСтрокой = Сред(БизнесСистемаСтрокой, 1 , ПозВторойКавычки - 1);
			НомерБизнесСистемы = Число(БизнесСистемаСтрокой);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если НомерБизнесСистемы = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Не найдена бизнес система на банковском ключе.
									|Необходимо проверить работу TLS VPN Key.'");
		Операция = НСтр("ru = 'Поиск бизнес системы на электронном ключе.'");
		ШаблонОшибки = НСтр("ru = 'На электронном ключе не найдена бизнес-система %1.
							|Содержимое списка возврата: %2'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, НазваниеБизнесСистемы, БизнесСистемы);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, 1);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, Неопределено);
		Возврат;
	КонецЕсли;
	
	БизнесСистемаСтрокой = Строка(НомерБизнесСистемы);
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещенияПослеОпределенияНомераБизнесСистемы, БизнесСистемаСтрокой);
	
КонецПроцедуры

// Осуществляет отправку запроса выписки после установки виртуального канала с банком.
// 
// Параметры:
//    Успех - Булево - установлен канал со Сбербанком;
//    Параметры - Структура - данные для отправки запроса.
//
Процедура ОтправитьЗапросВыпискиПослеУстановленияКаналаСбербанк(Успех, Параметры) Экспорт
	
	Если НЕ Успех Тогда
		Возврат;
	КонецЕсли;
	
	ВладелецФормы = Параметры.Владелец;
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, Пароль, Пользователь, СообщениеОбмена, ГотовыеВыписки,
		|ПринудительноеПолучениеВыписки");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);

	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, ВладелецФормы);

КонецПроцедуры

// Производит аутентификацию на токене.
//
// Параметры:
//    ОповещениеПослеАутентификацииНаТокене - ОписаниеОповещения - оповещение после выполнения процедуры:
//       * Результат - Булево - если Истина, то аутентификация успешно выполнена, иначе Ложь;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком (может быть не заполнена);
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка;
//    НомерКонтейнера - Число - номер бизнес системы. Для экранного токена значение не заполнено;
//    ПинКод - Строка - секретный код. Для экранного токена значение не заполнено;
//    ТипУстройства - Строка - тип токена (UNKNOWN - Неизвестно, ORDINARY - Обычный, BTN - Сенсорный(с кнопкой), SCR - Экранный).
//
Процедура ЗарегистрироватьсяНаТокенеСбербанка(ОповещениеПослеАутентификацииНаТокене, НастройкаОбмена, ПодключаемыйМодуль, НомерКонтейнера, ПинКод, ТипУстройства)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеПослеАутентификацииНаТокене);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НомерКонтейнера", НомерКонтейнера);
	ДополнительныеПараметры.Вставить("ПинКод", ПинКод);
	ДополнительныеПараметры.Вставить("ТипУстройства", ТипУстройства);

	Оповещение = Новый ОписаниеОповещения("ПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект);
	Если ТипУстройства = "SCR" Тогда // Экранный токен
		ПодключаемыйМодуль.НачатьВызовПредъявитьПинЭкр(Оповещение);
	Иначе
		ПодключаемыйМодуль.НачатьВызовПредъявитьПин(Оповещение, НомерКонтейнера, ПинКод);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При аутентификации на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Предъявление кода доступа на аппаратном устройстве'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, 1);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
	
КонецПроцедуры

Процедура ПослеПодписанияТестовойСтрокиСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда // Пользователь отказался подписывать
		Возврат;
	ИначеЕсли Результат.Успех Тогда
		ЗакэшироватьПараметрСбербанка("ТестоваяПодпись", Результат.ЭП);
	Иначе
		Параметры.Контекст.ОписаниеОшибки = Результат.ТекстОшибки;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
	
КонецПроцедуры

Процедура ПолучитьИнформациюОТокенеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура("Оповещение", Оповещение);
	ОповещениеПослеПолученияИнформацииОТокене = Новый ОписаниеОповещения("ПослеПолученияИнформацииОТокенеСбербанк",
		ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияИнформацииОТокенеСбербанк", ЭтотОбъект);
	
	ВерсияПрошивки = 0;
	ТипУстройства = "";

	Попытка
		ПодключаемыйМодуль.НачатьВызовПолучитьПараметрыVPNKeyTLS(
			ОповещениеПослеПолученияИнформацииОТокене, ВерсияПрошивки, ТипУстройства);
	Исключение
		Операция = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
		КраткоеПредставлениеОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонСообщения = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка:
								|%1'");
		ТекстСообщения = СтрШаблон(ШаблонСообщения, КраткоеПредставлениеОшибки);
		ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
		ВыполнитьОбработкуОповещения(Оповещение, Результат);
	КонецПопытки
	
КонецПроцедуры

Процедура ПослеПолученияИнформацииОТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
		ТекстОшибки = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка.
							|Внешний модуль VpnKey-TLS вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
	Иначе
		Результат = Новый Структура("Успех, ТипУстройства", Истина, ПараметрыВызова.Получить(1));
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияИнформацииОТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении информации об аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение информации об аппаратном устройстве.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , 1);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ПодписатьДанныеПослеПолученияИнформацииОТокенеСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ Результат.Успех Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ТипУстройства", Результат.ТипУстройства);
		
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатАсинхроннойПодписиСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодписиСбербанк", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодписатьДанныеЧерезVPNKeyTLSАсинхр(
		Оповещение, ДополнительныеПараметры.СтрокаПодписи, ДополнительныеПараметры.ИдентификаторСертификата);
	
КонецПроцедуры

Процедура ОбработатьРезультатАсинхроннойПодписиСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
							|Внешний модуль VpnKey-TLS при подписании вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = ДополнительныеПараметры.ПодключаемыйМодуль;
	ТипУстройства = ДополнительныеПараметры.ТипУстройства;
	Представление = ДополнительныеПараметры.Представление;
	
	Если ТипУстройства = "UNKNOWN" ИЛИ ТипУстройства = "ORDINARY" Тогда
		ПолучитьПодписанныеДанныеСбербанк(ДополнительныеПараметры.ОповещениеПослеПодписи, ПодключаемыйМодуль);
	Иначе // сенсорный или экранный
		ПараметрыФормы = Новый Структура("ОбъектПодписи, ТипУстройства", Представление, ТипУстройства);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ОжиданиеПодписиСбербанк", ПараметрыФормы, , , , ,
			ДополнительныеПараметры.ОповещениеПослеПодписи, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;

КонецПроцедуры

Процедура ОбработатьОшибкуПодписиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При подписи данных произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Подпись данных.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , 1);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ТекстОшибки", ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписи, Результат);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаСТокенаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'При получении данных сертификата произошла ошибка.'");
		ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при получении сертификата вернул код ошибки'") + РезультатВызова;
		Операция = НСтр("ru = 'Получение сертификата криптографии.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, 1);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	СертификатBase64 = ПараметрыВызова[1];
	
	СертификатBase64 = СтрЗаменить(СертификатBase64, "-----BEGIN CERTIFICATE-----" + Символы.ПС,""); 
	СертификатBase64 = СтрЗаменить(СертификатBase64, Символы.ПС + "-----END CERTIFICATE-----","");
	
	ДвоичныеДанныеСертификата = Base64Значение(СертификатBase64);
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, ДвоичныеДанныеСертификата);

КонецПроцедуры

Процедура ОбработатьОшибкуПослеПолученияДанныхСертификатаСТокенаСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении сертификата с аппаратного устройства произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение данных сертификата с аппаратного устройства.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, 1);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификата, Неопределено);
	
КонецПроцедуры

Процедура ПроверитьУстановкуПодписиПолученияСертификатовСТокенаСбербанк(Результат, Параметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		Параметры.Контекст.ОписаниеОшибки = Результат.ТекстОшибки;
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	
	СписокСертификатов = Новый СписокЗначений;
	
	РеквизитыПроверяемогоСертификата = Новый Структура("Отпечаток");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
		Параметры.Сертификат, РеквизитыПроверяемогоСертификата);
	
	Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
		Попытка
			СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(КлючЗначение.Значение);
		Исключение
			ТекстСообщения = НСтр("ru = 'Ошибка чтения сертификата.'");
			ТекстОшибки = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
			Параметры.Контекст.ОписаниеОшибки = ТекстСообщения + Символы.ПС + ТекстОшибки;
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);
			Возврат;
		КонецПопытки;
		
		Если СтруктураСертификата.Отпечаток = РеквизитыПроверяемогоСертификата.Отпечаток Тогда
			ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияТестовойСтрокиСбербанк", ЭтотОбъект, Параметры);
			СтрокаПодписиBase64 = "JiMxMDU4OyYjMTA3NzsmIzEwODk7JiMxMDkwOw=="; // "Тест" в Base64
			Представление = НСтр("ru = 'Тестовая строка'");
			ПодписатьДанныеСбербанк(
				ОписаниеОповещения, ПодключаемыйМодуль, СтрокаПодписиBase64, КлючЗначение.Ключ, Представление);
			Возврат
		КонецЕсли;
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru = 'Сертификат не найден на аппаратном устройстве.'");
	Параметры.Контекст.ОписаниеОшибки = ТекстСообщения;
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Параметры.Контекст);

КонецПроцедуры

Процедура ПослеПолученияСпискаСертификатовСТокенаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова <> 0 Тогда
		ШаблонОшибки = НСтр("ru = 'При получении списка сертификатов на банковском ключе произошла ошибка.
							|Внешний модуль VpnKey-TLS при получении списка доступных сертификатов вернул код ошибки: %1'");
		ТекстОшибки = СтрШаблон(ШаблонОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Подписание электронного документа.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		ОчиститьДанныеАвторизацииСбербанк();
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
		Возврат;
	КонецЕсли;
	
	ИдентификаторыСертификатов = ПараметрыВызова[1];
	
	СоответствиеСертификатов = Новый Соответствие;
	ТекстовыйДокумент = Новый ТекстовыйДокумент;
	ТекстовыйДокумент.УстановитьТекст(ИдентификаторыСертификатов);
	Индекс = 2;
	Пока Индекс < ТекстовыйДокумент.КоличествоСтрок() Цикл
		Текст = ТекстовыйДокумент.ПолучитьСтроку(Индекс);
		Текст = СтрЗаменить(Текст, ",", "");
		Текст = СтрЗаменить(Текст, ";", "");
		СоответствиеСертификатов.Вставить(Текст);
		Индекс = Индекс + 1;
	КонецЦикла;
	
	Если СоответствиеСертификатов.Количество() Тогда
		ДополнительныеПараметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
		КопияСоответствия = ОбщегоНазначенияКлиентСервер.СкопироватьСоответствие(СоответствиеСертификатов);
		ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(КопияСоответствия, ДополнительныеПараметры);
		Возврат;
	Иначе
		ТекстСообщения = НСтр("ru = 'Не найден ни один сертификат на аппаратном устройстве.'");
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);

КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаИдентификаторовСертификатов(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение списка сертификатов с аппаратного устройства.'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить информацию о сертификатах с аппаратного устройства.'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
	ОчиститьДанныеАвторизацииСбербанк();
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
	
КонецПроцедуры

Процедура ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(СоответствиеСертификатов, ДополнительныеПараметры)
	
	Если СоответствиеСертификатов.Количество() = 0 Тогда
		Результат = Новый Структура();
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("СоответствиеСертификатов", ДополнительныеПараметры.СоответствиеСертификатов);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСпискаСертификатов, Результат);
		Возврат;
	КонецЕсли;

	Для Каждого КлючЗначение Из СоответствиеСертификатов Цикл
		ИдентификаторСертификата = КлючЗначение.Ключ;
		Прервать;
	КонецЦикла;
	
	СоответствиеСертификатов.Удалить(ИдентификаторСертификата);
	
	Параметры = Новый Структура;
	Параметры.Вставить("СоответствиеСертификатов", СоответствиеСертификатов);
	Параметры.Вставить("Контекст", ДополнительныеПараметры);
	Параметры.Вставить("ИдентификаторСертификата", ИдентификаторСертификата);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДвоичныхДанныхСертификатаСбербанк", ЭтотОбъект, Параметры);
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
	
	ПолучитьДвоичныеДанныеСертификатаСбербанк(Оповещение, ПодключаемыйМодуль, ИдентификаторСертификата);
	
КонецПроцедуры

// Получает двоичные данные сертификата подписи с токена.
//
// Параметры:
//  Оповещение - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//     Результат - ДвоичныеДанные, Неопределено - данные сертификата или Неопределено в случае ошибки;
//     ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта Оповещение.
//  ПодключаемыйМодуль - ComОбъект - внешняя компонента сбербанка;
//  ИдентификаторСертификата  - Строка - уникальный идентификатор сертификата на токене.
//
Процедура ПолучитьДвоичныеДанныеСертификатаСбербанк(Оповещение, ПодключаемыйМодуль, ИдентификаторСертификата)
	
	СертификатBase64 = "";
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификата", Оповещение);
	
	ОповещениеПослеПолученияДанныхСертификата = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатаСТокенаСбербанк",
		ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПослеПолученияДанныхСертификатаСТокенаСбербанк", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовПолучитьСертификатVPNKeyTLS(
		ОповещениеПослеПолученияДанныхСертификата, ИдентификаторСертификата, СертификатBase64);
		
КонецПроцедуры

Процедура ПослеПолученияДвоичныхДанныхСертификатаСбербанк(ДвоичныеДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если ДвоичныеДанныеСертификата =  Неопределено Тогда
		ДополнительныеПараметры.Контекст.СоответствиеСертификатов.Удалить(ДополнительныеПараметры.ИдентификаторСертификата);
	Иначе
		ДополнительныеПараметры.Контекст.СоответствиеСертификатов.Вставить(
			ДополнительныеПараметры.ИдентификаторСертификата, ДвоичныеДанныеСертификата);
	КонецЕсли;
	
	ПолучитьДвоичныеДанныеОчередногоСертификатаСбербанк(
		ДополнительныеПараметры.СоответствиеСертификатов, ДополнительныеПараметры.Контекст);
	
КонецПроцедуры

Процедура ПодобратьСертификатПослеПолученияСпискаСТокенаСбербанк(Результат, ДополнительныеПараметры) Экспорт

	Если НЕ Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	СоответствиеСертификатов = Результат.СоответствиеСертификатов;
	
	СписокСертификатов = Новый СписокЗначений;

	ДоступныеСертификатыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовБанка(
		ДополнительныеПараметры.НастройкаОбмена);
	СоответствиеОтпечатокСертификат = Новый Соответствие;
	Для Каждого Элемент Из ДоступныеСертификатыНастройкиОбмена Цикл
		ПроверитьСрокДействияСертификатаБанка(
			Элемент.Сертификат, Элемент.ДействителенДО, Элемент.ПользовательОповещенОСрокеДействия);
		СоответствиеОтпечатокСертификат.Вставить(Элемент.Отпечаток, Элемент.Сертификат);
	КонецЦикла;

	ВыборкаСертификатов = Новый Соответствие;
	СоответствиеСертификатовСбербанка = Новый Соответствие;
	
	Для Каждого Элемент Из СоответствиеСертификатов Цикл
		
		ИдентификаторСертификата = Элемент.Ключ;
		ДвоичныеДанныеСертификата = Элемент.Значение;
		
		Если ДвоичныеДанныеСертификата = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		Попытка
			СтруктураСертификата = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСертификата(ДвоичныеДанныеСертификата);
		Исключение
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Ошибка чтения сертификата.'");
			ТекстОшибки = ОписаниеОшибки();
			Операция = НСтр("ru = 'Чтение данных сертификата.'");
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ТекстОшибки, ТекстСообщения, 1, ДополнительныеПараметры.НастройкаОбмена);
			Продолжить;
		КонецПопытки;
		Если СтруктураСертификата.ДействителенДо < ОбщегоНазначенияКлиент.ДатаСеанса() Тогда
			Продолжить;
		КонецЕсли;
		
		НайденныйСертификат = СоответствиеОтпечатокСертификат.Получить(СтруктураСертификата.Отпечаток);
		Если НайденныйСертификат = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		СоответствиеСертификатовСбербанка.Вставить(ИдентификаторСертификата, НайденныйСертификат);
		
		ВыборкаСертификатов.Вставить(ИдентификаторСертификата, Строка(НайденныйСертификат));
	КонецЦикла;
	
	Если ВыборкаСертификатов.Количество() = 1 Тогда
		Для Каждого КлючЗначение Из ВыборкаСертификатов Цикл
			СтруктураВозврата = Новый Структура();
			СтруктураВозврата.Вставить("ИдентификаторСертификата", КлючЗначение.Ключ);
			СертификатСсылка = СоответствиеСертификатовСбербанка.Получить(КлючЗначение.Ключ);
			СтруктураВозврата.Вставить("СертификатСсылка", СертификатСсылка);
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, СтруктураВозврата);
			Возврат;
		КонецЦикла;
	КонецЕсли;
	
	Если Не ВыборкаСертификатов.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат подписи.
									|Возможные причины:
									|- В настройке обмена через сервис 1С:ДиректБанк указан сертификат другой организации. Загрузите нужный сертификат с аппаратного устройства.
									|- Вставлено другое аппаратное устройство. Проверьте, какое устройство подключено к компьютеру.
									|- Указана неверная учетная запись. Выберите правильную учетную запись при входе на аппаратное устройство.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	Для Каждого Элемент Из ВыборкаСертификатов Цикл
		СписокСертификатов.Добавить(Элемент.Ключ, Элемент.Значение);
	КонецЦикла;
	
	ЗаголовокФормыВыбора = НСтр("ru = 'Выберите сертификат подписи'");
	
	Параметры = Новый Структура();
	Параметры.Вставить("ОповещениеПослеОпределенияСертификата", ДополнительныеПараметры.ОповещениеПослеОпределенияСертификата);
	Параметры.Вставить("СоответствиеСертификатовСбербанка", СоответствиеСертификатовСбербанка);
	ОО = Новый ОписаниеОповещения("ПослеВыбораСертификатаСбербанк", ЭтотОбъект, Параметры);

	СписокСертификатов.ПоказатьВыборЭлемента(ОО, ЗаголовокФормыВыбора);
	
КонецПроцедуры

Процедура ПослеАутентификацииНаТокенеОпределитьСтатусыПодписейСбербанк(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПроверкиПодписи, Ложь);
		Возврат;
	КонецЕсли;
	
	МассивСообщенийОбмена = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(Параметры.МассивСообщенийОбмена);
	
	ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры)
	
КонецПроцедуры

Процедура ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры)
	
	Если МассивСообщенийОбмена.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПроверкиПодписи, Истина);
		Возврат;
	КонецЕсли;
	
	ТекСообщениеОбмена = МассивСообщенийОбмена.Получить(0);
	МассивСообщенийОбмена.Удалить(0);
	
	СоответствиеПодписейИСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДанныеУстановленныхПодписейИСертификатов(
		ТекСообщениеОбмена);
	
	СтрокаФорматBase64 = ОбменСБанкамиСлужебныйВызовСервера.ПодписанныеДанныеBase64(ТекСообщениеОбмена);
	
	РезультатПроверки = Новый Массив();
		
	ОпределитьСтатусСледующейПодписиСбербанк(МассивСообщенийОбмена, ТекСообщениеОбмена, СтрокаФорматBase64,
		СоответствиеПодписейИСертификатов, РезультатПроверки, Параметры);
		
КонецПроцедуры

Процедура ОпределитьСтатусСледующейПодписиСбербанк(МассивСообщенийОбмена, СообщениеОбмена, ПодписанныеДанные, СоответствиеПодписейИСертификатов, РезультатПроверки, Параметры)
	
	Если СоответствиеПодписейИСертификатов.Количество() = 0 Тогда
		Если РезультатПроверки.Количество() Тогда
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(СообщениеОбмена, РезультатПроверки);
		КонецЕсли;
		ПроверитьСтатусыПодписейСледующегоСообщенияОбменаСбербанк(МассивСообщенийОбмена, Параметры);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из СоответствиеПодписейИСертификатов Цикл
		Прервать;
	КонецЦикла;
	
	СтрокаСертификат = КлючЗначение.Значение;
	Подпись = КлючЗначение.Ключ;
	
	СоответствиеПодписейИСертификатов.Удалить(КлючЗначение.Ключ);
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ПодписанныеДанные", ПодписанныеДанные);
	ДополнительныеПараметры.Вставить("СоответствиеПодписейИСертификатов", СоответствиеПодписейИСертификатов);
	ДополнительныеПараметры.Вставить("РезультатПроверки", РезультатПроверки);
	ДополнительныеПараметры.Вставить("Параметры", Параметры);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПроверкиПодписиНаТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПроверитьПодписьНаТокенеСбербанк(Оповещение, ПодключаемыйМодуль, ПодписанныеДанные, Подпись, СтрокаСертификат);
	
КонецПроцедуры

Процедура ПослеПроверкиПодписиНаТокенеСбербанк(ПодписьВерна, ДополнительныеПараметры) Экспорт
	
	СтруктураЗаписи = Новый Структура;
	
	Если НЕ ПодписьВерна = Неопределено Тогда
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		СтруктураЗаписи.Вставить("ПодписьВерна", ПодписьВерна);
	КонецЕсли;
	
	ДополнительныеПараметры.РезультатПроверки.Добавить(СтруктураЗаписи);
	
	ОпределитьСтатусСледующейПодписиСбербанк(ДополнительныеПараметры.МассивСообщенийОбмена,
		ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.ПодписанныеДанные,
		ДополнительныеПараметры.СоответствиеПодписейИСертификатов, ДополнительныеПараметры.РезультатПроверки,
		ДополнительныеПараметры.Параметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатПроверкиПодписиНаТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи, РезультатВызова = 0);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПослеПроверкиПодписиНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При аутентификации на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Предъявление данных аутентификации на аппаратном устройстве.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, 1);
	ОчиститьДанныеАвторизацииСбербанк();
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи, Неопределено);
	
КонецПроцедуры

// Получает данные подписи с токена
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое в результате выполнения процедуры
//       * Результат - Структура - данные подписи
//            ** Успех - Булево - признак успешности проведенной операции, если Истина - операция выполнена, иначе Ложь
//            ** ЭП - Строка - данные подписи в Base64
//            ** ТекстОшибки - Строка - текст ошибки. Элемент присутствует только если возникла ошибка
//    ПодключаемыйМодуль - COMОбъект - внешняя компонента банка.
//
Процедура ПолучитьПодписанныеДанныеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	Состояние = 0;
	ЭП = "";
	
	Параметры = Новый Структура();
	Параметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	Параметры.Вставить("ОповещениеПослеПолученияПодписи", Оповещение);
	
	ОповещениеВызова = Новый ОписаниеОповещения("ПослеПолученияДанныхПодписиСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПолученияДанныхПодписиСбербанк", ЭтотОбъект);

	ПодключаемыйМодуль.НачатьВызовПолучитьДанныеПодписиИзVPNKeyTLS(ОповещениеВызова, Состояние, ЭП);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхПодписиСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
								|Внешний модуль VpnKey-TLS при подписании вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
		Возврат;
	КонецЕсли;
	
	Состояние = ПараметрыВызова.Получить(0);
	
	Если Состояние = "COMPLETE" Тогда
		Результат = Новый Структура;
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("ЭП", ПараметрыВызова.Получить(1));
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	ИначеЕсли Состояние = "FAILED" ИЛИ Состояние = "REJECTED" Тогда
		Операция = НСтр("ru = 'Подписание данных'");
		ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка
								|Код состояния: %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, Состояние);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, , 1);
		Результат = Новый Структура;
		Результат.Вставить("Успех", Ложь);
		Результат.Вставить("ТекстОшибки", ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	Иначе
		ПолучитьПодписанныеДанныеСбербанк(
			ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, ДополнительныеПараметры.ПодключаемыйМодуль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияДанныхПодписиСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении данных подписи произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение данных подписи'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , 1);
	ОчиститьДанныеАвторизацииСбербанк();
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Ложь);
	Результат.Вставить("ТекстОшибки", ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПодписи, Результат);
	
КонецПроцедуры

Процедура ПровестиТестНастройкиСбербанк(Оповещение, НастройкаОбмена)
	
	ОчиститьДанныеАвторизацииСбербанк();
	УдалитьВнешнююКомпонентуИзКэш("VPNKeyTLS");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ОповещениеПослеТестаНастройки", Оповещение);

	ОписаниеОбработчика = Новый ОписаниеОповещения(
		"ПослеПроверкиВерсииВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	РеквизитыНастройкиОбмена = Новый Структура("АдресФайлаВК");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьРеквизитыНастройкиОбмена(НастройкаОбмена, РеквизитыНастройкиОбмена);
		
	ПроверитьВерсиюВКСбербанк(ОписаниеОбработчика, РеквизитыНастройкиОбмена.АдресФайлаВК);
	
КонецПроцедуры

Процедура ПослеПроверкиВерсииВКСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ТестНастройкиОбменаСбербанкПослеУстановкиКанала", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьВиртуальныйКаналСоСбербанком(Оповещение, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПолучитьВерсиюВКПослеПодключенияСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиВерсии, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияВерсииВКСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьВерсиюВнешнейКомпонентыСбербанк(Оповещение, Результат);
	
КонецПроцедуры

Процедура ПослеПолученияВерсииВКСбербанк(Версия, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Версия) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиВерсии, Ложь);
		Возврат;
	КонецЕсли;
	
	Если Версия <> ДополнительныеПараметры.ОжидаемаяВерсия Тогда
		
		#Если ВебКлиент Тогда
			ТекстСообщения = НСтр("ru = 'На компьютере уже установлен внешний модуль другой версии.
										|Удалите программу ""VpnKey-TLS для 1С:Предприятия 8"", перезагрузите компьютер и повторите операцию.'");
		#Иначе
			ТекстСообщения = НСтр("ru = 'Отличаются версии используемого и загруженного внешнего модуля.
										|Загрузите новый внешний модуль или перезапустите программу.'");
		#КонецЕсли
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиВерсии, Ложь);
		
		Возврат;
	КонецЕсли;

	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиВерсии, Истина);
	
КонецПроцедуры

Процедура ТестНастройкиОбменаСбербанкПослеУстановкиКанала(Успех, Параметры) Экспорт
	
	Если Не Успех Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк", ЭтотОбъект, Параметры);
	
	ОпределитьСертификатПодписиСбербанк(Оповещение, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОпределитьСертификатПослеАутентификацииНаТокенеСбербанк(Успех, Параметры) Экспорт

	Если Не Успех Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВыбораСертификатаПодписатьЭДСбербанк", ЭтотОбъект, Параметры);
	
	ОпределитьСертификатПодписиСбербанк(ОписаниеОповещения, Параметры.НастройкаОбмена);

КонецПроцедуры

Процедура ПослеВыбораСертификатаПодписатьЭДСбербанк(Результат, Параметры) Экспорт

	Если Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;
	КонецЕсли;

	Параметры.Вставить("ИдентификаторСертификата", Результат.ИдентификаторСертификата);
	МассивСообщенийОбменаДляПроверкиЭПСбербанк = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(
		Параметры.СообщенияОбменаСбербанк);
	Параметры.Вставить("МассивСообщенийОбменаДляПроверкиЭПСбербанк", МассивСообщенийОбменаДляПроверкиЭПСбербанк);
	
	ЗакэшироватьПараметрСбербанка("СертификатПодписи", Результат.СертификатСсылка);
	
	ПараметрыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
		Результат.СертификатСсылка);
	Параметры.Вставить("ПараметрыСертификата", ПараметрыСертификата);
	Параметры.Вставить("КоличествоПодписано", 0);
	Параметры.Вставить("СертификатСсылка", Результат.СертификатСсылка);

	ПодписатьОчереднойЭДСбербанк(Параметры);

КонецПроцедуры

Процедура ПослеПредъявленияПинаНаТокенеСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова = 24 Тогда  // сессия открыта в другом сеансе
		ДополнительныеПараметры.Вставить("РезультатАутентификацииНаТокене", РезультатВызова);
		Оповещение = Новый ОписаниеОповещения(
			"ПовторноАутентифицироватьсяНаТокенеПослеЗавершенияСессииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		ЗавершитьСессиюНаТокенеСбербанк(Оповещение);
		Возврат;
	КонецЕсли;
	
	ОбработатьРезультатАутентификацииНаТокенеСбербанка(ДополнительныеПараметры.НастройкаОбмена, РезультатВызова,
		ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене);
	
КонецПроцедуры

Процедура ПовторноАутентифицироватьсяНаТокенеПослеЗавершенияСессииСбербанк(Успех, Параметры) Экспорт

	Если Успех Тогда
		ПовторноАутентифицироватьсяНаТокенеСбербанка(Параметры);
	Иначе
		ОбработатьРезультатАутентификацииНаТокенеСбербанка(Параметры.НастройкаОбмена,
			Параметры.РезультатАутентификацииНаТокене, Параметры.ОповещениеПослеАутентификацииНаТокене);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПовторноАутентифицироватьсяНаТокенеСбербанка(Параметры)
	
	Оповещение = Новый ОписаниеОповещения("ПослеПовторногоПредъявленияПинКодаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПослеПредъявленияПинаНаТокенеСбербанк", ЭтотОбъект);
	Если Параметры.ТипУстройства = "SCR" Тогда // Экранный токен
		Параметры.ПодключаемыйМодуль.НачатьВызовПредъявитьПинЭкр(Оповещение);
	Иначе
		Параметры.ПодключаемыйМодуль.НачатьВызовПредъявитьПин(Оповещение, Параметры.НомерКонтейнера, Параметры.ПинКод);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПовторногоПредъявленияПинКодаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ОбработатьРезультатАутентификацииНаТокенеСбербанка(ДополнительныеПараметры.НастройкаОбмена, РезультатВызова,
		ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене);
	
КонецПроцедуры

Процедура ПодписатьТестовыйЗапросПослеОпределенияСертификатаСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	ИначеЕсли Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(Параметры.НастройкаОбмена);
	ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
	СтрокаПодписи = "ATTRIBUTES" + Символ(10) + "OrgId=" + ИдентификаторОрганизации + Символ(10)
					+ "RequestId=" + ИдентификаторЗапроса;

	СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаПодписи);
	Параметры.Вставить("СтрокаПодписиBase64", СтрокаПодписиBase64);

	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
	Представление = НСтр("ru = 'Тестовый запрос'");
	Параметры.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
	Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	Параметры.Вставить("ИдентификаторСертификата", Результат.ИдентификаторСертификата);
	Параметры.Вставить("СертификатСсылка", Результат.СертификатСсылка);
	ОписаниеОповещения = Новый ОписаниеОповещения("ПроверитьПодписьПослеУстановкиСбербанк", ЭтотОбъект, Параметры);
	ПодписатьДанныеСбербанк(
		ОписаниеОповещения, ПодключаемыйМодуль, СтрокаПодписиBase64, Результат.ИдентификаторСертификата, Представление);
	
КонецПроцедуры

Процедура ПроверитьПодписьПослеУстановкиСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда // Пользователь отказался подписывать
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	ИначеЕсли Результат.Успех Тогда
		ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
		ПараметрыСертификата = Новый Структура("СертификатBase64");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(
			Параметры.СертификатСсылка, ПараметрыСертификата);
		
		Параметры.Вставить("ДанныеПодписи", Результат.ЭП);
		Оповещение = Новый ОписаниеОповещения("ОтправитьТестовыйЗапросПослеПроверкиПодписиСбербанк", ЭтотОбъект, Параметры);
		
		ПроверитьПодписьНаТокенеСбербанк(
			Оповещение, ПодключаемыйМодуль, Параметры.СтрокаПодписиBase64, Результат.ЭП, ПараметрыСертификата.СертификатBase64);
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьТестовыйЗапросПослеПроверкиПодписиСбербанк(ПодписьВерна, Параметры) Экспорт
	
	Если ПодписьВерна = Неопределено Тогда
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Если Не ПодписьВерна Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Установленная подпись неверна.'"));
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ТестоваяСтрока = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(Параметры.НастройкаОбмена,
		Параметры.ИдентификаторЗапроса, Параметры.ИдентификаторОрганизации, Параметры.ДанныеПодписи,
		Параметры.СертификатСсылка);
	КодОшибки = ""; ТекстОшибки = "";
	ПодключаемыйМодуль1С = ПодключеннаяВнешняяКомпонентаБанка("SBRFServiceProxy");

	Оповещение = Новый ОписаниеОповещения("ЗавершитьТестНастройкиПослеОтправкиЗапросаСбербанк", ЭтотОбъект, Параметры);
	
	ОтправитьЗапросВСбербанк(Оповещение, ПодключаемыйМодуль1С, ТестоваяСтрока, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ЗавершитьТестНастройкиПослеОтправкиЗапросаСбербанк(Результат, Параметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеТестаНастройки, Результат);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаВСбербанк(РезультатВызова, ПараметрыВызова, Параметры) Экспорт
	
	КодОшибки = ПараметрыВызова.Получить(1);
	ТекстОшибки = ПараметрыВызова.Получить(2);
	ИдентификаторСобытия = "";

	ОбменСБанкамиСлужебныйВызовСервера.ЗаписатьСобытиеВЖурналАудита(
		Параметры.НастройкаОбмена, "sendRequests", Параметры.ОтправляемыеДанные, РезультатВызова, ИдентификаторСобытия);
	
	Если ЗначениеЗаполнено(КодОшибки) ИЛИ ЗначениеЗаполнено(ТекстОшибки) Тогда
		ВидОперации = НСтр("ru = 'Отправка запроса в банк'");
		ТекстСообщения = НСтр("ru = 'Нет связи с сервером банка.
									|Повторите операцию позже или обратитесь в техподдержку банка'");
		ШаблонОшибки = НСтр("ru = 'Произошла ошибка сетевого взаимодействия.
							|Код ошибки: %1
							|Текст ошибки: %2'");
		ТекстЗаписи = СтрШаблон(ШаблонОшибки, КодОшибки, ТекстОшибки);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстЗаписи, , 1, Параметры.НастройкаОбмена);
		Результат = Новый Структура("Успех, ТекстОшибки, СетеваяОшибка", Ложь, ТекстСообщения, Истина);
		ВыполнитьОбработкуОповещения(Параметры.Оповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Если Сред(РезультатВызова, 1, 23) = "00000000-0000-0000-0000" Тогда
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщенияСбербанк(РезультатВызова));
		ВыполнитьОбработкуОповещения(Параметры.Оповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Если РезультатВызова = "<!--REQUESTID_DUBLIC-->" Тогда
		ТекстОшибки = НСтр("ru = 'Документ с таким идентификатором есть в информационной базе банка. Повторная отправка невозможна.'");
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстОшибки);
		ВыполнитьОбработкуОповещения(Параметры.Оповещение, Результат);
		Возврат;
	КонецЕсли;
	
	Попытка
		ИдентификаторДокумента = Новый УникальныйИдентификатор(РезультатВызова);
	Исключение
		ТекстСообщения = "";
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьИзвещениеСОшибкойСбербанк(
			РезультатВызова, ИдентификаторСобытия, ТекстСообщения);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
		ВыполнитьОбработкуОповещения(Параметры.Оповещение, Результат);
		Возврат;
	КонецПопытки;
	
	Результат = Новый Структура("Успех, Тикет", Истина, ИдентификаторДокумента);
	ВыполнитьОбработкуОповещения(Параметры.Оповещение, Результат);

КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиЗапросаВСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При отправке запроса в банк произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Отправка запроса в банк.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , 1);
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ОтправитьПлатежныеПорученияПослеУстановкиКаналаСбербанк(Успех, Параметры) Экспорт

	Если Не Успех Тогда
		ОтправитьДокументыВСбербанк(Параметры);
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	МассивСообщенийОбмена = Параметры.ДанныеДляОтправкиВСбербанк.Получить(НастройкаОбмена);
	Параметры.ДанныеДляОтправкиВСбербанк.Удалить(НастройкаОбмена);
	Параметры.Вставить("ОтправленныеСообщенияОбмена", Новый Массив);
	
	ОтправитьОчереднойПлатежныйДокументСбербанк(МассивСообщенийОбмена, Параметры)
	
КонецПроцедуры

Процедура ОтправитьОчереднойПлатежныйДокументСбербанк(МассивСообщенийОбмена, Параметры)
	
	Если МассивСообщенийОбмена.Количество() = 0 Тогда // Все сообщения обмена были отправлены
		
		Если Параметры.ОтправленныеСообщенияОбмена.Количество() Тогда
			ОтправленныеДокументы = ОбменСБанкамиСлужебныйВызовСервера.ВладельцыСообщенийОбмена(
				Параметры.ОтправленныеСообщенияОбмена);
			Оповестить("ОтправленоDirectBank", ОтправленныеДокументы);
		КонецЕсли;
		
		ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение");
		Оповещение = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуДокументовПослеПолученияСтатусовЗапросовСбербанк", ЭтотОбъект, Параметры);
		Параметры.Вставить("ОповещениеПослеПолученияНовыхДокументов", Оповещение);
		ПолучитьРезультатыОбработкиЗапросовСбербанк(Параметры.НастройкаОбмена, ВидЭД, Параметры);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = МассивСообщенийОбмена.Получить(0);
	МассивСообщенийОбмена.Удалить(0);
	
	ИдентификаторЗапроса = Неопределено;
	ИдентификаторОрганизации = Неопределено;
	ПакетXML = ОбменСБанкамиСлужебныйВызовСервера.ПакетXMLСбербанка(
		СообщениеОбмена, Параметры.НастройкаОбмена, ИдентификаторЗапроса, ИдентификаторОрганизации);
		
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Контекст", Параметры);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОбработатьРезультатОтправкиПлатежногоДокументаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОтправитьЗапросЗаНесколькоПопытокВСбербанк(Оповещение, ПакетXML, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуДокументовПослеПолученияСтатусовЗапросовСбербанк(Результат, Параметры) Экспорт
	
	ОтправитьДокументыВСбербанк(Параметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатОтправкиПлатежногоДокументаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		СтатусОшибкаПередачи = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.ОшибкаПередачи");
		ОбменСБанкамиСлужебныйВызовСервера.УстановитьСтатусСообщенияОбмена(
			ДополнительныеПараметры.СообщениеОбмена, СтатусОшибкаПередачи);
		ТекстСообщения = НСтр("ru = 'Произошла ошибка при отправке документа %1:
									|%2'");
		ОбъектПривязки = ОбменСБанкамиСлужебныйВызовСервера.ОбъектПривязки(ДополнительныеПараметры.СообщениеОбмена);
		ТекстСообщения = СтрШаблон(ТекстСообщения, ОбъектПривязки, Результат.ТекстОшибки);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, ОбъектПривязки);
		ОтправитьДокументыВСбербанк(ДополнительныеПараметры.Контекст);
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторов = Новый Массив;
	МассивИдентификаторов.Добавить(Результат.Тикет);
	
	ВидЭДПлатежноеПоручение = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение");
	ОбменСБанкамиСлужебныйВызовСервера.СохранитьИдентификаторы(
		МассивИдентификаторов, ДополнительныеПараметры.Контекст.НастройкаОбмена, ВидЭДПлатежноеПоручение);
	
	СтатусОтправлен = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Отправлен");
	
	ОбменСБанкамиСлужебныйВызовСервера.УстановитьСтатусСообщенияОбмена(
		ДополнительныеПараметры.СообщениеОбмена, СтатусОтправлен);
	
	ДополнительныеПараметры.Контекст.ИтогКолОтправленных = ДополнительныеПараметры.Контекст.ИтогКолОтправленных + 1;
	
	ДополнительныеПараметры.Контекст.ОтправленныеСообщенияОбмена.Добавить(ДополнительныеПараметры.СообщениеОбмена);
	
	ОтправитьОчереднойПлатежныйДокументСбербанк(
		ДополнительныеПараметры.МассивСообщенийОбмена, ДополнительныеПараметры.Контекст);
	
КонецПроцедуры

Процедура ОтправитьЗапросЗаНесколькоПопытокВСбербанк(Оповещение, ОтправляемыеДанные, НастройкаОбмена)
	
	СчетчикПопыток = 1;
	ВыполнитьПопыткуОтправкиЗапросаВСбербанк(Оповещение, СчетчикПопыток, ОтправляемыеДанные, НастройкаОбмена);
	
КонецПроцедуры

Процедура ВыполнитьПопыткуОтправкиЗапросаВСбербанк(Оповещение, СчетчикПопыток, ОтправляемыеДанные, НастройкаОбмена)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("СчетчикПопыток", СчетчикПопыток);
	ДополнительныеПараметры.Вставить("ОтправляемыеДанные", ОтправляемыеДанные);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеОтправкиЗапроса = Новый ОписаниеОповещения(
		"ПослеПопыткиОтправкиДокументаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПодключаемыйМодуль1С = ПодключеннаяВнешняяКомпонентаБанка("SBRFServiceProxy");
	
	ОтправитьЗапросВСбербанк(ОповещениеПослеОтправкиЗапроса, ПодключаемыйМодуль1С, ОтправляемыеДанные, НастройкаОбмена);
	
КонецПроцедуры

Процедура ПослеПопыткиОтправкиДокументаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Если ДополнительныеПараметры.СчетчикПопыток >= 5 ИЛИ НЕ Результат.Свойство("СетеваяОшибка") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
			Возврат;
		КонецЕсли;
		СчетчикПопыток = ДополнительныеПараметры.СчетчикПопыток + 1;
		ВыполнитьПопыткуОтправкиЗапросаВСбербанк(ДополнительныеПараметры.Оповещение, СчетчикПопыток,
			ДополнительныеПараметры.ОтправляемыеДанные, ДополнительныеПараметры.НастройкаОбмена);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьРезультатыОбработкиЗапросовСбербанк(Знач НастройкаОбмена, ВидЭД, Параметры)
	
	МассивИдентификаторов = ОбменСБанкамиСлужебныйВызовСервера.МассивИдентификаторовЗапроса(НастройкаОбмена, ВидЭД);

	МассивСообщенийОбменаДляПроверки = Новый Массив;
	
	Параметры.Вставить("МассивСообщенийОбменаДляПроверки", МассивСообщенийОбменаДляПроверки);
	
	ПолучитьОчереднойДокументИзСбербанка(НастройкаОбмена, МассивИдентификаторов, Параметры);
	
КонецПроцедуры

Процедура ПолучитьОчереднойДокументИзСбербанка(НастройкаОбмена, МассивТикетов, Параметры)
	
	Если МассивТикетов.Количество() = 0 Тогда // Все документы получены
		Оповестить("ОбновитьСостояниеОбменСБанками");
		ОповещениеПослеПроверкиПодписей = Новый ОписаниеОповещения(
			"ПродолжитьОтправлятьЗапросыПослеПроверкиПодписейСбербанк", ЭтотОбъект, Параметры);
		ОпределитьСтатусыПодписейСбербанк(
			ОповещениеПослеПроверкиПодписей, НастройкаОбмена, Параметры.МассивСообщенийОбменаДляПроверки);
		Возврат;
	КонецЕсли;
	
	Тикет = МассивТикетов.Получить(0);
	МассивТикетов.Удалить(0);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Контекст", Параметры);
	ДополнительныеПараметры.Вставить("МассивТикетов", МассивТикетов);
	ДополнительныеПараметры.Вставить("Тикет", Тикет);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОбработатьРезультатПолученияДокументаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(НастройкаОбмена);
	
	ПолучитьДокументыЗаНесколькоПопытокСбербанк(
		Оповещение, Тикет, НастройкаОбмена, РеквизитыНастройкиОбмена.ИдентификаторОрганизации);
	
КонецПроцедуры

Процедура ОбработатьРезультатПолученияДокументаСбербанк(Результат, Параметры) Экспорт
	
	НастройкаОбмена = Параметры.Контекст.НастройкаОбмена;
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		Параметры.Контекст.Вставить("ЕстьОшибка", Истина);
		ОбменСБанкамиСлужебныйВызовСервера.УдалитьИдентификаторЗапроса(НастройкаОбмена, Параметры.Тикет);
		ПолучитьОчереднойДокументИзСбербанка(Параметры.НастройкаОбмена, Новый Массив, Параметры.Контекст);
		Возврат;
	КонецЕсли;
	
	МассивНовыхСообщенийОбмена = Новый Массив;
	Если Результат.ОтветБанка = "<!--NOT PROCESSED YET-->"
		ИЛИ Результат.ОтветБанка = "<!--NOT_PROCESSED_YET-->" Тогда // Запрос еще не был обработан
		ПолучитьОчереднойДокументИзСбербанка(Параметры.НастройкаОбмена, Параметры.МассивТикетов, Параметры.Контекст);
		Возврат
	ИначеЕсли Результат.ОтветБанка = "<!--REQUEST NOT FOUND-->"
		ИЛИ Результат.ОтветБанка = "<!--REQUEST_NOT_FOUND-->" Тогда // Идентификатор не найден в базе банка
		ОбменСБанкамиСлужебныйВызовСервера.УдалитьИдентификаторЗапроса(НастройкаОбмена, Параметры.Тикет);
		ПолучитьОчереднойДокументИзСбербанка(Параметры.НастройкаОбмена, Параметры.МассивТикетов, Параметры.Контекст);
		Возврат
	КонецЕсли;
	
	ЕстьОшибка = Ложь;
	ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветСбербанка(Результат.ОтветБанка, Параметры.НастройкаОбмена,
		МассивНовыхСообщенийОбмена, Параметры.Тикет, Результат.ИдентификаторСобытия, ЕстьОшибка);
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		Параметры.Контекст.МассивСообщенийОбменаДляПроверки, МассивНовыхСообщенийОбмена);

	Если ЕстьОшибка Тогда
		Параметры.Контекст.Вставить("ЕстьОшибка", Истина);
		ПолучитьОчереднойДокументИзСбербанка(Параметры.НастройкаОбмена, Новый Массив, Параметры.Контекст);
	Иначе
		ПолучитьОчереднойДокументИзСбербанка(Параметры.НастройкаОбмена, Параметры.МассивТикетов, Параметры.Контекст);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДокументыЗаНесколькоПопытокСбербанк(Оповещение, Тикет, НастройкаОбмена, ИдентификаторОрганизации)
	
	СчетчикПопыток = 1;
	ВыполнитьПопыткуПолученияДокументаСбербанк(
		Оповещение, СчетчикПопыток, Тикет, НастройкаОбмена, ИдентификаторОрганизации);
	
КонецПроцедуры

Процедура ВыполнитьПопыткуПолученияДокументаСбербанк(Оповещение, СчетчикПопыток, Тикет, НастройкаОбмена, ИдентификаторОрганизации)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("СчетчикПопыток", СчетчикПопыток);
	ДополнительныеПараметры.Вставить("Тикет", Тикет);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
	
	ОповещениеПослеПолученияДокумента = Новый ОписаниеОповещения(
		"ПослеПопыткиПолученияДокументаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ПодключаемыйМодуль1С = ПодключеннаяВнешняяКомпонентаБанка("SBRFServiceProxy");
	
	ПолучитьДокументСбербанк(
		ОповещениеПослеПолученияДокумента, ПодключаемыйМодуль1С, Тикет, ИдентификаторОрганизации, НастройкаОбмена);
	
КонецПроцедуры

Процедура ПослеПопыткиПолученияДокументаСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		Если ДополнительныеПараметры.СчетчикПопыток >= 5 Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
			Возврат;
		КонецЕсли;
		СчетчикПопыток = ДополнительныеПараметры.СчетчикПопыток + 1;
		ВыполнитьПопыткуПолученияДокументаСбербанк(ДополнительныеПараметры.Оповещение, СчетчикПопыток,
			ДополнительныеПараметры.Тикет, ДополнительныеПараметры.НастройкаОбмена,
			ДополнительныеПараметры.ИдентификаторОрганизации);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьДокументСбербанк(Оповещение, ПодключаемыйМодуль1С, Тикет, ИдентификаторОрганизации, НастройкаОбмена)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("Тикет", Тикет);
	ОповещениеПослеВыполненияЗапроса = Новый ОписаниеОповещения("ПослеПолученияДокументаСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПолученияДокументовИзБанка", ЭтотОбъект);
	КодОшибки = ""; ТекстОшибки = "";
	ПодключаемыйМодуль1С.BeginCallinggetRequestStatus(
		ОповещениеПослеВыполненияЗапроса, Тикет, ИдентификаторОрганизации, КодОшибки, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПолученияДокументаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ИдентификаторСобытия = Неопределено;
	ОбменСБанкамиСлужебныйВызовСервера.ЗаписатьСобытиеВЖурналАудита(ДополнительныеПараметры.НастройкаОбмена,
		"getRequestStatus", ДополнительныеПараметры.Тикет, РезультатВызова, ИдентификаторСобытия);
	
	КодОшибки = ПараметрыВызова.Получить(2);
	ТекстОшибки = ПараметрыВызова.Получить(3);
	
	Если ЗначениеЗаполнено(КодОшибки) ИЛИ ЗначениеЗаполнено(ТекстОшибки) Тогда
		ВидОперации = НСтр("ru = 'Получение документов из банка.'");
		ШаблонОшибки = НСтр("ru = 'Произошла ошибка сетевого взаимодействия.
								|Код ошибки: %1.
								|Текст ошибки: %2'");
		ТекстЗаписи = СтрШаблон(ШаблонОшибки, КодОшибки, ТекстОшибки);
		ТекстСообщения = НСтр("ru = 'Нет связи с сервером банка.
									|Повторите операцию позже или обратитесь в техподдержку банка.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстЗаписи, , 1, ДополнительныеПараметры.НастройкаОбмена);
		Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	Иначе
		Результат = Новый Структура();
		Результат.Вставить("Успех", Истина);
		Результат.Вставить("ОтветБанка", РезультатВызова);
		Результат.Вставить("ИдентификаторСобытия", ИдентификаторСобытия);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияДокументовИзБанка(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении документов из банка произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение документов из банка.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, , 1);
	Результат = Новый Структура("Успех, ТекстОшибки", Ложь, ТекстСообщения);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Результат);
	
КонецПроцедуры

Процедура ПродолжитьОтправлятьЗапросыПослеПроверкиПодписейСбербанк(Успех, Параметры) Экспорт

	Если Не Успех Тогда
		Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ЕстьОшибка = Параметры.Свойство("ЕстьОшибка");
	
	Если Не ЕстьОшибка Тогда
		Если Параметры.Свойство("ПолучитьРезультатыОбработкиПлатежныхПоручений") Тогда
			Параметры.Удалить("ПолучитьРезультатыОбработкиПлатежныхПоручений");
			ПолучитьРезультатыОбработкиЗапросовСбербанк(
				НастройкаОбмена, ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение"), Параметры);
			Возврат;
		ИначеЕсли Параметры.Свойство("ПолучитьРезультатыОбработкиВыпискиБанка") Тогда
			Параметры.Удалить("ПолучитьРезультатыОбработкиВыпискиБанка");
			ПолучитьРезультатыОбработкиЗапросовСбербанк(
				НастройкаОбмена, ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ВыпискаБанка"), Параметры);
			Возврат;
		ИначеЕсли Параметры.Свойство("ПолучитьРезультатыОбработкиЗапросовНочнойВыписки") Тогда
			Параметры.Удалить("ПолучитьРезультатыОбработкиЗапросовНочнойВыписки");
			ПолучитьРезультатыОбработкиЗапросовСбербанк(
				НастройкаОбмена, ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросНочнойВыписки"), Параметры);
			Возврат;
		ИначеЕсли Параметры.Свойство("ОтправитьЗапросНаПолучениеГотовыхВыписокСбербанк") Тогда
				Параметры.Удалить("ОтправитьЗапросНаПолучениеГотовыхВыписокСбербанк");
			ВидЭДЗапросВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
			Оповещение = Новый ОписаниеОповещения("ПослеОтправкиЗапросовСостоянийЗапросовВыпискиСбербанк", ЭтотОбъект, Параметры);
			ОтправитьЗапросыСостоянияДокументовСбербанк(Оповещение, НастройкаОбмена, ВидЭДЗапросВыписки);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, НЕ ЕстьОшибка);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры) И Параметры.Свойство("ВыполнитьОбменСБанками") Тогда
		Параметры.Удалить("ВыполнитьОбменСБанками");
		ВыполнитьОбменСБанками(Неопределено, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросовСостоянийЗапросовВыпискиСбербанк(Успех, Параметры) Экспорт
	
	Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Успех);
	КонецЕсли;

	Если ЗначениеЗаполнено(Параметры) И Параметры.Свойство("ВыполнитьОбменСБанками") Тогда
		Параметры.Удалить("ВыполнитьОбменСБанками");
		ВыполнитьОбменСБанками(Неопределено, Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Завершает открытую сессию на токене Сбербанка.
//
// Параметры:
//  Оповещение  - ОписаниеОповещение - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//     * Результат - Булево - если Истина - сессия завершена, Ложь - произошла ошибка;
//     * Параметры - Структура - контекст выполнения.
//
Процедура ЗавершитьСессиюНаТокенеСбербанк(Оповещение)
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");

	Если ПодключаемыйМодуль <> Неопределено Тогда
		Параметры = Новый Структура("ОповещениеПослеЗавершенияСессии", Оповещение);
		ОповещениеПослеЗавершенияСессииТокенаСбербанк = Новый ОписаниеОповещения("ПослеЗавершенияСессииНаТокенеСбербанка",
			ЭтотОбъект, Параметры, "ОбработатьОшибкуЗавершенияСессииНаТокенеСбербанк", ЭтотОбъект);
		ПодключаемыйМодуль.НачатьВызовЗавершитьСессию(ОповещениеПослеЗавершенияСессииТокенаСбербанк);
	Иначе
		ВыполнитьОбработкуОповещения(Оповещение, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеЗавершенияСессииНаТокенеСбербанка(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СессияЗавершена = РезультатВызова = 0;
	ЗакэшироватьПараметрСбербанка("КаналУстановлен", Ложь);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеЗавершенияСессии, СессияЗавершена);
	
КонецПроцедуры

Процедура ОбработатьОшибкуЗавершенияСессииНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ЗакэшироватьПараметрСбербанка("КаналУстановлен", Ложь);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеЗавершенияСессии, Ложь);
	
КонецПроцедуры

// Осуществляет отправку запроса выписки с банк.
//
// Параметры:
//  Оповещение		 - ОписаниеОповещение - Оповещение, вызываемое после выполнения процедуры:
//    * Успех - Булево - если Истина - запрос отправлен, Ложь - произошла ошибка.
//  НастройкаОбмена	 - СправочникСсылка.НастройкиОбменСБанками - текущая настройка обмена с банком;
//  ЗапросВыписки	 - ДокументСсылка.СообщениеОбменСБанками - сообщение обмена, содержащее запрос.
//
Процедура ОтправитьЗапросВыпискиСбербанк(Оповещение, НастройкаОбмена, ЗапросВыписки) Экспорт
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
	ПодключаемыйМодуль1С = ПодключеннаяВнешняяКомпонентаБанка("SBRFServiceProxy");
	
	ИдентификаторЗапроса = Неопределено;
	ИдентификаторОрганизации = Неопределено;
	ПакетXML = ОбменСБанкамиСлужебныйВызовСервера.ПакетXMLСбербанка(
		ЗапросВыписки, НастройкаОбмена, ИдентификаторЗапроса, ИдентификаторОрганизации);
		
	ВидОперации = НСтр("ru = 'Запрос выписки банка'");
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Оповещение", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ЗапросВыписки", ЗапросВыписки);
	ОповещениеПослеОтправкиЗапроса = Новый ОписаниеОповещения(
		"ПослеОтправкиЗапросаВыпискиВСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОтправитьЗапросЗаНесколькоПопытокВСбербанк(ОповещениеПослеОтправкиЗапроса, ПакетXML, НастройкаОбмена);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаВыпискиВСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторов = Новый Массив;
	МассивИдентификаторов.Добавить(Результат.Тикет);
	
	ВидЭДЗапросВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
	
	ОбменСБанкамиСлужебныйВызовСервера.СохранитьИдентификаторы(
		МассивИдентификаторов, ДополнительныеПараметры.НастройкаОбмена, ВидЭДЗапросВыписки);
		
	СтатусОтправлен = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Отправлен");
		
	ОбменСБанкамиСлужебныйВызовСервера.УстановитьСтатусСообщенияОбмена(
		ДополнительныеПараметры.ЗапросВыписки, СтатусОтправлен);
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Оповещение, Истина);
	
КонецПроцедуры

Процедура ПослеПодписиЗапросаНовыхДокументовСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Ложь);
		КонецЕсли;
	ИначеЕсли Результат.Успех Тогда
		
		СтрокаЗапроса = ОбменСБанкамиСлужебныйВызовСервера.ТекстЗапросаНочнойВыписки(Параметры.НастройкаОбмена,
			Параметры.ИдентификаторЗапроса, Параметры.ИдентификаторОрганизации, Результат.ЭП, Параметры.СертификатСсылка);
			
		Если Не ЗначениеЗаполнено(СтрокаЗапроса) Тогда
			Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
				ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Ложь);
			КонецЕсли;
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("Контекст", Параметры);
		Оповещение = Новый ОписаниеОповещения(
			"ПослеОтправкиЗапросаНовыхДокументовСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			
		ОтправитьЗапросЗаНесколькоПопытокВСбербанк(Оповещение, СтрокаЗапроса, Параметры.НастройкаОбмена);
		
	Иначе // ошибка
		Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Ложь);
		КонецЕсли;
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаНовыхДокументовСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		Если ДополнительныеПараметры.Контекст.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.Контекст.ОповещениеПослеПолученияНовыхДокументов, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;

	МассивИдентификаторов = Новый Массив;
	МассивИдентификаторов.Добавить(Результат.Тикет);
	
	ЗапросНочнойВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросНочнойВыписки");
	ОбменСБанкамиСлужебныйВызовСервера.СохранитьИдентификаторы(
		МассивИдентификаторов, ДополнительныеПараметры.Контекст.НастройкаОбмена, ЗапросНочнойВыписки);

	ЗапроситьСтатусыДокументовСбербанк(ДополнительныеПараметры.Контекст.НастройкаОбмена, ДополнительныеПараметры.Контекст)
	
КонецПроцедуры

Процедура ЗапроситьСтатусыДокументовСбербанк(НастройкаОбмена, Параметры)
	
	ВидЭДПлатежноеПоручение = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ПлатежноеПоручение");
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("Контекст", Параметры);
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиЗапросаСостоянияПлатежногоДокументаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОтправитьЗапросыСостоянияДокументовСбербанк(Оповещение, НастройкаОбмена, ВидЭДПлатежноеПоручение);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаСостоянияПлатежногоДокументаСбербанк(Успех, ДополнительныеПараметры) Экспорт

	Если Не Успех Тогда
		Возврат;
	КонецЕсли;
	
	ВидЭДЗапросВыписки = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
	Оповещение = Новый ОписаниеОповещения(
		"ПослеОтправкиЗапросаСостоянияЗапросовВыпискиСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	ОтправитьЗапросыСостоянияДокументовСбербанк(Оповещение, ДополнительныеПараметры.НастройкаОбмена, ВидЭДЗапросВыписки);
	
КонецПроцедуры

Процедура ПослеОтправкиЗапросаСостоянияЗапросовВыпискиСбербанк(Успех, ДополнительныеПараметры) Экспорт

	Если Не Успех Тогда
		Возврат;
	КонецЕсли;

	Параметры = ДополнительныеПараметры.Контекст;

	Если ЗначениеЗаполнено(Параметры) И Параметры.Свойство("ПолучитьРезультатыОбработкиЗапросовВыписки") Тогда
		ВидЭД = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросВыписки");
		ПолучитьРезультатыОбработкиЗапросовСбербанк(ДополнительныеПараметры.НастройкаОбмена, ВидЭД, Параметры);
	КонецЕсли

КонецПроцедуры

Процедура УстановитьПортПослеПодключенияКомпоненты1С(ВнешняяКомпонента, Параметры) Экспорт
	
	Если ВнешняяКомпонента = Неопределено Тогда
		ЗакэшироватьПараметрСбербанка("КаналУстановлен", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеУстановкиПортаВнешнейКомпоненты1С", ЭтотОбъект, Параметры);

	ВнешняяКомпонента.BeginSettingservicePort(Оповещение, 28016);
	
КонецПроцедуры

Процедура ПослеУстановкиПортаВнешнейКомпоненты1С(Параметры) Экспорт
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	КаналСоздан = Ложь;
	Если ЗначениеИзКэшаСбербанк("КаналУстановлен") = Истина
		И ЗначениеИзКэшаСбербанк("ТекущаяНастройкаОбмена") = НастройкаОбмена Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеУстановкиВиртуальногоКанала, Истина);
		КаналСоздан = Истина;
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить(
		"ОповещениеПослеУстановкиВиртуальногоКанала", Параметры.ОповещениеПослеУстановкиВиртуальногоКанала);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		
	ОповещениеПослеАутентификацииНаТокенеСбербанка = Новый ОписаниеОповещения(
		"УстановитьКаналПослеАутентификацииНаТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	АутентифицироватьсяНаТокенеСбербанка(ОповещениеПослеАутентификацииНаТокенеСбербанка, НастройкаОбмена);
		
КонецПроцедуры

Процедура ОтправитьДокументыВСбербанк(Параметры)
	
	Для Каждого КлючЗначение Из Параметры.ДанныеДляОтправкиВСбербанк Цикл
		Параметры.Вставить("НастройкаОбмена", КлючЗначение.Ключ);
		Обработчик = Новый ОписаниеОповещения("ОтправитьПлатежныеПорученияПослеУстановкиКаналаСбербанк", ЭтотОбъект, Параметры);
		УстановитьВиртуальныйКаналСоСбербанком(Обработчик, КлючЗначение.Ключ);
		Возврат;
	КонецЦикла;
	
	ЗавершитьОтправкуСообщенийОбмена(Неопределено, Параметры);
	
КонецПроцедуры

Процедура ПослеПодключенияКомпонентыУстановитьВиртуальныйКаналСбербанк(ВнешняяКомпонента, Параметры) Экспорт
	
	Если ВнешняяКомпонента = Неопределено Тогда
		ЗакэшироватьПараметрСбербанка("КаналУстановлен", Ложь);
		Возврат
	КонецЕсли;
	
	ОписаниеОбработчика = Новый ОписаниеОповещения("УстановитьПортПослеПодключенияКомпоненты1С", ЭтотОбъект, Параметры);
	
	ПодключитьВнешнююКомпонентуБанка(ОписаниеОбработчика, Параметры.НастройкаОбмена, "SBRFServiceProxy");

КонецПроцедуры

Процедура ПодписатьОчереднойЭДСбербанк(Параметры)
	
	Если Параметры.СообщенияОбменаСбербанк.Количество() = 0 Тогда // все ЭД подписаны
		ОбработчикПослеПроверкиПодписи = Новый ОписаниеОповещения(
			"ЗавершитьПроцессПодписанияЭДПослеПроверкиПодписей", ЭтотОбъект, Параметры);
		ОпределитьСтатусыПодписейСбербанк(ОбработчикПослеПроверкиПодписи, Параметры.НастройкаОбмена,
			Параметры.МассивСообщенийОбменаДляПроверкиЭПСбербанк);
		Возврат;
	КонецЕсли;
	
	ПодписываемоеСообщение = Параметры.СообщенияОбменаСбербанк[0];
	Параметры.СообщенияОбменаСбербанк.Удалить(0);
	Параметры.Вставить("СообщениеОбмена", ПодписываемоеСообщение);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияЭДСбербанк", ЭтотОбъект, Параметры);
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
	
	СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.ПодписанныеДанныеBase64(ПодписываемоеСообщение);

	ПодписатьДанныеСбербанк(ОписаниеОповещения, ПодключаемыйМодуль, СтрокаПодписиBase64,
		Параметры.ИдентификаторСертификата, ПодписываемоеСообщение);
	
КонецПроцедуры

Процедура ЗавершитьПроцессПодписанияЭДПослеПроверкиПодписей(Успех, Параметры) Экспорт
	
	Результат = Новый Структура;
	Результат.Вставить("Успех", Успех);
	Результат.Вставить("Количество", Параметры.КоличествоПодписано);
	ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, Результат);
	
КонецПроцедуры

Процедура ПослеПодписанияЭДСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ВозвращаемоеЗначение = Новый Структура("Успех, Количество", Ложь, Параметры.КоличествоПодписано);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, ВозвращаемоеЗначение);
	ИначеЕсли Результат.Успех Тогда
		
		ДвоичныеДанныеЭП = Base64Значение(Результат.ЭП);
		
		ДанныеПроверкиПодписи = Новый Структура;
		ДанныеПроверкиПодписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДанныеПроверкиПодписи.Вставить("ПодписьВерна", Истина);

		ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодпись(
			Параметры.СообщениеОбмена, ДвоичныеДанныеЭП, Параметры.СертификатСсылка, ДанныеПроверкиПодписи);
			
		Параметры.КоличествоПодписано = Параметры.КоличествоПодписано + 1;
		ПодписатьОчереднойЭДСбербанк(Параметры);
	
	Иначе
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ВозвращаемоеЗначение = Новый Структура("Успех, Количество", Ложь, Параметры.КоличествоПодписано);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПодписания, ВозвращаемоеЗначение);
	КонецЕсли;
	
КонецПроцедуры

Процедура НачатьПодписаниеЭДСбербанка(Знач ДанныеПодписи, Параметры)
	
	МассивОбработанныхНастроекОбмена = Параметры.МассивОбработанныхНастроекОбмена;
	СертификатПодписи = Параметры.СтруктураСертификата.ВыбранныйСертификат;
	ТекущийИндекс = 0;
	
	Для Каждого Элемент Из ДанныеПодписи Цикл
		
		ТекущийИндекс = ТекущийИндекс + 1;
		Если ТекущийИндекс <= Параметры.ИндексТретьейИтерации Тогда
			Продолжить;
		Иначе
			Параметры.ИндексТретьейИтерации = ТекущийИндекс;
		КонецЕсли;
		
		Если НЕ МассивОбработанныхНастроекОбмена.Найти(Элемент.Ключ) = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		МассивОбработанныхНастроекОбмена.Добавить(Элемент.Ключ);
		МассивСообщенийОбменаБанка = Элемент.Значение;
		Если МассивСообщенийОбменаБанка.Количество() > 0 Тогда
			Параметры.Вставить("ТекущиеДанныеПодписи", ДанныеПодписи);
			ОбработчикПослеПодписания = Новый ОписаниеОповещения("ПродолжитьПодписаниеЭДСбербанк", ЭтотОбъект, Параметры);
			ПодписатьЭДСбербанк(ОбработчикПослеПодписания, Элемент.Ключ, МассивСообщенийОбменаБанка);
			Возврат;
		КонецЕсли;
	КонецЦикла;
	
	ПродолжитьПодписаниеБанковскихЭД(Параметры.РезультатВыбораСертификатаБанка, Параметры);

КонецПроцедуры

Процедура ПродолжитьПодписаниеЭДСбербанк(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		Параметры.ИтогКолПодписанных = Параметры.ИтогКолПодписанных + Результат.Количество;
		НачатьПодписаниеЭДСбербанка(Параметры.ТекущиеДанныеПодписи, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыбораСертификатаСбербанк(ВыбраннаяСтрока, Параметры) Экспорт
	
	Если ВыбраннаяСтрока = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОпределенияСертификата, КодВозвратаДиалога.Отмена);
	Иначе
		СтруктураВозврата = Новый Структура();
		СтруктураВозврата.Вставить("ИдентификаторСертификата", ВыбраннаяСтрока.Значение);
		СертификатСсылка = Параметры.СоответствиеСертификатовСбербанка.Получить(ВыбраннаяСтрока.Значение);
		СтруктураВозврата.Вставить("СертификатСсылка", СертификатСсылка);
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеОпределенияСертификата, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьКаналПослеАутентификацииНаТокенеСбербанк(Результат, Параметры) Экспорт
	
	Если Не Результат Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
		Возврат;
	КонецЕсли;
	
	НазваниеБизнесСистемы = """УПШ СББОЛ""";
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
	
	// Если задано имя тестовой бизнес-системы в РС, то используем его.
	ИмяТестовойБизнесСистемы = ОбменСБанкамиСлужебныйВызовСервера.ИмяБизнесСистемыДляТестов(Параметры.НастройкаОбмена);
	Если ЗначениеЗаполнено(ИмяТестовойБизнесСистемы) Тогда
		НазваниеБизнесСистемы = ИмяТестовойБизнесСистемы;
	ИначеЕсли ОбменСБанкамиСлужебныйВызовСервера.ТестовыйРежим() Тогда
		НазваниеБизнесСистемы = "sbbolsbt_upg_int";
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения("УстановитьКаналПослеОпределенияНомераБизнесСистемыСбербанк", ЭтотОбъект, Параметры);
	
	ОпределитьНомерБизнесСистемыСбербанк(Оповещение, ПодключаемыйМодуль, НазваниеБизнесСистемы);
	
КонецПроцедуры

Процедура УстановитьКаналПослеОпределенияНомераБизнесСистемыСбербанк(НомерБизнесСистемы, Параметры) Экспорт
	
	Если НомерБизнесСистемы = Неопределено Тогда //не удалось определить бизнес систему
		ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");

	Оповещение = Новый ОписаниеОповещения("ПослеУстановкиКаналаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуУстановкиКаналаНаТокенеСбербанк", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовУстановитьTLSКаналСБизнесСистемой(Оповещение, НомерБизнесСистемы);
	
КонецПроцедуры

Процедура ПослеУстановкиКаналаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатВызова = 0 Тогда
		Операция = НСтр("ru = 'Установка виртуального канала'");
		ТекстСообщения = НСтр("ru = 'Не удалось установить связь с сервером.
									|Необходимо проверить работу TLS VPN Key.'");
		ТекстОшибки = НСтр("ru = 'Компонента AddIn.Bicrypt при установке виртуального канала вернула код ошибки'")
							+ " " + РезультатВызова;
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, 1);
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, РезультатВызова = 0);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиКаналаНаТокенеСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При установке канала на аппаратном устройстве произошла ошибка.
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Установка канала на аппаратном устройстве.'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, 1);
	ОчиститьДанныеАвторизацииСбербанк();
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиВиртуальногоКанала, Ложь);
	
КонецПроцедуры

Процедура ОтправитьЗапросыСостоянияДокументовСбербанк(Оповещение, НастройкаОбмена, ВидЭД)
	
	МассивЗапросов = ОбменСБанкамиСлужебныйВызовСервера.МассивЗапросовСостоянийОбработкиДокументов(
		НастройкаОбмена, ВидЭД);

	Параметры = Новый Структура;
	Параметры.Вставить("Оповещение", Оповещение);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	Параметры.Вставить("ВидЭД", ВидЭД);
	ОтправитьОчереднойЗапросСостоянияВСбербанк(МассивЗапросов, Параметры);
	
КонецПроцедуры

Процедура ОтправитьОчереднойЗапросСостоянияВСбербанк(МассивЗапросов, Параметры)
	
	Если МассивЗапросов.Количество() = 0 Тогда // Все сообщения обмена были отправлены
		ВыполнитьОбработкуОповещения(Параметры.Оповещение, Истина);
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = МассивЗапросов.Получить(0);
	МассивЗапросов.Удалить(0);
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Контекст", Параметры);
	ДополнительныеПараметры.Вставить("МассивЗапросов", МассивЗапросов);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОбработатьРезультатОтправкиЗапросаСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ОтправитьЗапросЗаНесколькоПопытокВСбербанк(Оповещение, ТекстЗапроса, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОбработатьРезультатОтправкиЗапросаСбербанк(Результат, Параметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ВыполнитьОбработкуОповещения(Параметры.Контекст.Оповещение, Ложь);
		Возврат;
	КонецЕсли;
	
	МассивИдентификаторов = Новый Массив;
	МассивИдентификаторов.Добавить(Результат.Тикет);
	
	ОбменСБанкамиСлужебныйВызовСервера.СохранитьИдентификаторы(
		МассивИдентификаторов, Параметры.Контекст.НастройкаОбмена, Параметры.Контекст.ВидЭД);
		
	ОтправитьОчереднойЗапросСостоянияВСбербанк(Параметры.МассивЗапросов, Параметры.Контекст);
	
КонецПроцедуры

// Производит процесс аутентификации на токене сбербанка.
// 
// Параметры:
//    ОповещениеОписание - оповещение после выполнения аутентификации на токене:
//          Результат - Булево - Истина, если аутентификация выполнена успешно, иначе Ложь;
//          ДополнительныеПараметры - Структура - параметры описания оповещения;
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена с банком;
//    ПринудительнаяАутентификация - Булево - если Истина, то аутентификация будет производится даже если она была выполнена ранее;
//
Процедура АутентифицироватьсяНаТокенеСбербанка(ОповещениеОВыполнении, НастройкаОбмена, ПринудительнаяАутентификация = Ложь) Экспорт
	
	Если НЕ ПринудительнаяАутентификация И ЗначениеИзКэшаСбербанк("АутентификацияНаТокенеВыполнена") = Истина
		И ЗначениеИзКэшаСбербанк("ТекущаяНастройкаОбмена") = НастройкаОбмена Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		Параметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеОВыполнении);
		Оповещение = Новый ОписаниеОповещения(
			"ОпределитьНеобходимостьПовторнойАутентификацииНаТокенеПослеПолученияСпискаБизнесСистемСбербанк",
			ЭтотОбъект, Параметры);
		ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
		ПолучитьБизнесСистемыНаТокенеСбербанк(Оповещение, ПодключаемыйМодуль);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииНаТокене", ОповещениеОВыполнении);
	ДополнительныеПараметры.Вставить("ПринудительнаяАутентификация", ПринудительнаяАутентификация);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"ПроверитьВерсиюПослеПодключенияВКСбербанка", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьВнешнююКомпонентуБанка(ОповещениеПослеПодключенияВК, НастройкаОбмена, "VPNKeyTLS");
	
КонецПроцедуры

Процедура ПослеПолученияВерсииКомпонентыСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Версия = Сред(РезультатВызова, 1, 7);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВерсии, Версия);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияВерсииКомпонентыСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При получении версии внешнего модуля банка произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Получение версии внешней компоненты банка'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, 1);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияВерсии, Ложь);
	
КонецПроцедуры

Процедура ПроверитьВерсиюПослеПодключенияВКСбербанка(ПодключаемыйМодуль, ДополнительныеПараметры) Экспорт
	
	Если ПодключаемыйМодуль = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ТихийСтартПослеПолученияВерсииКомпонентыСбербанк", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьВерсиюВнешнейКомпонентыСбербанк(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ТихийСтартПослеПолученияВерсииКомпонентыСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Строка") Тогда
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	Версия = Результат;
	
	Если Не ОбменСБанкамиКлиентСервер.ПоддерживаетсяВерсияКомпонентыСбербанк(Версия) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьПараметрыТокенаПослеТихогоСтартаСбербанк", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуТихогоСтартаСбербанк", ЭтотОбъект);
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовТихийСтарт(Оповещение);
	
КонецПроцедуры

Процедура ПолучитьПараметрыТокенаПослеТихогоСтартаСбербанк(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова <> 0 Тогда
		ТекстСообщения = НСтр("ru = 'Ошибка при запуске клиента аппаратного устройства.
									|Убедитесь, что устройство подключено к компьютеру.'");
		ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при запуске Start.exe вернул код ошибки %1'");
		ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатВызова);
		Операция = НСтр("ru = 'Запуск клиента аппаратного устройства.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, 1);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ЗарегистрироватьсяНаТокенеПослеПолученияИнформацииОТокенеСбербанк", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьИнформациюОТокенеСбербанк(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль);

КонецПроцедуры

Процедура ОбработатьОшибкуТихогоСтартаСбербанк(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	
	ПодробнаяИнформация = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	КраткаяИнформация = КраткоеПредставлениеОшибки(ИнформацияОбОшибке);
	ШаблонОшибки = НСтр("ru = 'При запуске программы банка произошла ошибка:
						|%1'");
	ТекстСообщения = СтрШаблон(ШаблонОшибки, КраткаяИнформация);
	ВидОперации = НСтр("ru = 'Запуск программы Start.exe'");
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(ВидОперации, ПодробнаяИнформация, ТекстСообщения, 1);
	ОчиститьДанныеАвторизацииСбербанк();
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
	
КонецПроцедуры

Процедура ЗарегистрироватьсяНаТокенеПослеПолученияИнформацииОТокенеСбербанк(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат.Успех Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.ТекстОшибки);
		ОчиститьДанныеАвторизацииСбербанк();
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат
	КонецЕсли;

	Если ДополнительныеПараметры.Свойство("ПринудительнаяАутентификация") Тогда
		ПринудительнаяАутентификация = ДополнительныеПараметры.ПринудительнаяАутентификация;
	Иначе
		ПринудительнаяАутентификация = Ложь;
	КонецЕсли;

	ДополнительныеПараметры.Вставить("ТипУстройства", Результат.ТипУстройства);
		
	НастройкаОбмена = ДополнительныеПараметры.НастройкаОбмена;
	
	Если ПринудительнаяАутентификация ИЛИ Не (ЗначениеЗаполнено(ЗначениеИзКэшаСбербанк("АутентификацияНаТокенеВыполнена"))
												И ЗначениеИзКэшаСбербанк("ТекущаяНастройкаОбмена") = НастройкаОбмена) Тогда
		
		Если ЗначениеЗаполнено(ЗначениеИзКэшаСбербанк("АутентификацияНаТокенеВыполнена")) Тогда
			ПустоеОповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
			ЗавершитьСессиюНаТокенеСбербанк(ПустоеОповещение);
		КонецЕсли;
		
		Если Результат.ТипУстройства <> "SCR" Тогда // не экранный токен
			Обработчик = Новый ОписаниеОповещения("ПослеВводаДанныхАутентификацииСбербанк", ЭтотОбъект, ДополнительныеПараметры);
			ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПинКодаСбербанк", , , , , , Обработчик);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	НомерКонтейнера = Строка(ЗначениеИзКэшаСбербанк("НомерКонтейнера"));
	ПинКод = ЗначениеИзКэшаСбербанк("ПинКод");
	
	ЗарегистрироватьсяНаТокенеСбербанка(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, НастройкаОбмена,
		ДополнительныеПараметры.ПодключаемыйМодуль, НомерКонтейнера, ПинКод, Результат.ТипУстройства);
	
КонецПроцедуры

Процедура ПослеВводаДанныхАутентификацииСбербанк(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено ИЛИ ДанныеАутентификации = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене, Ложь);
		Возврат;
	КонецЕсли;
	
	НомерКонтейнера = ДанныеАутентификации.НомерКонтейнера;
	ПинКод = ДанныеАутентификации.ПинКод;
	
	ЗакэшироватьПараметрСбербанка("НомерКонтейнера", НомерКонтейнера);
	ЗакэшироватьПараметрСбербанка("ПинКод", ПинКод);
	
	ЗарегистрироватьсяНаТокенеСбербанка(ДополнительныеПараметры.ОповещениеПослеАутентификацииНаТокене,
		ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.ПодключаемыйМодуль, НомерКонтейнера, ПинКод,
		ДополнительныеПараметры.ТипУстройства);
	
КонецПроцедуры

Процедура ОбработатьРезультатАутентификацииНаТокенеСбербанка(НастройкаОбмена, РезультатАвторизации, ОповещениеПослеАутентификацииНаТокене)
	
	ЕстьОшибка = Ложь;
	
	Если Не (РезультатАвторизации = 0) Тогда
		Если РезультатАвторизации = 28 Тогда
			ШаблонСообщения = НСтр("ru = 'PIN%1 заблокирован'");
			НомерКонтейнера = ЗначениеИзКэшаСбербанк("НомерКонтейнера");
			ТекстСообщения = СтрШаблон(ШаблонСообщения, НомерКонтейнера);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли РезультатАвторизации = 27 Тогда
			ШаблонСообщения = НСтр("ru = 'PUK%1 заблокирован'");
			ТекстСообщения = СтрШаблон(
								ШаблонСообщения, ЗначениеИзКэшаСбербанк("НомерКонтейнера"));
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ИначеЕсли РезультатАвторизации = 25 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Пользователь заблокирован'"));
		ИначеЕсли РезультатАвторизации = 29 ИЛИ РезультатАвторизации = 30 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Неверные данные аутентификации'"));
		Иначе
			ОчиститьСообщения();
			ТекстСообщения = НСтр("ru = 'Не удалось выполнить аутентификацию на аппаратном устройстве.
										|Необходимо выполнить его перезапуск'");
			ТекстОшибки = НСтр("ru = 'Внешний модуль VpnKey-TLS при авторизации на аппаратном устройстве вернул код ошибки: %1'");
			ТекстОшибки = СтрШаблон(ТекстОшибки, РезультатАвторизации);
			Операция = НСтр("ru = 'Авторизация на токене'");
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ТекстОшибки, ТекстСообщения, 1, НастройкаОбмена);
		КонецЕсли;
		ЕстьОшибка = Истина;
		ОчиститьДанныеАвторизацииСбербанк();
	КонецЕсли;
	ЗакэшироватьПараметрСбербанка("АутентификацияНаТокенеВыполнена", Не ЕстьОшибка);
	ЗакэшироватьПараметрСбербанка("ТекущаяНастройкаОбмена", НастройкаОбмена);
	
	ВыполнитьОбработкуОповещения(ОповещениеПослеАутентификацииНаТокене, Не ЕстьОшибка);
	
КонецПроцедуры

Процедура ПослеПолученияОтпечатковВыполнитьОбмен(ОтпечаткиСертификатов, Параметры) Экспорт
	
	МассивОтпечатковСертификатов = Новый Массив;
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.СоответствиеДоступныхСертификатовИПараметров(
		МассивОтпечатковСертификатов, Параметры.НастройкаОбмена);
			
	Сертификат = Неопределено;
	МассивСертификатов = Новый Массив;
	ПарольПолучен = Ложь;
	Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
		МассивСертификатов.Добавить(КлючЗначение.Ключ);
		Если КлючЗначение.Значение.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен = Истина Тогда
			МассивСертификатов.Очистить();
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
			Прервать;
		КонецЕсли;
	КонецЦикла;
	Если МассивСертификатов.Количество() Тогда
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		
		ПараметрыЗапросаИдентификатораСессии = Новый Структура;
		ПараметрыЗапросаИдентификатораСессии.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
		
		ДанныеДляПолученияИдентификатораСессии = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ПараметрыЗапросаИдентификатораСессии);

		ОписаниеДанных.Вставить("Данные", ДанныеДляПолученияИдентификатораСессии);
		
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ОтправитьИПолучитьДокументыВБанкПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
		
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
	Иначе
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПродолжения);
	КонецЕсли;

КонецПроцедуры

Процедура ПолучитьБизнесСистемыНаТокенеСбербанк(Оповещение, ПодключаемыйМодуль)
	
	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеПолученияСпискаБизнесСистем", Оповещение);
	ОповещениеПослеПолученияНомераБизнесСистемы = Новый ОписаниеОповещения(
		"ПослеПолученияСпискаБизнесСистемСТокенаСбербанк", ЭтотОбъект, Параметры,
		"ОбработатьОшибкуПолученияСпискаБизнесСистемНаТокенеСбербанк", ЭтотОбъект);
	БизнесСистемы = "";
	ПодключаемыйМодуль.НачатьВызовПолучитьСписокБизнесСистемVPNKeyTLS(
		ОповещениеПослеПолученияНомераБизнесСистемы, БизнесСистемы);
	
КонецПроцедуры

#КонецОбласти

#Область ПолучениеВыписки

Процедура ПослеПолученияОтпечатковПолучитьВыпискуБанка(Отпечатки, Параметры) Экспорт
	
	МассивОтпечатков = Новый Массив;
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатков.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	ДатаНачала = Параметры.ДатаНачала;
	ДатаОкончания = Параметры.ДатаОкончания;
	РеквизитыНастройкиОбмена = Параметры.РеквизитыНастройкиОбмена;
	НомерСчета = Параметры.НомерСчета;
	
	НастройкиОбмена = Неопределено;
	МассивЗапросов = ОбменСБанкамиСлужебныйВызовСервера.ЗапросыВыписок(
		НастройкаОбмена, ДатаНачала, ДатаОкончания, НомерСчета, МассивОтпечатков, НастройкиОбмена);
		
	Если НЕ МассивЗапросов.Количество() ИЛИ НастройкиОбмена = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Параметры.Вставить("МассивСообщенийОбмена", МассивЗапросов);
	
	Если НастройкиОбмена.Подписывать Тогда
		Если НЕ ЗначениеЗаполнено(НастройкиОбмена.СертификатОрганизацииДляПодписи)
				ИЛИ НЕ НастройкиОбмена.СертификатДоступен Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат для подписи документа Запрос выписки'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
			
		ПараметрыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
				НастройкиОбмена.СертификатОрганизацииДляПодписи);
		Соответствие = Новый Соответствие;
		Соответствие.Вставить(НастройкиОбмена.СертификатОрганизацииДляПодписи, ПараметрыСертификата);
		Параметры.Вставить("СоотвСертификатовИИхСтруктур", Соответствие);
		ПодписатьЗапросыВыписок(Неопределено, Параметры);
		Возврат;
	КонецЕсли;
		
	ПараметрыАвторизации = Новый Структура;
	
	Если РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		ДанныеДоступныхСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(НастройкаОбмена);
		Если ДанныеДоступныхСертификатов.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'Нет доступных сертификатов для аутентификации на сервере банка.
										|Проверьте настройки прямого обмена с банком или обратитесь к администратору'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		МассивСертификатов = Новый Массив;
		
		Для Каждого ДанныеСертификата Из ДанныеДоступныхСертификатов Цикл
			Если МассивОтпечатков.Найти(ДанныеСертификата.Значение.Отпечаток) <> Неопределено Тогда
				МассивСертификатов.Добавить(ДанныеСертификата.Ключ);
			КонецЕсли
		КонецЦикла;
		
		Если МассивСертификатов.Количество() = 0 Тогда
			ТекстСообщения = НСтр("ru = 'На компьютере не найдены сертификаты для аутентификации на сервере банка.
										|Установите сертификаты в личное хранилище сертификатов операционной системы.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			Возврат;
		КонецЕсли;
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
			
		ДополнительныеПараметры = Новый Структура("НастройкаОбмена", НастройкаОбмена);
		ОписаниеПолученияДанных = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ДополнительныеПараметры);
		ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
				
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаВыпискиПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
	Иначе
		Если НЕ ПолученыДанныеАвторизации(НастройкаОбмена, ПараметрыАвторизации) Тогда
			ОООЗ = Новый ОписаниеОповещения("ОткрытьФормуЗапросаВыписки", ЭтотОбъект, Параметры);
			ПолучитьДанныеАутентификации(НастройкаОбмена, ОООЗ);
			Возврат;
		Иначе
			ОткрытьФормуЗапросаВыписки(ПараметрыАвторизации, Параметры);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

Процедура ПродолжитьОтправкуЗапросаВыпискиПослеРасшифровкиМаркера(Результат, Параметры) Экспорт

	Если Результат.Успех Тогда
		
		ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
			|НастройкаОбмена, НомерСчета, Пароль, Пользователь, СообщениеОбмена");
		ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);
		ИдентификаторСессииБанка = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
																	Результат.РасшифрованныеДанные);
		ПараметрыФормы.Вставить("ИдентификаторСессииБанка", ИдентификаторСессииБанка);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, Параметры.Владелец);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область Криптография

Процедура ОбработатьРезультатПодписиЗапросовВыписок(Результат, ПараметрыОбработки) Экспорт
	
	КолПодписанных = 0;
	НаборДанных = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("НаборДанных", НаборДанных)
			И ТипЗнч(НаборДанных) = Тип("Массив") Тогда
			Для Каждого Структура Из НаборДанных Цикл
				СвойстваПодписи = Неопределено;
				Подпись = Неопределено;
				Если Структура.Свойство("СвойстваПодписи", СвойстваПодписи)
					И ТипЗнч(СвойстваПодписи) = Тип("Структура")
					И СвойстваПодписи.Свойство("Подпись", Подпись) Тогда
					КолПодписанных = КолПодписанных + 1;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	Если КолПодписанных > 0 Тогда
		СтатусПодписан = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Подписан");
		ОбменСБанкамиСлужебныйВызовСервера.УстановитьСтатусыСообщенийОбмена(ПараметрыОбработки.МассивСообщенийОбмена, СтатусПодписан);
		Оповещение = Новый ОписаниеОповещения("ОтравитьЗапросыВыписокПослеПроверкиПодписей", ЭтотОбъект, ПараметрыОбработки);
		ПроверитьПодписиЭлектронныхДокументов(Оповещение, ПараметрыОбработки.МассивСообщенийОбмена);
	КонецЕсли;
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
КонецПроцедуры

Процедура ОтравитьЗапросыВыписокПослеПроверкиПодписей(Результат, ПараметрыОбработки) Экспорт
	
	Если Не Результат Тогда //есть невалидные подписи
		Возврат;
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = ПараметрыОбработки.РеквизитыНастройкиОбмена;
	
	АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
		Если РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
			МассивСертификатов = Новый Массив;
			Для Каждого КлючЗначение Из ПараметрыОбработки.СоотвСертификатовИИхСтруктур Цикл
				МассивСертификатов.Добавить(КлючЗначение.Ключ);
			КонецЦикла;
			
			ОписаниеДанных = Новый Структура;
			ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
			ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
			ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
			ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
			ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
			
			ДополнительныеПараметры = Новый Структура("НастройкаОбмена", ПараметрыОбработки.НастройкаОбмена);
			ОписаниеПолученияДанных = Новый ОписаниеОповещения(
				"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ДополнительныеПараметры);
			ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
			
			ОписаниеОповещения = Новый ОписаниеОповещения(
				"ПродолжитьОтправкуЗапросаВыпискиПослеРасшифровкиМаркера", ЭтотОбъект, ПараметрыОбработки);
			ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
		Иначе
			ПараметрыАутентификации = Новый Структура;
			ПолученыДанныеАутентификации = ПолученыДанныеАвторизации(
				ПараметрыОбработки.НастройкаОбмена, ПараметрыАутентификации);
			Если ПолученыДанныеАутентификации Тогда
				ОткрытьФормуЗапросаВыписки(ПараметрыАутентификации, ПараметрыОбработки);
			Иначе
				
				ОООЗ = Новый ОписаниеОповещения(
					"ОткрытьФормуЗапросаВыписки", ЭтотОбъект, ПараметрыОбработки);
				ПолучитьДанныеАутентификации(ПараметрыОбработки.НастройкаОбмена, ОООЗ);
			КонецЕсли;
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	ВладелецФормы = ПараметрыОбработки.Владелец;
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, Пароль, Пользователь, СообщениеОбмена, ГотовыеВыписки,
		|ПринудительноеПолучениеВыписки");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, ПараметрыОбработки);
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

Процедура ПроверитьПодписиЭлектронныхДокументов(ОписаниеОповещения, СообщенияОбмена)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписейЭлектронныхДокументов", ОписаниеОповещения);
	ДополнительныеПараметры.Вставить("СообщенияОбмена", СообщенияОбмена);
	ДополнительныеПараметры.Вставить("ИндексТекущегоСообщенияОбмена", -1);
	ДополнительныеПараметры.Вставить("ВсеПодписиВалидны", Истина);
	
	ПроверитьПодписиОчередногоЭлектронногоДокумента(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПроверитьПодписиОчередногоЭлектронногоДокумента(ДополнительныеПараметры)
	
	ДополнительныеПараметры.ИндексТекущегоСообщенияОбмена = ДополнительныеПараметры.ИндексТекущегоСообщенияОбмена + 1;
	
	ВсегоСообщенийОбмена = ДополнительныеПараметры.СообщенияОбмена.Количество();
	
	Если ВсегоСообщенийОбмена <= ДополнительныеПараметры.ИндексТекущегоСообщенияОбмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписейЭлектронныхДокументов,
			ДополнительныеПараметры.ВсеПодписиВалидны);
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеПроверкиПодписейЭлектронногоДокумента = Новый ОписаниеОповещения(
		"ПослеПроверкиПодписейЭлектронногоДокумента", ЭтотОбъект, ДополнительныеПараметры);
		
	ПроверитьПодписи(ОповещениеПослеПроверкиПодписейЭлектронногоДокумента,
		ДополнительныеПараметры.СообщенияОбмена[ДополнительныеПараметры.ИндексТекущегоСообщенияОбмена]);
	
КонецПроцедуры

Процедура ПослеПроверкиПодписейЭлектронногоДокумента(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = Истина Тогда
		ДополнительныеПараметры.ВсеПодписиВалидны = Ложь;
	КонецЕсли;
	
	ПроверитьПодписиОчередногоЭлектронногоДокумента(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПроверитьОчереднуюПодпись(ДополнительныеПараметры)
	
	ДополнительныеПараметры.ИндексТекущейПодписи = ДополнительныеПараметры.ИндексТекущейПодписи + 1;
	
	Если ДополнительныеПараметры.МассивПодписей.Количество() <= ДополнительныеПараметры.ИндексТекущейПодписи Тогда
		
		Если ДополнительныеПараметры.РезультатыПроверкиПодписей.Количество() Тогда
			ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(
				ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.РезультатыПроверкиПодписей);
		КонецЕсли;
		
		ПодписиВалидны = Истина;
		
		Для Каждого Запись Из ДополнительныеПараметры.РезультатыПроверкиПодписей Цикл
			Если НЕ Запись.ПодписьВерна Тогда
				ПодписиВалидны = Ложь;
			КонецЕсли;
		КонецЦикла;
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, ПодписиВалидны);
		
		Возврат;
	КонецЕсли;
	
	ОповещениеПослеПроверкиПодписи = Новый ОписаниеОповещения(
		"ЗаписатьРезультатПослеПроверкиПодписи", ЭтотОбъект, ДополнительныеПараметры);
		
	ЭлектроннаяПодписьКлиент.ПроверитьПодпись(ОповещениеПослеПроверкиПодписи, ДополнительныеПараметры.ДвоичныеДанныеЭД,
		ДополнительныеПараметры.МассивПодписей[ДополнительныеПараметры.ИндексТекущейПодписи].Подпись);
	
КонецПроцедуры

#КонецОбласти

#Область Аутентификация

// Вызывается из ЭлектроннаяПодписьКлиент.Расшифровать.
//
// Параметры:
//  Результат - Структура - содержит поля;
//     * ОписаниеДанных - Структура - значение первого параметра процедуры ЭлектроннаяПодписьКлиент.Расшифровать;
//     * Оповещение - ОписаниеОповещение - оповещение, которое должно быть вызвано после получения зашифрованного идентификатора сессии.
//  ДополнительныеПараметры - Произвольный - Параметры описания оповещения из поля Данные первого параметра процедуры ЭлектроннаяПодписьКлиент.Расшифровать.
//
Процедура ПолучитьЗашифрованныйИдентификаторСессии(Результат, ДополнительныеПараметры) Экспорт
	
	Попытка
		МаркерЗашифрованный = ОбменСБанкамиСлужебныйВызовСервера.ЗашифрованныйИдентификаторСессии(
			ДополнительныеПараметры.НастройкаОбмена, Результат.ОписаниеДанных.ВыбранныйСертификат);
	Исключение
		Результат.ОписаниеДанных.Вставить("ОписаниеОшибки", КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
	КонецПопытки;
		
	Если НЕ МаркерЗашифрованный = Неопределено Тогда
		Результат.ОписаниеДанных.Вставить("Данные", МаркерЗашифрованный);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Результат.ОписаниеДанных);
	
КонецПроцедуры

Процедура ОтправитьСообщениеВБанкСАутентификациейЛогинПароль(ПараметрыАутентификации, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(ПараметрыАутентификации) Тогда
		НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Параметры.Результат, Параметры);
		Возврат;
	КонецЕсли;
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	РеквизитыНастройкиОбмена = Новый Структура("ПрограммаБанка, АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(НастройкаОбмена, РеквизитыНастройкиОбмена);
	АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
		Обработчик = Новый ОписаниеОповещения("ОтправитьСообщениеВБанкПослеПолученияМаркера", ЭтотОбъект, Параметры);
		ПараметрыАутентификации.Вставить("Пароль", ПараметрыАутентификации.ПарольПользователя);
		ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
			РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ПараметрыАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
			НастройкаОбмена);
	Иначе
		МассивСообщенийОбменаКОтправке = Параметры.МассивСообщенийОбменаКОтправкеВБанкСАвторизациейЛогинПароль;
		СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
		СоотвНастроекОбменаИСтруктурСертификатов.Вставить(Параметры.НастройкаОбмена, ПараметрыАутентификации);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль", ЭтотОбъект, Параметры);
		ПодготовитьИОтправитьПЭД(МассивСообщенийОбменаКОтправке, Ложь, СоотвНастроекОбменаИСтруктурСертификатов, , ОписаниеОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьСообщениеВБанкПослеПолученияМаркера(Маркер, Параметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Маркер) Тогда
		НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Параметры.Результат, Параметры);
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификации = Новый Структура;
	ПараметрыАутентификации.Вставить("МаркерРасшифрованный", Маркер);
	МассивСообщенийОбменаКОтправке = Параметры.МассивСообщенийОбменаКОтправкеВБанкСАвторизациейЛогинПароль;
	СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
	СоотвНастроекОбменаИСтруктурСертификатов.Вставить(Параметры.НастройкаОбмена, ПараметрыАутентификации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения(
		"НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль", ЭтотОбъект, Параметры);
	ПодготовитьИОтправитьПЭД(
		МассивСообщенийОбменаКОтправке, Ложь, СоотвНастроекОбменаИСтруктурСертификатов, Параметры, ОписаниеОповещения);
	
КонецПроцедуры

Процедура РасшифроватьМаркерБанка(Результат, ДополнительныеПараметры) Экспорт

	ВозврСоответствие = Новый Соответствие;
	
	// В результате приходят расшифрованные данные маркера, поместим их в ВозврСоответствие:
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Успех = Ложь;
		РасшифрованныеДанные = Неопределено;
		НастройкаОбмена = Неопределено;
		Если Результат.Свойство("РасшифрованныеДанные", РасшифрованныеДанные) Тогда
			Если ЭтоАдресВременногоХранилища(РасшифрованныеДанные) Тогда
				РасшифрованныеДанные = ПолучитьИзВременногоХранилища(РасшифрованныеДанные);
			КонецЕсли;
			Если ТипЗнч(РасшифрованныеДанные) = Тип("ДвоичныеДанные")
				И ДополнительныеПараметры.Свойство("НастройкаОбмена", НастройкаОбмена) Тогда
				
				ВыбранныйСертификат = Неопределено;
				Если Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
					И ТипЗнч(ВыбранныйСертификат) = Тип("Структура")
					И ВыбранныйСертификат.Свойство("Ссылка", ВыбранныйСертификат)
					И ДополнительныеПараметры.СоотвСертификатовИИхСтруктур.Получить(ВыбранныйСертификат) <> Неопределено Тогда
					
					ПараметрыСертификата = ДополнительныеПараметры.СоотвСертификатовИИхСтруктур[ВыбранныйСертификат];
					ПараметрыСертификата.Вставить("МаркерРасшифрованный", РасшифрованныеДанные);
					ВозврСоответствие.Вставить(НастройкаОбмена, ПараметрыСертификата);
				Иначе
					ВозврСоответствие.Вставить(НастройкаОбмена, Новый Структура("МаркерРасшифрованный", РасшифрованныеДанные));
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	ЗапуститьФинальныйОбработчикОповещения = Истина;
	ОбработчикОповещения = Неопределено;
	СоответствиеНастроекОбменаИСертификатов = Неопределено;
	Если ДополнительныеПараметры.Свойство("СоответствиеНастроекОбменаИСертификатов", СоответствиеНастроекОбменаИСертификатов)
		И ТипЗнч(СоответствиеНастроекОбменаИСертификатов) = Тип("Соответствие")
		И СоответствиеНастроекОбменаИСертификатов.Количество() > 0 Тогда
		
		Для Каждого Элемент Из СоответствиеНастроекОбменаИСертификатов Цикл
			НастройкаОбмена = Элемент.Ключ;
			Сертификаты = Элемент.Значение;
			
			Если НЕ (ТипЗнч(Сертификаты) = Тип("Массив") И ЗначениеЗаполнено(НастройкаОбмена))Тогда
				Продолжить;
			КонецЕсли;
			
			Маркер = Неопределено;
			МассивСертификатов = Новый Массив;
			Для Каждого Сертификат Из Сертификаты Цикл
				ПараметрыСтруктура = ДополнительныеПараметры.СоотвСертификатовИИхСтруктур.Получить(Сертификат);
				Если ПараметрыСтруктура.Свойство("МаркерРасшифрованный", Маркер) Тогда
					ВозврСоответствие.Вставить(НастройкаОбмена, ПараметрыСтруктура);
					Прервать;
				Иначе
					ПараметрыСтруктура.Свойство("МаркерЗашифрованный", Маркер);
					Если ПараметрыСтруктура.ПарольПолучен Тогда
						МассивСертификатов = Новый Массив;
						МассивСертификатов.Добавить(Сертификат);
						Прервать;
					Иначе
						МассивСертификатов.Добавить(Сертификат);
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			// Если массив сертификатов пустой, значит либо уже есть расшифрованный маркер, либо нет сертификатов,
			// в обоих случаях переходим к обработке следующей Настройки ЭДО.
			Если МассивСертификатов.Количество() > 0 Тогда
				ОписаниеДанных = Новый Структура;
				ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
				ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
				ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
				ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
				ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
				Если Маркер = Неопределено Тогда
					ПараметрыЗапросаМаркера = Новый Структура("НастройкаОбмена", НастройкаОбмена);
					Маркер = Новый ОписаниеОповещения(
						"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ПараметрыЗапросаМаркера);
				КонецЕсли;
				
				ОписаниеДанных.Вставить("Данные", Маркер);
				
				ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
				ДополнительныеПараметры.Вставить("ОписаниеДанных", ОписаниеДанных);
				
				// Удалим из соответствия обработанный элемент:
				СоответствиеНастроекОбменаИСертификатов.Удалить(НастройкаОбмена);
				ОписаниеОповещения = Новый ОписаниеОповещения("РасшифроватьМаркерБанка", ЭтотОбъект, ДополнительныеПараметры);
				
				ЗапуститьФинальныйОбработчикОповещения = Ложь;
				ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если ЗапуститьФинальныйОбработчикОповещения
		И ДополнительныеПараметры.Свойство("ОбработчикОповещения", ОбработчикОповещения)
		И ТипЗнч(ОбработчикОповещения) = Тип("ОписаниеОповещения") Тогда
		
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("СоответствиеНастроекОбменаИПараметровСертификатов", ВозврСоответствие);
		
		ВыполнитьОбработкуОповещения(ОбработчикОповещения, ПараметрыОповещения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПодписанияЗапросаЗонда(Результат, ПараметрыОбработки) Экспорт
	
	Если Результат.Успех Тогда
		
		ПараметрыОбработки.Вставить("ВыбранныйСертификат", Результат.ВыбранныйСертификат);
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда", ЭтотОбъект, ПараметрыОбработки);
		ПроверитьПодписи(ОписаниеОповещения, ПараметрыОбработки.ЗапросЗонд);
	Иначе
		СтруктураРезультата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ПараметрыОбработки.ОбработчикПослеТестаНастройки, СтруктураРезультата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьТестНастроекПослеПроверкиЭПЗапросаЗонда(Результат, ПараметрыОбработки) Экспорт
	
	Если НЕ Результат Тогда
		Возврат;
	КонецЕсли;
	
	Если ПараметрыОбработки.РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		
		МассивСертификатов = Новый Массив;
		МассивСертификатов.Добавить(ПараметрыОбработки.ВыбранныйСертификат.Ссылка);
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		
		ПараметрыЗапросаМаркера = Новый Структура("НастройкаОбмена", ПараметрыОбработки.НастройкаОбмена);
		ОбработчикПолученияМаркера = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ПараметрыЗапросаМаркера);
			
		ОписаниеДанных.Вставить("Данные", ОбработчикПолученияМаркера);
			
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера", ЭтотОбъект, ПараметрыОбработки);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
	Иначе
		ОООЗ = Новый ОписаниеОповещения(
			"ПолучитьМаркерБанкаПослеВводаДанныхАутентификации", ЭтотОбъект, ПараметрыОбработки);
		ПолучитьДанныеАутентификации(ПараметрыОбработки.НастройкаОбмена, ОООЗ, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьМаркерБанкаПослеВводаДанныхАутентификации(ДанныеАутентификации, Параметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ТестСвязиСБанкомAsync", ЭтотОбъект, Параметры);
	ДанныеАутентификации.Вставить("Пароль", ДанныеАутентификации.ПарольПользователя);
	
	ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, Параметры.РеквизитыНастройкиОбмена.АдресСервера,
		Параметры.РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации,
		Параметры.РеквизитыНастройкиОбмена.ВерсияФормата, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера(Результат, Параметры) Экспорт
	
	Если Результат.Успех Тогда
		ПараметрыЗапроса = Новый Структура;
		ПараметрыЗапроса.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
		ПараметрыЗапроса.Вставить("СообщениеОбмена", Параметры.ЗапросЗонд);
		ИдентификаторСессииБанка = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(
																	Результат.РасшифрованныеДанные);
		ПараметрыЗапроса.Вставить("ИдентификаторСессииБанка", ИдентификаторСессииБанка);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыЗапроса, , , , ,
			Параметры.ОбработчикПослеТестаНастройки);
	Иначе
		СтруктураРезультата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, СтруктураРезультата);
	КонецЕсли;

КонецПроцедуры

Процедура НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Результат, Параметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		// Вызов после выполнения метода ПодготовитьИОтправитьПЭД(...).
		КолПодготовленных = 0;
		КолОтправленных = 0;
		Если Результат.Свойство("КолПодготовленных", КолПодготовленных) И ТипЗнч(КолПодготовленных) = Тип("Число") Тогда
			//
			Параметры.ИтогКолПодготовленных = Параметры.ИтогКолПодготовленных + КолПодготовленных;
		КонецЕсли;
		Если Результат.Свойство("КолОтправленных", КолОтправленных) И ТипЗнч(КолОтправленных) = Тип("Число") Тогда
			//
			Параметры.ИтогКолОтправленных = Параметры.ИтогКолОтправленных + КолОтправленных;
		КонецЕсли;
	КонецЕсли;
	СтруктураКОтправке = Параметры.Результат.СтруктураКОтправке;
	СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке = СтруктураКОтправке.САвторизациейЛогинПароль;
	СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
	
	МассивКОтправкеБезПодписи = Новый Массив;
	ТекущийИндекс = 0;
	Для Каждого ТекЭл Из СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке Цикл
		
		ПолностьюПодписанныеСообщенияОбмена = Новый Массив;
		Для Каждого СообщениеОбмена Из ТекЭл.Значение Цикл
			Если ОбменСБанкамиСлужебныйВызовСервера.ЭлектронныйДокументПолностьюПодписан(СообщениеОбмена) Тогда
				ПолностьюПодписанныеСообщенияОбмена.Добавить(СообщениеОбмена);
			КонецЕсли;
		КонецЦикла;
		
		Если НЕ ПолностьюПодписанныеСообщенияОбмена.Количество() Тогда
			Продолжить;
		КонецЕсли;
		
		ТекущийИндекс = ТекущийИндекс + 1;
		Если ТекущийИндекс <= Параметры.ТекущийИндексОтправкиСообщенийВБанкСАвторизациейЛогинПароль Тогда
			Продолжить;
		КонецЕсли;
		Параметры.ТекущийИндексОтправкиСообщенийВБанкСАвторизациейЛогинПароль = ТекущийИндекс;

		НастройкаОбмена = ТекЭл.Ключ;
		
		Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
		Параметры.Вставить("МассивСообщенийОбменаКОтправкеВБанкСАвторизациейЛогинПароль", ТекЭл.Значение);
		ПараметрыАвторизации = Неопределено;
		Если ПолученыДанныеАвторизации(ТекЭл.Ключ, ПараметрыАвторизации) Тогда
			ОтправитьСообщениеВБанкСАутентификациейЛогинПароль(ПараметрыАвторизации, Параметры);
		Иначе
			ОООЗ = Новый ОписаниеОповещения("ОтправитьСообщениеВБанкСАутентификациейЛогинПароль", ЭтотОбъект, Параметры);
			ПолучитьДанныеАутентификации(ТекЭл.Ключ, ОООЗ);
			Возврат;
		КонецЕсли;
		
	КонецЦикла;
	Если МассивКОтправкеБезПодписи.Количество() > 0 Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьОтправкуСообщенийОбмена", ЭтотОбъект, Параметры);
		ПодготовитьИОтправитьПЭД(
			МассивКОтправкеБезПодписи, Ложь, СоотвНастроекОбменаИСтруктурСертификатов,, ОписаниеОповещения);
	Иначе
		ЗавершитьОтправкуСообщенийОбмена(Неопределено, Параметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправкаСообщенийОбмена(Параметры)
	
	Результат = Параметры.Результат;
	МассивСообщенийОбменаКУдалениюИзОтправки = Параметры.МассивСообщенийОбменаКУдалениюИзОтправки;
	СоотвСертификатовИИхСтруктур = Параметры.СоотвСертификатовИИхСтруктур;
	СоотвНастроекОбменаИСертификатовАвторизации = Неопределено;
	СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке = Неопределено;
	НастройкиОбменаИСообщенияКОтправке = Неопределено;
	СтруктураКОтправке = Неопределено;
	Если Результат.Свойство("СтруктураКОтправке", СтруктураКОтправке) Тогда
		Параметры.Вставить("СоотвНастроекОбменаИСтруктурСертификатов", Новый Соответствие);
		СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
		Если СтруктураКОтправке.Свойство("САвторизациейЛогинПароль", СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке)
			И ТипЗнч(СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке) = Тип("Соответствие")
			И СоотвНастроекОбменаИМассивовСообщенийОбменаКОтправке.Количество() > 0 Тогда
			
			Параметры.Вставить("ТекущийИндексОтправкиСообщенийВБанкСАвторизациейЛогинПароль", 0);
			НачатьОтправкуСообщенийВБанкСАвторизациейЛогинПароль(Неопределено, Параметры);
		Иначе
			МассивСообщенийОбмена = Неопределено;
			Если СтруктураКОтправке.Свойство("БезПодписи", МассивСообщенийОбмена) И ТипЗнч(МассивСообщенийОбмена) = Тип("Массив")
				И МассивСообщенийОбмена.Количество() > 0 Тогда
				
				Параметры.Вставить("МассивКОтправкеБезПодписи", МассивСообщенийОбмена);
			КонецЕсли;
			
			Если СтруктураКОтправке.Свойство("СПодписью", МассивСообщенийОбмена) И ТипЗнч(МассивСообщенийОбмена) = Тип("Массив") Тогда
				Если МассивСообщенийОбменаКУдалениюИзОтправки.Количество() > 0 И МассивСообщенийОбмена.Количество() > 0 Тогда
					Для Каждого УдаляемыйЭД Из МассивСообщенийОбменаКУдалениюИзОтправки Цикл
						ТекИндекс = МассивСообщенийОбмена.Найти(УдаляемыйЭД);
						Если ТекИндекс <> Неопределено Тогда
							МассивСообщенийОбмена.Удалить(ТекИндекс);
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			Иначе
				МассивСообщенийОбмена = Новый Массив;
			КонецЕсли;
			
			Параметры.Вставить("МассивКОтправке", МассивСообщенийОбмена);
			МассивКОтправке = Параметры.МассивКОтправке;
			
			Если НЕ (Результат.Свойство("СоотвНастроекОбменаИСертификатовАвторизации", СоотвНастроекОбменаИСертификатовАвторизации)
				И ТипЗнч(СоотвНастроекОбменаИСертификатовАвторизации) = Тип("Соответствие")) Тогда
				СоотвНастроекОбменаИСертификатовАвторизации = Новый Соответствие;
			КонецЕсли;
			
			Если СтруктураКОтправке.Свойство("ЧерезТокенСбербанка") И СтруктураКОтправке.ЧерезТокенСбербанка.Количество() Тогда
				ДанныеДляОтправкиВСбербанк = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляОтправкиВСбербанк(
					СтруктураКОтправке.ЧерезТокенСбербанка);
				Параметры.Вставить("ДанныеДляОтправкиВСбербанк", ДанныеДляОтправкиВСбербанк);
				ОтправитьДокументыВСбербанк(Параметры);
				Возврат;
			КонецЕсли;
			
			Если СтруктураКОтправке.Свойство("ЧерезВК") И СтруктураКОтправке.ЧерезВК.Количество() Тогда
				Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатОтправкиПакетовЧерезВК", ЭтотОбъект, Параметры);
				ДанныеКОтправке = ОбменСБанкамиСлужебныйВызовСервера.ДанныеДляОтправкиЧерезВК(СтруктураКОтправке.ЧерезВК);
				ОтправитьДокументыЧерезВК(Оповещение, ДанныеКОтправке);
				Возврат;
			КонецЕсли;
			
			РасшифроватьМаркерБанка = Ложь;
			Если СтруктураКОтправке.Свойство("САутентификациейПоСертификату", НастройкиОбменаИСообщенияКОтправке)
				И ТипЗнч(НастройкиОбменаИСообщенияКОтправке) = Тип("Соответствие") Тогда
				Для Каждого ТекЭл Из НастройкиОбменаИСообщенияКОтправке Цикл
					НастройкаОбмена = ТекЭл.Ключ;
					ЕстьСообщениеОбменаКОтправке = Ложь;
					Для Каждого ОтправляемоеСообщениеОбмена Из ТекЭл.Значение Цикл
						Если МассивСообщенийОбменаКУдалениюИзОтправки.Найти(ОтправляемоеСообщениеОбмена) = Неопределено
							И ОбменСБанкамиСлужебныйВызовСервера.ЭлектронныйДокументПолностьюПодписан(ОтправляемоеСообщениеОбмена) Тогда
							МассивКОтправке.Добавить(ОтправляемоеСообщениеОбмена);
							ЕстьСообщениеОбменаКОтправке = Истина;
						КонецЕсли;
					КонецЦикла;
					Если ЕстьСообщениеОбменаКОтправке Тогда
						МассивСертификатов = СоотвНастроекОбменаИСертификатовАвторизации.Получить(НастройкаОбмена);
						Если ЗначениеЗаполнено(МассивСертификатов) Тогда
							РасшифроватьМаркерБанка = Истина;
						КонецЕсли;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;

			Если РасшифроватьМаркерБанка Тогда
				ОбработчикОповещения = Новый ОписаниеОповещения(
					"ПродолжитьОтправкуСообщенийОбменаПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
				ДопПараметры = Новый Структура;
				ДопПараметры.Вставить("ОбработчикОповещения", ОбработчикОповещения);
				ДопПараметры.Вставить("СоответствиеНастроекОбменаИСертификатов", СоотвНастроекОбменаИСертификатовАвторизации);
				ДопПараметры.Вставить("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур);
				
				РасшифроватьМаркерБанка(, ДопПараметры);
			Иначе
				ЗавершитьОтправкуСообщенийОбмена(Неопределено, Параметры);
			КонецЕсли;
			
		КонецЕсли;
	Иначе
		ВыполнитьДействияПослеОтправки(Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Производит десериализацию данных.
//
// Параметры:
//   ПредставлениеXML - Строка - сериализованные данные;
//
// Возвращаемое значение:
//   Произвольный - десериализованные данные.
//
Функция ДеСериализованныеДанные(Знач ПредставлениеXML) Экспорт

	#Если НЕ ВебКлиент Тогда
	
		ЧтениеXML = Новый ЧтениеXML;
		ЧтениеXML.УстановитьСтроку(ПредставлениеXML);
		ЧтениеXML.Прочитать();

		Сериализатор = Новый СериализаторXDTO(ФабрикаXDTO);
		Возврат Сериализатор.ПрочитатьXML(ЧтениеXML);
	#Иначе
		
		Возврат ОбменСБанкамиСлужебныйВызовСервера.ДеСериализованныеДанные(ПредставлениеXML);
		
	#КонецЕсли

КонецФункции

Процедура ОткрытьФормуЗапросаВыписки(ПараметрыАутентификации, Параметры) Экспорт
	
	Если ПараметрыАутентификации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	РеквизитыНастройкиОбмена = Новый Структура("ПрограммаБанка, АдресСервера, ИдентификаторОрганизации, ВерсияФормата");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		
	АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
	
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
		Обработчик = Новый ОписаниеОповещения("ЗапроситьВыпискуПослеПолученияМаркераБанка", ЭтотОбъект, Параметры);
		ПараметрыАутентификации.Вставить("Пароль", ПараметрыАутентификации.ПарольПользователя);
		ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
			РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ПараметрыАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата,
			Параметры.НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	Если ПараметрыАутентификации.Количество() Тогда
		Параметры.Вставить("Пользователь", ПараметрыАутентификации.Пользователь);
		Параметры.Вставить("Пароль", ПараметрыАутентификации.ПарольПользователя);
	КонецЕсли;
	
	ВладелецФормы = Параметры.Владелец;
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, Пароль, Пользователь, СообщениеОбмена, ГотовыеВыписки,
		|ПринудительноеПолучениеВыписки");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);

	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

// Выполняет синхронизацию данных с банками
//
// Параметры:
//    Результат - Структура, Неопределено - в структуре возвращаются результаты очередной завершившейся итерации
//              отправки банковских ЭД в банк:
//       ИтогКолПодготовленных - Число.
//       ИтогКолОтправленных   - Число.
//    Параметры - Структура:
//       НастройкиОбмена - Массив.
//       ИтогКолПодготовленных - Число.
//       ИтогКолОтправленных   - Число.
//
Процедура ВыполнитьОбменСБанками(Результат, Параметры) Экспорт
	
	ИтогКолОтправленных = 0;
	ИтогКолПолученных = 0;
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Если Результат.Свойство("ИтогКолОтправленных", ИтогКолОтправленных) Тогда
			КолОтправленных = 0;
			Если НЕ (Параметры.Свойство("ИтогКолОтправленных", КолОтправленных)
					И ТипЗнч(КолОтправленных) = Тип("Число")) Тогда
				
				КолОтправленных = 0;
			КонецЕсли;
			КолОтправленных = КолОтправленных + ИтогКолОтправленных;
			Параметры.Вставить("ИтогКолОтправленных", КолОтправленных);
		КонецЕсли;
		Если Результат.Свойство("ИтогКолПолученных", ИтогКолПолученных) Тогда
			КолПолученных = 0;
			Если НЕ (Параметры.Свойство("ИтогКолПолученных", КолПолученных)
					И ТипЗнч(КолПолученных) = Тип("Число")) Тогда
				
				КолПолученных = 0;
			КонецЕсли;
			КолПолученных = КолПолученных + ИтогКолПолученных;
			Параметры.Вставить("ИтогКолПолученных", КолПолученных);
		КонецЕсли;
	КонецЕсли;
	
	НастройкиОбмена = Неопределено;
	Если НЕ Параметры.Свойство("НастройкиОбмена", НастройкиОбмена)
		ИЛИ НастройкиОбмена.Количество() = 0 Тогда
		ЗаголовокОповещения = НСтр("ru = 'Обмен с сервисом 1С:ДиректБанк'");
		ШаблонОповещения = НСтр("ru = 'Отправлено пакетов: (%1), получено пакетов: (%2).'");
		ТекстОповещения = СтрШаблон(
			ШаблонОповещения, Параметры.ИтогКолОтправленных, Параметры.ИтогКолПолученных);
		ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	Иначе
		НастройкаОбмена = НастройкиОбмена[0];
		Параметры.НастройкиОбмена.Удалить(0);
		ОписаниеОповещения = Новый ОписаниеОповещения("ВыполнитьОбменСБанками", ЭтотОбъект, Параметры);
		Структура = Новый Структура;
		Структура.Вставить("ОбработчикПродолжения", ОписаниеОповещения);
		Структура.Вставить("НастройкаОбмена", НастройкаОбмена);
		Структура.Вставить("ИтогКолОтправленных", 0);
		Структура.Вставить("ИтогКолПолученных", 0);
		ВыполнитьОбменСБанком(Структура);
	КонецЕсли;
	
КонецПроцедуры

// Отправка и получение электронных документов одной командой.
//
// Параметры:
//    Параметры - Структура:
//       ОбработчикПродолжения - ОписаниеОповещения.
//       НастройкаОбмена          - СправочникСсылка.НастройкиОбменСБанками
//       ИтогКолПодготовленных - Число.
//       ИтогКолОтправленных   - Число.
//
Процедура ВыполнитьОбменСБанком(Параметры)
	
	// Блок отправки и получения ЭД.
	ТекстСообщения = НСтр("ru = 'Отправка пакетов электронных документов в банк. Подождите...'");
	Состояние(НСтр("ru = 'Отправка.'"), , ТекстСообщения);
		
	ПараметрыАвторизации = Новый Соответствие;
	Параметры.Вставить("ПараметрыАвторизации", ПараметрыАвторизации);
	
	РеквизитыНастройкиОбмена = Новый Структура;
	РеквизитыНастройкиОбмена.Вставить("ПрограммаБанка");
	РеквизитыНастройкиОбмена.Вставить("АутентификацияПоСертификату");
	РеквизитыНастройкиОбмена.Вставить("ИспользуетсяКриптография");
	РеквизитыНастройкиОбмена.Вставить("Недействительна");
	
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		
	Если РеквизитыНастройкиОбмена.Недействительна Тогда
		ТекстСообщения = НСтр("ru = 'Настройка обмена недействительна.
									|Операция прервана.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		ВыполнитьОбменСБанками(Неопределено, Параметры);
	КонецЕсли;
	
	Параметры.Вставить("ТребуетсяАвторизация", Истина);
	ДанныеАвторизации = Неопределено;
	СбербанкОнлайн = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн");
	
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = СбербанкОнлайн Тогда
		Параметры.Вставить("ПолучитьРезультатыОбработкиЗапросовВыписки");
		Параметры.Вставить("ПолучитьРезультатыОбработкиВыпискиБанка");
		Параметры.Вставить("ПолучитьРезультатыОбработкиПлатежныхПоручений");
		Параметры.Вставить("ПолучитьРезультатыОбработкиЗапросовНочнойВыписки");
		Параметры.Вставить("ПринудительноеПолучениеНовыхДокументов", Истина);
		Параметры.Вставить("ОтправитьЗапросНаПолучениеГотовыхВыписокСбербанк");
		Параметры.Вставить("ВыполнитьОбменСБанками");
		
		Обработчик = Новый ОписаниеОповещения(
			"ОпределитьСертификатПодписиПослеУстановкиВиртуальногоКаналаСбербанк", ЭтотОбъект, Параметры);
		
		УстановитьВиртуальныйКаналСоСбербанком(Обработчик, Параметры.НастройкаОбмена);
		
		Возврат;
	КонецЕсли;
	
	Если РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен")
		И РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковВыполнитьОбмен", ЭтотОбъект, Параметры);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Ложь);
		Возврат;
	КонецЕсли;
	
	Если ПарольКСертификатуПолучен(Параметры.НастройкаОбмена, ДанныеАвторизации)
		ИЛИ РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку")
		ИЛИ РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
		ИЛИ (РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АльфаБанкОнлайн")
				И РеквизитыНастройкиОбмена.ИспользуетсяКриптография) Тогда
			
		Если ДанныеАвторизации = Неопределено Тогда
			Параметры.Вставить("ТребуетсяАвторизация", Ложь);
		КонецЕсли;
		ОтправитьДокументыВБанк(ДанныеАвторизации, Параметры);
	Иначе
		ОООЗ = Новый ОписаниеОповещения("ОтправитьДокументыВБанк", ЭтотОбъект, Параметры);
		ПолучитьДанныеАутентификации(Параметры.НастройкаОбмена, ОООЗ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьСертификатПодписиПослеУстановкиВиртуальногоКаналаСбербанк(КаналУстановлен, Параметры) Экспорт
	
	Если Не КаналУстановлен Тогда
		ВыполнитьОбменСБанками(Неопределено, Параметры);
		Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ВыполнитьСинхронизациюПослеОпределенияСертификатаСбербанк", ЭтотОбъект, Параметры);
	
	ОпределитьСертификатПодписиСбербанк(Оповещение, Параметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ВыполнитьСинхронизациюПослеОпределенияСертификатаСбербанк(Результат, Параметры) Экспорт
	
	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбменСБанками(Неопределено, Параметры);
		Если Параметры.Свойство("ОповещениеПослеПолученияНовыхДокументов") Тогда
			ВыполнитьОбработкуОповещения(Параметры.ОповещениеПослеПолученияНовыхДокументов, Ложь);
		КонецЕсли;
		Возврат;
	КонецЕсли;
		
	ОтправлятьЗапросНовыхДокументов = Истина;
	ЕстьОшибка = Ложь;
	
	Если НЕ Параметры.ПринудительноеПолучениеНовыхДокументов Тогда
		ОтправлятьЗапросНовыхДокументов = НЕ ОбменСБанкамиСлужебныйВызовСервера.ЕстьЗапросыНочныхВыписокБезОтветаСбербанк(
			Параметры.НастройкаОбмена);
	КонецЕсли;
	
	Параметры.Вставить("ИдентификаторСертификата", Результат.ИдентификаторСертификата);
	Параметры.Вставить("СертификатСсылка", Результат.СертификатСсылка);
	
	Если ОтправлятьЗапросНовыхДокументов Тогда
		
		ИдентификаторЗапроса = Строка(Новый УникальныйИдентификатор);
		РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(Параметры.НастройкаОбмена);
		ИдентификаторОрганизации = РеквизитыНастройкиОбмена.ИдентификаторОрганизации;
		СтрокаПодписи = "ATTRIBUTES" + Символ(10) + "OrgId=" + ИдентификаторОрганизации + Символ(10)
						+ "RequestId=" + ИдентификаторЗапроса;

		СтрокаПодписиBase64 = ОбменСБанкамиСлужебныйВызовСервера.СтрокаBase64БезBOM(СтрокаПодписи);

		ПодключаемыйМодуль = ПодключеннаяВнешняяКомпонентаБанка("VPNKeyTLS");
		Представление = НСтр("ru = 'Запрос новых документов'");
		Параметры.Вставить("ИдентификаторЗапроса", ИдентификаторЗапроса);
		Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписиЗапросаНовыхДокументовСбербанк", ЭтотОбъект, Параметры);
		ПодписатьДанныеСбербанк(
			ОписаниеОповещения, ПодключаемыйМодуль, СтрокаПодписиBase64, Результат.ИдентификаторСертификата, Представление);
			
	Иначе
		
		ЗапроситьСтатусыДокументовСбербанк(Параметры.НастройкаОбмена, Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

// Отправка и получение электронных документов одной командой.
//
// Параметры:
//    ДанныеАутентификации - Структура или Неопределено;
//    Параметры         - Структура - контекст выполнения.
//
Процедура ОтправитьДокументыВБанк(ДанныеАутентификации, Параметры) Экспорт
	
	Если НЕ Параметры.ТребуетсяАвторизация ИЛИ ЗначениеЗаполнено(ДанныеАутентификации) Тогда
		РеквизитыНастройкиОбмена = Новый Структура(
			"АдресСервера, ИдентификаторОрганизации, ПрограммаБанка, ИмяВнешнегоМодуля, ВерсияФормата");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			Параметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		АсинхронныйОбмен = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.АсинхронныйОбмен");
		Если РеквизитыНастройкиОбмена.ПрограммаБанка = АсинхронныйОбмен Тогда
			// При аутентификации по сертификату отправка выполняется после расшифровки маркера
			// из процедуры ПослеПолученияОтпечатковВыполнитьОбмен.
			Обработчик = Новый ОписаниеОповещения("ОтправитьИПолучитьДокументыВБанкПослеПолученияМаркера", ЭтотОбъект, Параметры);
			
			ДанныеАутентификации.Вставить("Пароль", ДанныеАутентификации.ПарольПользователя);
			ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, РеквизитыНастройкиОбмена.АдресСервера,
				РеквизитыНастройкиОбмена.ИдентификаторОрганизации, ДанныеАутентификации, РеквизитыНастройкиОбмена.ВерсияФормата, 
				Параметры.НастройкаОбмена);
			Возврат;
		ИначеЕсли РеквизитыНастройкиОбмена.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
			ПодготовленныеПакеты = ОбменСБанкамиСлужебныйВызовСервера.ПодготовленныеКОтправкеПакетыЭДО(
				Параметры.НастройкаОбмена);
			Оповещение = Новый ОписаниеОповещения("ПослеОправкиПакетовЧерезВК", ЭтотОбъект, Параметры);
			ОтправитьПакетыЧерезВК(Оповещение, Параметры.НастройкаОбмена, ПодготовленныеПакеты);
			Возврат;
		Иначе
			ПараметрыАвторизации = Параметры.ПараметрыАвторизации;
			НастройкаОбмена = Параметры.НастройкаОбмена;
			ПараметрыАвторизации.Вставить(НастройкаОбмена, ДанныеАутентификации);
			
			СтруктураВозврата = Новый Структура;
			ОбменСБанкамиСлужебныйВызовСервера.ОтправитьЭДВБанк(НастройкаОбмена, ПараметрыАвторизации, СтруктураВозврата);
			
			Если СтруктураВозврата.Свойство("КолОтправленныхПакетов") Тогда
				Параметры.ИтогКолОтправленных = Параметры.ИтогКолОтправленных + СтруктураВозврата.КолОтправленныхПакетов;
			КонецЕсли;
			// Т.к. отправка выполняется по конкретной настройке обмена,  то соответствия в СтруктуреВозврата
			// (ДанныеДляОтправкиЧерезДопОбработку) с ключом НастройкаОбмена, не могут иметь больше
			// одного элемента. Поэтому можно сразу извлечь данные для отправки из соответствия.
			Если СтруктураВозврата.Свойство("ДанныеДляОтправкиЧерезДопОбработку")
				И СтруктураВозврата.ДанныеДляОтправкиЧерезДопОбработку.Количество() > 0 Тогда
				
				ДанныеДляОтправки = СтруктураВозврата.ДанныеДляОтправкиЧерезДопОбработку.Получить(НастройкаОбмена);
				Параметры.Вставить("ДанныеДляОтправки", ДанныеДляОтправки);
				ОбработчикПослеПодключения = Новый ОписаниеОповещения(
					"НачатьОтправкуПакетовЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
				ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(
					ОбработчикПослеПодключения, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
				Возврат;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработчикПродолжения, Параметры.ИтогКолОтправленных);
	
КонецПроцедуры

#КонецОбласти

#Область ТестНастроек

Процедура ПослеПолученияОтпечатковПроверитьСвязь(ОтпечаткиСертификатов, Параметры) Экспорт
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	РеквизитыНастройкиОбмена = Параметры.РеквизитыНастройкиОбмена;
	
	МассивОтпечатковСертификатов = Новый Массив;
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	ИначеЕсли РеквизитыНастройкиОбмена.АутентификацияПоСертификату ИЛИ РеквизитыНастройкиОбмена.ТребуетсяПодпись Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиОбмена = Неопределено;
	СообщениеЗапросЗонд = Неопределено;
	ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
		НастройкаОбмена, МассивОтпечатковСертификатов, СообщениеЗапросЗонд, НастройкиОбмена);
	Параметры.Вставить("ЗапросЗонд", СообщениеЗапросЗонд);
	
	Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
		СтруктураРезультата = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, СтруктураРезультата);
		Возврат;
	КонецЕсли;
	Если НастройкиОбмена.Подписывать Тогда
		Если НЕ ЗначениеЗаполнено(НастройкиОбмена.СертификатОрганизацииДляПодписи)
			ИЛИ НЕ НастройкиОбмена.СертификатДоступен Тогда
			ТекстСообщения = НСтр("ru = 'Не найден подходящий сертификат для подписи документа Запрос-зонд'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			СтруктураРезультата = Новый Структура("Успех", Ложь);
			ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеТестаНастройки, СтруктураРезультата);
			Возврат;
		КонецЕсли;

		МассивСертификатов = Новый Массив;
		МассивСертификатов.Добавить(НастройкиОбмена.СертификатОрганизацииДляПодписи);
		Операция = НСтр("ru = 'Подписание электронного документа'");
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("Операция", Операция);
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
		ОписаниеДанных.Вставить("БезПодтверждения", Истина);
		НаборДанных = Новый Массив;
		Данные = Новый Структура;
		ПараметрыДляПолученияДД = Новый Структура("СообщениеОбмена, ОписаниеДанных", СообщениеЗапросЗонд, ОписаниеДанных);
		СсылкаНаДД = Новый ОписаниеОповещения("ПолучитьДвоичныеДанныеДляСообщенияОбмена", ЭтотОбъект, ПараметрыДляПолученияДД);
		Данные.Вставить("Данные", СсылкаНаДД);
		Данные.Вставить("Объект", ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеЗапросЗонд));
		Представление = ОбменСБанкамиСлужебныйВызовСервера.ПредставлениеЭлектронногоДокумента(СообщениеЗапросЗонд);
		ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
			"ПриОткрытииЭлектронногоДокумента", ЭтотОбъект, СообщениеЗапросЗонд);
		ДанныеДляПредставления = Новый Структура("Представление, Значение", Представление, ОбработчикОткрытияЭД);
		Данные.Вставить("Представление", ДанныеДляПредставления);
		НаборДанных.Добавить(Данные);
		ОписаниеДанных.Вставить("НаборДанных", НаборДанных);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПодписанияЗапросаЗонда", ЭтотОбъект, Параметры);
		ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеОповещения);
	ИначеЕсли РеквизитыНастройкиОбмена.АутентификацияПоСертификату Тогда
		ДанныеДоступныхСертификатов = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(НастройкаОбмена);
		МассивСертификатов = Новый Массив;
		Для Каждого ДанныеСертификата Из ДанныеДоступныхСертификатов Цикл
			МассивСертификатов.Добавить(ДанныеСертификата.Ключ);
		КонецЦикла;
		
		ОписаниеДанных = Новый Структура;
		ОписаниеДанных.Вставить("ОтборСертификатов", МассивСертификатов);
		ОписаниеДанных.Вставить("БезПодтверждения",  Истина);
		ОписаниеДанных.Вставить("ЭтоАутентификация", Истина);
		ОписаниеДанных.Вставить("Операция", НСтр("ru = 'Аутентификация на сервере банка'"));
		ОписаниеДанных.Вставить("РазрешитьЗапоминатьПароль", Истина);
		
		ДополнительныеПараметры = Новый Структура("НастройкаОбмена", НастройкаОбмена);
		
		ОписаниеПолученияДанных = Новый ОписаниеОповещения(
			"ПолучитьЗашифрованныйИдентификаторСессии", ЭтотОбъект, ДополнительныеПараметры);
		
		ОписаниеДанных.Вставить("Данные", ОписаниеПолученияДанных);
			
		ОписаниеОповещения = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуЗапросаЗондаПослеРасшифровкиМаркера", ЭтотОбъект, Параметры);
		ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОписаниеОповещения);
	Иначе
		ОООЗ = Новый ОписаниеОповещения("ПолучитьМаркерБанкаПослеВводаДанныхАутентификации", ЭтотОбъект, Параметры);
		ПолучитьДанныеАутентификации(НастройкаОбмена, ОООЗ, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ТестСвязиСБанкомAsync(Маркер, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыЗапроса = Новый Структура;
	ПараметрыЗапроса.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
	ПараметрыЗапроса.Вставить("СообщениеОбмена", Параметры.ЗапросЗонд);
	ПараметрыЗапроса.Вставить("ИдентификаторСессииБанка", Маркер);
	ОткрытьФорму(
		"Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыЗапроса, , , , , Параметры.ОбработчикПослеТестаНастройки);

КонецПроцедуры

// Выполняет получение идентификатора сессии банка (маркера).
//
// Параметры:
//    Обработчик - ОписаниеОповещения - обработчик, вызываемый после получения маркера;
//        * Результат - Строка - маркер банка;
//        * ДополнительныеПараметры - Произвольный - параметры, указанные при создании оповещения.
//    АдресСервера - Строка - URL адрес сервера банка;
//    ИдентификаторОрганизации - Строка - идентификатор организации на сервере банка;
//    ДанныеАутентификации - Структура - данные аутентификации на сервере банка:
//        * Пользователь - Строка - логин на сервере банка;
//        * Пароль - Строка - пароль аутентификации на сервере банка.
//    НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//    ПробнаяОперация - Булево - операция пробная, не нужно выводить сообщения об ошибках.
//
Процедура ПолучитьМаркерБанкаПоЛогинуИПаролю(Обработчик, АдресСервера, ИдентификаторОрганизации, ДанныеАутентификации, ВерсияAPI, НастройкаОбмена = Неопределено, ПробнаяОперация = Ложь) Экспорт
	
	ДанныеSMSАвторизации = Неопределено;
	НеверныеДанныеАутентификации = Ложь;
	ИдентификаторСессииБанка = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаБазоваяАутентификация(АдресСервера,
		ИдентификаторОрганизации, ДанныеАутентификации, ВерсияAPI, ДанныеSMSАвторизации, НастройкаОбмена,
		НеверныеДанныеАутентификации);
		
	Если НеверныеДанныеАутентификации Тогда
		УдалитьПарольИзСеанса(НастройкаОбмена);
	КонецЕсли;
	Если ЗначениеЗаполнено(ДанныеSMSАвторизации) Тогда
		Параметры = Новый Структура;
		Параметры.Вставить("ОбработчикПослеРасширеннойАутентификации", Обработчик);
		Параметры.Вставить("АдресСервера", АдресСервера);
		Параметры.Вставить("ИдентификаторОрганизации", ИдентификаторОрганизации);
		Параметры.Вставить("НеподтвержденныйИдентификаторСессииБанка", ИдентификаторСессииБанка);
		Параметры.Вставить("ВерсияAPI", ВерсияAPI);
		
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ИдентификаторСессии", ИдентификаторСессииБанка);
		ПараметрыФормы.Вставить("Телефон", ДанныеSMSАвторизации.МаскаТелефона);
		ОО = Новый ОписаниеОповещения("ОтправитьОдноразовыйПарольSMSВБанк", ЭтотОбъект, Параметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, , , , , ОО);
	Иначе
		ВыполнитьОбработкуОповещения(Обработчик, ИдентификаторСессииБанка);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОтправитьОдноразовыйПарольSMSВБанк(ОдноразовыйПароль, Параметры) Экспорт
	
	Если ЗначениеЗаполнено(ОдноразовыйПароль) Тогда
		ИдентификаторСессииБанка = ОбменСБанкамиСлужебныйВызовСервера.МаркерБанкаПоSMS(Параметры.АдресСервера,
		Параметры.ИдентификаторОрганизации, Параметры.НеподтвержденныйИдентификаторСессииБанка, ОдноразовыйПароль,
		Параметры.ВерсияAPI);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(Параметры.ОбработчикПослеРасширеннойАутентификации, ИдентификаторСессииБанка);

КонецПроцедуры

Процедура ЗапроситьВыпискуПослеПолученияМаркераБанка(ИдентификаторСессииБанка, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ИдентификаторСессииБанка) Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура("ДанныеСертификата, ДатаНачала, ДатаОкончания, МассивСообщенийОбмена,
		|НастройкаОбмена, НомерСчета, Пароль, Пользователь, СообщениеОбмена");
	ЗаполнитьЗначенияСвойств(ПараметрыФормы, Параметры);
	ВладелецФормы = Параметры.Владелец;
	ПараметрыФормы.Вставить("ИдентификаторСессииБанка", ИдентификаторСессииБанка);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, ВладелецФормы);
	
КонецПроцедуры

Процедура ОтправитьИПолучитьДокументыВБанкПослеПолученияМаркера(Маркер, Параметры) Экспорт
	
	Если НЕ ЗначениеЗаполнено(Маркер) Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПродолжения, Неопределено);
		Возврат;
		
	КонецЕсли;
	
	ДанныеАутентификации = Новый Структура;
	ДанныеАутентификации.Вставить("МаркерРасшифрованный", Маркер);
	
	ОтправитьИПолучитьДокументыВБанк(ДанныеАутентификации, Параметры)
	
КонецПроцедуры

Процедура ОтправитьИПолучитьДокументыВБанкПослеРасшифровкиМаркера(Результат, Параметры) Экспорт
	
	Если НЕ Результат.Успех Тогда
		ВыполнитьОбработкуОповещения(Параметры.ОбработчикПродолжения, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДанныеСтрокой = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.СтрокаИзДвоичныхДанных(Результат.РасшифрованныеДанные);
	ДанныеАутентификации = Новый Структура;
	ДанныеАутентификации.Вставить("МаркерРасшифрованный", ДанныеСтрокой);

	ОтправитьИПолучитьДокументыВБанк(ДанныеАутентификации, Параметры)
	
КонецПроцедуры

Процедура ОтправитьИПолучитьДокументыВБанк(ДанныеАутентификации, Параметры)
	
	ПараметрыАвторизации = Параметры.ПараметрыАвторизации;
		
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ПараметрыАвторизации.Вставить(НастройкаОбмена, ДанныеАутентификации);
		
	СтруктураВозврата = ОбменСБанкамиКлиентСервер.ПараметрыПолученияНовыхДокументовАсинхронныйОбмен();
	
	ОбменСБанкамиСлужебныйВызовСервера.ОтправитьЭДВБанк(НастройкаОбмена, ПараметрыАвторизации, СтруктураВозврата);
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЭДИзБанкаАсинхронныйОбмен(НастройкаОбмена, ДанныеАутентификации, СтруктураВозврата);
	
	ВызватьОповещения(СтруктураВозврата);
	
	Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
		Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
		Для Каждого КлючЗначение Из СтруктураВозврата.ДанныеЭП Цикл
			ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	// Подготовим вывод сообщения для пользователя об отправке/получению пакетов ЭД.
	КолОтправленныхПакетов = СтруктураВозврата.КолОтправленныхПакетов;
	КолПолученныхПакетов = СтруктураВозврата.КолПолученныхПакетов;
	ШаблонОповещения = НСтр("ru = 'Отправлено пакетов: (%1), получено пакетов: (%2).'");
	ТекстОповещения = СтрШаблон(ШаблонОповещения, КолОтправленныхПакетов, КолПолученныхПакетов);
	
	Оповестить("ОбновитьСостояниеОбменСБанками", СтруктураВозврата.ПараметрОповещения);
		
	ЗаголовокОповещения = НСтр("ru = '1С:ДиректБанк'");
	ПоказатьОповещениеПользователя(ЗаголовокОповещения, , ТекстОповещения);
	
	Структура = Новый Структура("ИтогКолОтправленных, ИтогКолПолученных", КолОтправленныхПакетов, КолПолученныхПакетов);
	Параметры.ИтогКолОтправленных = Параметры.ИтогКолОтправленных + СтруктураВозврата.КолОтправленныхПакетов;
	ВыполнитьОбработкуОповещения(Параметры.ОбработчикПродолжения, Структура);
	
КонецПроцедуры

Процедура ПолучитьДвоичныеДанныеДляСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	СообщениеОбмена = Неопределено;
	ОписаниеДанных = Неопределено;
	Если ТипЗнч(ДополнительныеПараметры) = Тип("Структура")
		И ДополнительныеПараметры.Свойство("СообщениеОбмена", СообщениеОбмена)
		И ТипЗнч(СообщениеОбмена) = Тип("ДокументСсылка.СообщениеОбменСБанками") Тогда
		ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(СообщениеОбмена);
	КонецЕсли;
	
	Параметры = Новый Структура;
	Параметры.Вставить("Данные", ДвоичныеДанныеЭД);
	
	ВыполнитьОбработкуОповещения(Результат.Оповещение, Параметры);
	
КонецПроцедуры

// Получает пароль к сертификату, если доступен текущему пользователю.
//
// Параметры:
//  СертификатЭП       - СправочникСсылка.СертификатыЭП - сертификат ЭП.
//  ПарольПользователя - Строка - пароль к сертификату ЭП, полученный из глобальной переменной.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен, иначе - Ложь.
//
Функция ПарольКСертификатуПолучен(СертификатЭП, ПарольПользователя, НаВремяСеанса = Ложь) Экспорт
	
	ПарольПользователя = Неопределено;
	СоответствиеСертификатаИПароля = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(СоответствиеСертификатаИПароля) = Тип("ФиксированноеСоответствие") Тогда
		ПарольПользователя = СоответствиеСертификатаИПароля.Получить(СертификатЭП);
		НаВремяСеанса = (ПарольПользователя <> Неопределено);
	КонецЕсли;
	
	Если ПарольПользователя = Неопределено Тогда
		ПарольПользователя = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ПарольКСертификату(СертификатЭП);
	КонецЕсли;
	
	Возврат (ПарольПользователя <> Неопределено);
	
КонецФункции

Процедура ВыполнитьДействияПослеОтправки(Параметры)
	
	Перем Действие, ИтогКолНовыхЭД, ИтогКолУтвержденныхЭД, ИтогКолПодписанных, ИтогКолПодготовленных, ИтогКолОтправленных;
	
	Если НЕ Параметры.Свойство("ИтогКолНовыхЭД", ИтогКолНовыхЭД) Тогда
		ИтогКолНовыхЭД = 0;
	КонецЕсли;
	Если НЕ Параметры.Свойство("ИтогКолУтвержденныхЭД", ИтогКолУтвержденныхЭД) Тогда
		ИтогКолУтвержденныхЭД = 0;
	КонецЕсли;
	Если НЕ Параметры.Свойство("ИтогКолПодписанных", ИтогКолПодписанных) Тогда
		ИтогКолПодписанных = 0;
	КонецЕсли;
	Если НЕ Параметры.Свойство("ИтогКолПодготовленных", ИтогКолПодготовленных) Тогда
		ИтогКолПодготовленных = 0;
	КонецЕсли;
	Если НЕ Параметры.Свойство("ИтогКолОтправленных", ИтогКолОтправленных) Тогда
		ИтогКолОтправленных = 0;
	КонецЕсли;

	Оповестить("ОбновитьСостояниеОбменСБанками");

	Если Параметры.Свойство("Действие")
		И ЭлектронноеВзаимодействиеКлиентСервер.ЕстьДействие(Параметры.Действие, "Показать") Тогда
		Результат = Параметры.Результат;
		МассивОбработки = "";
		Если Результат.Свойство("МассивНовыхСообщенийОбмена", МассивОбработки) И МассивОбработки <> Неопределено Тогда
			Для Каждого ТекЭл Из МассивОбработки Цикл
				ОткрытьЭДДляПросмотра(ТекЭл);
			КонецЦикла;
		КонецЕсли;
	КонецЕсли;
	
	ТекстЗаголовка = НСтр("ru = '1С:ДиректБанк'");
	ЭлектронноеВзаимодействиеСлужебныйКлиент.ВывестиИнформациюОбОбработанныхЭД(ТекстЗаголовка,
			ИтогКолНовыхЭД, ИтогКолУтвержденныхЭД, ИтогКолПодписанных, ИтогКолПодготовленных, ИтогКолОтправленных);
	
	ВсегоОбработано = ИтогКолНовыхЭД + ИтогКолУтвержденныхЭД + ИтогКолПодписанных + ИтогКолПодготовленных
					+ ИтогКолОтправленных;
	
	ОписаниеОповещения = Неопределено;
	Если Параметры.Свойство("ОписаниеОповещения", ОписаниеОповещения)
		И ТипЗнч(ОписаниеОповещения) = Тип("ОписаниеОповещения") Тогда
		
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, (ВсегоОбработано > 0));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОткрытиеФорм

// Открывает форму просмотра электронного документа.
//
// Параметры:
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - сообщение открываемое для просмотра;
//  ПараметрыОткрытия - Структура - дополнительные параметры просмотра;
//  ВладелецФормы     - УправляемаяФорма - форма - владелец;
//  ТолькоЧтение - Булево - признак открытия формы в режиме просмотра без команд.
//
Процедура ОткрытьЭДДляПросмотра(СообщениеОбмена, ПараметрыОткрытия = Неопределено, ВладелецФормы = Неопределено, ТолькоЧтение = Ложь) Экспорт
	
	Если НЕ ЗначениеЗаполнено(СообщениеОбмена) Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ПараметрыОткрытия", ПараметрыОткрытия);
	ДополнительныеПараметры.Вставить("ВладелецФормы", ВладелецФормы);
	ДополнительныеПараметры.Вставить("ТолькоЧтение", ТолькоЧтение);
	Описание = Новый ОписаниеОповещения(
		"ОткрытьФормуЭлектронногоДокументаПослеПолученияОтпечатков", ЭтотОбъект, ДополнительныеПараметры);
	ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Описание, Истина, Ложь);

КонецПроцедуры

// Готовит к отправке ЭД и ОТПРАВЛЯЕТ (подготовленные электронные документы).
//
// Параметры:
//  СообщенияОбмена - Массив ссылок на сообщения обмена, которые необходимо поместить в пакеты ЭДО;
//  ПризнакПодписи - булево, признак того, что электронные документы подписаны ЭП;
//  СтруктураПаролейИМаркеров - содержит данные о паролях сертификатов и маркеров;
//  Параметры - дополнительные параметры обработки;
//  ОбработчикПослеОтправкиПЭД - ОписаниеОповещения - оповещение, вызываемое после отправки пакетов.
//
Процедура ПодготовитьИОтправитьПЭД(СообщенияОбмена, ПризнакПодписи, СоответствиеНастроекОбменаИПараметровСертификатов = Неопределено, Параметры = Неопределено, ОбработчикПослеОтправкиПЭД = Неопределено)
	
	ДопПараметры = Новый Структура;
	ДопПараметры.Вставить("СообщенияОбмена", СообщенияОбмена);
	ДопПараметры.Вставить("ПризнакПодписи", ПризнакПодписи);
	ДопПараметры.Вставить("Параметры", Параметры);
	ДопПараметры.Вставить("ОбработчикПослеОтправкиПЭД", ОбработчикПослеОтправкиПЭД);
	
	Если СоответствиеНастроекОбменаИПараметровСертификатов = Неопределено Тогда
		СоответствиеНастроекОбменаИПараметровСертификатов = Новый Соответствие;
	КонецЕсли;
	
	РезультатОтправкиПЭД = ОбменСБанкамиСлужебныйВызовСервера.СоздатьИОтправитьДокументыПЭД(
		СообщенияОбмена, СоответствиеНастроекОбменаИПараметровСертификатов);
		
	Если РезультатОтправкиПЭД.ОтправленныеДокументы.Количество() Тогда
		Оповестить("ОтправленоDirectBank", РезультатОтправкиПЭД.ОтправленныеДокументы);
		
		// Попытка сразу после отправки получить статусы документов.
		Для Каждого ДанныеАутентификации Из СоответствиеНастроекОбменаИПараметровСертификатов Цикл
			
			СтруктураВозврата = ОбменСБанкамиКлиентСервер.ПараметрыПолученияНовыхДокументовАсинхронныйОбмен();
			ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЭДИзБанкаАсинхронныйОбмен(
				ДанныеАутентификации.Ключ, ДанныеАутентификации.Значение, СтруктураВозврата);
			ВызватьОповещения(СтруктураВозврата);
			Если СтруктураВозврата.Свойство("ДанныеЭП") И СтруктураВозврата.ДанныеЭП.Количество() Тогда
				Оповещение = Новый ОписаниеОповещения("ПустойОбработчик", ЭлектронноеВзаимодействиеСлужебныйКлиент);
				Для Каждого КлючЗначение Из СтруктураВозврата.ДанныеЭП Цикл
					ДобавитьПодписиИОпределитьСтатусы(Оповещение, КлючЗначение.Ключ, КлючЗначение.Значение);
				КонецЦикла;
			КонецЕсли;
			Оповестить("ОбновитьСостояниеОбменСБанками", СтруктураВозврата.ПараметрОповещения);
		КонецЦикла
	КонецЕсли;
	
	ДопПараметры.Вставить("РезультатОтправкиПЭД", РезультатОтправкиПЭД);
	ДопПараметры.Вставить(
		"СоответствиеНастроекОбменаИПараметровСертификатов", СоответствиеНастроекОбменаИПараметровСертификатов);
	
	ВыполнитьДействияПослеОтправкиПЭДЗавершить(Неопределено, ДопПараметры);
	
КонецПроцедуры

Процедура ОткрытьФормуЭлектронногоДокументаПослеПолученияОтпечатков(Отпечатки, ДополнительныеПараметры) Экспорт
	
	СообщениеОбмена = ДополнительныеПараметры.СообщениеОбмена;
	ПараметрыОткрытия = ДополнительныеПараметры.ПараметрыОткрытия;
	ВладелецФормы = ДополнительныеПараметры.ВладелецФормы;
	ТолькоЧтение = ДополнительныеПараметры.ТолькоЧтение;
	
	МассивОтпечатков = Новый Массив;
	Если ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из Отпечатки Цикл
			МассивОтпечатков.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;

	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыФормы.Вставить("ТолькоЧтение", ТолькоЧтение);
	ПараметрыФормы.Вставить("МассивОтпечатков", МассивОтпечатков);
	Если ВладелецФормы = Неопределено Тогда
		ОткрытьФорму("Документ.СообщениеОбменСБанками.Форма.ЭлектронныйДокумент", ПараметрыФормы, , СообщениеОбмена);
	Иначе
		Если ПараметрыОткрытия = Неопределено Тогда
			ОткрытьФорму(
				"Документ.СообщениеОбменСБанками.Форма.ЭлектронныйДокумент", ПараметрыФормы, ВладелецФормы, СообщениеОбмена);
		Иначе
			Окно = Неопределено;
			Если ТипЗнч(ПараметрыОткрытия) = Тип("ПараметрыВыполненияКоманды")
				ИЛИ ТипЗнч(ПараметрыОткрытия) = Тип("Структура")
				И ПараметрыОткрытия.Свойство("Окно") И ТипЗнч(ПараметрыОткрытия.Окно) = Тип("ОкноКлиентскогоПриложения") Тогда
				
				Окно = ПараметрыОткрытия.Окно;
			КонецЕсли;
			ОткрытьФорму("Документ.СообщениеОбменСБанками.Форма.ЭлектронныйДокумент", ПараметрыФормы, ВладелецФормы,
				ПараметрыОткрытия.Уникальность, Окно);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область НастройкиОбмена

// Функция проверяет, получен ли ранее пароль для авторизации на сервере банка.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - настройка обмена с банком;
//  ДанныеАвторизации - Структура, содержит следующие записи:
//    * Пользователь - строка - имя пользователя;
//    * ПарольПользователя - Строка - пароль пользователя.
//
// Возвращаемое значение:
//  Булево - Истина - если пароль для сертификата ЭП получен ранее, иначе - Ложь.
//
Функция ПолученыДанныеАвторизации(НастройкаОбмена, ДанныеАвторизации) Экспорт
	
	Если Не ЗначениеЗаполнено(НастройкаОбмена) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	СоответствиеСертификатаИПароля = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками.ПаролиСеанса"];
	Если ТипЗнч(СоответствиеСертификатаИПароля) = Тип("ФиксированноеСоответствие") Тогда
		ДанныеАвторизации = СоответствиеСертификатаИПароля.Получить(НастройкаОбмена);
		Если ЗначениеЗаполнено(ДанныеАвторизации) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

// Процедура интерактивно запрашивает логин и пароль у пользователя.
//
// Параметры:
//  НастройкаОбмена - СправочникСсылка.НастройкиОбменСБанками - ссылка на настройку обмена;
//  ОписаниеОповещенияОЗакрытии - ОписаниеОповещения - где будет продолжена работа программы;
//  ЭтоТест - Булево - признак теста настройки обмена.
//
Процедура ПолучитьДанныеАутентификации(НастройкаОбмена, ОписаниеОповещенияОЗакрытии, ЭтоТест = Ложь) Экспорт
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидОперации", НСтр("ru = 'Аутентификация на сервере банка'"));
	ПараметрыФормы.Вставить("ДляЗаписиВИБ", ЭтоТест);
	ПараметрыФормы.Вставить("НастройкаОбмена", НастройкаОбмена);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату",
		ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
КонецПроцедуры

Процедура НачатьПодписаниеБанковскихЭД(Структура)
	
	СоотвСертификатовИИхСтруктур = Неопределено;
	МассивСертификатов = Неопределено;
	ДанныеДляСпецОбработки = Неопределено;
	ОписаниеПодписатьЭД = Неопределено;
	Структура.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД);
	Если Структура.Свойство("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур)
		И ТипЗнч(СоотвСертификатовИИхСтруктур) = Тип("Соответствие")
		И Структура.Свойство("МассивСертификатов", МассивСертификатов)
		И ТипЗнч(МассивСертификатов) = Тип("Массив")
		И Структура.Свойство("ДанныеДляСпецОбработки", ДанныеДляСпецОбработки)
		И ТипЗнч(ДанныеДляСпецОбработки) = Тип("Соответствие") Тогда
		
		Соответствие = Новый Соответствие;
		Для Каждого Сертификат Из МассивСертификатов Цикл
			ПараметрыСертификата = СоотвСертификатовИИхСтруктур.Получить(Сертификат);
			Если ТипЗнч(ПараметрыСертификата) = Тип("Структура") Тогда
				Соответствие.Вставить(Сертификат, ПараметрыСертификата);
			КонецЕсли;
		КонецЦикла;
		
		МассивСообщенийОбменаКПодписи = Новый Массив;
		ВидОперации = НСтр("ru = 'Подписание электронных документов'");
		// ДанныеДляСпецОбработки - Соответствие:
		//   Ключ     - ПрограммаБанка.
		//   Значение - Соответствие:
		//     Ключ     - НастройкаОбмена.
		//     Значение - МассивСообщенийОбмена.
		// Т.к. это соответствие формируется в разрезе сертификата подписи, то оно однозначно:
		// массив сертификатов подписи по 1 программе банка и 1 настройке обмена (в каждом
		// рассматриваемом соответствии не больше одного элемента):
		Для Каждого КлючИЗначение Из ДанныеДляСпецОбработки Цикл
			Структура.Вставить("ПрограммаБанка", КлючИЗначение.Ключ);
			Для Каждого НастройкаИСообщениеОбмена Из КлючИЗначение.Значение Цикл
				Структура.Вставить("НастройкаОбмена", НастройкаИСообщениеОбмена.Ключ);
				Для Каждого СообщениеОбмена Из НастройкаИСообщениеОбмена.Значение Цикл
					МассивСообщенийОбменаКПодписи.Добавить(СообщениеОбмена);
				КонецЦикла;
			КонецЦикла;
		КонецЦикла;
		Структура.Вставить("МассивСообщенийОбменаКПодписи", МассивСообщенийОбменаКПодписи);
		ВызватьОповещение = Новый ОписаниеОповещения("ПродолжитьПодписаниеБанковскихЭД", ЭтотОбъект, Структура);
		ПолучитьПарольКСертификату(
			ВызватьОповещение, Соответствие, ВидОперации, МассивСообщенийОбменаКПодписи, Структура.ПрограммаБанка);
	ИначеЕсли ТипЗнч(ОписаниеПодписатьЭД) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

// Вызывается из ПолучитьПарольКСертификату по выполнению описания оповещения.
//
// Параметры:
//    Результат - Структура - результат выбора сертификата подписи и ввода пароля к нему:
//       ВыбранныйСертификат - СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования.
//       ПарольПользователя  - Строка - пароль к сертификату.
//    Параметры - Структура:
//       СоотвСертификатовИИхСтруктур - Соответствие.
//       ПрограммаБанка               - ПеречислениеСсылка.ПрограммыБанка.
//       ОбработчикПродолжения        - ОписаниеОповещения - описание, которое надо
//                                    выполнить после завершения обработки текущих ЭД.
//
Процедура ПродолжитьПодписаниеБанковскихЭД(Результат, Параметры) Экспорт
	
	ВыбранныйСертификат = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура") И Результат.Свойство("ВыбранныйСертификат", ВыбранныйСертификат)
		И ТипЗнч(ВыбранныйСертификат) = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
		
		ПараметрыСертификата = Параметры.СоотвСертификатовИИхСтруктур[ВыбранныйСертификат];
		ПараметрыСертификата.Вставить("ПарольПолучен", Истина);
		ПараметрыСертификата.Вставить("ПарольПользователя", Результат.ПарольПользователя);
		ПараметрыСертификата.Вставить("ВыбранныйСертификат", ВыбранныйСертификат);
		Параметры.Вставить("СтруктураСертификата", ПараметрыСертификата);
		
		Если Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.СбербанкОнлайн") Тогда
			Параметры.Вставить("МассивОбработанныхНастроекОбмена", Новый Массив);
			Параметры.Вставить("ОтправитьНаПодписьПослеОбработки");
			Параметры.Вставить("РезультатВыбораСертификатаБанка", Результат);
			ДанныеСпецОбработки = Параметры.ДанныеДляСпецОбработки;
			Для Каждого ЭлементСоответствия Из ДанныеСпецОбработки Цикл
				ТекущиеДанные = ЭлементСоответствия.Значение;
				Параметры.ДанныеДляСпецОбработки.Удалить(ЭлементСоответствия.Ключ);
				НачатьПодписаниеЭДСбербанка(ТекущиеДанные, Параметры);
				Возврат;
			КонецЦикла;
		ИначеЕсли Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК") Тогда
			ДополнительныеПараметры = Новый Структура;
			ДополнительныеПараметры.Вставить("ОповещениеПослеПодписания", Параметры.ОбработчикПродолжения);
			ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", Параметры.МассивСообщенийОбменаКПодписи);
			ДополнительныеПараметры.Вставить("СертификатСсылка", Результат.ВыбранныйСертификат);
			ДополнительныеПараметры.Вставить("Пароль", Результат.ПарольПользователя);
			ДополнительныеПараметры.Вставить("НастройкаОбмена", Параметры.НастройкаОбмена);
			ДополнительныеПараметры.Вставить("ИтогКолПодписанных", 0);
			ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры);
			Возврат;
		ИначеЕсли Параметры.ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезДопОбработку") Тогда
			ОбработчикПослеПодключения = Новый ОписаниеОповещения(
				"ПроверитьНеобходимостьУстановкиПинКода", ЭтотОбъект, Параметры);
			РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(Параметры.НастройкаОбмена);
			ПолучитьВнешнийМодульЧерезДополнительнуюОбработку(ОбработчикПослеПодключения, РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля);
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД) Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

// Обрабатывает результаты очередной итерации отправки ЭД, при необходимости (во входящих параметрах есть
// не отправленные ЭД) начинает следующую итерацию отправки ЭД.
//
// Параметры:
//    Результат - Структура, Неопределено - в структуре возвращается результат прошедшей итерации отправки ЭД:
//       КолПодготовленных - Число.
//       КолОтправленных   - Число.
//    Параметры - Структура:
//    МассивКОтправке           - Массив.
//    МассивКОтправкеБезПодписи - Массив.
//    Прочие параметры.
//
Процедура ЗавершитьОтправкуСообщенийОбмена(Результат, Параметры) Экспорт
	
	СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
	ПродолжитьВыполнитьДействияПослеОтправки = Истина;
	
	МассивСообщенийОбмена = Неопределено;
	МассивКОтправке = Неопределено;
	МассивКОтправкеБезПодписи = Неопределено;
	Если Параметры.Свойство("МассивКОтправке", МассивКОтправке)
		И ТипЗнч(МассивКОтправке) = Тип("Массив")
		И МассивКОтправке.Количество() > 0 Тогда
		
		МассивСообщенийОбмена = МассивКОтправке;
		Параметры.Удалить("МассивКОтправке");
		ПризнакПодписи = Истина;
	ИначеЕсли Параметры.Свойство("МассивКОтправкеБезПодписи", МассивКОтправкеБезПодписи)
		И ТипЗнч(МассивКОтправкеБезПодписи) = Тип("Массив")
		И МассивКОтправкеБезПодписи.Количество() > 0 Тогда
		
		МассивСообщенийОбмена = МассивКОтправкеБезПодписи;
		Параметры.Удалить("МассивКОтправкеБезПодписи");
		ПризнакПодписи = Ложь;
	КонецЕсли;
	
	Если МассивСообщенийОбмена <> Неопределено Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗавершитьОтправкуСообщенийОбмена", ЭтотОбъект, Параметры);
		ПодготовитьИОтправитьПЭД(МассивСообщенийОбмена, ПризнакПодписи, СоотвНастроекОбменаИСтруктурСертификатов, Параметры, ОписаниеОповещения);
		ПродолжитьВыполнитьДействияПослеОтправки = Ложь;
	КонецЕсли;
	
	Если ПродолжитьВыполнитьДействияПослеОтправки Тогда
		ВыполнитьДействияПослеОтправки(Параметры);
	КонецЕсли;
	
КонецПроцедуры

// Добавляет подписи в электронный документ.
//
// Параметры:
//   Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами:
//      * РезультатВызова - Булево - Истина - подписи добавлены и валидны, Ложь - произошла ошибка или невалидна подпись.
//      * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//   СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - ссылка на сообщение обмена;
//   Подписи - Массив:
//     * ДвоичныеДанные - данные подписей.
//
Процедура ДобавитьПодписиИОпределитьСтатусы(Оповещение, СообщениеОбмена, ДанныеПодписей) Экспорт
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("СообщениеОбмена", СообщениеОбмена);
	ПараметрыОбработки.Вставить("ДанныеПодписей", ОбщегоНазначенияКлиентСервер.СкопироватьМассив(ДанныеПодписей));
	ПараметрыОбработки.Вставить("ОповещениеПослеДобавленияИПроверкиПодписей", Оповещение);
		
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьСертификатыИзПодписиПослеПолученияМенеджераКриптографии", ЭтотОбъект, ПараметрыОбработки);
		
	ЭлектроннаяПодписьКлиент.СоздатьМенеджерКриптографии(Оповещение, "ПолучениеСертификатов");
	
КонецПроцедуры

Процедура ПолучитьСертификатыИзПодписиПослеПолученияМенеджераКриптографии(МенеджерКриптографии, ДополнительныеПараметры) Экспорт
	
	Если НЕ ТипЗнч(МенеджерКриптографии) = Тип("МенеджерКриптографии") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей, Ложь);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ДобавитьПодписиВЭДПослеПолученияСертификатов", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьСертификаты(Обработчик, МенеджерКриптографии, ДополнительныеПараметры.ДанныеПодписей);
	
КонецПроцедуры

Процедура ДобавитьПодписиВЭДПослеПолученияСертификатов(МассивСертификатовИПодписей, ДополнительныеПараметры) Экспорт
	
	Если МассивСертификатовИПодписей = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей, Ложь);
		Возврат;
	КонецЕсли;
	
	ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодписиВСообщениеОбмена(
		ДополнительныеПараметры.СообщениеОбмена, МассивСертификатовИПодписей);

	ОпределитьСтатусыПодписей(
		ДополнительныеПараметры.ОповещениеПослеДобавленияИПроверкиПодписей, ДополнительныеПараметры.СообщениеОбмена);
	
КонецПроцедуры

Процедура ПолучитьСертификаты(Обработчик, МенеджерКриптографии, МассивПодписей)
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("МенеджерКриптографии", МенеджерКриптографии);
	ПараметрыОбработки.Вставить("ОбработчикПослеПолученияСертификатов", Обработчик);
	ПараметрыОбработки.Вставить("МассивПодписей", ОбщегоНазначенияКлиентСервер.СкопироватьМассив(МассивПодписей));
	ПараметрыОбработки.Вставить("МассивПодписейИСертификатов", Новый Массив);
	
	НачатьПолучениеСертификатов(ПараметрыОбработки);
	
КонецПроцедуры

Процедура НачатьПолучениеСертификатов(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.МассивПодписей.Количество() = 0 Тогда
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОбработчикПослеПолученияСертификатов, ДополнительныеПараметры.МассивПодписейИСертификатов);
		Возврат;
	КонецЕсли;
	
	ДанныеПодписи = ДополнительныеПараметры.МассивПодписей.Получить(0);
	
	Оповещение = Новый ОписаниеОповещения("ПолучитьОчереднойСертификат", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуПолученияСертификатаИзПодписи", ЭтотОбъект);
	ДополнительныеПараметры.МенеджерКриптографии.НачатьПолучениеСертификатовИзПодписи(Оповещение, ДанныеПодписи);
	
КонецПроцедуры

Процедура ПолучитьОчереднойСертификат(Сертификаты, ДополнительныеПараметры) Экспорт
	
	Если Сертификаты.Количество() <> 0 Тогда
		Сертификат = Сертификаты[0];
		Оповещение = Новый ОписаниеОповещения("ОбработатьДанныеПослеВыгрузкиСертификата", ЭтотОбъект,
			ДополнительныеПараметры, "ОбработатьОшибкуВыгрузкиСертификата", ЭтотОбъект);
		Сертификат.НачатьВыгрузку(Оповещение);
	Иначе
		НачатьПолучениеСертификатов(ДополнительныеПараметры);
		ДополнительныеПараметры.МассивПодписей.Удалить(0);
	КонецЕсли
	
КонецПроцедуры

Процедура ОбработатьДанныеПослеВыгрузкиСертификата(ДанныеСертификата, ДополнительныеПараметры) Экспорт

	ДанныеСертификатаИПодписи = Новый Соответствие;
	ДанныеСертификатаИПодписи.Вставить(ДанныеСертификата, ДополнительныеПараметры.МассивПодписей[0]);
	ДополнительныеПараметры.МассивПодписей.Удалить(0);
	ДополнительныеПараметры.МассивПодписейИСертификатов.Добавить(ДанныеСертификатаИПодписи);
	
	НачатьПолучениеСертификатов(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСертификатаИзПодписи(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Получение сертификата из подписи'");
	ТекстСообщения = НСтр("ru = 'Не удалось получить сертификат из подписи'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, 1);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПослеПолученияСертификатов, Неопределено);
	
КонецПроцедуры

Процедура ОбработатьОшибкуВыгрузкиСертификата(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	Операция = НСтр("ru = 'Выгрузка сертификата подписи'");
	ТекстСообщения = НСтр("ru = 'Не удалось выгрузить сертификат полученной подписи'");
	ТекстОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке);
	ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(Операция, ТекстОшибки, ТекстСообщения, 1);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПослеПолученияСертификатов, Неопределено);
	
КонецПроцедуры

Процедура ВключитьЖурналированиеВК(Оповещение, ПодключаемыйМодуль, Каталог)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеВключенияЖурналирования", Оповещение);
	ОповещениеПослеВключенияЖурналирования = Новый ОписаниеОповещения("ПослеВключенияЖурналирования", ЭтотОбъект,
		ДополнительныеПараметры, "ПослеОшибкиВключенияЖурналированияВК", ЭтотОбъект);
		
	ПодключаемыйМодуль.НачатьВызовНачатьЖурналирование(ОповещениеПослеВключенияЖурналирования, Каталог);
	
КонецПроцедуры

Процедура ПродолжитьПодписаниеЧерезДополнительнуюОбработку(Параметры)
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	
	СертификатXML = Параметры.СертификатXMLЧерезДопОбработку;
	СтруктураСертификата = Параметры.СтруктураСертификата;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, СертификатXML, СтруктураСертификата.ПарольПользователя);
	
	Если ПарольУстановлен Тогда
		Оповещение = Новый ОписаниеОповещения("ПодписаниеЭДЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		УстановитьСоединениеЧерезДополнительнуюОбработку(
			Оповещение, ВнешнийПодключаемыйМодуль, СертификатXML, НастройкаОбмена);
		Возврат;
	КонецЕсли;
	
	ОписаниеПодписатьЭД = Неопределено;
	Если Параметры.Свойство("ОбработчикПродолжения", ОписаниеПодписатьЭД)Тогда
		ВыполнитьОбработкуОповещения(ОписаниеПодписатьЭД);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетаЧерезДополнительнуюОбработку(Параметры)
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	
	ПарольУстановлен = Параметры.ПарольУстановлен;
	ДанныеСертификата = Параметры.ДанныеСертификата;
	ПарольПользователя = Параметры.ПарольПользователя;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	Если ПарольУстановлен Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ОтправитьПакетыЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
		УстановитьСоединениеЧерезДополнительнуюОбработку(
			Оповещение, ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, НастройкаОбмена);
		Возврат;
	КонецЕсли;

КонецПроцедуры

Функция ИнициализироватьИнтерфейсДополнительнойОбработки(АдресФайлаВнешнегоМодуля, ИмяВнешнегоМодуля, АдресКомпоненты)
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		ВремФайл = ПолучитьИмяВременногоФайла("epf");
		ДвоичныеДанныеОбработки = ПолучитьИзВременногоХранилища(АдресФайлаВнешнегоМодуля);
		ДвоичныеДанныеОбработки.Записать(ВремФайл);
		ПодключаемыйМодуль = ВнешниеОбработки.ПолучитьФорму(ВремФайл);
	#Иначе
		ИмяФормы = "ВнешняяОбработка." + ИмяВнешнегоМодуля + ".Форма";
		ПодключаемыйМодуль = ПолучитьФорму(ИмяФормы, Новый Структура("РежимЭДО", Истина), , Новый УникальныйИдентификатор);
	#КонецЕсли
	
	Попытка
		ОписаниеОбработки = ПодключаемыйМодуль.ОписаниеОбработки();
		ВерсияAPI = ОписаниеОбработки.ВерсияAPI;
	Исключение
		ВерсияAPI = 1;
	КонецПопытки;
		
	Если ВерсияAPI = 1 Тогда
		Попытка
			ПодключаемыйМодуль.Инициализировать();
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации дополнительной обработки.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
			Возврат Ложь;
		КонецПопытки;
	Иначе
		Попытка
			ИнициализацияВыполнена = ПодключаемыйМодуль.НачатьИнициализацию(АдресКомпоненты);
		Исключение
			ШаблонОшибки = НСтр("ru = 'Ошибка инициализации дополнительной обработки.
										|Код ошибки: ДО-%1
										|%2'");
			ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
			ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
			Операция = НСтр("ru = 'Инициализация внешней обработки'");
			ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
			ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
				Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
			Возврат Ложь;
		КонецПопытки;
		
		Если НЕ ИнициализацияВыполнена Тогда
			Возврат Ложь;
		Иначе
			Попытка
				ПодключаемыйМодуль.ЗавершитьИнициализацию();
			Исключение
				ШаблонОшибки = НСтр("ru = 'Ошибка завершения инициализации.
											|Код ошибки: ДО-%1
											|%2'");
				ДеталиОшибки = ПодключаемыйМодуль.ДеталиОшибки();
				ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
				Операция = НСтр("ru = 'Завершение инициализации внешней обработки'");
				ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
				ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
					Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
				Возврат Ложь;
			КонецПопытки;
		КонецЕсли;

	КонецЕсли;
	
	ЗакэшироватьПодключаемыйМодуль(ИмяВнешнегоМодуля, ПодключаемыйМодуль);
	
	Возврат Истина;
	
КонецФункции

Функция ДоступноеХранилищеЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль)
	
	Хранилища = ПодключенныеХранилищаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль, Ложь);
	
	Если Хранилища = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	Для Каждого ИдентификаторХранилища Из Хранилища Цикл
		
		ЕстьОшибка = Ложь;
		НуженПин = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ЕстьОшибка);
			
		Если ЕстьОшибка Тогда
			Продолжить;
		КонецЕсли;
		
		УстановленPIN = УстановленPINКодХранилищаЧерезДополнительнуюОбработку(
			ВнешнийПодключаемыйМодуль, ИдентификаторХранилища, ЕстьОшибка);
			
		Если ЕстьОшибка Тогда
			Продолжить;
		КонецЕсли;
	
		Если НЕ НуженПин ИЛИ УстановленPIN Тогда
			Возврат ИдентификаторХранилища;

		КонецЕсли;
		
	КонецЦикла
	
КонецФункции

#КонецОбласти

#Область Транспорт

// Вызывается из процедуры РасшифроватьМаркер(), по выполнению Описания Оповещения созданного
// в процедуре ОтправкаЭД(Параметры).
//
// Параметры:
//   Результат - Структура:
//               СоответствиеПрофилейИПараметровСертификатов - Соответствие:
//                                   Ключ     - СправочникСсылка.ПараметрыНастроекЭДО.
//                                   Значение - Структура:
//                                              МаркерРасшифрованный - ДвоичныеДанные - расшифрованного маркера.
//                                              прочие реквизиты сертификата (необязательно).
//
//   Параметры - Структура:
//               СоотвНастроекОбменаИСтруктурСертификатов - Соответствие:
//                                                      Ключ     - СправочникСсылка.НастройкиОбменСБанками.
//                                                      Значение - Структура - Параметры сертификата.
//               МассивКОтправке - Массив - сообщения обмена подготовленные к отправке.
//
Процедура ПродолжитьОтправкуСообщенийОбменаПослеРасшифровкиМаркера(Результат, Параметры) Экспорт
	
	СоотвНастроекОбменаИСтруктурСертификатов = Неопределено;
	Если НЕ (Параметры.Свойство("СоотвНастроекОбменаИСтруктурСертификатов", СоотвНастроекОбменаИСтруктурСертификатов)
			 И ТипЗнч(СоотвНастроекОбменаИСтруктурСертификатов) = Тип("Соответствие")) Тогда
		
		Параметры.Вставить("СоотвНастроекОбменаИСтруктурСертификатов", Новый Соответствие);
		СоотвНастроекОбменаИСтруктурСертификатов = Параметры.СоотвНастроекОбменаИСтруктурСертификатов;
	КонецЕсли;
	
	ВозврСоответствие = Неопределено;
	Если ТипЗнч(Результат) = Тип("Структура")
		И Результат.Свойство("СоответствиеНастроекОбменаИПараметровСертификатов", ВозврСоответствие)
		И ТипЗнч(ВозврСоответствие) = Тип("Соответствие") Тогда
		
		Для Каждого КлючИЗначение Из ВозврСоответствие Цикл
			СоотвНастроекОбменаИСтруктурСертификатов.Вставить(КлючИЗначение.Ключ, КлючИЗначение.Значение);
		КонецЦикла;
	КонецЕсли;
	
	ЗавершитьОтправкуСообщенийОбмена(Неопределено, Параметры);
	
КонецПроцедуры

// Вызывается из процедуры ВыполнитьДействияПослеОтправкиПЭД(...).
// Вызывает процедуру НачатьОтправкуПакетовЧерезДополнительнуюОбработку(...),
//   либо выполняет описание оповещения переданное в параметре ОбработчикПослеОтправкиПЭД.
//
// Параметры:
//   ДопПараметры - Структура:
//      РезультатОтправкиПЭД       - Структура:
//         
//      Параметры                  - Структура - необязательный параметр, дополнительные параметры,
//                                     переданные из метода инициировавшего отправку ПЭД.
//      ОбработчикПослеОтправкиПЭД - ОписаниеОповещения - необязательный параметр, обработка результата отправки ПЭД.
//
Процедура ВыполнитьДействияПослеОтправкиПЭДЗавершить(Результат, ДопПараметры)
	
	Параметры = Неопределено;
	РезультатОтправкиПЭД = Неопределено;
	ОбработчикПослеОтправкиПЭД = Неопределено;
	ДопПараметры.Свойство("Параметры", Параметры);
	ДопПараметры.Свойство("РезультатОтправкиПЭД", РезультатОтправкиПЭД);
	ДопПараметры.Свойство("ОбработчикПослеОтправкиПЭД", ОбработчикПослеОтправкиПЭД);
	
	Если ДопПараметры.Свойство("СообщенияОбмена") Тогда
		МассивВладельцев = ОбменСБанкамиСлужебныйВызовСервера.ВладельцыСообщенийОбмена(ДопПараметры.СообщенияОбмена);
		Для Каждого Владелец Из МассивВладельцев Цикл
			Оповестить("ОбновитьСостояниеОбменСБанками", , Владелец);
		КонецЦикла;
	КонецЕсли;
	
	ВыполнитьОбработчикПослеОтправки = Истина;
	Если ТипЗнч(РезультатОтправкиПЭД) = Тип("Структура") Тогда
		Параметры.ИтогКолПодготовленных = Параметры.ИтогКолПодготовленных + РезультатОтправкиПЭД.КолПодготовленных;
		Параметры.ИтогКолОтправленных = Параметры.ИтогКолОтправленных + РезультатОтправкиПЭД.КолОтправленных;
		Если РезультатОтправкиПЭД.Свойство("ДанныеДляОтправкиЧерезДопОбработку")
			И РезультатОтправкиПЭД.ДанныеДляОтправкиЧерезДопОбработку.Количество() > 0 Тогда
			
			Параметры.Вставить("ДанныеДляОтправки", РезультатОтправкиПЭД.ДанныеДляОтправкиЧерезДопОбработку);
			Параметры.Вставить("ОбработчикПослеОтправкиПЭД", ОбработчикПослеОтправкиПЭД);
			ОтправитьЧерезДополнительнуюОбработку(Неопределено, Параметры);
			ВыполнитьОбработчикПослеОтправки = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ВыполнитьОбработчикПослеОтправки
		И ТипЗнч(ОбработчикПослеОтправкиПЭД) = Тип("ОписаниеОповещения") Тогда
		ВыполнитьОбработкуОповещения(ОбработчикПослеОтправкиПЭД, Параметры);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработкаЭД

Процедура ОбработатьОтклонениеЭД(СообщениеОбмена, ОписаниеОповещения) Экспорт
	
	ПродолжитьОбработку = ОбменСБанкамиСлужебныйВызовСервера.МожноОтклонитьЭтотЭД(СообщениеОбмена);
	Заголовок = НСтр("ru = 'Укажите причины отклонения документа'");
	
	Если ПродолжитьОбработку Тогда
		ТекстУточнения = "";
		Параметры = Новый Структура("СообщениеОбмена, ОписаниеОповещения", СообщениеОбмена, ОписаниеОповещения);
		ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьОтклонениеЭДЗавершить", ЭтотОбъект, Параметры);
		ПоказатьВводСтроки(ОписаниеОповещения, ТекстУточнения, Заголовок, , Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОтклонениеЭДЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(Результат) Тогда
		ОписаниеОповещения = ДополнительныеПараметры.ОписаниеОповещения;
		НовыйСтатус = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Отклонен");
		СтруктураПараметров = Новый Структура("Статус, ПричинаОтклонения", НовыйСтатус, Результат);
		СообщениеОбмена = ДополнительныеПараметры.СообщениеОбмена;
		ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(СообщениеОбмена, СтруктураПараметров);
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, Истина);
	ИначеЕсли Результат <> Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Причина не указана, действие отменено.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьСтатусыПодписей(Оповещение, СообщениеОбмена)

	СтруктураСодержимогоСообщенияОбмена = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(
		СообщениеОбмена);
		
	Если СтруктураСодержимогоСообщенияОбмена = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Ложь);
		Возврат;
	КонецЕсли;

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеЭД", СтруктураСодержимогоСообщенияОбмена.ДанныеЭД);
	ДополнительныеПараметры.Вставить("Подписи", СтруктураСодержимогоСообщенияОбмена.Подписи);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("РезультатПроверки", Новый Массив);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписей", Оповещение);
	ОпределитьСтатусОчереднойПодписи(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОпределитьСтатусОчереднойПодписи(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.Подписи.Количество() = 0 Тогда
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(
			ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.РезультатПроверки);
		ВсеПодписиВерны = Истина;
		Для Каждого Элемент Из ДополнительныеПараметры.РезультатПроверки Цикл
			Если Не Элемент.ПодписьВерна Тогда
				ВсеПодписиВерны = Ложь;
			КонецЕсли;
		КонецЦикла;
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, ВсеПодписиВерны);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеОпределенияСтатусаОчереднойПодписи", ЭтотОбъект, ДополнительныеПараметры);
	ДвоичныеДанныеПодписи = ДополнительныеПараметры.Подписи.Получить(0).Подпись;
	ДополнительныеПараметры.Подписи.Удалить(0);
	ЭлектроннаяПодписьКлиент.ПроверитьПодпись(
		Оповещение, ДополнительныеПараметры.ДвоичныеДанныеЭД, ДвоичныеДанныеПодписи);

КонецПроцедуры
	
Процедура ПослеОпределенияСтатусаОчереднойПодписи(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Ложь);
		Возврат; //  Не удалось создать менеджер криптографии, дальнейшая проверка не имеет смысла
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура();
	
	Если Результат = Истина Тогда
		СтруктураЗаписи.Вставить("ПодписьВерна", Истина);
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДополнительныеПараметры.РезультатПроверки.Добавить(СтруктураЗаписи);
	ИначеЕсли ТипЗнч(Результат) = Тип("Строка") Тогда
		ВидОперации = НСтр("ru = 'Проверка подписи'");
		ТекстСообщения = НСтр("ru = 'При проверке подписи электронного документа произошла ошибка.
									|Вид электронного документа: %1,
									|Сообщение обмена: %2,
									|Текст ошибки: %3'");
		ПредставлениеЭД = ОбменСБанкамиСлужебныйВызовСервера.ПредставлениеСообщенияОбмена(
			ДополнительныеПараметры.СообщениеОбмена);
		ТекстСообщения = СтрШаблон(ТекстСообщения, ПредставлениеЭД, ДополнительныеПараметры.СообщениеОбмена, Результат);
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстСообщения, ТекстСообщения, 1, ДополнительныеПараметры.СообщениеОбмена);
		СтруктураЗаписи.Вставить("ПодписьВерна", Ложь);
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДополнительныеПараметры.РезультатПроверки.Добавить(СтруктураЗаписи);
	КонецЕсли;

	ОпределитьСтатусОчереднойПодписи(ДополнительныеПараметры)
	
КонецПроцедуры

// Только для внутреннего использования
Процедура УтвердитьЭД(ПараметрКоманды, СообщениеОбмена = Неопределено, ФлагОтправки = Истина) Экспорт
	
	#Если ТолстыйКлиентОбычноеПриложение Тогда
		Если НЕ (ЭлектронноеВзаимодействиеПереопределяемый.ЕстьПравоОбработкиЭД()) Тогда
			ЭлектронноеВзаимодействиеСлужебныйКлиент.СообщитьПользователюОНарушенииПравДоступа();
			Возврат;
		КонецЕсли;
	#КонецЕсли
	
	МассивСсылок = ЭлектронноеВзаимодействиеСлужебныйКлиент.МассивПараметров(ПараметрКоманды);
	Если МассивСсылок = Неопределено Тогда
		Если СообщениеОбмена = Неопределено Тогда
			Возврат;
		Иначе
			МассивСсылок = Новый Массив;
		КонецЕсли;
	КонецЕсли;
	
	Если ФлагОтправки Тогда
		ИмяКоманды = "УтвердитьОтправить";
	Иначе
		ИмяКоманды = "Утвердить";
	КонецЕсли;
	ОбработатьЭД(МассивСсылок, ИмяКоманды, СообщениеОбмена);
	
	Оповестить("ОбновитьСостояниеОбменСБанками");
	
КонецПроцедуры

// Обрабатывает переданные документы ИБ в системе обмена электронными документами в соответствие с параметрами.
//
// Параметры:
//  МассивСсылокНаОбъект - массив ссылок на объекты ИБ или на ЭД, которые необходимо обработать,
//  Действие - строка, представление действия, которое необходимо произвести с электронными документами,
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - ссылка на сообщение обмена, если нужно обработать только один ЭД.
//
Процедура ОбработатьЭД(Знач МассивСсылокНаОбъект, Действие, Знач СообщениеОбмена = Неопределено) Экспорт

	Параметры = Новый Структура;
	Параметры.Вставить("МассивСсылокНаОбъект", МассивСсылокНаОбъект);
	Параметры.Вставить("Действие", Действие);
	Параметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	
	Если (ЭлектронноеВзаимодействиеКлиентСервер.ЕстьДействие(Действие, "Подписать")
			ИЛИ ЭлектронноеВзаимодействиеКлиентСервер.ЕстьДействие(Действие, "Отправить")) Тогда
		ТребуетсяКриптография = ОбменСБанкамиСлужебныйВызовСервера.ТребуетсяКриптографияДляОбработкиДокументов(
			МассивСсылокНаОбъект);
		
		Если ТребуетсяКриптография Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковОбработатьЭД", ЭтотОбъект, Параметры);
			ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина);
			Возврат;
		КонецЕсли;
		
	КонецЕсли;
		
	ПослеПолученияОтпечатковОбработатьЭД(Неопределено, Параметры)
	
КонецПроцедуры

Процедура ПослеПолученияОтпечатковОбработатьЭД(ОтпечаткиСертификатов, Параметры) Экспорт
	
	ИтогКолНовыхЭД = 0;
	ИтогКолУтвержденныхЭД = 0;
	ИтогКолПодписанных = 0;
	ИтогКолПодготовленных = 0;
	ИтогКолОтправленных = 0;
	
	ДопПараметры = Новый Структура;
	МассивСсылокНаОбъект = Параметры.МассивСсылокНаОбъект;
	Действие = Параметры.Действие;
	СообщениеОбмена = Параметры.СообщениеОбмена;
	
	МассивОтпечатковСертификатов = Новый Массив;
	ОшибкаНастройкиКриптографии =Ложь;
	
	Если ТипЗнч(ОтпечаткиСертификатов) = Тип("Строка") Тогда
		МассивОтпечатковСертификатов = Новый Массив;
		ОшибкаНастройкиКриптографии = Истина;
		ДопПараметры.Вставить("ОшибкаНастройкиКриптографии", ОшибкаНастройкиКриптографии);
	ИначеЕсли ТипЗнч(ОтпечаткиСертификатов) = Тип("Соответствие") Тогда
		Для Каждого КлючЗначение Из ОтпечаткиСертификатов Цикл
			МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СообщениеОбмена) И Не ОшибкаНастройкиКриптографии
		И Не ОбменСБанкамиСлужебныйВызовСервера.ЕстьДоступныеСертификаты(МассивОтпечатковСертификатов, СообщениеОбмена) Тогда
			ДопПараметры.Вставить("ОшибкаНастройкиСертификата", Истина);
	КонецЕсли;
	
	СоответствиеСертификатаИПароля = Новый Соответствие;
	
	Результат = ОбменСБанкамиСлужебныйВызовСервера.ВыполнитьДействияПоЭД(МассивСсылокНаОбъект,
		МассивОтпечатковСертификатов, Действие, ДопПараметры, СообщениеОбмена, СоответствиеСертификатаИПароля);
	
	ВыполнятьКриптооперацииНаСервере = Неопределено;
	ВыполнитьОповещение = Истина;
	Если ТипЗнч(Результат) = Тип("Структура") Тогда
		Результат.Свойство("ВыполнятьКриптооперацииНаСервере", ВыполнятьКриптооперацииНаСервере);
		
		Если Результат.ОтправленныеДокументы.Количество() Тогда
			Оповестить("ОтправленоDirectBank", Результат.ОтправленныеДокументы);
		КонецЕсли;
		
		Если НЕ Результат.Свойство("КолПодписанных", ИтогКолПодписанных) Тогда
			ИтогКолПодписанных = 0;
		КонецЕсли;
		
		Если НЕ Результат.Свойство("КолПодготовленных", ИтогКолПодготовленных) Тогда
			ИтогКолПодготовленных = 0;
		КонецЕсли;
		
		Если НЕ Результат.Свойство("КолОтправленных", ИтогКолОтправленных) Тогда
			ИтогКолОтправленных = 0;
		КонецЕсли;
		
		Если НЕ Результат.Свойство("КоличествоНовыхЭД", ИтогКолНовыхЭД) Тогда
			ИтогКолНовыхЭД = 0;
		КонецЕсли;
		
		Если ИтогКолУтвержденныхЭД = 0 И Результат.Свойство("КоличествоУтвержденныхЭД") Тогда
			ИтогКолУтвержденныхЭД = Результат.КоличествоУтвержденныхЭД;
		КонецЕсли;
		
		Если ИтогКолНовыхЭД + ИтогКолУтвержденныхЭД > 0 Тогда
			Оповестить("ОбновитьСостояниеОбменСБанками");
		КонецЕсли;
		
		// Подписание ЭД:
		
		СоотвСертификатовИМассивовСообщенийОбменаКПодписи = Новый Соответствие;
		
		МассивСообщенийОбменаКУдалениюИзОтправки = Новый Массив;
		СертификатыСообщенийОбмена = Неопределено;
		СоотвСообщенийОбменаИНастроекОбмена = Неопределено;
		СоотвСертификатовИИхСтруктур = Неопределено;
		
		ПараметрыКонтекста = Новый Структура();
		ПараметрыКонтекста.Вставить("Результат", Результат);
		ПараметрыКонтекста.Вставить("ВыполнятьКриптооперацииНаСервере", ВыполнятьКриптооперацииНаСервере);
		ПараметрыКонтекста.Вставить("ИтогКолОтправленных", ИтогКолОтправленных);
		ПараметрыКонтекста.Вставить("ИтогКолПодготовленных", ИтогКолПодготовленных);
		ПараметрыКонтекста.Вставить("ИтогКолНовыхЭД", ИтогКолНовыхЭД);
		ПараметрыКонтекста.Вставить("ИтогКолУтвержденныхЭД", ИтогКолУтвержденныхЭД);
		ПараметрыКонтекста.Вставить("ИтогКолПодписанных", ИтогКолПодписанных);
		ПараметрыКонтекста.Вставить("Действие", Действие);
		ПараметрыКонтекста.Вставить("МассивСсылокНаОбъект", МассивСсылокНаОбъект);
		ПараметрыКонтекста.Вставить("МассивСообщенийОбменаКУдалениюИзОтправки", МассивСообщенийОбменаКУдалениюИзОтправки);
		ПараметрыКонтекста.Вставить("СоотвСертификатовИМассивовСообщенийОбменаКПодписи", СоотвСертификатовИМассивовСообщенийОбменаКПодписи);
		ПараметрыКонтекста.Вставить("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур);
		
		Если Результат.Свойство("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур)
			И Результат.Свойство("СертификатыСообщенийОбмена", СертификатыСообщенийОбмена)
			И Результат.Свойство("СоотвСообщенийОбменаИНастроекОбмена", СоотвСообщенийОбменаИНастроекОбмена) Тогда
			
			СоответствиеНастроекОбменаИСертификатов = Неопределено;
			Если ДопПараметры.Свойство("СоответствиеСертификатов", СоответствиеНастроекОбменаИСертификатов)
				И ЗначениеЗаполнено(СоответствиеНастроекОбменаИСертификатов) Тогда
				СоответствиеСертификатов = ПараметрыСертификатов(СоответствиеНастроекОбменаИСертификатов);
				Для Каждого КлючЗначение Из СоотвСертификатовИИхСтруктур Цикл
					ПараметрыСертификата = СоответствиеСертификатов.Получить(КлючЗначение.Ключ);
					ЗаполнитьПароли(КлючЗначение.Значение, ПараметрыСертификата);
				КонецЦикла;
			КонецЕсли;
			
			ПараметрыКонтекста.Вставить(
				"СертификатыСообщенийОбмена", СертификатыСообщенийОбмена);
			ПараметрыКонтекста.Вставить("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур);
			ПараметрыКонтекста.Вставить("ИндексПервойИтерации", 0);
			ПараметрыКонтекста.Вставить("ИндексВторойИтерации", 0);
			ПараметрыКонтекста.Вставить("ИндексТретьейИтерации", 0);
			ВыполнитьОповещение = Ложь;
			ПодписатьЭД(Неопределено, ПараметрыКонтекста);
		Иначе
			Если СоотвСертификатовИИхСтруктур = Неопределено Тогда
				СоотвСертификатовИИхСтруктур = Новый Соответствие;
			КонецЕсли;
			ПараметрыКонтекста.Вставить("СоотвСертификатовИИхСтруктур", СоотвСертификатовИИхСтруктур);
			// Отправка ЭД:
			ВыполнитьОповещение = Ложь;
			ОтправкаСообщенийОбмена(ПараметрыКонтекста);
		КонецЕсли;
	ИначеЕсли Результат <> Неопределено Тогда
		Оповестить("ОбновитьСостояниеОбменСБанками");
	КонецЕсли;
	
КонецПроцедуры

Функция ПараметрыСертификатов(СоответствиеНастроекОбмена)
	
	Результат = Новый Соответствие;
	Для Каждого КлючЗначение Из СоответствиеНастроекОбмена Цикл
		Результат.Вставить(КлючЗначение.Значение.СертификатПодписи, КлючЗначение.Значение);
	КонецЦикла;
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьПароли(ПараметрыСертификата, Результат)
	
	ПараметрыСертификата.ПарольПользователя = Результат.ПарольПользователя;
	Если ПараметрыСертификата.Свойство("Пользователь") И Результат.Свойство("Пользователь") Тогда
		ПараметрыСертификата.Пользователь = Результат.Пользователь;
	КонецЕсли;
	ПараметрыСертификата.ПарольПолучен = Истина;

КонецПроцедуры

Процедура ПодписатьЭД(РезультатВыполнения, Параметры) Экспорт
	
	СертификатыСообщенийОбмена = Неопределено;
	ВходящаяСтруктура = Параметры.Результат;
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		
		Параметры.ИтогКолПодписанных = Параметры.ИтогКолПодписанных
			+ ЭлектронноеВзаимодействиеСлужебныйКлиент.КоличествоПодписанныхЭД(РезультатВыполнения);
			
		Если РезультатВыполнения.Свойство("ИтогКолПодписанных") Тогда // подписание через внешний модуль
			Параметры.ИтогКолПодписанных = Параметры.ИтогКолПодписанных + РезультатВыполнения.ИтогКолПодписанных;
		КонецЕсли;
		
		// Оповещение произошло из процедуры БСП
		Если РезультатВыполнения.Свойство("НаборДанных") Тогда
			// Если Успех, необходимо перебрать элементы массива Набор данных
			// в подписанных эд в элементе массива являющимся структурой будет свойство "Свойства подписи"
			// такие СообщенияОбмена надо добавить в массив "МассивСообщенийОбмена" для обновления их статусов.
			МассивСообщенийОбмена = Неопределено;
			Если НЕ (Параметры.Свойство("МассивСообщенийОбменаКОбновлениюСтатуса", МассивСообщенийОбмена)
						ИЛИ ТипЗнч(МассивСообщенийОбмена) = Тип("Массив")) Тогда
						
				Параметры.Вставить("МассивСообщенийОбменаКОбновлениюСтатуса", Новый Массив);
				МассивСообщенийОбмена = Параметры.МассивСообщенийОбменаКОбновлениюСтатуса;
				
			КонецЕсли;

			Для Каждого ПодписываемыеДанные Из РезультатВыполнения.НаборДанных Цикл
				СообщениеОбмена = ОбменСБанкамиСлужебныйВызовСервера.ВладелецФайла(ПодписываемыеДанные.Объект);
				Если Не ПодписываемыеДанные.Свойство("СвойстваПодписи") Тогда
					Параметры.МассивСообщенийОбменаКУдалениюИзОтправки.Добавить(СообщениеОбмена);
					Продолжить;
				КонецЕсли;
				МассивСообщенийОбмена.Добавить(СообщениеОбмена);
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВходящаяСтруктура.Свойство("СертификатыСообщенийОбмена", СертификатыСообщенийОбмена)
		И СертификатыСообщенийОбмена.Количество() > 0 Тогда
		
		СоответствиеСообщенийОбменаИПодписей = Неопределено;
		Если НЕ (Параметры.Свойство("СоответствиеСообщенийОбменаИПодписей", СоответствиеСообщенийОбменаИПодписей)
			ИЛИ ТипЗнч(СоответствиеСообщенийОбменаИПодписей) = Тип("Соответствие")) Тогда
			
			Параметры.Вставить("СоответствиеСообщенийОбменаИПодписей", Новый Соответствие);
		КонецЕсли;
		
		Для Каждого Элемент Из СертификатыСообщенийОбмена Цикл
			
			Структура = Элемент.Значение;
			СертификатыСообщенийОбмена.Удалить(Элемент.Ключ);
			
			МассивСертификатов = Структура.МассивСертификатов;
			ОписаниеПодписатьЭД = Новый ОписаниеОповещения("ПодписатьЭД", ЭтотОбъект, Параметры);
			ДанныеДляСпецОбработки = Неопределено;
			СоответствиеСообщенийОбменаИДД = Неопределено;
			Если Структура.Свойство("СоответствиеСообщенийОбменаИДД", СоответствиеСообщенийОбменаИДД)
				И ТипЗнч(СоответствиеСообщенийОбменаИДД) = Тип("Соответствие") Тогда
				
				Если СоответствиеСообщенийОбменаИДД.Количество() = 1 Тогда
					Операция = НСтр("ru = 'Подписание электронного документа'");
				Иначе
					Операция = НСтр("ru = 'Подписание электронных документов'");
				КонецЕсли;
				
				ОписаниеДанных = Новый Структура;
				ОписаниеДанных.Вставить("Операция",            Операция);
				ОписаниеДанных.Вставить("ОтборСертификатов",   МассивСертификатов);
				ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
				ОписаниеДанных.Вставить("НаборДанных",         Новый Массив);
				ОписаниеДанных.Вставить("ЗаголовокДанных",     НСтр("ru = 'Документ'"));
				ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
				
				НаборДанных = ОписаниеДанных.НаборДанных;
				МассивСообщенийОбменаДляПредставления = Новый Массив;
				Для Каждого ЭлементДанных Из СоответствиеСообщенийОбменаИДД Цикл
					СообщениеОбмена = ЭлементДанных.Ключ;
					Данные = Новый Структура;
					Если ЭлементДанных.Значение = Неопределено ИЛИ НЕ ЭтоАдресВременногоХранилища(ЭлементДанных.Значение) Тогда
						ПараметрыДляПолученияДД = Новый Структура("СообщениеОбмена, ОписаниеДанных", СообщениеОбмена, ОписаниеДанных);
						СсылкаНаДД = Новый ОписаниеОповещения(
							"ПолучитьДвоичныеДанныеДляСообщенияОбмена", ЭтотОбъект, ПараметрыДляПолученияДД);
					Иначе
						СсылкаНаДД = ЭлементДанных.Значение;
					КонецЕсли;
					Данные.Вставить("Данные", СсылкаНаДД);
					Данные.Вставить("Объект", ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеОбмена));
					
					НаборДанных.Добавить(Данные);
					МассивСообщенийОбменаДляПредставления.Добавить(СообщениеОбмена);
				КонецЦикла;
				
				Если МассивСообщенийОбменаДляПредставления.Количество() = 1 Тогда
					ПредставлениеСообщенияОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПредставлениеСообщенияОбмена(
						МассивСообщенийОбменаДляПредставления[0]);
					ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
						"ПриОткрытииЭлектронногоДокумента", ЭтотОбъект, МассивСообщенийОбменаДляПредставления[0]);
					СтруктураПредставления = Новый Структура(
						"Представление, Значение", ПредставлениеСообщенияОбмена, ОбработчикОткрытияЭД);
					НаборДанных[0].Вставить("Представление", СтруктураПредставления);
				Иначе
					ПредставлениеСообщенияОбмена = НСтр("ru = 'Электронные документы (%1)'");
					СписокПредставленийСообщенийОбмена = ОбменСБанкамиСлужебныйВызовСервера.СписокПредставленийСообщенийОбмена(
						МассивСообщенийОбменаДляПредставления);
					МассивПредставлений = Новый Массив;
					Для Каждого Элемент Из СписокПредставленийСообщенийОбмена Цикл
						ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
							"ПриОткрытииЭлектронногоДокумента", ЭтотОбъект, Элемент.Значение);
						СтруктураПредставления = Новый Структура(
							"Представление, Значение", Элемент.Представление, ОбработчикОткрытияЭД);
						МассивПредставлений.Добавить(СтруктураПредставления);
					КонецЦикла;
					ОписаниеДанных.Вставить("СписокПредставлений", МассивПредставлений);
					ОписаниеДанных.Вставить("ПредставлениеНабора", ПредставлениеСообщенияОбмена);
				КонецЕсли;
				ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеПодписатьЭД);
			ИначеЕсли Структура.Свойство("ДанныеДляСпецОбработки", ДанныеДляСпецОбработки)
				И ТипЗнч(ДанныеДляСпецОбработки) = Тип("Соответствие") И ДанныеДляСпецОбработки.Количество() Тогда
				Параметры.Вставить("ДанныеДляСпецОбработки", ДанныеДляСпецОбработки);
				Параметры.Вставить("МассивСертификатов", МассивСертификатов);
				Параметры.Вставить("ОбработчикПродолжения", ОписаниеПодписатьЭД);
				НачатьПодписаниеБанковскихЭД(Параметры);
			Иначе
				ПодписатьЭД(Неопределено, Параметры);
			КонецЕсли;
			Прервать;
		КонецЦикла;
	Иначе
		
		ДействияПослеПодписанияЭД(Параметры);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ДействияПослеПодписанияЭД(Параметры)
	
	МассивСообщенийОбменаКОбновлениюСтатуса = Неопределено;
	Если ТипЗнч(Параметры) = Тип("Структура")
		И Параметры.Свойство("МассивСообщенийОбменаКОбновлениюСтатуса", МассивСообщенийОбменаКОбновлениюСтатуса)
		И ТипЗнч(МассивСообщенийОбменаКОбновлениюСтатуса) = Тип("Массив") Тогда
		
		ОбменСБанкамиСлужебныйВызовСервера.ДействияПослеПодписанияЭДНаСервере(МассивСообщенийОбменаКОбновлениюСтатуса);
		
		МассивСообщенийОбменаДляПроверкиПодписей = ОбщегоНазначенияКлиентСервер.СкопироватьМассив(
			МассивСообщенийОбменаКОбновлениюСтатуса);
			
		Параметры.Вставить("МассивСообщенийОбменаДляПроверкиПодписей", МассивСообщенийОбменаДляПроверкиПодписей);
		
		ПроверитьСтатусыПодписейСообщенийОбменаРекурсивно(Неопределено, Параметры);
		
		Возврат;
		
	КонецЕсли;
	
	ОтправкаСообщенийОбмена(Параметры);
	
КонецПроцедуры

Процедура ПроверитьСтатусыПодписейСообщенийОбменаРекурсивно(Результат, ДополнительныеПараметры) Экспорт
	
	Если ДополнительныеПараметры.МассивСообщенийОбменаДляПроверкиПодписей.Количество() = 0 Тогда
		ОтправкаСообщенийОбмена(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	СообщениеОбмена = ДополнительныеПараметры.МассивСообщенийОбменаДляПроверкиПодписей.Получить(0);
	ДополнительныеПараметры.МассивСообщенийОбменаДляПроверкиПодписей.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьСтатусыПодписейСообщенийОбменаРекурсивно", ЭтотОбъект, ДополнительныеПараметры);
	
	ОпределитьСтатусыПодписей(Оповещение, СообщениеОбмена);
	
КонецПроцедуры

Процедура ПроверитьСрокДействияСертификатаБанка(Сертификат, ДействителенДо, ПользовательОповещенОСрокеДействия)
	
	Если ПользовательОповещенОСрокеДействия Тогда
		Возврат;
	КонецЕсли;
	
	ОповеститьОбОкончанииСрокаДействия = ДобавитьМесяц(ОбщегоНазначенияКлиент.ДатаСеанса(), 1) > ДействителенДо;
	
	Если ОповеститьОбОкончанииСрокаДействия Тогда
		ПараметрыФормы = Новый Структура("Сертификат", Сертификат);
		ОткрытьФорму("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.Форма.ОповещениеОбОкончанииСрокаДействия",
			ПараметрыФормы);
	КонецЕсли;
	
КонецПроцедуры

Процедура ТестНастройкиОбменаЧерезДополнительнуюОбработку(НастройкаОбмена, ИдентификаторНазначения)
	
	Перем АдресФайла;
		
	РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	РеквизитыНастройкиОбмена = ОбменСБанкамиСлужебныйВызовСервера.РеквизитыНастройкиОбмена(НастройкаОбмена);
	// Блок проверки наличия внешней обработки для обмена с банком
	ОписаниеТеста = НСтр("ru = 'Тест. Проверка наличия внешней обработки для обмена с банком.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	ВнешняяОбработкаПодключена = ОбменСБанкамиСлужебныйВызовСервера.ПодключитьАктуальнуюВнешнююОбработку(
		РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, АдресФайла);
	Если Не ЗначениеЗаполнено(АдресФайла) Тогда
		ТекстСообщения = НСтр("ru = 'В информационной базе отсутствует внешний модуль обмена с банком.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	Если Не ВнешняяОбработкаПодключена Тогда
		Возврат;
	КонецЕсли;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	
	// Блок инициализации интерфейса.
	ОписаниеТеста = НСтр("ru = 'Тест. Инициализация служебного интерфейса.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(НастройкаОбмена);
	
	Параметры = Новый Структура;
	Параметры.Вставить("ИндексТестаСертификата", 0);
	Параметры.Вставить("ДоступныеСертификаты", ДоступныеСертификаты);
	Параметры.Вставить("ИдентификаторНазначения", ИдентификаторНазначения);
	Параметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	АдресВК = Неопределено;
	ВнешнийПодключаемыйМодуль = ВнешнийПодключаемыйМодульЧерезДополнительнуюОбработку(
		РеквизитыНастройкиОбмена.ИмяВнешнегоМодуля, АдресВК);
	
	Если ВнешнийПодключаемыйМодуль = Неопределено Тогда
		Если ЗначениеЗаполнено(АдресВК) Тогда
			Параметры.Вставить("ТекущаяНастройкаОбменаЧерезДополнительнуюОбработку", НастройкаОбмена);
			Параметры.Вставить("УстановленаВнешняяКомпонента");
			ОО = Новый ОписаниеОповещения("НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку", ЭтотОбъект, Параметры);
			НачатьУстановкуВнешнейКомпоненты(ОО, АдресВК);
		КонецЕсли;
		Возврат;
	КонецЕсли;
	
	НачатьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры);
	
КонецПроцедуры

Функция АктуаленКэшПароляСертификатаЧерезДополнительнуюОбработку(ДанныеСертификата, ВнешнийПодключаемыйМодуль)
	
	РеквизитыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(
		ДанныеСертификата.СертификатПодписи);
	
	СлужебныеДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ВнешнийПодключаемыйМодуль,
		РеквизитыСертификата.ДвоичныеДанныеСертификата);
		
	Если СлужебныеДанныеСертификата = Неопределено Тогда
		Возврат Ложь;
	КонецЕсли;

	ЕстьОшибка = Ложь;
	ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку = ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, СлужебныеДанныеСертификата.ИдентификаторХранилища, ЕстьОшибка);
	Если  ЕстьОшибка ИЛИ ТребуетсяУстановкаPINКодаХранилищаЧерезДополнительнуюОбработку Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Попытка
		УстановленПарольСертификата = ВнешнийПодключаемыйМодуль.УстановленПарольСертификата(
			РеквизитыСертификата.ДвоичныеДанныеСертификата);
		Возврат УстановленПарольСертификата = Истина;
	Исключение
		Возврат Ложь;
	КонецПопытки;

КонецФункции

Процедура ПродолжитьПолучениеВыпискиЧерезДополнительнуюОбработку(Параметры)
	
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ДанныеСертификата = Параметры.ДанныеСертификата;
	ПарольПользователя = Параметры.ПарольПользователя;
	
	ПарольУстановлен = УстановитьПарольСертификатаЧерезДополнительнуюОбработку(
		ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, ПарольПользователя);
	Если НЕ ПарольУстановлен Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьПолучениеВыписки", ЭтотОбъект, Параметры);
	УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ВнешнийПодключаемыйМодуль, ДанныеСертификата.ДвоичныеДанныеСертификата, НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработку(Параметры)
	
	НастройкаОбмена = Параметры.НастройкаОбмена;
	
	ИдентификаторНазначения = Параметры.ИдентификаторНазначения;
	ВнешнийПодключаемыйМодуль = Параметры.ВнешнийПодключаемыйМодуль;
	СертификатXML = Параметры.СертификатXML;
	ПарольПользователя = Параметры.ПарольПользователя;
		
	РезультатТеста = НСтр("ru = 'Пройден успешно.'");
	
	Если Параметры.Свойство("УстановленPINКод") Тогда
		СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	КонецЕсли;
	
	// Блок проверка авторизации на ресурсе банка.
	ОписаниеТеста = НСтр("ru = 'Тест. Аутентификация на ресурсе банка.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
	ВидОперации = НСтр("ru = 'Аутентификация на ресурсе банка'");
	Попытка
		ВнешнийПодключаемыйМодуль.УстановитьПарольСертификата(СертификатXML, ПарольПользователя);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка авторизации на ресурсе банка.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Аутентификация на ресурсе банка'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1, НастройкаОбмена);
		Возврат;
	КонецПопытки;
	СообщитьПользователю(РезультатТеста, ИдентификаторНазначения);
	
	// Блок проверка установки соединения с банком.
	ОписаниеТеста = НСтр("ru = 'Тест. Установка соединения с банком.'");
	СообщитьПользователю(ОписаниеТеста, ИдентификаторНазначения);
		
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьТестНастройкиОбменаЧерезДополнительнуюОбработкуПослеАутентификации", ЭтотОбъект, Параметры);
	УстановитьСоединениеЧерезДополнительнуюОбработку(
		Оповещение, ВнешнийПодключаемыйМодуль, СертификатXML, НастройкаОбмена);
		
КонецПроцедуры

Процедура ПодписатьЗапросыВыписок(Результат, ПараметрыОбработки)
	
	СоотвСертификатовИИхСтруктур = ПараметрыОбработки.СоотвСертификатовИИхСтруктур;
	МассивСообщенийОбмена = ПараметрыОбработки.МассивСообщенийОбмена;
	
	МассивСертификатов = Новый Массив;
	Для Каждого КлючИЗначение Из СоотвСертификатовИИхСтруктур Цикл
		МассивСертификатов.Добавить(КлючИЗначение.Ключ);
	КонецЦикла;
	
	Если МассивСообщенийОбмена.Количество() = 1 Тогда
		Операция = НСтр("ru = 'Подписание электронного документа'");
	Иначе
		Операция = НСтр("ru = 'Подписание электронных документов'");
	КонецЕсли;
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция",            Операция);
	ОписаниеДанных.Вставить("ОтборСертификатов",   МассивСертификатов);
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
	
	НаборДанных = Новый Массив;
	Для Каждого СообщениеОбмена Из МассивСообщенийОбмена Цикл
		Данные = Новый Структура;
		ПараметрыДляПолученияДД = Новый Структура("СообщениеОбмена, ОписаниеДанных", СообщениеОбмена, ОписаниеДанных);
		СсылкаНаДД = Новый ОписаниеОповещения("ПолучитьДвоичныеДанныеДляСообщенияОбмена", ЭтотОбъект, ПараметрыДляПолученияДД);
		Данные.Вставить("Данные", СсылкаНаДД);
		
		Данные.Вставить("Объект", ОбменСБанкамиСлужебныйВызовСервера.ПрисоединенныйФайл(СообщениеОбмена));
		
		НаборДанных.Добавить(Данные);
	КонецЦикла;
	
	ПредставлениеНабора = НСтр("ru = 'Запросы выписок (%1)'");
	СписокПредставлений = ОбменСБанкамиСлужебныйВызовСервера.СписокПредставленийСообщенийОбмена(МассивСообщенийОбмена);
	
	МассивПредставлений = Новый Массив;
	Для Каждого Элемент Из СписокПредставлений Цикл
		ОбработчикОткрытияЭД = Новый ОписаниеОповещения(
			"ПриОткрытииЭлектронногоДокумента", ЭтотОбъект, Элемент.Значение);
		СтруктураПредставления = Новый Структура(
			"Представление, Значение", Элемент.Представление, ОбработчикОткрытияЭД);
		МассивПредставлений.Добавить(СтруктураПредставления);
	КонецЦикла;
	ОписаниеДанных.Вставить("СписокПредставлений", МассивПредставлений);
	ОписаниеДанных.Вставить("ПредставлениеНабора", ПредставлениеНабора);
	
	ОписаниеДанных.Вставить("НаборДанных", НаборДанных);
	ПараметрыОбработки.Вставить("ОписаниеДанных", ОписаниеДанных);
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработатьРезультатПодписиЗапросовВыписок", ЭтотОбъект, ПараметрыОбработки);
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеОповещения);
	
КонецПроцедуры

#КонецОбласти

#Область ЗарплатныйПроект

// Вызывает оповещения об изменении объектов, которые были заполнены в переопределяемой части.
//
// Параметры:
//    Структура - данные возврата переопределяемой части:
//      * МассивОповещений - Массив - оповещения, которые необходимо вызвать.
//
Процедура ВызватьОповещения(СтруктураДанных) Экспорт
	
	Если СтруктураДанных.Свойство("МассивОповещений") Тогда
		Для Каждого Элемент Из СтруктураДанных.МассивОповещений Цикл
			Для Каждого КлючЗначение Из Элемент Цикл
				Если КлючЗначение.Значение = Неопределено Тогда
					Оповестить(КлючЗначение.Ключ);
				Иначе
					ОповеститьОбИзменении(КлючЗначение.Значение);
				КонецЕсли;
			КонецЦикла;
		КонецЦикла
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПолученияВерсииВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если НЕ РезультатВызова = ДополнительныеПараметры.ОжидаемаяВерсия Тогда
		#Если ВебКлиент Тогда
			ТекстСообщения = НСтр("ru = 'На компьютере уже установлен внешний модуль другой версии.
										|Удалите расширение для браузера, перезагрузите компьютер и повторите операцию.'");
		#Иначе
			ТекстСообщения = НСтр("ru = 'Отличаются версии используемого и загруженного внешнего модуля.
										|Перезапустите программу.'");
		#КонецЕсли
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНомераВерсии, ТекстСообщения);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНомераВерсии, Истина);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияВерсииВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение версии внешней компоненты.'");
	ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении версии внешнего модуля.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияНомераВерсии,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеОправкиПакетовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.Результат);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПродолжения , Результат);
		Возврат;
	ИначеЕсли Результат.Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОбработчикПродолжения , Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("РезультатСинхронизацииЧерезВК", Результат);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьСинхронизациюПослеПолученияНовыхДокументовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьНовыеДокументыВК(Оповещение, Результат.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ПродолжитьСинхронизациюПослеПолученияНовыхДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.Результат);
	КонецЕсли;
	ДополнительныеПараметры.РезультатСинхронизацииЧерезВК.Вставить("ИтогКолПолученных", Результат.ИтогКолПолученных);
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОбработчикПродолжения, ДополнительныеПараметры.РезультатСинхронизацииЧерезВК);
	
КонецПроцедуры

Процедура ОтправитьЗапросыВыписокВБанкПослеПодписанияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ИтогКолПодписанных = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение =Новый ОписаниеОповещения("ЗапуститьПроцессПолученияВыпискиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ДанныеОтправки = Новый Соответствие;
	ДанныеОтправки.Вставить(
		ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.МассивСообщенийОбменаКОтправке);
	ОтправитьДокументыЧерезВК(Оповещение, ДанныеОтправки, Ложь);
	
КонецПроцедуры

// Асинхронный обработчик после ввода пароля сертификата пользователем.
// Вызывается из модуля ОбменСБанкамиКлиент.
//
// Параметры:
//  Результат - Структура - сертификат и пароль для подписи электронного документа
//  ДополнительныеПараметры - Структура - контекст выполнения.
//
Процедура ПодписатьЗапросыВыписокПослеВводаПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьЗапросыВыписокВБанкПослеПодписанияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписания", Оповещение);
	ДополнительныеПараметры.Вставить("СертификатСсылка", Результат.ВыбранныйСертификат);
	ДополнительныеПараметры.Вставить("Пароль", Результат.ПарольПользователя);
	ДополнительныеПараметры.Вставить("ИтогКолПодписанных", 0);
	МассивСообщенийОбмена = Новый Массив;
	ОбщегоНазначенияКлиентСервер.ДополнитьМассив(
		МассивСообщенийОбмена, ДополнительныеПараметры.МассивСообщенийОбменаКОтправке);
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
		
	ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ЗапуститьПроцессПолученияВыпискиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат.ИтогКолОтправленных = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("МассивСообщенийОбмена", ДополнительныеПараметры.МассивСообщенийОбменаКОтправке);
	ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
	
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросВБанк", ПараметрыФормы, ДополнительныеПараметры.Владелец);
	
КонецПроцедуры

Процедура ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.МассивСообщенийОбмена.Количество() = 0 Тогда
		СтруктураВозврата = Новый Структура("ИтогКолПодписанных", ДополнительныеПараметры.ИтогКолПодписанных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	СообщениеОбмена = ДополнительныеПараметры.МассивСообщенийОбмена.Получить(0);
	ДополнительныеПараметры.МассивСообщенийОбмена.Удалить(0);
	
	Оповещение = Новый ОписаниеОповещения("ПродолжитьРекурсивноеПодписаниеЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодписатьЭДПоСертификатуЧерезВК(Оповещение, СообщениеОбмена, ДополнительныеПараметры.НастройкаОбмена,
		ДополнительныеПараметры.СертификатСсылка, ДополнительныеПараметры.Пароль);
	
КонецПроцедуры

Процедура ПродолжитьРекурсивноеПодписаниеЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		СтруктураВозврата = Новый Структура("ИтогКолПодписанных", ДополнительныеПараметры.ИтогКолПодписанных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если Результат = Неопределено Или Результат = Ложь Тогда
		СтруктураВозврата = Новый Структура("ИтогКолПодписанных", ДополнительныеПараметры.ИтогКолПодписанных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписания, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ИтогКолПодписанных = ДополнительныеПараметры.ИтогКолПодписанных + 1;
	
	ПодписатьРекурсивноЭДПоСертификатуЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатОтправкиПакетовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	ЗаполнитьЗначенияСвойств(ДополнительныеПараметры, Результат);
	
	ЗавершитьОтправкуСообщенийОбмена(Неопределено, ДополнительныеПараметры);
	
КонецПроцедуры

// Предлагает пользователю выбрать сертификат и ввести пароль (асинхронно).
//
// Параметры:
//    Оповещение - ОписаниеОповещения - оповещение, вызываемое после выполнения процедуры:
//       * Результат - Структура - данные выбора пользователя:
//                       ** СертификатBase64 - Строка - данные сертификата;
//                       ** Пароль - Строка - введенный пароль к сертификату.
//                     Строка - текст ошибки для вывода в виде сообщения;
//                     Неопределено - пользователь отказался продолжать операцию.
//       * ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//    ПодключаемыйМодуль - AddIn - внешняя компонента банка.
//
Процедура ПолучитьДанныеАутентификацииНаТокенеВК(Оповещение, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПолученияКлючей = Новый ОписаниеОповещения(
		"ПоказатьВыборПослеПолученияКлючейВК", ЭтотОбъект, ДополнительныеПараметры);

	ПолучитьСертификатыИзХранилищаВК(ОповещениеПослеПолученияКлючей, ПодключаемыйМодуль);
		
КонецПроцедуры

// Получает свойства сертификатов (асинхронно).
//
// Параметры:
//  Оповещение - ОписаниеОповещения - Содержит описание процедуры, которая будет вызвана после завершения вызова метода со следующими параметрами: 
//                   * Результат - Массив - результат вызова метода. В элементах структуры со свойствами:
//                                   ** СертификатBase64 - Строка - содержимое сертификата;
//                                   ** ДанныеСертификата - Структура - свойства сертификата:
//                                       *** Псевдоним - Строка - псевдоним ключа ЭП;
//                                       *** Отпечаток - Строка - уникальный идентификатор ключа ЭП;
//                                       *** СерийныйНомер - Строка - серийный номер ключа в hex;
//                                       *** ИмяИздателя - Строка - имя издателя сертификата.
//                               - Строка - текст ошибки.
//                   * ДополнительныеПараметры - значение, которое было указано при создании объекта ОписаниеОповещения.
//  ДанныеСертификатовBase64 - Массив - данные сертификатов в формате Base64, для которых необходимо получить свойства;
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка.
//
Процедура ПолучитьСвойстваСертификатовЧерезВК(Оповещение, ДанныеСертификатовBase64, ПодключаемыйМодуль)
	
	ДанныеСертификатов = Новый Массив;
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	ДополнительныеПараметры.Вставить("СертификатыКлюча", ДанныеСертификатовBase64);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияКлючей", Оповещение);
	ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры);
	
КонецПроцедуры

// Устанавливает соединение с банком через внешнюю компоненту (асинхронно). Аутентификация на токене должна уже быть выполнена.
//
// Параметры:
//  Оповещение - ОписаниеОповещения - оповещение, вызываемое при выполнении процедуры:
//      Результат - Булево - возвращает Истина, если соединение успешно установлено.
//                - Строка - содержит текст ошибки, если соединение не было установлено.
//                - Неопределено - пользователь не ввел одноразовый пароль.
//  ПодключаемыйМодуль - AddIn - внешняя компонента банка;
//  СертификатBase64 - Строка - данные сертификата аутентификации в формате Base64
//  ПараметрыСоединения - Структура - параметры соединения. Содержит следующие элементы:
//     * ИдентификаторОрганизации - Строка - идентификатор организации из настройки обмена;
//     * БИК - Строка - БИК банка;
//     * КлючУникальности - идентифицирующая пользователя строка, например “<инн>_<бик>”. Используется для сбора статистики подключений.
//
Процедура УстановитьСоединениеВК(Оповещение, ПодключаемыйМодуль, СертификатBase64, ПараметрыСоединения)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиСоединения", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ПараметрыСтрокой = СериализованныеДанные(ПараметрыСоединения);
	
	НастройкиПроксиСервера = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().НастройкиПроксиСервера;
	НастройкиПроксиСтрокой = СериализованныеДанные(НастройкиПроксиСервера);

	ОповещениеПослеУстановкиСоединения = Новый ОписаниеОповещения("ПослеУстановкиСоединенияВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуУстановкиСоединенияВК", ЭтотОбъект);
		
	ПараметрыРасширеннойАутентификации = Неопределено;
	ПодключаемыйМодуль.НачатьВызовУстановитьСоединение(ОповещениеПослеУстановкиСоединения, СертификатBase64,
		НастройкиПроксиСтрокой, ПараметрыСтрокой, ПараметрыРасширеннойАутентификации);
	
КонецПроцедуры

Процедура ПослеПолученияПодключаемогоМодуляВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК, Результат);
		Возврат;
	КонецЕсли;
	
	ПодключаемыйМодуль = Результат;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(
		ДополнительныеПараметры.НастройкаОбмена);
		
	ДополнительныеПараметры.Вставить("ДоступныеСертификаты", ДоступныеСертификаты);
	
	Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
		Пароль = Неопределено;
		Если ПарольКСертификатуПолучен(КлючЗначение.Ключ, Пароль) Тогда
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если Пароль = Неопределено Тогда
		
		Оповещение = Новый ОписаниеОповещения(
			"ПолучитьПарольАутентификацииПослеОпределенияСертификата", ЭтотОбъект, ДополнительныеПараметры);
			
		МассивСертификатов = Новый Массив;
		Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
			МассивСертификатов.Добавить(КлючЗначение.Значение.ДанныеСертификата);
		КонецЦикла;
			
		ОпределитьСертификатСУстановленнымПаролемЧерезВК(Оповещение, ПодключаемыйМодуль, МассивСертификатов);
		
	Иначе
		УстановитьСоединениеПоСертификатуЧерезВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК,
			ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена, КлючЗначение.Ключ, Пароль);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьПарольАутентификацииПослеОпределенияСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Успех Тогда
		ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(
			ДополнительныеПараметры.НастройкаОбмена);
		УстановитьСоединениеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК,
			ДополнительныеПараметры.ПодключаемыйМодуль, Результат.СертификатBase64, ПараметрыСоединения);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"УстановитьПарольПослеВводаПользователемЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ВидОперации = НСтр("ru = 'Аутентификация на сервере банка'") + " "; // Для запроса пароля сертификата
		Параметры = Новый Структура("ВызватьОповещение", Оповещение);
		ПолучитьПарольКСертификату(Оповещение, ДополнительныеПараметры.ДоступныеСертификаты, ВидОперации);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОпределитьСертификатСУстановленнымПаролемЧерезВК(Оповещение, ПодключаемыйМодуль, МассивСертификатов)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПоискаСертификатаБезПароля", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("МассивСертификатов", МассивСертификатов);
	НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.МассивСертификатов.Количество() = 0 Тогда // нет сертификата с паролем
		Результат = Новый Структура("Успех", Ложь);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПоискаСертификатаБезПароля, Результат);
		Возврат;
	КонецЕсли;
	
	ДанныеСертификата = ДополнительныеПараметры.МассивСертификатов.Получить(0);
	ДополнительныеПараметры.МассивСертификатов.Удалить(0);
	
	ДополнительныеПараметры.Вставить("СертификатBase64", ДанныеСертификата);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьУстановкиПинПослеПолученияСвойствСертификатаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДанныеСертификата);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПинПослеПолученияСвойствСертификатаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПоискаСертификатаБезПароля, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьУстановкуПароляСертификатаПослеПроверкиПинЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПроверитьНеобходимостьУстановкиПинЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПроверитьУстановкуПароляСертификатаПослеПроверкиПинЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // ошибку не обрабатываем, т.к. это только пробный поиск сертификата с паролем.
		НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Если Результат Тогда //требуется установка пин-кода, значит пароль сертификата не сохранен.
		НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры)
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ПроверитьУстановкуПароляСледующегоСертификатаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ВызватьПроверкуНеобходимостиУстановкиПароляСертификатаЧерезВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64)
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьУстановкуПароляСледующегоСертификатаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПоискаСертификатаБезПароля, Результат);
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат Тогда  // не требуется установка пароля
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Успех", Истина);
		СтруктураВозврата.Вставить("СертификатBase64", ДополнительныеПараметры.СертификатBase64);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПоискаСертификатаБезПароля, СтруктураВозврата);
	Иначе
		НайтиСертификатСУстановленнымПаролемРекурсивноЧерезВК(ДополнительныеПараметры)
	КонецЕсли;
	
КонецПроцедуры

Процедура ВызватьПроверкуНеобходимостиУстановкиПароляСертификатаЧерезВК(Оповещение, ПодключаемыйМодуль, СертификатBase64)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиНеобходимостиУстановкиПароля", Оповещение);
	ОповещениеПослеВызоваПроверки = Новый ОписаниеОповещения("ПослеПроверкиНеобходимостиУстановкиПароляВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиНеобходимостиУстановкиПароляВК", ЭтотОбъект);
	ПодключаемыйМодуль.НачатьВызовТребуетсяПароль(ОповещениеПослеВызоваПроверки, СертификатBase64);
	
КонецПроцедуры

Процедура ПослеПроверкиНеобходимостиУстановкиПароляВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиУстановкиПароля, РезультатВызова);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиНеобходимостиУстановкиПароляВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При аутентификации произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Проверка необходимости установки пароля.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиУстановкиПароля,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура УстановитьПарольПослеВводаПользователемЧерезВК(ДанныеАутентификации, ДополнительныеПараметры) Экспорт
	
	Если ДанныеАутентификации = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК, Неопределено);
		Возврат;
	КонецЕсли;
	
	УстановитьСоединениеПоСертификатуЧерезВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединенияВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена,
		ДанныеАутентификации.ВыбранныйСертификат, ДанныеАутентификации.ПарольПользователя);
	
КонецПроцедуры

Процедура УстановитьСоединениеПоСертификатуЧерезВК(Оповещение, ПодключаемыйМодуль, НастройкаОбмена, СертификатСсылка, Пароль)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиСоединения", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	РеквизитыСертификата = Новый Структура("ДанныеСертификата");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовСертификата(СертификатСсылка, РеквизитыСертификата);
	ДополнительныеПараметры.Вставить("СертификатBase64", РеквизитыСертификата.ДанныеСертификата);
	
	ОповещениеПослеАутентификации = Новый ОписаниеОповещения(
		"УстановитьСоединениеПослеАутентификацииЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	АутентифицироватьсяНаЭлектронномКлючеЧерезВК(
		ОповещениеПослеАутентификации, ПодключаемыйМодуль, СертификатСсылка, РеквизитыСертификата.ДанныеСертификата, Пароль);
	
КонецПроцедуры

Процедура УстановитьСоединениеПослеАутентификацииЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Не Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Результат);
		Возврат;
	КонецЕсли;

	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(
		ДополнительныеПараметры.НастройкаОбмена);
	
	УстановитьСоединениеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64, ПараметрыСоединения);
	
КонецПроцедуры

Процедура ПослеПолученияХранилищКлючейЭПВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Хранилища = ДеСериализованныеДанные(РезультатВызова);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовКлючей, Хранилища);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияХранилищКлючейЭП(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение информации об аппаратных устройствах.'");
	ТекстСообщения = НСтр("ru = 'При поиске аппаратных устройств, подключенных к компьютеру, произошла ошибка.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовКлючей,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеОтправки.Количество() = 0 Тогда // все настройки обмена обработаны
		Результат = Новый Структура("ИтогКолОтправленных, ИтогКолПодготовленных", 0, 0);
		ЗаполнитьЗначенияСвойств(Результат, ДополнительныеПараметры);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиСообщенийОбменаПоНастройкам, Результат);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеОтправки Цикл
		НастройкаОбмена = КлючЗначение.Ключ;
		МассивСообщенийОбмена = КлючЗначение.Значение;
		Прервать;
	КонецЦикла;
	
	ДополнительныеПараметры.ДанныеОтправки.Удалить(НастройкаОбмена);
	
	РеквизитыНастройкиОбмена = Новый Структура("ИмяВнешнегоМодуля");
	ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
		НастройкаОбмена, РеквизитыНастройкиОбмена);
	
	ДополнительныеПараметры.Вставить("МассивСообщенийОбмена", МассивСообщенийОбмена);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеПолученияВК = Новый ОписаниеОповещения(
		"ПолучитьСвойстваСертификатовПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодключитьИИнициализироватьВК(ОповещениеПослеПолученияВК, НастройкаОбмена);
	
КонецПроцедуры

Процедура ПолучитьСвойстваСертификатовСообщенийОбменаЧерезВК(Оповещение, ПодключаемыйМодуль, МассивСообщенийОбмена)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияДанныхСертификатов", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	СообщенияОбменаИСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДанныеСертификатовСообщенийОбмена(
		МассивСообщенийОбмена);
		
	ДополнительныеПараметры.Вставить("СообщенияОбменаИСертификаты", СообщенияОбменаИСертификаты);
	ДополнительныеПараметры.Вставить("СообщенияОбменаИСвойстваСертификатов", Новый Соответствие);
		
	ПолучитьДанныеСертификатовРекурсивноЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьСвойстваСертификатовПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
		Возврат
	ИначеЕсли Результат = Неопределено Тогда
		ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	ПодключаемыйМодуль = Результат;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьПакетыЭДПослеПолученияДанныхСертификатовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьСвойстваСертификатовСообщенийОбменаЧерезВК(
		Оповещение, ПодключаемыйМодуль, ДополнительныеПараметры.МассивСообщенийОбмена);
	
КонецПроцедуры

Процедура ОтправитьПакетыЭДПослеПолученияДанныхСертификатовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат);
		ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	МассивПакетовЭД = Новый Массив;
		
	Для Каждого СообщениеОбменаИСертификаты Из Результат Цикл
		
		СвойстваСертификатов = Новый Соответствие;
		Для Каждого Элемент Из СообщениеОбменаИСертификаты.Значение Цикл
			СвойстваСертификата = Новый Структура("СерийныйНомер, ИмяИздателя");
			ЗаполнитьЗначенияСвойств(СвойстваСертификата, Элемент.ДанныеСертификата);
			СвойстваСертификатов.Вставить(Элемент.ДанныеСертификата.Отпечаток, СвойстваСертификата)
		КонецЦикла;
		
		ПакетОбменСБанками = ОбменСБанкамиСлужебныйВызовСервера.СоздатьПакетВК(
			ДополнительныеПараметры.НастройкаОбмена, СообщениеОбменаИСертификаты.Ключ, СвойстваСертификатов);
			
		ДополнительныеПараметры.ИтогКолПодготовленных = ДополнительныеПараметры.ИтогКолПодготовленных + 1;
			
		Оповестить("ОбновитьСостояниеОбменСБанками");
		МассивПакетовЭД.Добавить(ПакетОбменСБанками);
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПродолжитьОтправкуПакетовПоНастройкамОбменаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ОтправитьПакетыЧерезВК(Оповещение, ДополнительныеПараметры.НастройкаОбмена, МассивПакетовЭД);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.СообщенияОбменаИСертификаты.Количество() = 0 Тогда // обработаны все сообщения обмена
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатов,
			ДополнительныеПараметры.СообщенияОбменаИСвойстваСертификатов);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.СообщенияОбменаИСертификаты Цикл
		МассивСертификатовBase64 = КлючЗначение.Значение;
		ДополнительныеПараметры.Вставить("СообщениеОбмена", КлючЗначение.Ключ);
		ДополнительныеПараметры.СообщенияОбменаИСертификаты.Удалить(КлючЗначение.Ключ);
		Прервать;
	КонецЦикла;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьСвойстваСертификатовСледующегоСообщенияОбмена", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьСвойстваСертификатовЧерезВК(Оповещение, МассивСертификатовBase64, ДополнительныеПараметры.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПакетовПоНастройкамОбменаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.ИтогКолОтправленных = ДополнительныеПараметры.ИтогКолОтправленных + Результат.ИтогКолОтправленных;
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Результат.Результат);
	КонецЕсли;
	
	Если ДополнительныеПараметры.ПолучитьНовыеДокументы Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПродолжитьОтправкуПлатежныхДокументовПоНастройкамиПослеПолученияНовыхДокументовЧерезВК", ЭтотОбъект,
			ДополнительныеПараметры);
		ПолучитьНовыеДокументыВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	Иначе
		ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПродолжитьОтправкуПлатежныхДокументовПоНастройкамиПослеПолученияНовыхДокументовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") Тогда
		ВидОперации = НСтр("ru = 'Получение новых документов из банка.'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, Результат.Результат, Результат.Результат, 1, ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
	ОтправитьРекурсивноСообщенияОбменаПоНастройкамЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьСвойстваСертификатовСледующегоСообщенияОбмена(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатов, Результат);
		Возврат;
	КонецЕсли;
		
	ДополнительныеПараметры.СообщенияОбменаИСвойстваСертификатов.Вставить(ДополнительныеПараметры.СообщениеОбмена, Результат);
	
	ПолучитьДанныеСертификатовРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ВызватьАутентификациюНаЭлектронномКлючеВК(Оповещение, ПодключаемыйМодуль, СертификатBase64, Пароль, СертификатСсылка = Неопределено)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификацииВК", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатСсылка", СертификатСсылка);
	
	ОповещениеПослеУстановкиПароляСертификата = Новый ОписаниеОповещения(
		"ПослеУстановкиПароляВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуУстановкиПароляВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовУстановитьПароль(ОповещениеПослеУстановкиПароляСертификата, СертификатBase64, Пароль);
	
КонецПроцедуры

Процедура АутентифицироватьсяНаЭлектронномКлючеЧерезВК(Оповещение, ПодключаемыйМодуль, СертификатСсылка, СертификатBase64, Пароль, ЭтоТест = Ложь)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("ОповещениеПослеАутентификации", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатСсылка", СертификатСсылка);
	ДополнительныеПараметры.Вставить("Пароль", Пароль);
	ДополнительныеПараметры.Вставить("ЭтоТест", ЭтоТест);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьУстановкиПинПослеПолученияДанныхСертификата", ЭтотОбъект, ДополнительныеПараметры);
		
	ПолучитьДанныеКлючаЧерезВК(Оповещение, ПодключаемыйМодуль, СертификатBase64);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПинПослеПолученияДанныхСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", Результат.ИдентификаторХранилища);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНаличиеПинТокенаПослеПроверкиПодключенияКлюча", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПроверитьНаличиеПинТокенаПослеПроверкиПодключенияКлюча(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Массив") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьУстановкиПароляСертификатаПослеУстановкиПин", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьПинЕслиТребуетсяВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища = Неопределено)

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодключенияЭлектронногоКлюча", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ОповещениеПослеПолученияИдентификаторов = Новый ОписаниеОповещения(
		"ПредложитьПодключитьКлючПослеПолученияИдентификаторовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИдентификаторыПодключенныхКлючейЧерезВК(ОповещениеПослеПолученияИдентификаторов, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПредложитьПодключитьКлючПослеПолученияИдентификаторовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() = 0
		ИЛИ (ЗначениеЗаполнено(ДополнительныеПараметры.ИдентификаторХранилища)
				И Результат.Найти(ДополнительныеПараметры.ИдентификаторХранилища) = Неопределено) Тогда
		
		// кэшируем компоненту т.к. она будет нужна на открытой форме
		ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		Если ТипЗнч(ПараметрыПодсистемыОбменСБанками) <> Тип("Соответствие") Тогда
			ПараметрыПриложения.Вставить("ЭлектронноеВзаимодействие.ОбменСБанками", Новый Соответствие);
			ПараметрыПодсистемыОбменСБанками = ПараметрыПриложения["ЭлектронноеВзаимодействие.ОбменСБанками"];
		КонецЕсли;

		ПараметрыПодсистемыОбменСБанками.Вставить("ПодключаемыйМодуль", ДополнительныеПараметры.ПодключаемыйМодуль);
		
		Параметры = Новый Структура("ИдентификаторКлюча", ДополнительныеПараметры.ИдентификаторХранилища);
		
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения(
			"ПослеПодключенияЭлектронногоКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
		
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ОжиданиеПодключенияЭлектронногоКлюча", Параметры, , , , ,
			ОписаниеОповещенияОЗакрытии);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеПодключенияЭлектронногоКлючаВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Неопределено);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодключенияЭлектронногоКлюча, Результат);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПароляСертификатаПослеУстановкиПин(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Неопределено);
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЭтоТест Тогда // При тесте всегда устанавливаем пароль
		ВыполнитьАутентификациюПослеПроверкиНеобходимостиУстановкиПароляЧерезВК(Истина, ДополнительныеПараметры);
	Иначе
		Оповещение = Новый ОписаниеОповещения(
			"ВыполнитьАутентификациюПослеПроверкиНеобходимостиУстановкиПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
		ВызватьПроверкуНеобходимостиУстановкиПароляСертификатаЧерезВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВыполнитьАутентификациюПослеПроверкиНеобходимостиУстановкиПароляЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат Тогда // Требуется установить пароль сертификата
		ВызватьАутентификациюНаЭлектронномКлючеВК(ДополнительныеПараметры.ОповещениеПослеАутентификации,
			ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64,
			ДополнительныеПараметры.Пароль, ДополнительныеПараметры.СертификатСсылка);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификации, Истина);
	КонецЕсли

КонецПроцедуры

Процедура ПроверитьВерсиюПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	
	Оповещение = Новый ОписаниеОповещения("ИнициализироватьПослеПроверкиВерсииВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПроверитьВерсиюВК(Оповещение, Результат, ДополнительныеПараметры.ОжидаемаяВерсияВК);
	
КонецПроцедуры

Процедура ИнициализироватьПослеПроверкиВерсииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено ИЛИ ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ОбработатьРезультатИнициализацииВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ИнициализироватьВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ОбработатьРезультатИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, Результат);
	Иначе
		Если ДополнительныеПараметры.ИспользоватьЖурналирование Тогда
			Оповещение = Новый ОписаниеОповещения("ПослеВключенияЖурналированияВК", ЭтотОбъект, ДополнительныеПараметры);
			ВключитьЖурналированиеВК(
				Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.КаталогДляЖурналирования);
		Иначе
			ВыполнитьОбработкуОповещения(
				ДополнительныеПараметры.ОповещенияПослеПодключенияИИнициализацииВК, ДополнительныеПараметры.ПодключаемыйМодуль);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура АутентифицироватьсяНаТокенеПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьНеобходимостьДополненияЭДПослеАутентификацииЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	АутентифицироватьсяНаЭлектронномКлючеЧерезВК(Оповещение, Результат, ДополнительныеПараметры.СертификатСсылка,
		ДополнительныеПараметры.СертификатBase64, ДополнительныеПараметры.Пароль, ДополнительныеПараметры.ЭтоТест);
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьДополненияЭДПослеАутентификацииЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
		
	Оповещение = Новый ОписаниеОповещения("ПослеПроверкиНеобходимостиДополненияЭДЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиДополненияЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовТребуетсяДополнитьЭД(
		Оповещение, ДополнительныеПараметры.ДанныеЭДBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиДополненияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При подписании документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Проверка дополнения электронного документа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПроверкиНеобходимостиДополненияЭДЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если РезультатВызова Тогда // требуется дополнить ЭД
		Оповещение = Новый ОписаниеОповещения("ДополнитьЭДПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
		ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(ДополнительныеПараметры.НастройкаОбмена);
		УстановитьСоединениеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
			ДополнительныеПараметры.СертификатBase64, ПараметрыСоединения);
	Иначе
		
		ВызватьПодписаниеЭДЧерезВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, ДополнительныеПараметры.СообщениеОбмена,
			ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64);
	КонецЕсли;
	
КонецПроцедуры

Процедура ДополнитьЭДПослеУстановкиСоединенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеДополненияЭДЧерезВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуДополненияЭДЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовДополнитьЭД(Оповещение, ДополнительныеПараметры.ДанныеЭДBase64);
	
КонецПроцедуры

Процедура ПослеДополненияЭДЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДвоичныеДанныеЭД = Base64Значение(РезультатВызова);
	
	ОбменСБанкамиСлужебныйВызовСервера.ОбновитьДанныеЭД(ДополнительныеПараметры.СообщениеОбмена, ДвоичныеДанныеЭД);
	
	ВызватьПодписаниеЭДЧерезВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК,
		ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.СертификатBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуДополненияЭДЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При дополнении документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Дополнение электронного документа.'");
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияПричиныОтклоненияЭДЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьИнформациюОбОшибкеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПолученияПричиныОтклоненияЭДЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	СтатусОтклоненБанком = ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.ОтклоненБанком");
	
	СтруктураПараметров = Новый Структура;
	СтруктураПараметров.Вставить("Статус", СтатусОтклоненБанком);
	СтруктураПараметров.Вставить("ПричинаОтклонения", Результат);
	
	ОбменСБанкамиСлужебныйВызовСервера.ИзменитьСообщениеОбмена(ДополнительныеПараметры.СообщениеОбмена, СтруктураПараметров);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЧерезВК, Результат);
	
КонецПроцедуры

Процедура ПолучитьСертификатыСКлючаПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда // ошибка подключения ВК с выводом сообщения
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // ошибка подключения ВК
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДанныхСертификатовСКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ИдентификаторХранилища = Неопределено;
	Если ДополнительныеПараметры.ПараметрыУстановленногоСоединения <> Неопределено Тогда
		ИдентификаторХранилища = ДополнительныеПараметры.ПараметрыУстановленногоСоединения.ИдентификаторХранилища;
	КонецЕсли;
	
	ПолучитьСертификатыИзХранилищаВК(Оповещение, Результат, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатовСКлючаВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) <> Тип("Массив") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	ДоступныеСертификаты = ОбменСБанкамиСлужебныйВызовСервера.ДоступныеСертификаты(
		ДополнительныеПараметры.НастройкаОбмена);
		
	Если ДоступныеСертификаты.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'В настройке обмена с сервисом 1С:ДиректБанк нет ни одного доступного сертификата.'");
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецЕсли;
		
	ПараметрыСертификатовНастройкиОбмена = Новый Соответствие;
	Для Каждого КлючЗначение Из ДоступныеСертификаты Цикл
		СтруктураДанных = Новый Структура;
		СтруктураДанных.Вставить("СсылкаНаСертификат", КлючЗначение.Ключ);
		СтруктураДанных.Вставить("СертификатBase64", КлючЗначение.Значение.ДанныеСертификата);
		СтруктураДанных.Вставить("КомуВыдан", КлючЗначение.Значение.КомуВыдан);
		ПараметрыСертификатовНастройкиОбмена.Вставить(КлючЗначение.Значение.Отпечаток, СтруктураДанных);
	КонецЦикла;
		
	ДанныеВыбора = Новый Соответствие;
	МассивОтпечатков = Новый Массив;
		
	Для Каждого Элемент Из Результат Цикл
		СвойстваСертификата = ПараметрыСертификатовНастройкиОбмена.Получить(Элемент.ДанныеСертификата.Отпечаток);
		Если СвойстваСертификата <> Неопределено Тогда
			СтруктураСертификата = Новый Структура;
			СтруктураСертификата.Вставить("СертификатBase64", СвойстваСертификата.СертификатBase64);
			СтруктураСертификата.Вставить("Псевдоним", Элемент.ДанныеСертификата.Псевдоним);
			СтруктураСертификата.Вставить("КомуВыдан", СвойстваСертификата.КомуВыдан);
			СтруктураСертификата.Вставить("Отпечаток", Элемент.ДанныеСертификата.Отпечаток);
			МассивОтпечатков.Добавить(Элемент.ДанныеСертификата.Отпечаток);
			СтруктураСертификата.Вставить("ПарольПолучен", Ложь);
			ДанныеВыбора.Вставить(СвойстваСертификата.СсылкаНаСертификат, СтруктураСертификата);
		КонецЕсли;
	КонецЦикла;
	
	Если ДанныеВыбора.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'На банковском ключе не найдены сертификаты, указанные в настройке обмена с банком.'");
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ДанныеВыбораСертификатов", ДанныеВыбора);
		
	ВидЭДЗапросЗонд = ПредопределенноеЗначение("Перечисление.ВидыЭДОбменСБанками.ЗапросЗонд");
	
	НастройкиОбмена = Неопределено;
	СообщениеЗапросЗонд = Неопределено;
	
	ОбменСБанкамиСлужебныйВызовСервера.СформироватьЗапросЗонд(
		ДополнительныеПараметры.НастройкаОбмена, МассивОтпечатков, СообщениеЗапросЗонд, НастройкиОбмена);
		
	Если НЕ ЗначениеЗаполнено(СообщениеЗапросЗонд) ИЛИ НастройкиОбмена = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеЗапросЗонд);
	
	// Данные аутентификации были предъявлены при получении настроек с сервера банка и сертификат сохранен в БД
	// Добавим информацию о сертификате в параметры.
	Если ДополнительныеПараметры.ПараметрыУстановленногоСоединения <> Неопределено
		И ПараметрыСертификатовНастройкиОбмена.Получить(ДополнительныеПараметры.ПараметрыУстановленногоСоединения.Отпечаток) <> Неопределено Тогда
		
		ДанныеСертификата = ПараметрыСертификатовНастройкиОбмена.Получить(
			ДополнительныеПараметры.ПараметрыУстановленногоСоединения.Отпечаток);
			
		ДополнительныеПараметры.ПараметрыУстановленногоСоединения.Вставить(
			"СертификатBase64", ДанныеСертификата.СертификатBase64);
			
		ДополнительныеПараметры.ПараметрыУстановленногоСоединения.Вставить("КомуВыданСертификат", ДанныеСертификата.КомуВыдан);
		
		Если НастройкиОбмена.Подписывать Тогда
			// запрос сразу отправляется, т.к. соединения установлено.
			Оповещение = Новый ОписаниеОповещения(
				"ОтправитьТестовыйЗапросПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
			ПодписатьЭДПоСертификатуЧерезВК(Оповещение, СообщениеЗапросЗонд, ДополнительныеПараметры.НастройкаОбмена,
				ДанныеСертификата.СсылкаНаСертификат, "", ДополнительныеПараметры.ПодключаемыйМодуль); // пароль пустой, т.к. он не потребуется
		Иначе
			ОтправитьТестовыйЗапросПослеУстановкиСоединенияВК(Истина, ДополнительныеПараметры);
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если НастройкиОбмена.Подписывать Тогда
		ВидОперации = НСтр("ru = 'Подписание электронного документа.'");
		Оповещение = Новый ОписаниеОповещения(
			"ПодписатьЗапросЗондПослеВыбораСертификатаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьПарольКСертификату(Оповещение, ДанныеВыбора, ВидОперации, СообщениеЗапросЗонд);
	Иначе
		МассивСертификатов = Новый Массив;
		Для Каждого КлючЗначение Из ДанныеВыбора Цикл
			МассивСертификатов.Добавить(КлючЗначение.Ключ);
		КонецЦикла;
		ПараметрыФормы = Новый Структура();
		ПараметрыФормы.Вставить("ВидОперации", НСтр("ru = 'Аутентификация на сервере банка'"));
		ПараметрыФормы.Вставить("ДляЗаписиВИБ", Истина);
		ПараметрыФормы.Вставить("НастройкаОбмена", ДополнительныеПараметры.НастройкаОбмена);
		ПараметрыФормы.Вставить("МассивСертификатов", МассивСертификатов);
		ОписаниеОповещенияОЗакрытии = Новый ОписаниеОповещения("АутентификацияПослеПолученияПароляЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросПароляКСертификату",
			ПараметрыФормы, , , , , ОписаниеОповещенияОЗакрытии, РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс);
	
	КонецЕсли;
	
КонецПроцедуры

Процедура АутентификацияПослеПолученияПароляЧерезВК(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Неопределено);
		Возврат;
	КонецЕсли;
	
	СвойстваСертификата = ДополнительныеПараметры.ДанныеВыбораСертификатов.Получить(ДанныеВыбора.ВыбранныйСертификат);
	
	ПараметрыУстановленногоСоединения = Новый Структура;
	ПараметрыУстановленногоСоединения.Вставить("Отпечаток", СвойстваСертификата.Отпечаток);
	ПараметрыУстановленногоСоединения.Вставить("КомуВыданСертификат", СвойстваСертификата.КомуВыдан);
	ПараметрыУстановленногоСоединения.Вставить("СертификатBase64", СвойстваСертификата.СертификатBase64);
	ДополнительныеПараметры.Вставить("ПараметрыУстановленногоСоединения", ПараметрыУстановленногоСоединения);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьТестовоеСоединениеПослеАутентификацииНаКлючеВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьАутентификациюНаЭлектронномКлючеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		СвойстваСертификата.СертификатBase64, ДанныеВыбора.ПарольПользователя, ДанныеВыбора.ВыбранныйСертификат);
	
КонецПроцедуры

Процедура ПодписатьЗапросЗондПослеВыбораСертификатаЧерезВК(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если ДанныеВыбора = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Неопределено);
		Возврат;
	КонецЕсли;
	
	СвойстваСертификата = ДополнительныеПараметры.ДанныеВыбораСертификатов.Получить(ДанныеВыбора.ВыбранныйСертификат);
	
	ПараметрыУстановленногоСоединения = Новый Структура;
	ПараметрыУстановленногоСоединения.Вставить("Отпечаток", СвойстваСертификата.Отпечаток);
	ПараметрыУстановленногоСоединения.Вставить("КомуВыданСертификат", СвойстваСертификата.КомуВыдан);
	ПараметрыУстановленногоСоединения.Вставить("СертификатBase64", СвойстваСертификата.СертификатBase64);
	ДополнительныеПараметры.Вставить("ПараметрыУстановленногоСоединения", ПараметрыУстановленногоСоединения);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьТестовоеСоединениеПослеАутентификацииНаКлючеВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПодписатьЭДПоСертификатуЧерезВК(Оповещение, ДополнительныеПараметры.СообщениеОбмена,
		ДополнительныеПараметры.НастройкаОбмена, ДанныеВыбора.ВыбранныйСертификат, ДанныеВыбора.ПарольПользователя, ,
		Истина);
	
КонецПроцедуры

Процедура УстановитьТестовоеСоединениеПослеАутентификацииНаКлючеВК(Результат, ДополнительныеПараметры) Экспорт

	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;

	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьТестовыйЗапросПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(
		ДополнительныеПараметры.НастройкаОбмена);
		
	УстановитьСоединениеВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.ПараметрыУстановленногоСоединения.СертификатBase64, ПараметрыСоединения);
	
КонецПроцедуры

Процедура ОтправитьТестовыйЗапросПослеУстановкиСоединенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено ИЛИ Результат = Ложь Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ОтправитьТестовыйПакетПослеПолученияСвойствСертификатовЧерезВК", ЭтотОбъект, ДополнительныеПараметры);

	МассивСообщенийОбмена = Новый Массив;
	МассивСообщенийОбмена.Добавить(ДополнительныеПараметры.СообщениеОбмена);
		
	ПолучитьСвойстваСертификатовСообщенийОбменаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, МассивСообщенийОбмена);

КонецПроцедуры

Процедура ОтправитьТестовыйПакетПослеПолученияСвойствСертификатовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;

	Для Каждого СообщениеОбменаИСертификаты Из Результат Цикл
		СвойстваСертификатов = Новый Соответствие;
		Для Каждого Элемент Из СообщениеОбменаИСертификаты.Значение Цикл
			СвойстваСертификата = Новый Структура("СерийныйНомер, ИмяИздателя");
			ЗаполнитьЗначенияСвойств(СвойстваСертификата, Элемент.ДанныеСертификата);
			СвойстваСертификатов.Вставить(Элемент.ДанныеСертификата.Отпечаток, СвойстваСертификата)
		КонецЦикла;
		Прервать;
	КонецЦикла;
	
	ПакетОбменСБанками = ОбменСБанкамиСлужебныйВызовСервера.СоздатьПакетВК(
		ДополнительныеПараметры.НастройкаОбмена, ДополнительныеПараметры.СообщениеОбмена, СвойстваСертификатов);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПолучитьИзвещениеПослеОтправкиЗапросаЗондаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ОтправитьПакетЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена,
		ПакетОбменСБанками, ДополнительныеПараметры.ПараметрыУстановленногоСоединения.СертификатBase64);
	
КонецПроцедуры

Процедура ПолучитьИзвещениеПослеОтправкиЗапросаЗондаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат);
		Возврат;
	КонецЕсли;
		
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовВК", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьНовыеДокументыВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры


Процедура ОтправитьПакетЧерезВК(Оповещение, ПодключаемыйМодуль, НастройкаОбмена, ПакетЭД, СертификатBase64)
	
	// Аутентификация на токене уже выполнена.
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиПакетаЭД", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ПакетЭД", ПакетЭД);
	ОповещениеПослеУстановкиСоединения = Новый ОписаниеОповещения(
		"ОтправитьПакетЭДПослеУстановкиСоединенияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ПараметрыСоединения = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыСоединенияВК(НастройкаОбмена);
	
	УстановитьСоединениеВК(ОповещениеПослеУстановкиСоединения, ПодключаемыйМодуль, СертификатBase64, ПараметрыСоединения);
	
КонецПроцедуры

Процедура ОтправитьПакетЭДПослеУстановкиСоединенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетаЭД, Результат);
		Возврат;
	КонецЕсли;
	
	ВызватьОтправкуПакетаЧерезВК(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетаЭД,
		ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ПакетЭД);
	
КонецПроцедуры

Процедура ОтправитьПакетыЧерезВК(Оповещение, НастройкаОбмена, ПакетыЭД)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПакетыЭД", ПакетыЭД);
	ДополнительныеПараметры.Вставить("ИтогКолОтправленных", 0);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиПакетов", Оповещение);
	
	ОповещениеПослеПодключенияВК = Новый ОписаниеОповещения(
		"УстановитьСоединениеПослеПодключенияВК", ЭтотОбъект, ДополнительныеПараметры);
	ПодключитьИИнициализироватьВК(ОповещениеПослеПодключенияВК, НастройкаОбмена);
	
КонецПроцедуры

Процедура УстановитьСоединениеПослеПодключенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("ИтогКолОтправленных", ДополнительныеПараметры.ИтогКолОтправленных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", Результат);
		
	ОповещениеПослеУстановкиСоединения = Новый ОписаниеОповещения(
		"ОтправитьПакетыПослеУстановкиСоединенияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	УстановитьСоединениеЧерезВК(ОповещениеПослеУстановкиСоединения, ДополнительныеПараметры.НастройкаОбмена);
	
КонецПроцедуры

Процедура ОтправитьПакетыПослеУстановкиСоединенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("ИтогКолОтправленных", ДополнительныеПараметры.ИтогКолОтправленных);
		ДанныеВозврата.Вставить("ИтогКолПолученных", 0);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ВызватьОтправкуПакетовРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ВызватьОтправкуПакетовРекурсивноЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ПакетыЭД.Количество() = 0 Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Истина);
		ДанныеВозврата.Вставить("ИтогКолОтправленных", ДополнительныеПараметры.ИтогКолОтправленных);
		ДанныеВозврата.Вставить("ПодключаемыйМодуль", ДополнительныеПараметры.ПодключаемыйМодуль); // используется для дальнейшего получения новых документов
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ПакетЭД = ДополнительныеПараметры.ПакетыЭД.Получить(0);
	ДополнительныеПараметры.Вставить("ПакетЭД", ПакетЭД);
	ДополнительныеПараметры.ПакетыЭД.Удалить(0);
	
	ОповещениеПослеОтправкиПакета = Новый ОписаниеОповещения(
		"ОтправитьСледующийПакетЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьОтправкуПакетаЧерезВК(ОповещениеПослеОтправкиПакета, ДополнительныеПараметры.ПодключаемыйМодуль, ПакетЭД)
	
КонецПроцедуры

Процедура ОтправитьСледующийПакетЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДанныеВозврата = Новый Структура;
		ДанныеВозврата.Вставить("Результат", Результат);
		ДанныеВозврата.Вставить("ИтогКолОтправленных", ДополнительныеПараметры.ИтогКолОтправленных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакетов, ДанныеВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ИтогКолОтправленных = ДополнительныеПараметры.ИтогКолОтправленных + 1;
	
	ВызватьОтправкуПакетовРекурсивноЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ВызватьОтправкуПакетаЧерезВК(Оповещение, ПодключаемыйМодуль, ПакетЭД)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеОтправкиПакета", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ПакетЭД", ПакетЭД);
	ДвоичныеДанныеПакета = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(ПакетЭД);
	ДанныеПакетаBase64 = Base64Строка(ДвоичныеДанныеПакета);
	
	ОповещениеПослеОтправкиПакета = Новый ОписаниеОповещения("ПослеОтправкиПакетаЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуОтправкиПакетаЧерезВК", ЭтотОбъект);
				
	ПодключаемыйМодуль.НачатьВызовОтправитьПакет(ОповещениеПослеОтправкиПакета, ДанныеПакетаBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиПакетаЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При отправке данных в банк произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Отправка пакетов электронных документов.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеОтправкиПакета,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеОтправкиПакетаЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СтруктураЭД = Новый Структура("Статус", ПредопределенноеЗначение("Перечисление.СтатусыОбменСБанками.Отправлен"));
	СтатусПакетаОтправлен = ПредопределенноеЗначение("Перечисление.СтатусыПакетовЭД.Отправлен");
	ОбменСБанкамиСлужебныйВызовСервера.ОбновитьСтатусыДокументовПакетаЭДО(
		ДополнительныеПараметры.ПакетЭД, СтатусПакетаОтправлен, СтруктураЭД);
		
	Оповестить("ОбновитьСостояниеОбменСБанками");
		
	Попытка
		ОтветБанка = Base64Значение(РезультатВызова);
		ОбменСБанкамиСлужебныйВызовСервера.ОбработатьОтветБанкаНаОтправкуДокументаAsync(
			ОтветБанка, ДополнительныеПараметры.ПакетЭД);
	Исключение
		ТекстСообщения = КраткоеПредставлениеОшибки(ИнформацияОбОшибке());
		ТекстИсключения = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ВидОперации = НСтр("ru = 'Отправка документа в банк'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			ВидОперации, ТекстИсключения, , 1, ДополнительныеПараметры.ПакетЭД);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакета, ТекстСообщения);
		Возврат;
	КонецПопытки;
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиПакета, Истина);
	
КонецПроцедуры

Процедура ПослеПолученияНовыхДокументовВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат.Результат) = Тип("Строка") ИЛИ Результат.Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Результат.Результат);
		Возврат;
	КонецЕсли;
	
	Если НЕ Результат.Результат Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	ЕстьОшибка = Ложь;
	Извещение = Неопределено;
	ПолученоИзвещение = ОбменСБанкамиСлужебныйВызовСервера.ПолученоИзвещениеПоЗапросу(
		ДополнительныеПараметры.СообщениеОбмена, ЕстьОшибка);
		
	Если ЕстьОшибка Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
		Возврат;
	КонецЕсли;
	
	Если ПолученоИзвещение Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Истина);
	Иначе
		Если ПроцессПрерван(ДополнительныеПараметры.НастройкаОбмена) Тогда
			ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеТестаНастройки, Ложь);
			Возврат;
		КонецЕсли;
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияНовыхДокументовВК", ЭтотОбъект, ДополнительныеПараметры);
		ПолучитьНовыеДокументыВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.НастройкаОбмена);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВызватьПолучениеИдентификаторовНовыхПакетовЧерезВК(Оповещение, НастройкаОбмена, ПодключаемыйМодуль)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияИдентификаторовПакетов", Оповещение);
	ДополнительныеПараметры.Вставить("НастройкаОбмена", НастройкаОбмена);
	
	ОповещениеПослеЗапросаСпискаПакетов = Новый ОписаниеОповещения("ПослеПолученияСпискаПакетовВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПолученияСпискаПакетов", ЭтотОбъект);
		
	ПараметрыОбмена = ОбменСБанкамиСлужебныйВызовСервера.ПараметрыОбменаСБанком(НастройкаОбмена);

	ДатаСтрокой = "";
	
	Если ЗначениеЗаполнено(ПараметрыОбмена.ПоследняяДатаПолученияЭД) Тогда
		ДатаСтрокой = Формат(ПараметрыОбмена.ПоследняяДатаПолученияЭД, "ДФ=yyyy-MM-ddTHH:mm:ss");
	КонецЕсли;
		
	ПодключаемыйМодуль.НачатьВызовПолучитьСписокПакетов(ОповещениеПослеЗапросаСпискаПакетов, ДатаСтрокой);
		
КонецПроцедуры

Процедура ПослеПолученияСпискаПакетовВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ЕстьОшибка = Ложь;
	МассивИдентификаторов = Новый Массив;
	ОбменСБанкамиСлужебныйВызовСервера.ПрочитатьИдентификаторыПакетов(
		ДополнительныеПараметры.НастройкаОбмена, РезультатВызова, МассивИдентификаторов, ЕстьОшибка);
	
	Если ЕстьОшибка Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовПакетов, Неопределено);
		Возврат;
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовПакетов, МассивИдентификаторов);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияСпискаПакетов(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'Ошибка получения списка пакетов документов из банка.'");
	ВидОперации = НСтр("ru = 'Получение списка пакетов.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияИдентификаторовПакетов,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПолучитьПакетыПослеПолученияИдентификаторовЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Или Результат = Неопределено Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Результат);
		СтруктураВозврата.Вставить("ИтогКолПолученных", 0);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() Тогда
		ПолучитьОчереднойПакетВК(Результат, ДополнительныеПараметры);
	Иначе
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Истина);
		СтруктураВозврата.Вставить("ИтогКолПолученных", 0);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПолучитьОчереднойПакетВК(МассивИдентификаторов, ДополнительныеПараметры)
	
	Если МассивИдентификаторов.Количество() = 0 Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Истина);
		Если ДополнительныеПараметры.Свойство("ТекстОшибки") И ЗначениеЗаполнено(ДополнительныеПараметры.ТекстОшибки) Тогда
			СтруктураВозврата.Вставить("Результат", ДополнительныеПараметры.ТекстОшибки);
		КонецЕсли;
		СтруктураВозврата.Вставить("ИтогКолПолученных", ДополнительныеПараметры.ИтогКолПолученных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ИдентификаторПакета = МассивИдентификаторов.Получить(0);
	МассивИдентификаторов.Удалить(0);
	ДополнительныеПараметры.Вставить("МассивИдентификаторов", МассивИдентификаторов);
	
	Оповещение = Новый ОписаниеОповещения("СохранитьПакетПослеПолученияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьПолучениеПакетаЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторПакета);
	
КонецПроцедуры

Процедура СохранитьПакетПослеПолученияЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Результат);
		СтруктураВозврата.Вставить("ИтогКолПолученных", ДополнительныеПараметры.ИтогКолПолученных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДанныеВозврата = ОбменСБанкамиКлиентСервер.ПараметрыПолученияНовыхДокументовАсинхронныйОбмен();
	
	ОбменСБанкамиСлужебныйВызовСервера.СохранитьПолученныйПакетЧерезВК(
		ДополнительныеПараметры.НастройкаОбмена, Результат.ПакетBase64, ДанныеВозврата);
		
	Оповестить("ОбновитьСостояниеОбменСБанками", ДанныеВозврата.ПараметрОповещения);

	Если ДанныеВозврата.ЕстьОшибка Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Неопределено);
		СтруктураВозврата.Вставить("ИтогКолПолученных", ДополнительныеПараметры.ИтогКолПолученных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.ИтогКолПолученных = ДополнительныеПараметры.ИтогКолПолученных + 1;
	
	Если ДанныеВозврата.ДанныеЭП.Количество() Тогда
		
		РеквизитыНастройкиОбмена = Новый Структура("СертификатБанкаBase64");
		ОбменСБанкамиСлужебныйВызовСервера.ПолучитьЗначенияРеквизитовНастройкиОбмена(
			ДополнительныеПараметры.НастройкаОбмена, РеквизитыНастройкиОбмена);
		
		ДополнительныеПараметры.Вставить("ДанныеЭД", ДанныеВозврата.ДанныеЭП);
		ДополнительныеПараметры.Вставить("СертификатБанкаBase64", РеквизитыНастройкиОбмена.СертификатБанкаBase64);
		
		Оповещение = Новый ОписаниеОповещения(
			"СохранитьДанныеПодписейПослеПолученияСвойствСертификатаБанка", ЭтотОбъект, ДополнительныеПараметры);
		
		ПолучитьДанныеКлючаЧерезВК(
			Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, РеквизитыНастройкиОбмена.СертификатБанкаBase64);
		
	Иначе
		ПолучитьОчереднойПакетВК(ДополнительныеПараметры.МассивИдентификаторов, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВызватьПолучениеПакетаЧерезВК(Оповещение, ПодключаемыйМодуль, ИдентификаторПакета)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияПакета", Оповещение);
	
	ОповещениеВызоваПолученияПакета = Новый ОписаниеОповещения(
		"ПослеПолученияПакетаВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияПакетаВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовПолучитьПакет(ОповещениеВызоваПолученияПакета, ИдентификаторПакета);
	
КонецПроцедуры

Процедура ПослеПолученияПакетаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Результат = Новый Структура("ПакетBase64", РезультатВызова);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияПакета, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияПакетаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'Ошибка получения пакета документов из банка.'");
	ВидОперации = НСтр("ru = 'Получение пакета из банка.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияПакета,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
		
КонецПроцедуры

Процедура СохранитьДанныеПодписейПослеПолученияСвойствСертификатаБанка(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		СтруктураВозврата = Новый Структура;
		СтруктураВозврата.Вставить("Результат", Результат);
		СтруктураВозврата.Вставить("ИтогКолПолученных", ДополнительныеПараметры.ИтогКолПолученных);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияНовыхДокументов, СтруктураВозврата);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СвойстваСертификатаБанка", Результат);
	
	ПроверитьПодписиПолученныхДокументовЧерезВК(ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПроверитьПодписиПолученныхДокументовЧерезВК(ДополнительныеПараметры)
	
	Если ДополнительныеПараметры.ДанныеЭД.Количество() = 0 Тогда // Обработаны все сообщения обмена
		ПолучитьОчереднойПакетВК(ДополнительныеПараметры.МассивИдентификаторов, ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	Для Каждого КлючЗначение Из ДополнительныеПараметры.ДанныеЭД Цикл
		СообщениеОбмена = КлючЗначение.Ключ;
		МассивПодписей = КлючЗначение.Значение;
		Прервать;
	КонецЦикла;
	
	ДополнительныеПараметры.ДанныеЭД.Удалить(СообщениеОбмена);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("МассивПодписей", МассивПодписей);
	ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(
		ДополнительныеПараметры.СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДанныеЭДBase64", Base64Строка(ДвоичныеДанныеЭД));
	ДополнительныеПараметры.Вставить("ДанныеПроверкиПодписи", Новый Массив);
	ДополнительныеПараметры.Вставить("ТекстОшибки");
	
	ПроверитьОчереднуюПодписьСообщенияОбменаЧерезВК(Неопределено, ДополнительныеПараметры)
	
КонецПроцедуры

Процедура ПроверитьОчереднуюПодписьСообщенияОбменаЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ДополнительныеПараметры.Вставить("ТекстОшибки", Результат);
		// возврат не делаем, т.к. нужно сохранить все подписи
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ДанныеПодписи", ДополнительныеПараметры.ДвоичныеДанныеПодписи);
		СтруктураЗаписи.Вставить("ПодписьВерна");
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи");
		ДополнительныеПараметры.ДанныеПроверкиПодписи.Добавить(СтруктураЗаписи);
	КонецЕсли;
	
	Если Результат <> Неопределено Тогда
		СтруктураЗаписи = Новый Структура;
		СтруктураЗаписи.Вставить("ДанныеПодписи", ДополнительныеПараметры.ДвоичныеДанныеПодписи);
		СтруктураЗаписи.Вставить("ПодписьВерна", Результат);
		СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
		ДополнительныеПараметры.ДанныеПроверкиПодписи.Добавить(СтруктураЗаписи);
	КонецЕсли;
	
	Если ДополнительныеПараметры.МассивПодписей.Количество() = 0 Тогда // проверены все подписи
		
		СвойстваСертификатаБанка = Новый Структура;
		СвойстваСертификатаБанка.Вставить("ДвоичныеДанные", Base64Значение(ДополнительныеПараметры.СертификатБанкаBase64));
		СвойстваСертификатаБанка.Вставить("КомуВыдан", ДополнительныеПараметры.СвойстваСертификатаБанка.ВладелецФИО);
		СвойстваСертификатаБанка.Вставить("Отпечаток", ДополнительныеПараметры.СвойстваСертификатаБанка.Отпечаток);
		
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьДанныеЭлектронныхПодписей(
			ДополнительныеПараметры.СообщениеОбмена, СвойстваСертификатаБанка, ДополнительныеПараметры.ДанныеПроверкиПодписи);
			
		ПроверитьПодписиПолученныхДокументовЧерезВК(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеПодписи = ДополнительныеПараметры.МассивПодписей.Получить(0);
	ДополнительныеПараметры.МассивПодписей.Удалить(0);
	ДополнительныеПараметры.Вставить("ДвоичныеДанныеПодписи", ДвоичныеДанныеПодписи);
	
	ПодписьBase64 = Base64Строка(ДвоичныеДанныеПодписи);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьОчереднуюПодписьСообщенияОбменаЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ПроверитьПодписьЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.ДанныеЭДBase64, ДополнительныеПараметры.СертификатБанкаBase64, ПодписьBase64);
		
КонецПроцедуры

Процедура ПослеПроверкиПодписиЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи, РезультатВызова);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиПодписиЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт

	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При проверке подписи документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Проверка подписи электронного документа.'");

	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписи,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ВызватьПодписаниеЭДЧерезВК(Оповещение, СообщениеОбмена, ПодключаемыйМодуль, СертификатBase64)
	
	ДвоичныеДанныеЭД = ОбменСБанкамиСлужебныйВызовСервера.ДвоичныеДанныеПрисоединенногоФайла(СообщениеОбмена);

	Если ДвоичныеДанныеЭД = Неопределено Тогда
		ВыполнитьОбработкуОповещения(Оповещение, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДанныеЭДBase64 = Base64Строка(ДвоичныеДанныеЭД);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПодписанияЭД", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатBase64);
	ДополнительныеПараметры.Вставить("СообщениеОбмена", СообщениеОбмена);
	ДополнительныеПараметры.Вставить("ДанныеЭДBase64", ДанныеЭДBase64);
	
	ОповещениеПослеПодготовкиПодписи = Новый ОписаниеОповещения("ПодписатьЭДПослеПодготовкиПодписиЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПодготовкиПодписиЧерезВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодготовитьПодпись(
		ОповещениеПослеПодготовкиПодписи, СертификатBase64, ДанныеЭДBase64);

КонецПроцедуры

Процедура ПодписатьЭДПослеПодготовкиПодписиЧерезВК(Результат, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	СтруктураРезультата = ДеСериализованныеДанные(Результат);
	
	Если СтруктураРезультата.Время = -1 Тогда
		Оповещение = Новый ОписаниеОповещения(
			"ПослеПодписанияЧерезВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПодписанияЧерезВК", ЭтотОбъект);
		
		ДанныеПодписи = Неопределено;
	
		ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовПодписать(
			Оповещение, ДополнительныеПараметры.СертификатBase64, ДополнительныеПараметры.ДанныеЭДBase64, ДанныеПодписи);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодготовкиПодписиЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При подготовки подписи электронного документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Подготовка подписи электронного документа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура ПослеПодписанияЧерезВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт

	Если Не РезультатВызова Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ПодписьBase64", ПараметрыВызова[2]);
	
	Оповещение = Новый ОписаниеОповещения(
		"СохранитьПодписьПослеПолученияСвойствСертификата", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.СертификатBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПодписанияЧерезВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'При подписании электронного документа произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Подписание электронного документа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
	
КонецПроцедуры

Процедура СохранитьПодписьПослеПолученияСвойствСертификата(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД, Результат);
		Возврат;
	КонецЕсли;
	
	ДвоичныеДанныеПодписи = Base64Значение(ДополнительныеПараметры.ПодписьBase64);
	
	СтруктураСертификата = Новый Структура;
	СтруктураСертификата.Вставить("Отпечаток", Результат.Отпечаток);
	СтруктураСертификата.Вставить("КомуВыдан", Результат.ВладелецФИО);
	СтруктураСертификата.Вставить("ДвоичныеДанные", Base64Значение(ДополнительныеПараметры.СертификатBase64));
	
	ДанныеПроверкиПодписи = Новый Структура;
	ДанныеПроверкиПодписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
	ДанныеПроверкиПодписи.Вставить("ПодписьВерна", Истина);
	ОбменСБанкамиСлужебныйВызовСервера.ДобавитьПодпись(
		ДополнительныеПараметры.СообщениеОбмена, ДвоичныеДанныеПодписи, СтруктураСертификата, ДанныеПроверкиПодписи);
		
	Оповестить("ОбновитьСостояниеОбменСБанками");
		
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПодписанияЭД, Истина);
	
КонецПроцедуры

Процедура ПослеУстановкиСоединенияВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Если РезультатВызова Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Истина);
		Возврат;
	КонецЕсли;
	
	// Необходима расширенная аутентификация.
	
	Сессия = ДеСериализованныеДанные(ПараметрыВызова[3]).Сессия;
	СессияСтрокой = СериализованныеДанные(Сессия);
	ДополнительныеПараметры.Вставить("Сессия", Сессия);
	
	Оповещение = Новый ОписаниеОповещения("ПослеОтправкиПароляПоSMSВК", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуОтправкиПароляПоSMSВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовОтправитьПарольРасширеннойАутентификацииПоSMS(
		Оповещение, СессияСтрокой);
	
КонецПроцедуры

Процедура ПослеОтправкиПароляПоSMSВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаОдноразовогоПароляВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПараметрыФормы = Новый Структура("ИдентификаторСессии", ДополнительныеПараметры.Сессия.Идентификатор);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, ЭтотОбъект, , , , Оповещение,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПослеВводаОдноразовогоПароляВК(ОдноразовыйПароль, ДополнительныеПараметры) Экспорт
	
	Если ОдноразовыйПароль = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Неопределено);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеРасширеннойАутентификации", ЭтотОбъект, ДополнительныеПараметры,
		"ОбработатьОшибкуРасширеннойАутентификации", ЭтотОбъект);
		
	ПараметрыАутентификации = Новый Структура;
	ПараметрыАутентификации.Вставить("Способ", "SMS");
	ПараметрыАутентификации.Вставить("Сессия", ДополнительныеПараметры.Сессия);
	ПараметрыАутентификации.Вставить("Пароль", ОдноразовыйПароль);
	
	ПараметрыАутентификацииСтрокой = СериализованныеДанные(ПараметрыАутентификации);
		
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовРасширеннаяАутентификация(
		Оповещение, ПараметрыАутентификацииСтрокой);
	
КонецПроцедуры

Процедура ПослеРасширеннойАутентификации(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения, Истина);
	
КонецПроцедуры

Процедура ОбработатьОшибкуРасширеннойАутентификации(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'Ошибка аутентификации по SMS.'");
	ВидОперации = НСтр("ru = 'Отправка одноразового пароля из SMS.'");
	
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОбработатьОшибкуОтправкиПароляПоSMSВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'Ошибка отправки одноразового пароля по SMS.'");
	ВидОперации = НСтр("ru = 'Отправка одноразового пароля в SMS.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиСоединенияВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При установке соединения с банком произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Установка соединения с банком.'");
	
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиСоединения,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПредъявитьДанныеАутентификацииПослеПолучения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("СертификатBase64", Результат.СертификатBase64);
	
	Оповещение = Новый ОписаниеОповещения(
		"УстановитьСоединениеПослеАутентификацииНаКлючеВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ВызватьАутентификациюНаЭлектронномКлючеВК(
		Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, Результат.СертификатBase64, Результат.Пароль);
	
КонецПроцедуры

Процедура УстановитьСоединениеПослеАутентификацииНаКлючеВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения(
		"ДополнитьСертификатПослеУстановкиСоединенияВК", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьСоединениеВК(Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль,
		ДополнительныеПараметры.СертификатBase64, ДополнительныеПараметры.ПараметрыСоединения);
	
КонецПроцедуры

Процедура ДополнитьСертификатПослеУстановкиСоединенияВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Истина Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения("ПослеДополненияКлючаВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуДополненияДанныхКлючаВК", ЭтотОбъект);
		
	// Сертификат необходимо дополнить для проверки подписи при тесте настроек
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовДополнитьДанныеКлючаЭП(
		Оповещение, ДополнительныеПараметры.СертификатBase64)
	
КонецПроцедуры

Процедура ПослеДополненияКлючаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДополнительныеПараметры.Вставить("СертификатBase64", РезультатВызова);
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияДанныхКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, РезультатВызова);
	
КонецПроцедуры

Процедура ПослеПолученияДанныхКлючаВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(Результат) = Тип("Структура") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
		Возврат;
	КонецЕсли;
	
	Результат.Вставить("СертификатBase64", ДополнительныеПараметры.СертификатBase64);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата, Результат);
	
КонецПроцедуры

Процедура ОбработатьОшибкуДополненияДанныхКлючаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При работе с электронным ключем произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Дополнение данных ключа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещенияПослеПолученияДанныхСертификата,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеУстановкиПароляВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеАутентификацииВК, Истина);
	
КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиПароляВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	Если ЗначениеЗаполнено(ДополнительныеПараметры.СертификатСсылка) Тогда
		УдалитьПарольИзСеанса(ДополнительныеПараметры.СертификатСсылка);
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	ТекстОшибки = НСтр("ru = 'Ошибка установки пароля.'");
	ВидОперации = НСтр("ru = 'Установка пароля ключа.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеАутентификацииВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстОшибки);
		
КонецПроцедуры

Процедура ВыбратьКлючПослеПолученияХранилищКлючейЭП(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат.Количество() > 1 Тогда
		СписокВыбора = Новый СписокЗначений;
		Для Каждого Элемент Из Результат Цикл
			СписокВыбора.Добавить(Элемент);
		КонецЦикла;
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПослеВыбораКлючаВК", ЭтотОбъект, ДополнительныеПараметры);
		ЗаголовокВыбора = НСтр("ru = 'Выберите электронный ключ:'");
		СписокВыбора.ПоказатьВыборЭлемента(ОповещениеОЗакрытии, ЗаголовокВыбора);
	Иначе
		ИдентификаторХранилища = Результат[0];
		ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
		ОбработчикПродолжения = Новый ОписаниеОповещения(
			"ПолучитьКлючиПослеУстановкиПинВК", ЭтотОбъект, ДополнительныеПараметры);
		УстановитьПинЕслиТребуетсяВК(
			ОбработчикПродолжения, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторХранилища);
	КонецЕсли;
	
КонецПроцедуры

Процедура ПослеВыбораКлючаВК(ВыбранныйЭлемент, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныйЭлемент = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Неопределено);
		Возврат;
	КонецЕсли;
	
	ИдентификаторХранилища = ВыбранныйЭлемент.Значение;
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	ОбработчикПродолжения = Новый ОписаниеОповещения(
		"ПолучитьКлючиПослеУстановкиПинВК", ЭтотОбъект, ДополнительныеПараметры);
	УстановитьПинЕслиТребуетсяВК(
		ОбработчикПродолжения, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура УстановитьПинЕслиТребуетсяВК(Обработчик, ПодключаемыйМодуль, ИдентификаторХранилища)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеУстановкиПин", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ОповещениеПослеПроверкиНеобходимостиPIN = Новый ОписаниеОповещения(
		"УстановитьПинПослеПроверкиНеобходимостиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ПроверитьНеобходимостьУстановкиПинЧерезВК(
		ОповещениеПослеПроверкиНеобходимостиPIN, ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура УстановитьПинПослеПроверкиНеобходимостиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Результат);
		Возврат;
	КонецЕсли;
	
	Если Результат Тогда
		ОбработчикВводаPIN = Новый ОписаниеОповещения("ОбработатьВводPINВК", ЭтотОбъект, ДополнительныеПараметры);
		ПараметрыФормы = Новый Структура;
		ПараметрыФормы.Вставить("ИдентификаторХранилища", ДополнительныеПараметры.ИдентификаторХранилища);
		ОткрытьФорму(
			"Обработка.ОбменСБанками.Форма.ЗапросPINКода", ПараметрыФормы, ЭтотОбъект, Истина , , , ОбработчикВводаPIN);
	Иначе
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Истина); // не требуется пин
	КонецЕсли
	
КонецПроцедуры

Процедура ПроверитьНеобходимостьУстановкиПинЧерезВК(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиНеобходимостиПин", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	ОповещениеПослеПроверкиНеобходимостиPIN = Новый ОписаниеОповещения("ПослеПроверкиНеобходимостиПИНВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиНеобходимостиПИНВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовТребуетсяПин(ОповещениеПослеПроверкиНеобходимостиPIN, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПослеПроверкиНеобходимостиПИНВК(ТребуетсяПин, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиПин, ТребуетсяПин);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПроверкиНеобходимостиПИНВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Проверка необходимости установки ПИН-кода.'");
	ТекстСообщения = НСтр("ru = 'При проверке необходимости ПИН-кода произошла ошибка.'");
	
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПроверкиНеобходимостиПин,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ОбработатьВводPINВК(Результат, ДополнительныеПараметры) Экспорт

	Если Результат = Неопределено ИЛИ Результат = КодВозвратаДиалога.Отмена Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Ложь);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеУстановкиПинВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуУстановкиПинВК", ЭтотОбъект);
	
	ДополнительныеПараметры.ПодключаемыйМодуль.НачатьВызовУстановитьПин(
		Оповещение, ДополнительныеПараметры.ИдентификаторХранилища, Результат);

КонецПроцедуры

Процедура ОбработатьОшибкуУстановкиПинВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Установка PIN кода.'");
	ТекстСообщения = НСтр("ru = 'При установки PIN кода произошла ошибка.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеУстановкиПин,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеУстановкиПинВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеУстановкиПин, Истина);
	
КонецПроцедуры

Процедура ПолучитьКлючиПослеУстановкиПинВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Результат);
		Возврат;
	КонецЕсли;
	
	Если Не Результат Тогда // Пользователь отказался вводить пин-код
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, Неопределено);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения(
		"ПолучитьДанныеСертификатовПослеПолученияДвоичныхДанныхЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	ПолучитьДанныеСертификатовХранилищаВК(
		Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовПослеПолученияДвоичныхДанныхЧерезВК(СертификатыКлюча, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(СертификатыКлюча) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, СертификатыКлюча);
		Возврат;
	КонецЕсли;
	
	ДанныеСертификатов = Новый Массив;
	ДополнительныеПараметры.Вставить("ДанныеСертификатов", ДанныеСертификатов);
	ДополнительныеПараметры.Вставить("СертификатыКлюча", СертификатыКлюча);
	ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры)
	
	Если Не ДополнительныеПараметры.СертификатыКлюча.Количество() Тогда // закончен перебор массива
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, ДополнительныеПараметры.ДанныеСертификатов);
		Возврат;
	КонецЕсли;
	
	СертификатВК = ДополнительныеПараметры.СертификатыКлюча[0];
	Если ТипЗнч(СертификатВК) = Тип("ДвоичныеДанные") Тогда
		СертификатВК = Base64Строка(СертификатВК);
	КонецЕсли;
	ДополнительныеПараметры.СертификатыКлюча.Удалить(0);
	ДополнительныеПараметры.Вставить("СертификатBase64", СертификатВК);
	
	Обработчик = Новый ОписаниеОповещения(
		"ДобавитьСертификатВМассивПослеПолученияДанныхЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ПолучитьДанныеКлючаЧерезВК(Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль, СертификатВК);
	
КонецПроцедуры

Процедура ДобавитьСертификатВМассивПослеПолученияДанныхЧерезВК(ДанныеСертификата, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(ДанныеСертификата) = Тип("Структура") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияКлючей, ДанныеСертификата);
		Возврат;
	КонецЕсли;
	
	СтруктураСертификата = Новый Структура;
	СтруктураСертификата.Вставить("СертификатBase64", ДополнительныеПараметры.СертификатBase64);
	СтруктураСертификата.Вставить("ДанныеСертификата", ДанныеСертификата);
	СтруктураСертификата.Вставить("ИдентификаторХранилища",ДанныеСертификата.ИдентификаторХранилища);
	ДополнительныеПараметры.ДанныеСертификатов.Добавить(СтруктураСертификата);
	
	ПолучитьДанныеСертификатовРекурсивноВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ПоказатьВыборПослеПолученияКлючейВК(ДанныеСертификатов, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(ДанныеСертификатов) = Тип("Массив") Тогда // произошла ошибка или пользователь нажал Отмена.
		ВыполнитьОбработкуОповещения(
			ДополнительныеПараметры.ОповещениеПослеПолученияДанныхАутентификации, ДанныеСертификатов);
		Возврат;
	КонецЕсли;
	
	Обработчик = Новый ОписаниеОповещения("ПослеВыбораСертификатаВК", ЭтотОбъект, ДополнительныеПараметры);
	ВыбратьКлючВК(Обработчик, ДополнительныеПараметры.ПодключаемыйМодуль, ДанныеСертификатов);
	
КонецПроцедуры

Процедура ВыбратьКлючВК(Обработчик, ПодключаемыйМодуль, ДанныеСертификатов)
	
	ДанныеВыбора = Новый Соответствие;
		
	Для Каждого Элемент Из ДанныеСертификатов Цикл
		СтруктураСертификата = Новый Структура;
		СтруктураСертификата.Вставить("СертификатBase64", Элемент.СертификатBase64);
		СтруктураСертификата.Вставить("Псевдоним", Элемент.ДанныеСертификата.Псевдоним);
		ДанныеВыбора.Вставить(Элемент.ДанныеСертификата.Отпечаток, СтруктураСертификата);
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура("ДанныеВыбора", ДанныеВыбора);
	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ВыборСертификатаДляДобавления", ПараметрыФормы, ЭтотОбъект, , , ,
		Обработчик, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
		
КонецПроцедуры

Процедура ПослеПолученияДанныхСертификатаВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ДанныеСертификата = ДеСериализованныеДанные(РезультатВызова);
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатаВК, ДанныеСертификата);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияДанныхСертификатаВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение данных ключа ЭП.'");
	ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении данных ключа ЭП.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхСертификатаВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);

КонецПроцедуры

Процедура ПослеВыбораСертификатаВК(ДанныеВыбора, ДополнительныеПараметры) Экспорт
	
	Если Не ТипЗнч(ДанныеВыбора) = Тип("Структура") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхАутентификации, ДанныеВыбора);
		Возврат;
	КонецЕсли;
	
	СтруктураВозврата = Новый Структура();
	СтруктураВозврата.Вставить("СертификатBase64", ДанныеВыбора.СертификатBase64);
	СтруктураВозврата.Вставить("Пароль", ДанныеВыбора.Пароль);
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияДанныхАутентификации, СтруктураВозврата);
	
КонецПроцедуры

Процедура ПолучитьДанныеСертификатовХранилищаВК(Обработчик, ПодключаемыйМодуль, ИдентификаторХранилища)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияСертификатовХранилища", Обработчик);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПослеПолученияКлючейЭПВК", ЭтотОбъект, ДополнительныеПараметры, "ОбработатьОшибкуПолученияКлючейВК", ЭтотОбъект);
	
	ПодключаемыйМодуль.НачатьВызовКлючиЭП(Оповещение, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПослеПолученияКлючейЭПВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	МассивДвоичныхДанныхСертификатов = ДеСериализованныеДанные(РезультатВызова);
	
	Если НЕ МассивДвоичныхДанныхСертификатов.Количество() Тогда
		ТекстСообщения = НСтр("ru = 'Отсутствуют сертификаты на банковском ключе'");
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификатовХранилища, ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	МассивСертификатовBase64 = Новый Массив;
	
	Для Каждого Элемент Из МассивДвоичныхДанныхСертификатов Цикл
		МассивСертификатовBase64.Добавить(Base64Строка(Элемент));
	КонецЦикла;
	
	ВыполнитьОбработкуОповещения(
		ДополнительныеПараметры.ОповещениеПослеПолученияСертификатовХранилища, МассивСертификатовBase64);
	
КонецПроцедуры

Процедура ОбработатьОшибкуПолученияКлючейВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ВидОперации = НСтр("ru = 'Получение ключей'");
	ТекстСообщения = НСтр("ru = 'Произошла ошибка при получении ключей.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеПолученияСертификатовХранилища,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
	
КонецПроцедуры

Процедура ПослеИнициализацииВК(РезультатВызова, ПараметрыВызова, ДополнительныеПараметры) Экспорт
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеИнициализацииВК, Истина);
	
КонецПроцедуры

Процедура ОбработатьОшибкуИнициализацииВК(ИнформацияОбОшибке, СтандартнаяОбработка, ДополнительныеПараметры) Экспорт
	
	СтандартнаяОбработка = Ложь;
	ТекстСообщения = НСтр("ru = 'При инициализации внешнего модуля произошла ошибка.'");
	ВидОперации = НСтр("ru = 'Инициализация внешней компоненты.'");
	ПолучитьИнформациюОбОшибкеВК(ДополнительныеПараметры.ОповещениеПослеИнициализацииВК,
		ДополнительныеПараметры.ПодключаемыйМодуль, ВидОперации, ТекстСообщения);
		
КонецПроцедуры

Процедура ПослеВводаОдноразовогоПароляСессияЧерезДопОбработку(Пароль, ДополнительныеПараметры) Экспорт
	
	Если Не ЗначениеЗаполнено(Пароль) Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиОдноразовогоПароля, Ложь);
		Возврат;
	КонецЕсли;
	
	ДанныеРасширеннойАутентификации = Новый Структура("Способ, Сессия, Пароль");
	ДанныеРасширеннойАутентификации.Способ = "SMS";
	ДанныеРасширеннойАутентификации.Сессия = ДополнительныеПараметры.Сессия;
	ДанныеРасширеннойАутентификации.Пароль = Пароль;

	Попытка
		ДополнительныеПараметры.ВнешнийПодключаемыйМодуль.РасширеннаяАутентификация(
			ДополнительныеПараметры.Сертификат, ДанныеРасширеннойАутентификации);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиОдноразовогоПароля, Истина);
	Исключение
		ШаблонОшибки = НСтр("ru = 'Ошибка SMS аутентификации.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ДополнительныеПараметры.ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'SMS аутентификация'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеОтправкиОдноразовогоПароля, Ложь);
	КонецПопытки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбменСИспользованиемДополнительнойОбработки

Процедура РасширеннаяАутентификацияЧерезДопОбработку(ВнешнийПодключаемыйМодуль, Сертификат, Сессия, ОООЗ)

	ИдентификаторСессии = Сессия.Идентификатор;
	
	Попытка
		ВнешнийПодключаемыйМодуль.ОтправитьОдноразовыйПарольРасширеннойАутентификацииПоSMS(
			Сертификат, Сессия);
	Исключение
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ШаблонОшибки = НСтр("ru = 'Ошибка запуска аутентификации по SMS.
									|Код ошибки: ДО-%1
									|%2'");
		ДеталиОшибки = ВнешнийПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(
			ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Установка соединения'");
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		ВыполнитьОбработкуОповещения(ОООЗ, Ложь);
		Возврат;
	КонецПопытки;

	Параметры = Новый Структура;
	Параметры.Вставить("ОповещениеПослеОтправкиОдноразовогоПароля", ОООЗ);
	Параметры.Вставить("Сессия", Сессия);
	Параметры.Вставить("ВнешнийПодключаемыйМодуль", ВнешнийПодключаемыйМодуль);
	Параметры.Вставить("Сертификат", Сертификат);
	Оповещение = Новый ОписаниеОповещения(
		"ПослеВводаОдноразовогоПароляСессияЧерезДопОбработку", ЭтотОбъект, Параметры);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИдентификаторСессии", Сессия.Идентификатор);

	ОткрытьФорму("Обработка.ОбменСБанками.Форма.ЗапросОдноразовогоПароля", ПараметрыФормы, , , , , Оповещение);

КонецПроцедуры

Процедура ПослеУстановкиСоединенияЧерезДополнительнуюОбработку(Успех, ДополнительныеПараметры) Экспорт
	
	Если Не Успех Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецЕсли;
	
	Попытка
		СертификатXML = ДополнительныеПараметры.ПодключаемыйМодуль.ДополнитьСертификат(
			ДополнительныеПараметры.СертификатXML);
	Исключение
		ШаблонОшибки = НСтр("ru = 'При загрузке сертификата произошла ошибка.
								|Код ошибки: ДО-%1
								|%2'");
		ДеталиОшибки = ДополнительныеПараметры.ПодключаемыйМодуль.ДеталиОшибки();
		ТекстСообщения = СтрШаблон(ШаблонОшибки, Формат(ДеталиОшибки.Код, "ЧН=0; ЧГ="), ДеталиОшибки.Сообщение);
		Операция = НСтр("ru = 'Получение дополнительных данных сертификата'");
		ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
		ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ОбработатьОшибку(
			Операция, ПодробноеПредставлениеОшибки, ТекстСообщения, 1);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, Неопределено);
		Возврат;
	КонецПопытки;

	ДанныеСертификата = ДанныеСертификатаЧерезДополнительнуюОбработку(ДополнительныеПараметры.ПодключаемыйМодуль, СертификатXML);
	
	Если Не ДанныеСертификата = Неопределено Тогда
		ДанныеСертификата.Вставить("СертификатXML", СертификатXML);
	КонецЕсли;
	
	ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПолученияСертификата, ДанныеСертификата);
	
КонецПроцедуры

#КонецОбласти

#Область ОбменЧерезВК

// Предлагает пользователю выбрать хранилище и получает список ключей из хранилища (асинхронно).
//
// Параметры:
//  Оповещение - ОписаниеОповещения - вызывается после выполнения метода
//      * Результат - Массив - данные ключей. В элементах содержится структура с элементами:
//                       * СертификатBase64 - Строка - данные сертификата в Base64;
//                       * ДанныеСертификата - Структура - содержит информацию о сертификате:
//                            ** Псевдоним - Строка - псевдоним ключа ЭП;
//                            ** Отпечаток - Строка - уникальный идентификатор ключа ЭП;
//                            ** СерийныйНомер - Строка - серийный номер ключа в hex;
//                            ** ИмяИздателя - Строка - имя издателя сертификата.
//                  - Строка - текст ошибки
//                  - Неопределено - пользователь отказался от продолжать операцию.
//      * ДополнительныеПараметры - контекст вызова.
//  ПодключаемыйМодуль - значение, которое было указано при создании объекта ОписаниеОповещения;
//  ИдентификаторХранилища - Строка - хранилище из которого будут получены сертификаты.
//
Процедура ПолучитьСертификатыИзХранилищаВК(Оповещение, ПодключаемыйМодуль, ИдентификаторХранилища = Неопределено)
	
	ДополнительныеПараметры = Новый Структура();
	ДополнительныеПараметры.Вставить("ОповещениеПослеПолученияКлючей", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ДополнительныеПараметры.Вставить("ИдентификаторХранилища", ИдентификаторХранилища);
	
	ОповещениеПослеПолученияХранилищКлючей = Новый ОписаниеОповещения(
		"ВыбратьКлючПослеПолученияХранилищКлючейЭП", ЭтотОбъект, ДополнительныеПараметры);
			
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(ОповещениеПослеПолученияХранилищКлючей, ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ПроверитьПодписьЧерезВК(Оповещение, ПодключаемыйМодуль, ДанныеBase64, СертификатBase64, ПодписьBase64)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ОповещениеПослеПроверкиПодписи", Оповещение);
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	ОповещениеПослеВызоваПроверкиПодписи = Новый ОписаниеОповещения("ПослеПроверкиПодписиЧерезВК", ЭтотОбъект,
		ДополнительныеПараметры, "ОбработатьОшибкуПроверкиПодписиЧерезВК", ЭтотОбъект);
		
	ПодключаемыйМодуль.НачатьВызовПроверитьПодпись(
		ОповещениеПослеВызоваПроверкиПодписи, СертификатBase64, ДанныеBase64, ПодписьBase64);
	
КонецПроцедуры

Процедура ВойтиНаАппаратноеУстройствоПослеПодключенияЧерезВК(Результат, ДополнительныеПараметры) Экспорт

	Если Не ТипЗнч(Результат) = Тип("Массив") Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;

	ИдентификаторХранилища = Результат.Получить(0);
	
	Оповещение = Новый ОписаниеОповещения(
		"ПроверитьПодписиПослеВходаНаАппаратноеУстройствоЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	УстановитьПинЕслиТребуетсяВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ИдентификаторХранилища);
	
КонецПроцедуры

Процедура ОбеспечитьПодключениеАппаратногоУстройстваПослеИнициализацииВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") ИЛИ Результат = Неопределено Тогда
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;

	ПодключаемыйМодуль = Результат;
	ДополнительныеПараметры.Вставить("ПодключаемыйМодуль", ПодключаемыйМодуль);
	
	Оповещение = Новый ОписаниеОповещения("ВойтиНаАппаратноеУстройствоПослеПодключенияЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбеспечитьПодключениеЭлектронногоКлючаЧерезВК(Оповещение, ПодключаемыйМодуль);
	
КонецПроцедуры

Процедура ПроверитьОчереднуюПодписьЧерезВК(ДополнительныеПараметры)
	
	КоличествоПроверенныхПодписей = ДополнительныеПараметры.РезультатыПроверкиПодписей.Количество();
	Если КоличествоПроверенныхПодписей >= ДополнительныеПараметры.Подписи.Количество() Тогда
		ОбменСБанкамиСлужебныйВызовСервера.СохранитьРезультатыПроверкиПодписей(ДополнительныеПараметры.СообщениеОбмена, ДополнительныеПараметры.РезультатыПроверкиПодписей);
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Истина);
		Возврат;
	КонецЕсли;
	
	Оповещение = Новый ОписаниеОповещения(
		"ОбработатьРезультатПроверкиПодписиЧерезВК", ЭтотОбъект, ДополнительныеПараметры);
		
	ТекущиеДанныеПодписи = ДополнительныеПараметры.Подписи.Получить(КоличествоПроверенныхПодписей);
	
	ПодписьСтрокой = Base64Строка(ТекущиеДанныеПодписи.Подпись);
	Если ТипЗнч(ТекущиеДанныеПодписи.Сертификат) = Тип("Строка") Тогда
		СертификатСтрокой = ТекущиеДанныеПодписи.Сертификат;
	Иначе
		СертификатСтрокой = Base64Строка(ТекущиеДанныеПодписи.Сертификат);
	КонецЕсли;
	ПроверитьПодписьЧерезВК(Оповещение, ДополнительныеПараметры.ПодключаемыйМодуль, ДополнительныеПараметры.ДанныеBase64,
		СертификатСтрокой, ПодписьСтрокой);
	
КонецПроцедуры

Процедура ПроверитьПодписиПослеВходаНаАппаратноеУстройствоЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда // произошла ошибка
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;
	
	Если Не Результат Тогда // Пользователь отказался вводить пин-код
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Неопределено);
		Возврат;
	КонецЕсли;
	
	ДанныеДляПроверкиПодписей = ОбменСБанкамиСлужебныйВызовСервера.СтруктураСодержимогоСообщенияОбмена(
		ДополнительныеПараметры.СообщениеОбмена);
	
	Если ДанныеДляПроверкиПодписей = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры.Вставить("ДанныеBase64", Base64Строка(ДанныеДляПроверкиПодписей.ДанныеЭД));
	
	ДополнительныеПараметры.Вставить("РезультатыПроверкиПодписей", Новый Массив);
	ДополнительныеПараметры.Вставить("Подписи", ДанныеДляПроверкиПодписей.Подписи);
	
	ПроверитьОчереднуюПодписьЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

Процедура ОбработатьРезультатПроверкиПодписиЧерезВК(Результат, ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(Результат) = Тип("Строка") Тогда
		
		ВыполнитьОбработкуОповещения(ДополнительныеПараметры.ОповещениеПослеПроверкиПодписей, Результат);
		Возврат;
	КонецЕсли;
	
	СтруктураЗаписи = Новый Структура;
	СтруктураЗаписи.Вставить("ДатаПроверкиПодписи", ОбщегоНазначенияКлиент.ДатаСеанса());
	СтруктураЗаписи.Вставить("ПодписьВерна", Результат);
	ДополнительныеПараметры.РезультатыПроверкиПодписей.Добавить(СтруктураЗаписи);
	ПроверитьОчереднуюПодписьЧерезВК(ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти
