Процедура ПечатьДоговоров(ИмяМенеджераПечати, ИмяМакета, СоставДокументов, ФормаИсточник) Экспорт
	
	Если Не УправлениеПечатьюКлиент.ПроверитьДокументыПроведены(СоставДокументов, ФормаИсточник) Тогда
		Возврат;	
	КонецЕсли;
	
	// Проверим количество объектов
	Если СоставДокументов.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТекстСообщения = ?(СоставДокументов.Количество() > 1, 
		НСтр("ru = 'Выполняется формирование печатных форм...'"),
		НСтр("ru = 'Выполняется формирование печатной формы...'"));
	Состояние(ТекстСообщения);
	
	МакетИДанныеОбъекта = УправлениеПечатьюВызовСервера.ПолучитьМакетыИДанныеОбъектов(ИмяМенеджераПечати,ИмяМакета, СоставДокументов);
	
	
	Для Каждого ДокументСсылка Из СоставДокументов Цикл
		НапечататьУченическийДоговорMSWord(ДокументСсылка, МакетИДанныеОбъекта, ИмяМакета, МакетИДанныеОбъекта.ЛокальныйКаталогФайловПечати);
	КонецЦикла;
	
КонецПроцедуры

Процедура НапечататьУченическийДоговорMSWord(ДокументСсылка, МакетИДанныеОбъекта, ИмяМакета, ЛокальныйКаталогФайловПечати)
	
	ТипМакета				= МакетИДанныеОбъекта.Макеты.ТипыМакетов[ИмяМакета];
	ДвоичныеДанныеМакетов	= МакетИДанныеОбъекта.Макеты.ДвоичныеДанныеМакетов;
	Области					= МакетИДанныеОбъекта.Макеты.ОписаниеОбластей;
	ДанныеОбъекта = МакетИДанныеОбъекта.Данные[ДокументСсылка][ИмяМакета];
	
	Попытка
		Макет = УправлениеПечатьюКлиент.ИнициализироватьМакет(ДвоичныеДанныеМакетов[ИмяМакета], ТипМакета, ЛокальныйКаталогФайловПечати, ИмяМакета);
		Если Макет = Неопределено Тогда
			Возврат;
		КонецЕсли;
		ПечатнаяФорма = УправлениеПечатьюКлиент.ИнициализироватьПечатнуюФорму(ТипМакета, Макет.НастройкиСтраницыМакета, Макет);
		Если ПечатнаяФорма = Неопределено Тогда
			УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
			Возврат;
		КонецЕсли;
		
		СписокОбластей = Области.Получить(ИмяМакета);
		
		Для каждого Область Из СписокОбластей Цикл
			
			ОбластьМакета = УправлениеПечатьюКлиент.ПолучитьОбласть(Макет, Области[ИмяМакета][Область.Ключ]);
			Если ОбластьМакета.ОписаниеОбласти.ТипОбласти = "Список" Тогда
				УправлениеПечатьюКлиент.ПрисоединитьИЗаполнитьКоллекцию(ПечатнаяФорма, ОбластьМакета, ДанныеОбъекта[ОбластьМакета.ОписаниеОбласти.ИмяОбласти],Ложь);   
				
			ИначеЕсли ОбластьМакета.ОписаниеОбласти.ТипОбласти = "СтрокаТаблицы" Тогда
				УправлениеПечатьюMSWordКлиент.ПрисоединитьИЗаполнитьОбластьТаблицы(ПечатнаяФорма, ОбластьМакета, ДанныеОбъекта[ОбластьМакета.ОписаниеОбласти.ИмяОбласти], );
				
			ИначеЕсли ОбластьМакета.ОписаниеОбласти.ТипОбласти = "Общая" Тогда
				УправлениеПечатьюКлиент.ПрисоединитьОбластьИЗаполнитьПараметры(ПечатнаяФорма, ОбластьМакета, ДанныеОбъекта, Ложь);
			КонецЕсли;
		КонецЦикла;
		
		//Показать документа после заполнения параметров макета
		УправлениеПечатьюКлиент.ПоказатьДокумент(ПечатнаяФорма);
		
		
	Исключение
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
		УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма);
		УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);
		Возврат;
	КонецПопытки;
			
	
	УправлениеПечатьюКлиент.ОчиститьСсылки(ПечатнаяФорма, Ложь);
	УправлениеПечатьюКлиент.ОчиститьСсылки(Макет);

КонецПроцедуры
