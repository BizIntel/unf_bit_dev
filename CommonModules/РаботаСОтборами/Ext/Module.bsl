
#Область МеткиОтборов

Процедура ПрикрепитьМеткуОтбора(Форма, ИмяПоляОтбора, ИмяГруппыРодителя, Метка, НаименованиеМетки, ИмяСписка = "", ИмяПараметраЗапроса="", ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	СоздатьЭлементыМеток(Форма, ИмяПоляОтбора, ИмяГруппыРодителя, Метка, НаименованиеМетки, ИмяСписка, ИмяПараметраЗапроса, ИмяТЧДанныеМеток);
	ОбновитьЭлементыМеток(Форма,, ИмяТЧДанныеМеток);
	
КонецПроцедуры

Процедура ПрикрепитьМеткиОтбораИзМассива(Форма, ИмяПоляОтбора, ИмяГруппыРодителя, МассивМеток) Экспорт
	
	Для Каждого значениеМетки Из МассивМеток Цикл
		РаботаСОтборами.СоздатьЭлементыМеток(Форма, ИмяПоляОтбора, ИмяГруппыРодителя, значениеМетки, Строка(значениеМетки));
	КонецЦикла;
	
КонецПроцедуры

Процедура СоздатьЭлементыМеток(Форма, ИмяПоляОтбора, ИмяГруппыРодителя, Метка, НаименованиеМетки, ИмяСписка = "", ИмяПараметраЗапроса="", ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	Элементы = Форма.Элементы;
	ДанныеМеток = Форма[ИмяТЧДанныеМеток];
	
	Если ДанныеМеток.Количество() > 0 Тогда
		Если ИмяСписка <> "" И ДанныеМеток[0].Свойство("ИмяСписка") Тогда
			СтруктураПоискаЗначенияОтбора = Новый Структура("Метка, ИмяСписка, ИмяПоляОтбора", Метка, ИмяСписка, ИмяПоляОтбора);
		Иначе
			СтруктураПоискаЗначенияОтбора = Новый Структура("Метка, ИмяПоляОтбора", Метка, ИмяПоляОтбора);
		КонецЕсли;
		Если ДанныеМеток.НайтиСтроки(СтруктураПоискаЗначенияОтбора).Количество() > 0 Тогда
			//добавляется значение отбора, по которому уже есть отбор
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(Метка)=Тип("Массив") Тогда
		СписокМеток = Новый СписокЗначений;
		СписокМеток.ЗагрузитьЗначения(Метка);
		Метка = СписокМеток;
	КонецЕсли; 
	
	СтрокаМеток = ДанныеМеток.Добавить();
	НавигационнаяСсылкаФС = ПолучитьНачалоИмениМетки(ИмяТЧДанныеМеток) + СтрокаМеток.ПолучитьИдентификатор();
	
	СтрокаМеток.Метка = Метка;
	СтрокаМеток.ИмяПоляОтбора		= ИмяПоляОтбора;
	СтрокаМеток.ИмяГруппыРодителя	= ИмяГруппыРодителя;
	Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() <> ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
		ПредставлениеМетки = ФорматированнаяСтрокаПредставленияМетки(Лев(НаименованиеМетки,16), НавигационнаяСсылкаФС);
	Иначе
		ПредставлениеМетки = ФорматированнаяСтрокаПредставленияМетки(Лев(НаименованиеМетки,21), НавигационнаяСсылкаФС);
	КонецЕсли;
	СтрокаМеток.ПредставлениеМетки = ПредставлениеМетки;
	Если СтрокаМеток.Свойство("ИмяСписка") Тогда
		СтрокаМеток.ИмяСписка = ИмяСписка;
	КонецЕсли;
	Если СтрокаМеток.Свойство("ИмяПараметраЗапроса") Тогда
		СтрокаМеток.ИмяПараметраЗапроса= ИмяПараметраЗапроса;
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьОтборСписка(Форма, СписокОтбора, ИмяПоляОтбора, ИмяСписка = "", ИспользованиеОтбора=Неопределено, ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	МассивОтбора = Новый Массив;
	Для каждого стр Из Форма[ИмяТЧДанныеМеток] Цикл
		Если стр.ИмяПоляОтбора = ИмяПоляОтбора И (ИмяСписка = "" ИЛИ (стр.Свойство("ИмяСписка") И ИмяСписка = стр.ИмяСписка)) Тогда
			Если ТипЗнч(стр.Метка)=Тип("СписокЗначений") Тогда
				Для каждого значениеСписка Из стр.Метка Цикл
				    МассивОтбора.Добавить(значениеСписка.Значение);
				КонецЦикла; 
			Иначе	
				МассивОтбора.Добавить(стр.Метка);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если ИспользованиеОтбора=Неопределено Тогда
		ИспользованиеОтбора = ЗначениеЗаполнено(МассивОтбора);
	КонецЕсли;
	
	УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(СписокОтбора, ИмяПоляОтбора, МассивОтбора, ИспользованиеОтбора,
		ВидСравненияКомпоновкиДанных.ВСписке);
	
КонецПроцедуры

Процедура УстановитьПараметрЗапросаСписка(Форма, СписокОтбора, ИмяПоляОтбора, ИмяПараметраЗапроса, ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	МассивОтбора = Новый Массив;
	СтрокиЗначенийОтбора = Форма[ИмяТЧДанныеМеток].НайтиСтроки(Новый Структура("ИмяПоляОтбора", ИмяПоляОтбора));
	Для каждого строкаОтборов Из СтрокиЗначенийОтбора Цикл
		Если ТипЗнч(строкаОтборов.Метка)=Тип("СписокЗначений") Тогда
			Для каждого значениеСписка Из строкаОтборов.Метка Цикл
				МассивОтбора.Добавить(значениеСписка.Значение);
			КонецЦикла;
		Иначе	
			МассивОтбора.Добавить(строкаОтборов.Метка);
		КонецЕсли;
	КонецЦикла;
	
	СписокОтбора.Параметры.УстановитьЗначениеПараметра("БезОтбора", НЕ ЗначениеЗаполнено(МассивОтбора));
	СписокОтбора.Параметры.УстановитьЗначениеПараметра(ИмяПараметраЗапроса, МассивОтбора);
	
КонецПроцедуры

Функция ПолучитьНачалоИмениМетки(ИмяТЧДанныеМеток) Экспорт
	
	Если ИмяТЧДанныеМеток = "ДанныеМеток" Тогда
		НачалоИмениМетки = "Метка_";
	ИначеЕсли ИмяТЧДанныеМеток = "ДанныеМетокПП" Тогда
		НачалоИмениМетки = "МеткаПП_";
	Иначе
		НачалоИмениМетки = "МеткаОплатаКартами_";
	КонецЕсли;
	
	Возврат НачалоИмениМетки;
	
КонецФункции

Процедура ОбновитьЭлементыМеток(Форма, СписокГруппФормыДляУдаленияДобавленныхЭлементов=Неопределено, ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	Элементы = Форма.Элементы;
	ДанныеМеток = Форма[ИмяТЧДанныеМеток];
	
	Если СписокГруппФормыДляУдаленияДобавленныхЭлементов=Неопределено Тогда
		СписокГруппФормыДляУдаленияДобавленныхЭлементов = ПолучитьСписокИмяГруппыРодителя(ДанныеМеток);
	КонецЕсли;
	
	УдаляемыеЭлементы = Новый Массив;
	
	Для каждого группаФормы Из СписокГруппФормыДляУдаленияДобавленныхЭлементов Цикл
		ДобавитьМеткиДляУдаления(Форма.Элементы[группаФормы], УдаляемыеЭлементы);
	КонецЦикла;
	Для Каждого УдаляемыйЭлемент Из УдаляемыеЭлементы Цикл
		Элементы.Удалить(УдаляемыйЭлемент);
	КонецЦикла;
	
	НомерМетки = 0;
	Для Каждого ДанныеМетки Из ДанныеМеток Цикл
		
		ГруппаРодитель = Форма.Элементы[ДанныеМетки.ИмяГруппыРодителя];
		
		ПолеМетки = Элементы.Добавить(ПолучитьНачалоИмениМетки(ИмяТЧДанныеМеток) + НомерМетки, Тип("ПолеФормы"), ГруппаРодитель);
		ПолеМетки.Вид = ВидПоляФормы.ПолеНадписи;
		ПолеМетки.ПутьКДанным = ИмяТЧДанныеМеток + "[" + НомерМетки + "].ПредставлениеМетки";
		ПолеМетки.ПоложениеЗаголовка = ПоложениеЗаголовкаЭлементаФормы.Нет;
		ПолеМетки.РастягиватьПоГоризонтали = Истина;
		ПолеМетки.УстановитьДействие("ОбработкаНавигационнойСсылки", "Подключаемый_МеткаОбработкаНавигационнойСсылки");
		ПолеМетки.Подсказка = ДанныеМетки.Метка;
		// Обход ошибки платформы для тонкого клиента и интерфейсов, которые не "Такси".
		Если ТекущийВариантИнтерфейсаКлиентскогоПриложения() <> ВариантИнтерфейсаКлиентскогоПриложения.Такси Тогда
			Шрифт = Новый Шрифт(ПолеМетки.Шрифт,, 10);
			ПолеМетки.Шрифт = Шрифт;
		КонецЕсли;
		
		НомерМетки = НомерМетки + 1;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура СвернутьРазвернутьОтборыНаСервере(Форма, Видимость, СтруктураИменЭлементов = Неопределено) Экспорт
	
	Элементы = Форма.Элементы;
	
	ИнтерфейсТакси = (ТекущийВариантИнтерфейсаКлиентскогоПриложения() = ВариантИнтерфейсаКлиентскогоПриложения.Такси);
	Если СтруктураИменЭлементов = Неопределено Тогда
		Если Элементы.Найти("ДекорацияРазвернутьОтборы")=Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Элементы.ФильтрыНастройкиИДопИнфо.Видимость	= Видимость;
		Элементы.ДекорацияРазвернутьОтборы.Видимость	= НЕ Видимость;
		Элементы.ПраваяПанель.Ширина = ?(Видимость, ?(ИнтерфейсТакси, 25, 24), 0);
	Иначе
		Элементы[СтруктураИменЭлементов.ФильтрыНастройкиИДопИнфо].Видимость	= Видимость;
		Элементы[СтруктураИменЭлементов.ДекорацияРазвернутьОтборы].Видимость = НЕ Видимость;
		Элементы[СтруктураИменЭлементов.ПраваяПанель].Ширина = ?(Видимость, ?(ИнтерфейсТакси, 25, 24), 0);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьМеткуОтбораСервер(Форма, СписокОтбора, МеткаИД, ИмяСписка = "", ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	ДанныеМеток = Форма[ИмяТЧДанныеМеток];
	
	СтрокаМеток = ДанныеМеток[Число(МеткаИД)];
	ИмяПоляОтбора = СтрокаМеток.ИмяПоляОтбора;
	ИмяПараметраЗапроса = ?(СтрокаМеток.Свойство("ИмяПараметраЗапроса"), СтрокаМеток.ИмяПараметраЗапроса,"");
	
	СписокГруппФормыДляУдаленияДобавленныхЭлементов = ПолучитьСписокИмяГруппыРодителя(ДанныеМеток);
	
	ДанныеМеток.Удалить(СтрокаМеток);
	
	ОбновитьЭлементыМеток(Форма, СписокГруппФормыДляУдаленияДобавленныхЭлементов, ИмяТЧДанныеМеток);
	Если ИмяПараметраЗапроса="" Тогда
		УстановитьОтборСписка(Форма, СписокОтбора, ИмяПоляОтбора, ИмяСписка);
	Иначе
		УстановитьПараметрЗапросаСписка(Форма, СписокОтбора, ИмяПоляОтбора, ИмяПараметраЗапроса);
	КонецЕсли;
	
КонецПроцедуры

Процедура УдалитьМеткиОтбораСервер(Форма, СписокОтбора, Метки, ИмяСписка = "", ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	Элементы = Форма.Элементы;
	ДанныеМеток = Форма[ИмяТЧДанныеМеток];
	
	Метки.СортироватьПоЗначению(НаправлениеСортировки.Убыв);
	
	СписокГруппФормыДляУдаленияДобавленныхЭлементов = ПолучитьСписокИмяГруппыРодителя(ДанныеМеток);
	
	Для каждого МеткаИД Из Метки Цикл
		
		СтрокаМеток = ДанныеМеток[Число(МеткаИД.Значение)];
		ИмяПоляОтбора = СтрокаМеток.ИмяПоляОтбора;
		ИмяПараметраЗапроса = ?(СтрокаМеток.Свойство("ИмяПараметраЗапроса"), СтрокаМеток.ИмяПараметраЗапроса,"");
		
		ДанныеМеток.Удалить(СтрокаМеток);
		
		Если ИмяПараметраЗапроса="" Тогда
			УстановитьОтборСписка(Форма, СписокОтбора, ИмяПоляОтбора, ИмяСписка);
		Иначе
			УстановитьПараметрЗапросаСписка(Форма, СписокОтбора, ИмяПоляОтбора, ИмяПараметраЗапроса);
		КонецЕсли;
		
	КонецЦикла;
	
	ОбновитьЭлементыМеток(Форма, СписокГруппФормыДляУдаленияДобавленныхЭлементов, ИмяТЧДанныеМеток);
	
КонецПроцедуры

Функция ПолучитьСписокИмяГруппыРодителя(ДанныеМеток)
	
	СписокГруппФормыДляУдаленияДобавленныхЭлементов = ДанныеМеток.Выгрузить();
	СписокГруппФормыДляУдаленияДобавленныхЭлементов.Свернуть("ИмяГруппыРодителя","");
	
	Возврат СписокГруппФормыДляУдаленияДобавленныхЭлементов.ВыгрузитьКолонку("ИмяГруппыРодителя");
	
КонецФункции

Процедура СохранитьНастройкиОтборов(Знач Форма, ИмяСписка = "", СтруктураИменЭлементов = Неопределено, ВариантОтборовФормы="", УстановитьОтборПоПериоду = Истина, ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	ИмяКлючаОбъекта = СтрЗаменить(Форма.ИмяФормы,".","")+ВариантОтборовФормы;
	
	Если ИмяСписка = "" Тогда
		ДанныеМеток = Форма[ИмяТЧДанныеМеток].Выгрузить();
	Иначе
		ДанныеМеток = Форма[ИмяТЧДанныеМеток].Выгрузить();
	КонецЕсли;
	
	Если СтруктураИменЭлементов = Неопределено Тогда
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ДанныеМеток", ДанныеМеток);
		Если УстановитьОтборПоПериоду Тогда
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ОтборПоПериоду", Форма.ОтборПериод);
		КонецЕсли;
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ВидимостьПанелиОтборов", Форма.Элементы.ФильтрыНастройкиИДопИнфо.Видимость);
	Иначе
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ДанныеМеток", ДанныеМеток);
		Если УстановитьОтборПоПериоду Тогда
			ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ОтборПоПериоду", Форма[СтруктураИменЭлементов.ОтборПериод]);
		КонецЕсли; 
		ОбщегоНазначения.ХранилищеОбщихНастроекСохранить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ВидимостьПанелиОтборов", Форма.Элементы[СтруктураИменЭлементов.ФильтрыНастройкиИДопИнфо].Видимость);
	КонецЕсли;
	
КонецПроцедуры

Процедура ВосстановитьНастройкиОтборов(Форма, СписокОтбора, ИмяСписка = "", 
	СтруктураИменЭлементов = Неопределено, СтруктураИменПолейОтборов = Неопределено,
	ВариантОтборовФормы="", УстановитьОтборПоПериоду = Истина, ИмяТЧДанныеМеток = "ДанныеМеток") Экспорт
	
	ИмяКлючаОбъекта = СтрЗаменить(Форма.ИмяФормы,".","")+ВариантОтборовФормы;
	//Отбор по полям правой панели
	СохраненноеЗначение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ДанныеМеток");
	ЕстьОтборТекущегоСписка = Ложь;
	
	Если ЗначениеЗаполнено(СохраненноеЗначение) Тогда
		
		//Проверить сохраненные отборы, удалить строки, которых нет ДоступныхПоляхКомпоновкиДанных
		Если СохраненноеЗначение.Колонки.Найти("ИмяПараметраЗапроса")=Неопределено Тогда
			МассивДоступныхПолейКомпоновкиДанных = Новый Массив;
			Для каждого ПолеОтбора Из СписокОтбора.Отбор.ДоступныеПоляОтбора.Элементы Цикл
				МассивДоступныхПолейКомпоновкиДанных.Добавить(Строка(ПолеОтбора.Поле));
			КонецЦикла;
			МассивУдалитьОтборы = Новый Массив;
			Для каждого СохраненноеПолеОтбора Из СохраненноеЗначение Цикл
				Если СтрНайти(СохраненноеПолеОтбора.ИмяПоляОтбора,".")<>0 Тогда
					//Для полей табличной части, которые представлены через точку
					МассивПолей = СтрРазделить(СохраненноеПолеОтбора.ИмяПоляОтбора, ".");
					Если МассивПолей.Количество()>0 Тогда
						СохраненноеПолеОтбораИмя = МассивПолей[0];
					КонецЕсли;
				Иначе
					СохраненноеПолеОтбораИмя = СохраненноеПолеОтбора.ИмяПоляОтбора;
				КонецЕсли;
				Если МассивДоступныхПолейКомпоновкиДанных.Найти(СохраненноеПолеОтбораИмя)=Неопределено Тогда
					МассивУдалитьОтборы.Добавить(СохраненноеПолеОтбора);
				КонецЕсли;
			КонецЦикла;
			Для каждого строкаУдалить Из МассивУдалитьОтборы Цикл
				СохраненноеЗначение.Удалить(строкаУдалить);
			КонецЦикла;
		КонецЕсли; 
		
		Форма[ИмяТЧДанныеМеток].Загрузить(СохраненноеЗначение);
		
		//Установить отборы списка по данным в таблице ДанныеМеток
		ЕстьИмяСписка = Ложь; // для форм с несколькими динамическими списками
		ЕстьИмяПараметраЗапроса = Ложь; // для форм, где отбор устанавливается через параметр запроса динамического списка
		
		СтрокаПолейТаблицыДляСвертки = "ИмяПоляОтбора";
		Если СохраненноеЗначение.Колонки.Найти("ИмяСписка")<>Неопределено Тогда
			СтрокаПолейТаблицыДляСвертки = СтрокаПолейТаблицыДляСвертки + ",ИмяСписка";
			ЕстьИмяСписка = Истина;
		КонецЕсли;
		Если СохраненноеЗначение.Колонки.Найти("ИмяПараметраЗапроса")<>Неопределено  Тогда
			СтрокаПолейТаблицыДляСвертки = СтрокаПолейТаблицыДляСвертки + ",ИмяПараметраЗапроса";
			ЕстьИмяПараметраЗапроса = Истина;
		КонецЕсли;
		ТаблицаИменПолейОтборов = СохраненноеЗначение.Скопировать(,СтрокаПолейТаблицыДляСвертки);
		ТаблицаИменПолейОтборов.Свернуть(СтрокаПолейТаблицыДляСвертки, "");
		Для каждого строкаПолейОтборов Из ТаблицаИменПолейОтборов Цикл //цикл по именам полей отбора
			
			//Если нет колонки "ИмяСписка", отбор через компоновку
			//Если есть имя списка, нужно проверить, что поле отбора принадлежит этому списку
			ПолеОтбораПринадлежитСписку = НЕ ЕстьИмяСписка ИЛИ (ЕстьИмяСписка И строкаПолейОтборов.ИмяСписка = ИмяСписка);
			
			Если (ЕстьИмяПараметраЗапроса И строкаПолейОтборов.ИмяПараметраЗапроса<>""
				И ПолеОтбораПринадлежитСписку) Тогда
				//отбор через установку параметров запроса списка
				УстановитьПараметрЗапросаСписка(Форма, СписокОтбора, строкаПолейОтборов.ИмяПоляОтбора, строкаПолейОтборов.ИмяПараметраЗапроса, ИмяТЧДанныеМеток);
				ЕстьОтборТекущегоСписка = Истина;
			ИначеЕсли ПолеОтбораПринадлежитСписку Тогда
				//отбор через компоновку
				УстановитьОтборСписка(Форма, СписокОтбора, строкаПолейОтборов.ИмяПоляОтбора,, Истина, ИмяТЧДанныеМеток);
				ЕстьОтборТекущегоСписка = Истина;
			КонецЕсли;
			
		КонецЦикла;
		
		ОбновитьЭлементыМеток(Форма,, ИмяТЧДанныеМеток);
		
	КонецЕсли;
	
	//Отбор по периоду
	Если УстановитьОтборПоПериоду Тогда
		Если СтруктураИменЭлементов = Неопределено Тогда
			Форма.ОтборПериод = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ОтборПоПериоду");
			Форма.ПредставлениеПериода = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Форма.ОтборПериод);
			Если СтруктураИменПолейОтборов<> Неопределено И СтруктураИменПолейОтборов.Свойство("ОтборПериод") Тогда
				ИмяПоляОтборПериод = СтруктураИменПолейОтборов.ОтборПериод;
			Иначе
				ИмяПоляОтборПериод = "Дата";
			КонецЕсли;
			
			РаботаСОтборамиКлиентСервер.УстановитьОтборПоПериоду(СписокОтбора.КомпоновщикНастроек.Настройки.Отбор, Форма.ОтборПериод.ДатаНачала, Форма.ОтборПериод.ДатаОкончания, ИмяПоляОтборПериод);
		Иначе
			Форма[СтруктураИменЭлементов.ОтборПериод] = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ОтборПоПериоду");
			Форма[СтруктураИменЭлементов.ПредставлениеПериода] = РаботаСОтборамиКлиентСервер.ОбновитьПредставлениеПериода(Форма[СтруктураИменЭлементов.ОтборПериод]);
			РаботаСОтборамиКлиентСервер.УстановитьОтборПоПериоду(СписокОтбора.КомпоновщикНастроек.Настройки.Отбор, 
				Форма[СтруктураИменЭлементов.ОтборПериод].ДатаНачала, 
				Форма[СтруктураИменЭлементов.ОтборПериод].ДатаОкончания);
		КонецЕсли;
	КонецЕсли;
	
	//Видимость панели отборов
	Если Не ЕстьОтборТекущегоСписка И (НЕ УстановитьОтборПоПериоду ИЛИ НЕ ЗначениеЗаполнено(Форма.ОтборПериод)) Тогда
		СохраненноеЗначение = ОбщегоНазначения.ХранилищеОбщихНастроекЗагрузить(ИмяКлючаОбъекта, ИмяКлючаОбъекта+ИмяСписка+"_ВидимостьПанелиОтборов", Истина);
		Если ЗначениеЗаполнено(СохраненноеЗначение) Тогда
			СвернутьРазвернутьОтборыНаСервере(Форма, СохраненноеЗначение, СтруктураИменЭлементов);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьМеткиДляУдаления(ЭлементГруппа, УдаляемыеЭлементы)
	
	Для Каждого СтрокаМеток Из ЭлементГруппа.ПодчиненныеЭлементы Цикл
		Если СтрокаМеток.Вид=ВидПоляФормы.ПолеВвода Тогда
			Продолжить;
		КонецЕсли;
		УдаляемыеЭлементы.Добавить(СтрокаМеток);
	КонецЦикла;
	
КонецПроцедуры

Функция ФорматированнаяСтрокаПредставленияМетки(НаименованиеМетки, НавигационнаяСсылкаФС)
	
	Цвет = ЦветаСтиля.ТекстВторостепеннойНадписи;
	Шрифт = ШрифтыСтиля.ШрифтПравойПанелиОтборов;
	
	КомпонентыФС = Новый Массив;
	КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(НаименованиеМетки + " ", Шрифт, Цвет));
	КомпонентыФС.Добавить(Новый ФорматированнаяСтрока(БиблиотекаКартинок.Очистить, , , , НавигационнаяСсылкаФС));
	
	Возврат Новый ФорматированнаяСтрока(КомпонентыФС);
	
КонецФункции

#КонецОбласти
