
Процедура ЗаполнитьПолеРодительВТаблицеСопоставленияДанных(Значение, ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных)
	
	Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты" Тогда
		
		ИмяПроверяемогоПоля = "Контрагент";
		ИмяЗаполняемогоПоля = "Родитель";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
		
		ИмяПроверяемогоПоля = "Номенклатура";
		ИмяЗаполняемогоПоля = "Родитель";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "РегистрСведений.ЦеныНоменклатуры" Тогда
		
		ИмяПроверяемогоПоля = "ВидЦен";
		ИмяЗаполняемогоПоля = "ВидЦен";
		
	КонецЕсли;

	Для каждого СтрокаТаблицы Из ТаблицаСопоставленияДанных Цикл
		
		Если ИмяЗаполняемогоПоля = "Родитель" Тогда
			
			ОбщееЗначениеЗаполнено = ЗначениеЗаполнено(СтрокаТаблицы[ИмяПроверяемогоПоля]);
			Если НЕ ОбщееЗначениеЗаполнено Тогда
				
				СтрокаТаблицы[ИмяЗаполняемогоПоля] = Значение;
				
			КонецЕсли;
			
		ИначеЕсли ИмяЗаполняемогоПоля = "ВидЦен" Тогда
			
			СтрокаТаблицы[ИмяЗаполняемогоПоля] = Значение;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

Процедура ПриУстановкеОбщегоЗначения(Форма, НастройкиЗагрузкиДанных, ТаблицаСопоставленияДанных) Экспорт
	
	ДополнительныеНастройки = Новый Структура("Форма, НастройкиЗагрузкиДанных, ТаблицаСопоставленияДанных", Форма, НастройкиЗагрузкиДанных, ТаблицаСопоставленияДанных);
	ОписаниеОповещения 		= Новый ОписаниеОповещения("ПриОбработаткеРезультатаВыбораОбщегоЗначения", ЭтотОбъект, ДополнительныеНастройки);
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Истина);
	ПараметрыОткрытия.Вставить("ВыборГруппИЭлементов", ГруппыИЭлементы.Группы);
	
	Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты" Тогда
		
		ИмяФормыВыбораГруппы = "Справочник.Контрагенты.ФормаВыбораГруппы";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
		
		ИмяФормыВыбораГруппы = "Справочник.Номенклатура.ФормаВыбораГруппы";
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "РегистрСведений.ЦеныНоменклатуры" Тогда
		
		ПараметрыОткрытия.ВыборГруппИЭлементов = ГруппыИЭлементы.Элементы;
		ИмяФормыВыбораГруппы = "Справочник.ВидыЦен.ФормаВыбора";
		
	КонецЕсли;
	
	ОткрытьФорму(ИмяФормыВыбораГруппы, ПараметрыОткрытия, ЭтотОбъект, , , , ОписаниеОповещения, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

Процедура ПриОчисткеОбщегоЗначения(Форма, НастройкиЗагрузкиДанных, ТаблицаСопоставленияДанных) Экспорт
	
	Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты" 
		ИЛИ НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
		
		Форма.Элементы.ОбщееЗначениеСправочник.Заголовок = Нстр("ru ='< не указана >'");
		ЗаполнитьПолеРодительВТаблицеСопоставленияДанных(Неопределено, ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных);
		
	ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "РегистрСведений.ЦеныНоменклатуры" Тогда
		
		Форма.Элементы.ОбщееЗначениеРС.Заголовок = Нстр("ru ='< не указана >'");
		ЗаполнитьПолеРодительВТаблицеСопоставленияДанных(ЗагрузкаДанныхИзВнешнегоИсточникаПереопределяемый.ОсновнойВидЦен(), ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриОбработаткеРезультатаВыбораОбщегоЗначения(Результат, ДополнительныеНастройки) Экспорт
	
	Форма = ДополнительныеНастройки.Форма;
	НастройкиЗагрузкиДанных = ДополнительныеНастройки.НастройкиЗагрузкиДанных;
	ТаблицаСопоставленияДанных = ДополнительныеНастройки.ТаблицаСопоставленияДанных;
	
	Если ТаблицаСопоставленияДанных.Количество() > 0 
		И ЗначениеЗаполнено(Результат) Тогда
		
		ЗаполнитьПолеРодительВТаблицеСопоставленияДанных(Результат, ТаблицаСопоставленияДанных, НастройкиЗагрузкиДанных);
		Если НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Контрагенты"
			ИЛИ НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "Справочник.Номенклатура" Тогда
			
			Форма.Элементы.ОбщееЗначениеСправочник.Заголовок = Нстр("ru ='< '") + Строка(Результат) + Нстр("ru =' >'");
			
		ИначеЕсли НастройкиЗагрузкиДанных.ПолноеИмяОбъектаЗаполнения = "РегистрСведений.ЦеныНоменклатуры" Тогда
			
			Форма.Элементы.ОбщееЗначениеРС.Заголовок = Нстр("ru ='< '") + Строка(Результат) + Нстр("ru =' >'");
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры
