////////////////////////////////////////////////////////////////////////////////
// Подсистема "Обмен с кассовым сервером Штрих-М".
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Возвращает результат установки соединенения с сервером.
//
// Параметры:
//  Настройки - Структура - настройки соединения.
//
// Возвращаемое значение:
//  Структура - результат установки соединенения с сервером:
//    * HTTPСоединение - HTTPСоединение - соединение с сервером;
//    * СообщениеОбОшибке - Строка - подробное представление ошибки.
//
Функция HTTPСоединение(Настройки) Экспорт
	
	Результат = Новый Структура("HTTPСоединение, СообщениеОбОшибке", Неопределено, "");
	HTTPСоединение = Неопределено;
	ЗащищенноеСоединение = Неопределено;
	Прокси = Неопределено;
	МодульПолучениеФайловИзИнтернетаКлиентСервер = Неопределено;
	
	СтруктураURL = ОбщегоНазначенияКлиентСервер.СтруктураURI(Настройки.АдресСервера);
	
	Протокол = ?(НЕ ЗначениеЗаполнено(СтруктураURL.Схема), "http", СтруктураURL.Схема);
	Если ВРЕГ(Протокол) = "HTTPS" Тогда
		ЗащищенноеСоединение = Новый ЗащищенноеСоединениеOpenSSL;
	КонецЕсли;
	
	Если СтруктураURL.Порт = Неопределено Тогда
		Если ЗащищенноеСоединение = Неопределено Тогда
			Порт = 80;
		Иначе
			Порт = 443;
		КонецЕсли;
	Иначе
		Порт = СтруктураURL.Порт
	КонецЕсли;
	
	Таймаут = 600;
	
	#Если Сервер Тогда
		Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернетаКлиентСервер =
				ОбщегоНазначения.ОбщийМодуль("ПолучениеФайловИзИнтернетаКлиентСервер");
		КонецЕсли;
	#ИначеЕсли Клиент Тогда
		Если ОбщегоНазначенияКлиент.ПодсистемаСуществует("СтандартныеПодсистемы.ПолучениеФайловИзИнтернета") Тогда
			МодульПолучениеФайловИзИнтернетаКлиентСервер =
				ОбщегоНазначенияКлиент.ОбщийМодуль("ПолучениеФайловИзИнтернетаКлиентСервер");
		КонецЕсли;
	#КонецЕсли
	
	Если НЕ МодульПолучениеФайловИзИнтернетаКлиентСервер = Неопределено Тогда
		Прокси = МодульПолучениеФайловИзИнтернетаКлиентСервер.ПолучитьПрокси(Протокол);
	КонецЕсли;
	
	Попытка
		Настройки.Вставить("ПутьНаСервере", СтруктураURL.ПутьНаСервере);
		HTTPСоединение = Новый HTTPСоединение(СтруктураURL.Хост, Порт, Настройки.ИмяПользователя, Настройки.Пароль,
			Прокси, Таймаут, ЗащищенноеСоединение);
	Исключение
		СообщениеОбОшибке = СтрШаблон(НСтр("ru = 'Ошибка при создании HTTP-соединения с сервером Штрих-М': %1",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()), ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		Результат.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	КонецПопытки;
	Результат.Вставить("HTTPСоединение", HTTPСоединение);
	
	Возврат Результат;
КонецФункции

#КонецОбласти
