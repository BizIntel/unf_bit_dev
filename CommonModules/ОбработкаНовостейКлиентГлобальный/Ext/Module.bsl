////////////////////////////////////////////////////////////////////////////////
// Подсистема "Новости".
// ОбщийМодуль.ОбработкаНовостейКлиентГлобальный.
//
////////////////////////////////////////////////////////////////////////////////

#Область РегулярнаяПроверкаНовостейТребующихПрочтения

// Процедура выполняет проверку новостей (Справочник.Новости) при запуске программы
//  на предмет наличия важных и очень важных новостей, требующих особого внимания (прочтения).
//
// Параметры:
//  Нет.
//
Процедура ПроверитьВажныеНовостиСВключеннымиНапоминаниями_ПервыйЗапуск() Экспорт

	ПроверитьВажныеНовостиСВключеннымиНапоминаниями();

	ОбработкаНовостейКлиент.ПодключитьОбработчикОповещенияОВажныхИОченьВажныхНовостях();

КонецПроцедуры

// Процедура выполняет регулярную проверку новостей (Справочник.Новости)
// на предмет наличия важных и очень важных новостей, требующих особого внимания (прочтения).
//
// Параметры:
//  Нет.
//
Процедура ПроверитьВажныеНовостиСВключеннымиНапоминаниями() Экспорт

	Перем ОченьВажныеНовостиСВключеннымиНапоминаниями, ВажныеНовостиСВключеннымиНапоминаниями;

	ОбработкаНовостейВызовСервера.ПолучитьНовостиСНапоминаниями(
		ОченьВажныеНовостиСВключеннымиНапоминаниями,
		ВажныеНовостиСВключеннымиНапоминаниями);

	// При просмотре глобальных очень важных новостей, открывать каждую в отдельной форме.
	// При просмотре контекстных очень важных новостей, их можно открывать в одной форме.
	Для каждого ТекущаяНовость Из ОченьВажныеНовостиСВключеннымиНапоминаниями Цикл
		Если НЕ ТекущаяНовость.Новость.Пустая() Тогда
			// Возможна ситуация, когда очень важная новость является ссылкой или на интернет-ресурс или на раздел помощи.
			// Необходимо сбросить признак оповещения в форме новости сразу, т.к. по-другому сбросить этот признак не получится.
			ФормаНовости = ОбработкаНовостейКлиент.ПоказатьНовость(
				ТекущаяНовость.Новость, // НовостьСсылка
				, // ПараметрыОткрытияФормы. БлокироватьОкноВладельца не нужно, т.к. неизвестно что будет за владелец
				       // и блокировать первое попавшееся окно неправильно.
				, // ФормаВладелец
				СтрШаблон(
					НСтр("ru='Автопроверка новостей: %1'"),
					ТекущаяНовость.НовостьУникальныйИдентификатор)); // Отдельная уникальность, не зависит от новостей, открытых из формы списка
		КонецЕсли;
	КонецЦикла;

	Если ВажныеНовостиСВключеннымиНапоминаниями.Количество() > 0 Тогда
		ОбработкаНовостейКлиент.НачатьПоследовательныйПоказВажныхНовостей(ВажныеНовостиСВключеннымиНапоминаниями);
	КонецЕсли;

КонецПроцедуры

// Процедура регулярно смотрит в глобальную переменную
//  ПараметрыПриложения["ИнтернетПоддержкаПользователей.Новости.ПоследовательныйПоказВажныхНовостей"] и в случае наличия
//  информации для показа (есть важные новости и они не все показаны) - последовательно выводит их до последней новости.
//
// Параметры:
//  Нет.
//
Процедура ПоследовательныйПоказВажныхНовостей() Экспорт

	ОбработкаНовостейКлиент.ПродолжитьПоследовательныйПоказВажныхНовостей();

КонецПроцедуры

#КонецОбласти

#Область КонтекстныеНовости

// Процедура запускает оптимизацию кеша контекстных новостей
//  (глобальной переменной ПараметрыПриложения["ИнтернетПоддержкаПользователей.Новости.КэшКонтекстныхНовостей"])).
//
// Параметры:
//  Нет.
//
Процедура ОптимизацияВременногоХранилищаКонтекстныхНовостей() Экспорт

	ОбработкаНовостейКлиент.ОптимизацияКонтекстныхНовостейВКешеПриложения();

КонецПроцедуры

#КонецОбласти