#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ЭтаФорма.ТолькоПросмотр = Параметры.ВладелецТолькоПросмотр;
	ЭтаФорма.ВидимостьПоУмолчанию = Параметры.ВидимостьПоУмолчанию;
	ЭтаФорма.СписокИсключений.ЗагрузитьЗначения(Параметры.СписокИсключений.ВыгрузитьЗначения());

	Если ЭтаФорма.ТолькоПросмотр = Истина Тогда
		Элементы.КнопкаОК.Видимость = Ложь;
		Элементы.КнопкаОтмена.Заголовок = НСтр("ru='Закрыть'");
		Элементы.СписокИсключений.ИзменятьПорядокСтрок = Ложь;
		Элементы.СписокИсключений.ИзменятьСоставСтрок  = Ложь;
		Элементы.СписокИсключенийДобавить.Видимость    = Ложь;
		Элементы.СписокИсключенийУдалить.Видимость     = Ложь;
		Элементы.СписокИсключенийСкопировать.Видимость = Ложь;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	// Данные для передачи владельцу
	Результат = ПодготовитьСтруктуруДляВозвратаПоОК();
	ЭтаФорма.Закрыть(Результат);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПодготовитьСтруктуруДляВозвратаПоОК()

	СписокИсключенийБезПовторов = Новый СписокЗначений;
	Для каждого ТекущийЭлементСписка Из ЭтаФорма.СписокИсключений Цикл
		Если НЕ ТекущийЭлементСписка.Значение.Пустая() Тогда
			НайденнаяСтрока = СписокИсключенийБезПовторов.НайтиПоЗначению(ТекущийЭлементСписка.Значение);
			Если НайденнаяСтрока = Неопределено Тогда
				СписокИсключенийБезПовторов.Добавить(ТекущийЭлементСписка.Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;

	Результат = Новый Структура("ВидимостьПоУмолчанию, СписокИсключений",
		ЭтаФорма.ВидимостьПоУмолчанию,
		СписокИсключенийБезПовторов);

	Возврат Результат;

КонецФункции

#КонецОбласти

