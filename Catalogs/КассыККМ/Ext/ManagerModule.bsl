#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

// Функция получает блокируемые реквизиты объекта.
//
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	Результат = Новый Массив;
	Результат.Добавить("ВалютаДенежныхСредств");
	Результат.Добавить("Владелец");
	Результат.Добавить("СтруктурнаяЕдиница");
	Результат.Добавить("Подразделение");
	Результат.Добавить("ТипКассы");
	
	Возврат Результат;
	
КонецФункции // ПолучитьБлокируемыеРеквизитыОбъекта()

// Функция определяет кассу кассу по умолчанию.
//
// Возвращает кассу ККМ, если найдена одна касса.
// Возвращает Неопределено, если касса не найдена или касс больше одной.
//
// Возвращаемое значение:
//	СправочникСсылка.КассыККМ - Найденная касса ККМ
//
Функция ПолучитьКассуККМПоУмолчанию(ТипКассыККМ = Неопределено) Экспорт
	
	Если ТипКассыККМ = Неопределено Тогда
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	КассыККМ.Ссылка КАК КассаККМ
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	(НЕ КассыККМ.ПометкаУдаления)");
	Иначе
		Запрос = Новый Запрос(
		"ВЫБРАТЬ ПЕРВЫЕ 2
		|	КассыККМ.Ссылка КАК КассаККМ
		|ИЗ
		|	Справочник.КассыККМ КАК КассыККМ
		|ГДЕ
		|	НЕ КассыККМ.ПометкаУдаления
		|	И КассыККМ.ТипКассы = &ТипКассы");
		
		Запрос.УстановитьПараметр("ТипКассы", ТипКассыККМ);
	КонецЕсли;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Количество() = 1 
	   И Выборка.Следующий()
	Тогда
		КассаККМ = Выборка.КассаККМ;
	Иначе
		КассаККМ = Неопределено;
	КонецЕсли;
	
	Возврат КассаККМ;

КонецФункции // ПолучитьКассуПоУмолчанию()

// Функция определяет кассу кассу по умолчанию.
//
// Возвращает кассу ККМ, если найдена одна касса.
// Возвращает Неопределено, если касса не найдена или касс больше одной.
//
// Возвращаемое значение:
//	СправочникСсылка.КассыККМ - Найденная касса ККМ
//
Функция ПолучитьКассуККМПоФискальномуУстройству(ФискальноеУстройство) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	КассыККМ.Ссылка КАК КассаККМ
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	КассыККМ.ПодключаемоеОборудование = &ФискальноеУстройство");
	Запрос.УстановитьПараметр("ФискальноеУстройство", ФискальноеУстройство);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		КассаККМ = Выборка.КассаККМ;
	Иначе
		КассаККМ = Неопределено;
	КонецЕсли;
	
	Возврат КассаККМ;

КонецФункции // ПолучитьКассуПоУмолчанию()

// Функция определяет валюту выбранной кассы ККМ.
//
// Параметры:
//  КассаККМ - СправочникСсылка.КассыККМ - Ссылка на кассу ККМ
//
// Возвращаемое значение:
//	Структура - Организация и Валюта выбранной кассы ККМ
//
Функция ПолучитьРеквизитыКассыККМ(Знач КассаККМ) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КассыККМ.СтруктурнаяЕдиница КАК СтруктурнаяЕдиница,
	|	КассыККМ.Подразделение КАК Подразделение,
	|	КассыККМ.Владелец КАК Организация,
	|	КассыККМ.СтруктурнаяЕдиница.РозничныйВидЦен КАК ВидЦен,
	|	КассыККМ.СтруктурнаяЕдиница.РозничныйВидЦен.ЦенаВключаетНДС КАК СуммаВключаетНДС,
	|	КассыККМ.ВалютаДенежныхСредств КАК ВалютаДокумента,
	|	КассыККМ.ТипКассы КАК ТипКассы,
	|	КассыККМ.Ссылка КАК КассаККМ,
	|	КассыККМ.ПодключаемоеОборудование КАК ФискальноеУстройство
	|ИЗ
	|	Справочник.КассыККМ КАК КассыККМ
	|ГДЕ
	|	КассыККМ.Ссылка = &КассаККМ";
	
	Запрос.УстановитьПараметр("КассаККМ", КассаККМ);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		СтруктурнаяЕдиница = Выборка.СтруктурнаяЕдиница;
		Подразделение = Выборка.Подразделение;
		Организация = Выборка.Организация;
		ВидЦен = Выборка.ВидЦен;
		СуммаВключаетНДС = Выборка.СуммаВключаетНДС;
		Если НЕ ЗначениеЗаполнено(ВидЦен) Тогда
			ВидЦен = Справочники.ВидыЦен.Оптовая;
			СуммаВключаетНДС = ВидЦен.ЦенаВключаетНДС;
		КонецЕсли;
		ВалютаДокумента = Выборка.ВалютаДокумента;
		ТипКассы = Выборка.ТипКассы;
		КассаККМ = Выборка.КассаККМ;
		ФискальноеУстройство = Выборка.ФискальноеУстройство;
	Иначе
		СтруктурнаяЕдиница = Неопределено;
		Подразделение = Неопределено;
		Организация = Неопределено;
		ВидЦен = Неопределено;
		СуммаВключаетНДС = Неопределено;
		ВалютаДокумента = Неопределено;
		ТипКассы = Неопределено;
		КассаККМ = Неопределено;
		ФискальноеУстройство = Неопределено;
	КонецЕсли;
	
	СтруктураРеквизитов = Новый Структура("СтруктурнаяЕдиница, Подразделение, Организация, ВидЦен, СуммаВключаетНДС, ВалютаДокумента, ТипКассы, КассаККМ, ФискальноеУстройство",
		СтруктурнаяЕдиница,
		Подразделение,
		Организация,
		ВидЦен,
		СуммаВключаетНДС,
		ВалютаДокумента,
		ТипКассы,
		КассаККМ,
		ФискальноеУстройство
	);
	
	Возврат СтруктураРеквизитов;

КонецФункции // ПолучитьРеквизитыКассыККМ()

#Область ИнтерфейсПечати

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли