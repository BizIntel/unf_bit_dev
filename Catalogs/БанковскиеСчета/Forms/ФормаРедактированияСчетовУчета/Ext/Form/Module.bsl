
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СчетУчета = Параметры.СчетУчета;
	Ссылка = Параметры.Ссылка;
	ВладелецОрганизация = ТипЗнч(Ссылка.Владелец) = Тип("СправочникСсылка.Организации");
	
	Если ОтказИзменитьСчетУчета(Ссылка) Тогда
		Элементы.ГруппаСчетовУчета.Подсказка = НСтр("ru = 'В базе есть движения по этому банковскому счету! Изменение счета учета запрещено!'");
		Элементы.ГруппаСчетовУчета.Доступность = Ложь;
		Элементы.ПоУмолчанию.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если НЕ ВладелецОрганизация Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(, НСтр("ru='Счета учетов редактируются только для банковских счетов организаций!'"));
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СчетУчетаПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СчетУчета) Тогда
		СчетУчета = ПредопределенноеЗначение("ПланСчетов.Управленческий.Банк");
	КонецЕсли;
	ОповеститьОбИзмененииСчетов();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПоУмолчанию(Команда)
	
	СчетУчета = ПредопределенноеЗначение("ПланСчетов.Управленческий.Банк");
	ОповеститьОбИзмененииСчетов();
	
КонецПроцедуры // ПоУмолчанию()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Функция проверяет возможность изменения счета учета.
//
&НаСервере
Функция ОтказИзменитьСчетУчета(Ссылка)
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДенежныеСредства.Период,
	|	ДенежныеСредства.Регистратор,
	|	ДенежныеСредства.НомерСтроки,
	|	ДенежныеСредства.Активность,
	|	ДенежныеСредства.ВидДвижения,
	|	ДенежныеСредства.Организация,
	|	ДенежныеСредства.ТипДенежныхСредств,
	|	ДенежныеСредства.БанковскийСчетКасса,
	|	ДенежныеСредства.Валюта,
	|	ДенежныеСредства.Сумма,
	|	ДенежныеСредства.СуммаВал,
	|	ДенежныеСредства.СодержаниеПроводки,
	|	ДенежныеСредства.Статья
	|ИЗ
	|	РегистрНакопления.ДенежныеСредства КАК ДенежныеСредства
	|ГДЕ
	|	ДенежныеСредства.БанковскийСчетКасса = &БанковскийСчетКасса");
	
	Запрос.УстановитьПараметр("БанковскийСчетКасса", ?(ЗначениеЗаполнено(Ссылка), Ссылка, Неопределено));
	
	Результат = Запрос.Выполнить();
	
	Возврат НЕ Результат.Пустой();

КонецФункции // ОтказИзменитьСчетУчета()

&НаКлиенте
Процедура ОповеститьОбИзмененииСчетов()
	
	СтруктураПараметры = Новый Структура(
		"СчетУчета",
		СчетУчета
	);
	
	Оповестить("ИзменилисьСчетаБанковскиеСчета", СтруктураПараметры);
	
КонецПроцедуры

#КонецОбласти
