#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ОшибкаЗаполнения Экспорт;

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ПометкаУдаления ИЛИ Ссылка.ПометкаУдаления Тогда
		Возврат;
	КонецЕсли;
	
	Наименование = "Доверенность" + ?(ЗначениеЗаполнено(НомерДовер), " №" + НомерДовер, "") + " от " + Формат(ДатаВыдачи, "ДФ='dd.MM.yyyy ""г.""'");
	
	Заголовок = "Запись доверенности налогоплательщика (" + Владелец + ") " + Наименование + ", код " + Код;
	
	// Проверка заполнения обязательных реквизитов
	Если НЕ ЗначениеЗаполнено(Владелец) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не выбрана организация налогоплательщика'"), ЭтотОбъект, "Владелец", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаВыдачи) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указана дата выдачи доверенности'"), ЭтотОбъект, "ДатаВыдачи", , Отказ);
	КонецЕсли;
	
	Если ПолномочияПредставителя.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнены сведения о полномочиях представителя'"), ЭтотОбъект, "ПолномочияПредставителя", , Отказ);
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	// проверяем юрлицо-представителя
	Если ПредставительЯвляетсяЮЛ Тогда
		
		// проверка ИНН юрлица-представителя
		Если ЗначениеЗаполнено(ПредставительЮЛ_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(ПредставительЮЛ_ИНН, Ложь) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН организации-представителя'"), ЭтотОбъект, "ПредставительЮЛ_ИНН", , Отказ);
		КонецЕсли;
		
		// проверка КПП юрлица-представителя
		Если ЗначениеЗаполнено(ПредставительЮЛ_КПП) И НЕ РегламентированнаяОтчетностьКлиентСервер.КППСоответствуетТребованиям(ПредставительЮЛ_КПП) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан КПП организации-представителя'"), ЭтотОбъект, "ПредставительЮЛ_КПП", , Отказ);
		КонецЕсли;
		
		// проверка ОГРН юрлица-представителя
		Если ЗначениеЗаполнено(ПредставительЮЛ_ОГРН) И СтрДлина(СокрЛП(ПредставительЮЛ_ОГРН)) <> 13 Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ОГРН организации-представителя'"), ЭтотОбъект, "ПредставительЮЛ_ОГРН", , Отказ);
		КонецЕсли;
		
	Иначе
		
		//представитель является физлицом
		Если НЕ ПредставительЯвляетсяСотрудником Тогда
			//должен быть заполнен ИНН физлица-представителя
			Если НЕ ЗначениеЗаполнено(ПредставительФЛ_ИНН) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'У представителя, являющегося физлицом и не являющегося сотрудником организации должен быть задан ИНН'"), ЭтотОбъект, "ПредставительФЛ_ИНН", , Отказ);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	// проверяем физлицо-представителя
	
	// проверка ИНН физлица-представителя
	Если ЗначениеЗаполнено(ПредставительФЛ_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(ПредставительФЛ_ИНН, Истина) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН физлица-представителя'"), ЭтотОбъект, "ПредставительФЛ_ИНН", , Отказ);
	КонецЕсли;
	
	// проверка ОГРН физлица-представителя
	Если ЗначениеЗаполнено(ПредставительФЛ_ОГРН) И СтрДлина(СокрЛП(ПредставительФЛ_ОГРН)) <> 15 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ОГРН физлица-представителя'"), ЭтотОбъект, "ПредставительФЛ_ОГРН", , Отказ);
	КонецЕсли;
	
	Если ПризнакДоверителя = 2 ИЛИ ПризнакДоверителя = 3 Тогда
		
		// проверяем юрлицо-доверителя
		Если ПризнакДоверителя = 3 И ДоверительЯвляетсяЮЛ Тогда // доверитель - уполномоченный представитель
			
			// проверка ИНН юрлица-доверителя
			Если ЗначениеЗаполнено(ДоверительЮЛ_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(ДоверительЮЛ_ИНН, Ложь) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН доверителя-юрлица'"), ЭтотОбъект, "ДоверительЮЛ_ИНН", , Отказ);
			КонецЕсли;
			
			// проверка КПП юрлица-доверителя
			Если ЗначениеЗаполнено(ДоверительЮЛ_КПП) И НЕ РегламентированнаяОтчетностьКлиентСервер.КППСоответствуетТребованиям(ДоверительЮЛ_КПП) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан КПП доверителя-юрлица'"), ЭтотОбъект, "ДоверительЮЛ_КПП", , Отказ);
			КонецЕсли;
			
			// проверка ОГРН юрлица-доверителя
			Если ЗначениеЗаполнено(ДоверительЮЛ_ОГРН) И СтрДлина(СокрЛП(ДоверительЮЛ_ОГРН)) <> 13 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ОГРН доверителя-юрлица'"), ЭтотОбъект, "ДоверительЮЛ_ОГРН", , Отказ);
			КонецЕсли;
			
			// проверка ИНН руководителя юрлица-доверителя
			Если ЗначениеЗаполнено(ДоверительРук_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(ДоверительРук_ИНН, Истина) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН руководителя организации-доверителя'"), ЭтотОбъект, "ДоверительРук_ИНН", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		// проверяем физлицо-доверителя
		Если ПризнакДоверителя = 2 ИЛИ (ПризнакДоверителя = 3 И ДоверительИмеетУЛ) Тогда
			
			// проверка ИНН физлица-доверителя
			Если ЗначениеЗаполнено(ДоверительФЛ_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(ДоверительФЛ_ИНН, Истина) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН физлица-доверителя'"), ЭтотОбъект, "ДоверительФЛ_ИНН", , Отказ);
			КонецЕсли;
			
			// проверка ОГРН физлица-доверителя
			Если ЗначениеЗаполнено(ДоверительФЛ_ОГРН) И СтрДлина(СокрЛП(ДоверительФЛ_ОГРН)) <> 15 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ОГРНИП физлица-доверителя'"), ЭтотОбъект, "ДоверительФЛ_ОГРН", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// проверяем нотариуса
	Если ЗаверенаНотариально Тогда
		
		// проверяем юрлицо-нотариус
		Если НотариусЯвляетсяЮЛ Тогда
			
			// проверка ИНН юрлица-нотариуса
			Если ЗначениеЗаполнено(НотариусЮЛ_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(НотариусЮЛ_ИНН, Ложь) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН юрлица нотариуса'"), ЭтотОбъект, "НотариусЮЛ_ИНН", , Отказ);
			КонецЕсли;
			
			// проверка КПП юрлица-нотариуса
			Если ЗначениеЗаполнено(НотариусЮЛ_КПП) И НЕ РегламентированнаяОтчетностьКлиентСервер.КППСоответствуетТребованиям(НотариусЮЛ_КПП) Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан КПП юрлица нотариуса'"), ЭтотОбъект, "НотариусЮЛ_КПП", , Отказ);
			КонецЕсли;
			
			// проверка ОГРН юрлица-нотариуса
			Если ЗначениеЗаполнено(НотариусЮЛ_ОГРН) И СтрДлина(СокрЛП(НотариусЮЛ_ОГРН)) <> 13 Тогда
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ОГРН юрлица нотариуса'"), ЭтотОбъект, "НотариусЮЛ_ОГРН", , Отказ);
			КонецЕсли;
			
		КонецЕсли;
		
		// проверка ИНН юрлица-нотариуса
		Если ЗначениеЗаполнено(НотариусФЛ_ИНН) И НЕ РегламентированнаяОтчетностьВызовСервера.ИННСоответствуетТребованиямНаСервере(НотариусФЛ_ИНН, Истина) Тогда
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Некорректно задан ИНН нотариуса-физлица'"), ЭтотОбъект, "НотариусФЛ_ИНН", , Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
        Владелец = Модуль.ОрганизацияПоУмолчанию();
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли