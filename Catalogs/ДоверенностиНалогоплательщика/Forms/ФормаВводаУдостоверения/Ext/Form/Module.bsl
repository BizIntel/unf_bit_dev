#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		Элементы.ДокументВид.РежимВыбораИзСписка = Ложь;
		Элементы.ДокументВид.ОграничениеТипа = Новый ОписаниеТипов("СправочникСсылка.ВидыДокументовФизическихЛиц");
	Иначе
		Элементы.ДокументВид.РежимВыбораИзСписка = Истина;
		Элементы.ДокументВид.ОграничениеТипа = Новый ОписаниеТипов("Строка");
	КонецЕсли;
	
	СтруктураДанных = Параметры.СтруктураДанных;
	Если СтруктураДанных <> Неопределено Тогда
		ДокументВид					= СтруктураДанных.ДокументВид;
		ДокументДатаВыдачи			= СтруктураДанных.ДокументДатаВыдачи;
		ДокументКемВыдан			= СтруктураДанных.ДокументКемВыдан;
		ДокументКодПодразделения	= СтруктураДанных.ДокументКодПодразделения;
		ДокументНомер				= СтруктураДанных.ДокументНомер;
		ДокументСерия				= СтруктураДанных.ДокументСерия;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодтвердитьИзменения(Команда)
	
	Если НЕ СохранениеВозможно() Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураДанных = Новый Структура;
	СтруктураДанных.Вставить("ДокументВид", 				ДокументВид);
	СтруктураДанных.Вставить("ДокументСерия", 				ДокументСерия);
	СтруктураДанных.Вставить("ДокументНомер", 				ДокументНомер);
	СтруктураДанных.Вставить("ДокументДатаВыдачи", 			ДокументДатаВыдачи);
	СтруктураДанных.Вставить("ДокументКемВыдан", 			ДокументКемВыдан);
	СтруктураДанных.Вставить("ДокументКодПодразделения",	ДокументКодПодразделения);
	
	Закрыть(СтруктураДанных);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Функция СохранениеВозможно()
	
	Отказ = Ложь;
	
	ОчиститьСообщения();
	
	Если НЕ ЗначениеЗаполнено(ДокументВид) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задан вид документа.'"), , "ДокументВид", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(ДокументСерия) + СокрЛП(ДокументНомер)) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заданы серия и номер документа.'"), , "ДокументСерия", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументДатаВыдачи) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задана дата выдачи документа.'"), , "ДокументДатаВыдачи", , Отказ);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДокументКемВыдан) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не задано наименование органа, выдавшего документ.'"), , "ДокументКемВыдан", , Отказ);
	КонецЕсли;
	
	Возврат НЕ Отказ;
	
КонецФункции

#КонецОбласти