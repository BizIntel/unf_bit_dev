#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

//Функция определяет, используются или нет группы доступа контрагентов.
//
//	Возвращаемое значение:
//		Булево - если ИСТИНА, значит группы доступа используются
//
Функция ИспользуютсяГруппыДоступа() Экспорт
	
	Возврат
		ПолучитьФункциональнуюОпцию("ОграничиватьДоступНаУровнеЗаписей")
		И ПолучитьФункциональнуюОпцию("ИспользоватьГруппыДоступаКонтрагентов");
	
КонецФункции

Функция ГруппаДоступаПоУмолчанию(Пользователь = Неопределено) Экспорт

	Если Пользователь = Неопределено Тогда
		Пользователь = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ГруппаДоступа = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователь, "ОсновнаяГруппаДоступаКонтрагентов");
	Если Не ЗначениеЗаполнено(ГруппаДоступа) Тогда
		Возврат ГруппаДоступа;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ГруппыДоступаКонтрагентов.Ссылка
	|ИЗ
	|	Справочник.ГруппыДоступаКонтрагентов КАК ГруппыДоступаКонтрагентов
	|ГДЕ
	|	ГруппыДоступаКонтрагентов.Ссылка = &Ссылка";
	
	Запрос.УстановитьПараметр("Ссылка", ГруппаДоступа);
	Результат = Запрос.Выполнить();
	
	Если Не Результат.Пустой() Тогда
		Возврат ГруппаДоступа;
	Иначе
		Возврат Справочники.ГруппыДоступаКонтрагентов.ПустаяСсылка();
	КонецЕсли;

КонецФункции 

#КонецОбласти

#КонецЕсли