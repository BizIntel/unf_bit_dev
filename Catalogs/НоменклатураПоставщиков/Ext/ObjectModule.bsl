#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Владелец)
		ИЛИ Не ЗначениеЗаполнено(Идентификатор) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	НоменклатураПоставщиков.Ссылка
	|ИЗ
	|	Справочник.НоменклатураПоставщиков КАК НоменклатураПоставщиков
	|ГДЕ
	|	НоменклатураПоставщиков.Владелец = &Владелец
	|	И НоменклатураПоставщиков.Артикул = &Артикул
	|	И НоменклатураПоставщиков.Идентификатор = &Идентификатор
	|	И НоменклатураПоставщиков.Номенклатура = &Номенклатура
	|	И НоменклатураПоставщиков.Характеристика = &Характеристика
	|	И НоменклатураПоставщиков.Ссылка <> &ТекущаяСсылка";
	
	Запрос.УстановитьПараметр("Владелец", Владелец);
	Запрос.УстановитьПараметр("Артикул", Артикул);
	Запрос.УстановитьПараметр("Идентификатор", Идентификатор);
	Запрос.УстановитьПараметр("Номенклатура", Номенклатура);
	Запрос.УстановитьПараметр("Характеристика", Характеристика);
	Запрос.УстановитьПараметр("ТекущаяСсылка", Ссылка);
	
	Результат = Запрос.Выполнить();
	Если Не Результат.Пустой() Тогда
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Соответствие ""%1,%2,%3 - %4,%5"" уже присутствует в справочнике. Запись отменена.'"),
			Владелец, Артикул, Идентификатор, Номенклатура, Характеристика
		);
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = ТекстСообщения;
		Сообщение.Сообщить();
		
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецЕсли