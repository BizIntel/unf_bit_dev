#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ДозаполнитьПоУмолчанию();
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ВРег(Метаданные.Имя) = ВРег("УправлениеНебольшойФирмойБазовая")
		И ЭтоНовый() Тогда
		Запрос = Новый Запрос();
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ Организации.Ссылка) КАК КоличествоОрганизаций
		|ИЗ
		|	Справочник.Организации КАК Организации";
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			Если Выборка.КоличествоОрганизаций >= 2 Тогда
				Отказ = Истина;
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru='Ограничение базовой версии. В информационной базе могут быть введены только две организации.'"));
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	// 1. Действия, выполняемые всегда, в том числе и при обмене данными
	
	Если ЭтоНовый() Тогда
		
		// Свойство "Ссылка" заведомо не заполнено.
		// Но в ходе обмена ссылка может быть передана.
		
		СсылкаНового = ПолучитьСсылкуНового();
		Если СсылкаНового.Пустая() Тогда
			УстановитьСсылкуНового(Справочники.Организации.ПолучитьСсылку());
		КонецЕсли;
		
	КонецЕсли;
	
	// Не выполнять дальнейшие действия при обмене данными
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	// 2. Дальнейшие действия не выполняются в случае, когда запись инициирована механизмом обмена данными
	
	// Проверим возможность внесения изменений
	Если ЭтоНовый() И Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'В программе отключен учет по нескольким организациям.'"));
		Отказ = Истина;
	КонецЕсли;
	
	РегламентированнаяОтчетностьУСН.ПроверитьРегистрациюВИФНС(ЭтотОбъект, Отказ);
	
	// Не выполнять дальнейшие действия при отказе в записи
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// 3. Дальнейшие действия не выполняются при отказе в записи объекта
	
	// Настройка данных реквизитов в зависимости от других реквизитов
	ПривестиДанныеКСогласованномуСостоянию();
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	БанковскийСчетПоУмолчанию = Неопределено;
	ФайлЛоготип = Неопределено;
	ФайлФаксимильнаяПечать = Неопределено;
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ИспользуетсяОтчетность И Не ПолучитьФункциональнуюОпцию("ИспользоватьОтчетность") Тогда
		Константы.ФункциональнаяОпцияИспользоватьОтчетность.Установить(Истина);
	КонецЕсли;
	
	Если ЭтотОбъект.ИспользуетсяОтчетность И (ЭтотОбъект.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо
		ИЛИ ЭтотОбъект.ИПИспользуетТрудНаемныхРаботников) Тогда
		Если Не ПолучитьФункциональнуюОпцию("ИспользоватьПодсистемуЗарплата") Тогда
			Константы.ФункциональнаяОпцияИспользоватьПодсистемуЗарплата.Установить(Истина);
		КонецЕсли;
		
		Если Не ПолучитьФункциональнуюОпцию("ВестиУчетНалогаНаДоходыИВзносов") Тогда
			Константы.ФункциональнаяОпцияВестиУчетНалогаНаДоходыИВзносов.Установить(Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДозаполнитьПоУмолчанию()
	
	Если Не ЗначениеЗаполнено(ПроизводственныйКалендарь) Тогда
		ПроизводственныйКалендарь = УправлениеНебольшойФирмойСервер.ПолучитьКалендарьПоПроизводственномуКалендарюРФ();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СтавкаНДСПоУмолчанию) Тогда
		СтавкаНДСПоУмолчанию = УправлениеНебольшойФирмойПовтИсп.ПолучитьСтавкуНДС(18);
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(КассаПоУмолчанию) Тогда
		КассаПоУмолчанию = Справочники.Кассы.ПолучитьКассуПоУмолчанию();
	КонецЕсли;
	
КонецПроцедуры

// Процедура согласовывает состояние одних реквизитов объекта в зависимости от других
//
Процедура ПривестиДанныеКСогласованномуСостоянию()
	
	Если ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		
		ИНН = Лев(ИНН, 10); // ИНН 10 цифр
		ОГРН = Лев(ОГРН, 13); // ОГРН 13 цифр
		СвидетельствоСерияНомер = "";
		СвидетельствоДатаВыдачи = '00010101';
		ФизическоеЛицо = Неопределено;
		
	ИначеЕсли ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		
		КПП = "";
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#КонецЕсли