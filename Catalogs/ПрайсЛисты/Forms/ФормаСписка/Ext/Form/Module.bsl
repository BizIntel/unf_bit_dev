
#Область СлужебныеМетоды

&НаСервере
Процедура ИзменитьНастройкуОсновнойФормыПрайсЛистов()
	
	Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка = НЕ Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка;
	ЗначениеНастройки = Перечисления.ОсновнаяФормаПрайсЛиста[?(Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка, "СписокПрайсЛистов", "ФормированиеПрайсЛистов")];
	УправлениеНебольшойФирмойСервер.УстановитьНастройкуПользователя(ЗначениеНастройки, "ОсновнаяФормаПрайсЛиста");
	ОбновитьПовторноИспользуемыеЗначения();
	
КонецПроцедуры

#КонецОбласти

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеНастройки("ОсновнаяФормаПрайсЛиста");
	Элементы.ЭтоОсновнаяФормаПрайсЛистов.Пометка = (НЕ ЗначениеЗаполнено(ЗначениеНастройки) ИЛИ ЗначениеНастройки = Перечисления.ОсновнаяФормаПрайсЛиста.СписокПрайсЛистов);
	
	РазрешеноРедактированиеЦенДокументов = УправлениеНебольшойФирмойУправлениеДоступомПовтИсп.РазрешеноРедактированиеЦенДокументов();
	Если НЕ РазрешеноРедактированиеЦенДокументов Тогда
		
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаСоздать", "Доступность", Ложь);
		ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ФормаИзменить", "Доступность", Ложь);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Сформировать(Команда)
	
	ДанныеТекущейСтроки = Элементы.Список.ТекущиеДанные;
	
	Если ДанныеТекущейСтроки <> Неопределено Тогда
		
		ОткрытьФорму("Обработка.ФормированиеПрайсЛистов.Форма", Новый Структура("ПрайсЛист", ДанныеТекущейСтроки.Ссылка), ЭтаФорма);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЭтоОсновнаяФормаПрайсЛистов(Команда)
	
	ИзменитьНастройкуОсновнойФормыПрайсЛистов();
	
КонецПроцедуры
#КонецОбласти