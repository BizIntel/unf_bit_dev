
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Не Пользователи.ЭтоПолноправныйПользователь() Тогда
		ТолькоПросмотр = Истина;
	КонецЕсли;
	
	ЗакрыватьПриВыбореЭлемента = Параметры.ЗакрыватьПриВыборе; // УНФ
	
	МожноОбновлятьКлассификатор =
		Не ОбщегоНазначенияПовтИсп.РазделениеВключено() // В модели сервиса обновляется автоматически.
		И Не ОбщегоНазначения.ЭтоПодчиненныйУзелРИБ()   // В узле РИБ обновляется автоматически.
		И ПравоДоступа("Изменение", Метаданные.Справочники.КлассификаторБанковРФ); // Пользователь с необходимыми правами.

	Элементы.ФормаЗагрузитьКлассификатор.Видимость = МожноОбновлятьКлассификатор;
	
	ПереключитьВидимостьНедействующихБанков(Ложь);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура СписокВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОбработатьВыбор(ВыбраннаяСтрока, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	ОбработатьВыбор(Значение, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
		НСтр("ru = 'Интерактивное добавление в классификатор не поддерживается.
			|Воспользуйтесь командой ""Загрузить классификатор""'"));
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ЗагрузитьКлассификатор(Команда)
	ОткрытьФорму("Справочник.КлассификаторБанковРФ.Форма.ЗагрузкаКлассификатора", , ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ПоказыватьНедействующиеБанки(Команда)
	ПереключитьВидимостьНедействующихБанков(Не Элементы.ФормаПоказыватьНедействующиеБанки.Пометка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПереключитьВидимостьНедействующихБанков(Видимость)
	
	Элементы.ФормаПоказыватьНедействующиеБанки.Пометка = Видимость;
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			Список, "ДеятельностьПрекращена", Ложь, , , Не Видимость);
			
КонецПроцедуры

&НаСервере
Процедура ПодобратьБанкИзКлассификатора(Ссылки)
	
	РаботаСБанкамиПереопределяемый.ПодобратьБанкИзКлассификатора(Ссылки);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыбор(ВыбранныеСтроки, СтандартнаяОбработка)
	
	Если ТипЗнч(ВыбранныеСтроки) <> Тип("Массив") Тогда
		Возврат;
	КонецЕсли;
	
	СтандартнаяОбработка = ЗакрыватьПриВыбореЭлемента; // УНФ
	
	Ссылки = Новый Массив;
	Для каждого Ссылка Из ВыбранныеСтроки Цикл
		Если Элементы.Список.ДанныеСтроки(Ссылка).ЭтоГруппа Тогда
			Продолжить;
		КонецЕсли;
		
		Ссылки.Добавить(Ссылка);
	КонецЦикла;
	
	Если Ссылки.Количество() > 0 Тогда
		ПодобратьБанкИзКлассификатора(Ссылки);
		Оповестить("ОбновитьПослеДобавления");
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти