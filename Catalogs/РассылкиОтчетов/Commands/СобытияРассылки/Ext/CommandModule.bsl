
#Область ОбработчикиСобытий

&НаКлиенте
Процедура ОбработкаКоманды(Рассылка, Параметры)
	ПараметрыФормыЖурналаРегистрации = ПараметрыФормыЖурналаРегистрации(Рассылка);
	Если ПараметрыФормыЖурналаРегистрации = Неопределено Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Рассылка еще не выполнялась.'"));
		Возврат;
	КонецЕсли;
	ОткрытьФорму("Обработка.ЖурналРегистрации.Форма", ПараметрыФормыЖурналаРегистрации, ЭтотОбъект);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ПараметрыФормыЖурналаРегистрации(Рассылка)
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	Состояния.ПоследнийЗапуск,
	|	Состояния.НомерСеанса
	|ИЗ
	|	РегистрСведений.СостоянияРассылокОтчетов КАК Состояния
	|ГДЕ
	|	Состояния.Рассылка = &Рассылка";
	Запрос.УстановитьПараметр("Рассылка", Рассылка);
	
	УстановитьПривилегированныйРежим(Истина);
	Выборка = Запрос.Выполнить().Выбрать();
	Если Не Выборка.Следующий() Тогда
		Возврат Неопределено;
	КонецЕсли;
	Результат = Новый Структура;
	Результат.Вставить("ДатаНачала", Выборка.ПоследнийЗапуск);
	Если Не ЗначениеЗаполнено(Выборка.НомерСеанса) Тогда
		Результат.Вставить("Данные", Рассылка);
	Иначе
		Сеансы = Новый СписокЗначений;
		Сеансы.Добавить(Выборка.НомерСеанса);
		Результат.Вставить("Сеанс", Сеансы);
	КонецЕсли;
	Возврат Результат;
КонецФункции


#КонецОбласти
