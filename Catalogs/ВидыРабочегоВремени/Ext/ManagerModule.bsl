#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ (Параметры.Свойство("СтрокаПоиска") И ЗначениеЗаполнено(Параметры.СтрокаПоиска)) Тогда
		Возврат
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	МассивЗначений = Новый Массив;
	
	Для Счетчик = 1 По 5 Цикл
		Если Параметры.Свойство("ВидВремени" + Счетчик) И ЗначениеЗаполнено(Параметры["ВидВремени" + Счетчик]) Тогда		
			МассивЗначений.Добавить(Параметры["ВидВремени" + Счетчик]);
		КонецЕсли;
	КонецЦикла;
	
	Запрос = Новый Запрос("ВЫБРАТЬ
				  |	ВидыРабочегоВремени.Ссылка
				  |ИЗ
				  |	Справочник.ВидыРабочегоВремени КАК ВидыРабочегоВремени
				  |ГДЕ
				  |	НЕ (ВидыРабочегоВремени.Ссылка В(&МассивЗначений))
				  |
				  |СГРУППИРОВАТЬ ПО
				  |	ВидыРабочегоВремени.Ссылка
				  |
				  |ИМЕЮЩИЕ
				  |	ПОДСТРОКА(ВидыРабочегоВремени.Наименование, 1, &ДлинаПодстроки) ПОДОБНО &СтрокаПоиска
				  |
				  |УПОРЯДОЧИТЬ ПО
				  |	ВидыРабочегоВремени.Наименование");
				  
	Запрос.УстановитьПараметр("МассивЗначений", МассивЗначений);
	Запрос.УстановитьПараметр("СтрокаПоиска", Параметры.СтрокаПоиска);
	Запрос.УстановитьПараметр("ДлинаПодстроки", СтрДлина(Параметры.СтрокаПоиска));
	
	Результат = Запрос.Выполнить();
	Если НЕ Результат.Пустой() Тогда
		ДанныеВыбора = Новый СписокЗначений;
		Выборка = Результат.Выбрать();
		Пока Выборка.Следующий() Цикл
			ДанныеВыбора.Добавить(Выборка.Ссылка);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПолученияДанныхВыбора()

#КонецОбласти

#КонецЕсли