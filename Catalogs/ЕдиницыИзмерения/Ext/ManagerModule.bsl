#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

// Процедура - обработчик события ОбработкаПолученияДанныхВыбора.
//
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Рекурсия")
		И Параметры.Отбор.Свойство("Владелец") И ТипЗнч(Параметры.Отбор.Владелец) = Тип("СправочникСсылка.Номенклатура") Тогда
		// При первом входе, если задана связь параметров выбора по значению номенклатуры,
		// то дополним параметры выбора отбором по владельцу - номенклатурным группам по иерархии.
		
		СтандартнаяОбработка = Ложь;
		
		Номенклатура 		  = Параметры.Отбор.Владелец;
		КатегорияНоменклатуры = Параметры.Отбор.Владелец.КатегорияНоменклатуры;
		
		МассивОтбора = Новый Массив;
		МассивОтбора.Добавить(Номенклатура);
		МассивОтбора.Добавить(КатегорияНоменклатуры);
		
		Родитель = КатегорияНоменклатуры.Родитель;
		Пока ЗначениеЗаполнено(Родитель) Цикл
			МассивОтбора.Добавить(Родитель);
			Родитель = Родитель.Родитель;
		КонецЦикла;
		
		Параметры.Отбор.Вставить("Владелец", МассивОтбора);
		
		// Признак повторного входа.
		Параметры.Вставить("Рекурсия");
		
		// Получим стандартный список выбора с учетом дополненного отбора.
		СтандартныйСписок = ПолучитьДанныеВыбора(Параметры);
		
		Если НЕ (Параметры.Свойство("НеИспользоватьКлассификатор") И Параметры.НеИспользоватьКлассификатор = Истина) Тогда
			Если ЗначениеЗаполнено(Параметры.Отбор.Владелец) Тогда
			// Дополним стандартный список базовой единицей измерения номенклатуры по классификатору.
				ПредставлениеЕдиницыИзмерения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = '%1 (ед. хранения)'"),
					Номенклатура.ЕдиницаИзмерения.Наименование);
				СтандартныйСписок.Вставить(0, Номенклатура.ЕдиницаИзмерения, 
					Новый ФорматированнаяСтрока(ПредставлениеЕдиницыИзмерения, Новый Шрифт(,,Истина)));
			Иначе
				ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнена номенклатура!'"));
			КонецЕсли;
		КонецЕсли;
		
		ДанныеВыбора = СтандартныйСписок;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли