#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Проверяет, имеются ли в базе тарифные планы с заполненными условиями по перевыставлению затрат:
//  — если имеются запланированные затраты,
//  — если задано какое-либо правило для внеплановых затрат.
// 
// Возвращаемое значение:
//   - Булево
//
Функция ЕстьТарифныеПланыСУчетомЗатрат() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ТарифныеПланыДоговоровОбслуживания.Ссылка
	|ИЗ
	|	Справочник.ТарифныеПланыДоговоровОбслуживания КАК ТарифныеПланыДоговоровОбслуживания
	|ГДЕ
	|	(ТарифныеПланыДоговоровОбслуживания.НезапланированныеЗатратыВключатьВСчет
	|			ИЛИ ТарифныеПланыДоговоровОбслуживания.НезапланированныеЗатратыЗапрещать)
	|
	|ОБЪЕДИНИТЬ
	|
	|ВЫБРАТЬ
	|	ТарифныеПланыДоговоровОбслуживанияУчетЗатрат.Ссылка
	|ИЗ
	|	Справочник.ТарифныеПланыДоговоровОбслуживания.УчетЗатрат КАК ТарифныеПланыДоговоровОбслуживанияУчетЗатрат";
	
	Возврат НЕ Запрос.Выполнить().Пустой();
	
КонецФункции

// Возвращает представление в счете объекта договора обслуживания в соответствии с тарифным планом.
//
// Параметры:
//  ТарифныйПлан                - СправочникСсылка.ТарифныеПланыДоговоровОбслуживания
//  ОбъектДоговораОбслуживания  - СправочникСсылка.Номенклатура, ПланСчетовСсылка.Управленческий
// 
// Возвращаемое значение:
//   - СправочникСсылка.Номенклатура
//
Функция ПредставлениеВСчете(ТарифныйПлан, ОбъектДоговораОбслуживания) Экспорт
	
	Если ТипЗнч(ОбъектДоговораОбслуживания) = Тип("СправочникСсылка.Номенклатура") Тогда
		Строка = ТарифныйПлан.УчетНоменклатуры.Найти(ОбъектДоговораОбслуживания, "Номенклатура");
		Если Строка <> Неопределено Тогда
			Возврат Строка.ПредставлениеВСчете;
		КонецЕсли;
	ИначеЕсли ТипЗнч(ОбъектДоговораОбслуживания) = Тип("ПланСчетовСсылка.Управленческий") Тогда
		Строка = ТарифныйПлан.УчетЗатрат.Найти(ОбъектДоговораОбслуживания, "СтатьяЗатрат");
		Если Строка <> Неопределено Тогда
			Возврат Строка.ПредставлениеВСчете;
		КонецЕсли;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти

#КонецЕсли