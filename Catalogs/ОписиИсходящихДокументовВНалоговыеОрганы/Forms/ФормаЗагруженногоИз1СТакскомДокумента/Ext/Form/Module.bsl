&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Параметры.СтруктураПараметров;
	
	Если СтруктураПараметров.ДоступностьДоговора <> Истина Тогда
		Элементы.НомерОснования.ТолькоПросмотр 	= Истина;
		Элементы.ДатаОснования.ТолькоПросмотр 	= Истина;
	КонецЕсли;
	
	ВидДокумента 			= СтруктураПараметров.ВидДокумента;
	Номер 					= СтруктураПараметров.Номер;
	Дата 					= СтруктураПараметров.Дата;
	ПредставлениеДокумента 	= Строка(ВидДокумента) + " " + Номер + " от " + Формат(Дата, "ДЛФ=DD"); 
	
	Контрагент 				= СтруктураПараметров.Контрагент;
	КНД 					= СтруктураПараметров.КНД;
	Направление 			= ?(СтруктураПараметров.Направление = "Входящий", "Полученный", "Выданный");
	
	ИмяФайлаДанных 			= СтруктураПараметров.ИмяФайлаДанных;
	АдресФайлаДанных 		= СтруктураПараметров.АдресФайлаДанных;

	ИмяФайлаПодписи 		= СтруктураПараметров.ИмяФайлаПодписи;
	АдресФайлаПодписи 		= СтруктураПараметров.АдресФайлаПодписи;
	
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот Тогда
		
		НомерДокументаОснования = СтруктураПараметров.НомерДокументаОснования;
		ДатаДокументаОснования 	= СтруктураПараметров.ДатаДокументаОснования;
		
	Иначе
		
		Элементы.ГруппаОснование.Видимость = Ложь;
	
	КонецЕсли;
	
	// инициализируем контекст ЭДО - модуль обработки
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	
	// считываем текст из файла данных
	Попытка
		ПутьКФайлу = ПолучитьИмяВременногоФайла();
		ПолучитьИзВременногоХранилища(АдресФайлаДанных).Записать(ПутьКФайлу);
		СодержимоеФайлаДанных = КонтекстЭДОСервер.ПрочитатьТекстИзФайла(ПутьКФайлу, ,Истина);
		ОперацииСФайламиЭДКО.УдалитьВременныйФайл(ПутьКФайлу); // xml-файл
	Исключение
		Отказ = Истина;
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru='Ошибка чтения содержимого из файла: %1'"), Символы.ПС + Символы.ПС + ИнформацияОбОшибке().Описание);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

#КонецОбласти

