#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриКопировании(ОбъектКопирования)
	
	Цвет = Новый ХранилищеЗначения(ОбъектКопирования.Цвет.Получить());
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект.ДополнительныеСвойства.Вставить("ЭтоНовый", ЭтоНовый());
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ЭтотОбъект.ДополнительныеСвойства.Свойство("ЭтоНовый")
		И ЭтотОбъект.ДополнительныеСвойства.ЭтоНовый
		И Не ПолучитьФункциональнуюОпцию("ИспользоватьВидыЗаказовПокупателей") Тогда
		
		ОсновнойВидЗаказаОбъект = Справочники.ВидыЗаказовПокупателей.Основной.ПолучитьОбъект();
		НоваяСтрока = ОсновнойВидЗаказаОбъект.ПорядокСостояний.Добавить();
		НоваяСтрока.Состояние = ЭтотОбъект.Ссылка;
		ОсновнойВидЗаказаОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
