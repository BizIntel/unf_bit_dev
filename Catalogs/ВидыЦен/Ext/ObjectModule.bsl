#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеОбработчики

Процедура ПодготовитьДополнительныеСвойства()
	
	ДополнительныеСвойства.Вставить("ТаблицыДляДвижений", Новый Структура);
	
КонецПроцедуры

Процедура ЗаписатьНаборыЗаписей()
	
	Для каждого ЗаписьОТаблице Из ДополнительныеСвойства.ТаблицыДляДвижений Цикл
		
		НаборЗаписей = РегистрыСведений[ЗаписьОТаблице.Ключ].СоздатьНаборЗаписей();
		НаборЗаписей.Отбор.ВидЦенРасчетный.Установить(Ссылка, Истина);
		НаборЗаписей.Загрузить(ЗаписьОТаблице.Значение);
		НаборЗаписей.Записать(Истина);
		
	КонецЦикла;
	
	Если ОбщегоНазначенияПовтИсп.РазделениеВключено()
		И ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
	
		ЦенообразованиеСервер.ВыключитьВключитьИспользованиеЗаданияРасчетаОчередиЦен(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ПредопределенныеОбработчикиОбъекта

// Процедура - обработчик события ОбработкаПроверкиЗаполнения объекта
//
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ТипВидаЦен = Перечисления.ТипыВидовЦен.ДинамическийФормула Тогда
		
		ПроверяемыеРеквизиты.Добавить("Валюта");
		ПроверяемыеРеквизиты.Добавить("Формула");
		
	ИначеЕсли ТипВидаЦен = Перечисления.ТипыВидовЦен.ДинамическийПроцент Тогда
		
		ПроверяемыеРеквизиты.Добавить("БазовыйВидЦен");
		ПроверяемыеРеквизиты.Добавить("Процент");
		
		Если БазовыйВидЦен = Ссылка Тогда
			
			ТекстОшибки = НСтр("ru='Базовый вид цены не может быть равен текущему виду цены'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстОшибки, ЭтотОбъект, "БазовыйВидЦен",, Отказ);
			
		КонецЕсли;
		
	Иначе
		
		ПроверяемыеРеквизиты.Добавить("Валюта");
		
	КонецЕсли;
	
	Если ПустаяСтрока(ИдентификаторФормул)
		ИЛИ ЦенообразованиеФормулыСервер.НайтиВидЦенПоИдентификатору(ИдентификаторФормул, Ссылка).ИдентификаторЗанят Тогда
		
		ЦенообразованиеФормулыСервер.СформироватьНовыйИдентификаторВидаЦен(ИдентификаторФормул, Наименование);
		
	КонецЕсли;
	
КонецПроцедуры // ОбработкаПроверкиЗаполнения()

// Процедура - обработчик события ПриЗаписи объекта
//
Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка = Истина Тогда
		
		Возврат;
		
	КонецЕсли;
	
	Если ТипВидаЦен <> Перечисления.ТипыВидовЦен.Статический 
		И НЕ ЦеныАктуальны Тогда
		
		ПодготовитьДополнительныеСвойства();
		Справочники.ВидыЦен.ИнициализироватьДанные(Ссылка, ДополнительныеСвойства, ЭтотОбъект);
		
		ЗаписатьНаборыЗаписей();
		
	КонецЕсли;
	
КонецПроцедуры // ПриЗаписи()

#КонецОбласти

#КонецЕсли