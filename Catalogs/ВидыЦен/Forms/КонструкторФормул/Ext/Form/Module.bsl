
#Область Служебные

&НаСервере
Процедура ДобавитьРасчетныйОперанд(НоваяГруппа, НаименованиеОперанда, ПредставлениеОперанда, ЭтоВидЦенНоменклатуры = Неопределено, ИндексКартинка = 0)
	
	НоваяСтрока = НоваяГруппа.Строки.Добавить();
	
	НоваяСтрока.Операнд					= НаименованиеОперанда;
	НоваяСтрока.Представление			= ПредставлениеОперанда;
	НоваяСтрока.ЭтоВидЦенНоменклатуры	= ЭтоВидЦенНоменклатуры;
	НоваяСтрока.Картинка				= ИндексКартинка;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьПараметрыПеретаскивания(Элемент, ПараметрыПеретаскивания)
	
	ДанныеТекущейСтроки = Элементы.ДеревоОперандов.ТекущиеДанные;
	ПараметрыПеретаскивания.Значение = КэшЗначений.ОперандНачало + ДанныеТекущейСтроки.Операнд + КэшЗначений.ОперандКонец;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоОперандов(ИсключитьСсылку, ЭтоФормированиеЦен, ВключаяЦеныНоменклатуры, ВключаяЦеныКонтрагентов)
	
	Запрос = Новый Запрос(
	"Выбрать различные 
	|	ВидыЦен.ИдентификаторФормул КАК Операнд, Представление(ВидыЦен.Ссылка) КАК Представление, Истина КАК ЭтоВидЦенНоменклатуры, 0 КАК Картинка 
	|	Из Справочник.ВидыЦен КАК ВидыЦен Где ВидыЦен.ТипВидаЦен = Значение(Перечисление.ТипыВидовЦен.Статический) И ВидыЦен.Ссылка <> &ИсключитьСсылку
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|Выбрать различные 
	|	ВидыЦенКонтрагентов.ИдентификаторФормул, Представление(ВидыЦенКонтрагентов.Ссылка), Ложь, 0 
	|	Из Справочник.ВидыЦенКонтрагентов КАК ВидыЦенКонтрагентов
	|
	|Итоги Выбор Когда ЭтоВидЦенНоменклатуры = Истина Тогда ""ЦЕНЫ НОМЕНКЛАТУРЫ"" Иначе ""ЦЕНЫ КОНТРАГЕНТОВ"" Конец КАК Представление, 1 КАК Картинка 
	|	ПО ЭтоВидЦенНоменклатуры");
	
	Запрос.УстановитьПараметр("ИсключитьСсылку", ИсключитьСсылку);
	ДеревоРезультата = Запрос.Выполнить().Выгрузить(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	Если ЭтоФормированиеЦен Тогда
		
		НоваяГруппа 				= ДеревоРезультата.Строки.Вставить(0);
		НоваяГруппа.Картинка		= 1;
		НоваяГруппа.Представление	= НСтр("ru ='РАСЧЕТНЫЕ ЗНАЧЕНИЯ'");
		
		ДобавитьРасчетныйОперанд(НоваяГруппа, "ТекущееЗначение",		НСтр("ru ='Текущее значение'"));
		ДобавитьРасчетныйОперанд(НоваяГруппа, "ПоследняяЦенаВПриходе",	НСтр("ru ='Последняя цена поступления'"));
		ДобавитьРасчетныйОперанд(НоваяГруппа, "ПоследняяЦенаВРасходе",	НСтр("ru ='Последняя цена реализации'"));
		
		Если ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций") Тогда
			
			Если Справочники.Валюты.НайтиПоКоду("840") <> Неопределено Тогда
				
				ДобавитьРасчетныйОперанд(НоваяГруппа, "КурсДоллара", 	НСтр("ru ='Текущий курс доллара'"));
				
			КонецЕсли;
			
			Если Справочники.Валюты.НайтиПоКоду("978") <> Неопределено Тогда
				
				ДобавитьРасчетныйОперанд(НоваяГруппа, "КурсЕвро", 		НСтр("ru ='Текущий курс евро'"));
				
			КонецЕсли;
			
		КонецЕсли;
		
		ДобавитьРасчетныйОперанд(НоваяГруппа, "Себестоимость", 			НСтр("ru ='Себестоимость'"));
		
		Если Константы.ВалютаУчета.Получить() <> Константы.НациональнаяВалюта.Получить() Тогда
			
			ДобавитьРасчетныйОперанд(НоваяГруппа, "СебестоимостьНацВалюта", НСтр("ru ='Себестоимость (нац. валюта)'"));
			
		Конецесли;
		
	КонецЕсли;
	
	Если ВключаяЦеныНоменклатуры = Ложь Тогда
		
		НайденнаяСтрока = ДеревоРезультата.Строки.Найти("ЦЕНЫ НОМЕНКЛАТУРЫ", "Представление", Ложь);
		Если НайденнаяСтрока <> Неопределено Тогда
			
			ДеревоРезультата.Строки.Удалить(НайденнаяСтрока);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Если ВключаяЦеныКонтрагентов = Ложь Тогда
		
		НайденнаяСтрока = ДеревоРезультата.Строки.Найти("ЦЕНЫ КОНТРАГЕНТОВ", "Представление", Ложь);
		Если НайденнаяСтрока <> Неопределено Тогда
			
			ДеревоРезультата.Строки.Удалить(НайденнаяСтрока);
			
		КонецЕсли;
		
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ДеревоРезультата, "ДеревоОперандов");
	
КонецПроцедуры

&НаКлиенте
Процедура ВставитьТекстВФормулу(ТекстВставки)
	
	Если ПустаяСтрока(ТекстВставки) Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СтрокаНач = 0;
	СтрокаКон = 0;
	КолонкаНач = 0;
	КолонкаКон = 0;
	
	Элементы.Формула.ПолучитьГраницыВыделения(СтрокаНач, КолонкаНач, СтрокаКон, КолонкаКон);
	Если (КолонкаКон = КолонкаНач) И (КолонкаКон + СтрДлина(ТекстВставки)) > Элементы.Формула.Ширина / 8 Тогда
		
		Элементы.Формула.ВыделенныйТекст = "";
		
	КонецЕсли;
		
	Элементы.Формула.ВыделенныйТекст = ТекстВставки;
	
	ЭтаФорма.ТекущийЭлемент = Элементы.Формула;
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьДеревоОператоров()
	
	ДеревоОператоров = РеквизитФормыВЗначение("Операторы", Тип("ДеревоЗначений"));
	
	ГруппаСтрок 				= ДеревоОператоров.Строки.Добавить();
	ГруппаСтрок.Наименование	= НСтр("ru ='АРИФМЕТИЧЕСКИЕ ОПЕРАТОРЫ'");
	ГруппаСтрок.Картинка		= 1;
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Сложение ""+""'");
	НоваяСтрока.Оператор		= " + ";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Вычитание ""-""'");
	НоваяСтрока.Оператор		= " - ";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Умножение ""*""'");
	НоваяСтрока.Оператор		= " * ";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Деление ""/""'");
	НоваяСтрока.Оператор		= " / ";
	
	ГруппаСтрок 				= ДеревоОператоров.Строки.Добавить();
	ГруппаСтрок.Наименование	= НСтр("ru ='ПРОЧЕЕ'");
	ГруппаСтрок.Картинка		= 1;
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Процент ""1%""'");
	НоваяСтрока.Оператор		= " %1";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Процент ""5%""'");
	НоваяСтрока.Оператор		= " %5";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Процент ""20%""'");
	НоваяСтрока.Оператор		= " %20";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Процент ""50%""'");
	НоваяСтрока.Оператор		= " %50";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Открывающая скобка ""(""'");
	НоваяСтрока.Оператор		= " (";
	
	НоваяСтрока 				= ГруппаСтрок.Строки.Добавить();
	НоваяСтрока.Наименование	= НСтр("ru ='Закрывающая скобка "")""'");
	НоваяСтрока.Оператор		= ") ";
	
	ЗначениеВРеквизитФормы(ДеревоОператоров, "Операторы");
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьФормулуНаСервере(Ошибки)
	
	ЦенообразованиеФормулыСервер.ПроверитьФормулу(Ошибки, Формула);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьУмножениеВФормулу()
	
	// Процедура заменяет "][" на "]*["
	ТаблицаВставок = Новый ТаблицаЗначений;
	ТаблицаВставок.Колонки.Добавить("ПозицияНачалаОперанда");
	ТаблицаВставок.Колонки.Добавить("ПозицияКонцаОперанда");
	
	НачалоОперанда	= ЦенообразованиеФормулыСервер.СтрокаНачалаОперанда();
	КонецОперанда	= ЦенообразованиеФормулыСервер.СтрокаКонцаОперанда();
	
	ПозицияНачалаОперанда = 0;
	ПозицияКонцаОперанда = 0;
	
	СтрокаМежду			= "";
	ДлинаСтроки 		= СтрДлина(Формула);
	Для ИндексСимвола = 0 По ДлинаСтроки Цикл
		
		Символ = Сред(Формула, ИндексСимвола, 1);
		Если Символ = КонецОперанда Тогда
			
			СтрокаМежду 			= "";
			ПозицияКонцаОперанда 	= ИндексСимвола;
			ПозицияНачалаОперанда	= 0;
			
		ИначеЕсли Символ = НачалоОперанда Тогда
			
			ПозицияНачалаОперанда	= ИндексСимвола;
			
		КонецЕсли;
			
		Если ПозицияКонцаОперанда <> 0 
			И ПозицияНачалаОперанда = 0
			И Символ <> КонецОперанда Тогда
			
			СтрокаМежду = СтрокаМежду + Символ;
			
		ИначеЕсли ПозицияКонцаОперанда <> 0 
			И ПозицияНачалаОперанда <> 0 Тогда
			
			Если ПустаяСтрока(СокрЛП(СтрокаМежду)) Тогда
				
				НоваяСтрока							= ТаблицаВставок.Добавить();
				НоваяСтрока.ПозицияНачалаОперанда	= ПозицияНачалаОперанда;
				НоваяСтрока.ПозицияКонцаОперанда	= ПозицияКонцаОперанда;
				
			КонецЕсли;
			
			СтрокаМежду				= "";
			ПозицияНачалаОперанда	= 0;
			ПозицияКонцаОперанда	= 0;
			
		КонецЕсли;
		
	КонецЦикла;
	
	КоличествоВставок = ТаблицаВставок.Количество();
	Если КоличествоВставок > 0 Тогда
		
		Пока КоличествоВставок <> 0 Цикл
			
			СтрокаТаблицы = ТаблицаВставок.Получить(КоличествоВставок - 1);
			
			ПерваяПодстрока = Лев(Формула, СтрокаТаблицы.ПозицияКонцаОперанда);
			ВтораяПодстрока = Сред(Формула, СтрокаТаблицы.ПозицияНачалаОперанда);
			
			Формула 		= ПерваяПодстрока + " * " + ВтораяПодстрока;
			
			КоличествоВставок = КоличествоВставок - 1;
			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ДополнительнаяОбработкаФормулы()
	
	ДобавитьУмножениеВФормулу();
	
	Формула = СтрЗаменить(Формула, ",", ".");
	
КонецПроцедуры

#КонецОбласти

#Область СобытияФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Перем ИсключитьСсылку;
	
	Параметры.Свойство("Формула", Формула);
	Параметры.Свойство("Ссылка", ИсключитьСсылку);
	
	Если Параметры.Свойство("ИдентификаторВидаЦен") Тогда
		
		ИсключитьСсылку = ЦенообразованиеФормулыСервер.НайтиВидЦенПоИдентификатору(Параметры.ИдентификаторВидаЦен).ВидЦен;
		
	КонецЕсли;
	
	КэшЗначений = Новый Структура;
	КэшЗначений.Вставить("ЦеныКонтрагентов", ПолучитьФункциональнуюОпцию("УчетЦенКонтрагентов"));
	КэшЗначений.Вставить("ОперандНачало", ЦенообразованиеФормулыСервер.СтрокаНачалаОперанда());
	КэшЗначений.Вставить("ОперандКонец", ЦенообразованиеФормулыСервер.СтрокаКонцаОперанда());
	
	ЭтоФормированиеЦен = Ложь;
	Если Параметры.Свойство("ЭтоФормированиеЦен") Тогда
		
		ЭтоФормированиеЦен = Параметры.ЭтоФормированиеЦен;
		
	КонецЕсли;
	
	ЗаполнитьДеревоОперандов(ИсключитьСсылку, ЭтоФормированиеЦен, Истина, КэшЗначений.ЦеныКонтрагентов);
	ЗаполнитьДеревоОператоров();
	
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ДеревоОперандовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеТекущейСтроки = Элементы.ДеревоОперандов.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВставки = КэшЗначений.ОперандНачало + ДанныеТекущейСтроки.Операнд + КэшЗначений.ОперандКонец;
	
	ВставитьТекстВФормулу(ТекстВставки);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоОперандовНачалоПеретаскивания(Элемент, ПараметрыПеретаскивания, Выполнение)
	
	ЗаполнитьПараметрыПеретаскивания(Элемент, ПараметрыПеретаскивания);
	
КонецПроцедуры

&НаКлиенте
Процедура ОператорыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ДанныеТекущейСтроки = Элементы.Операторы.ТекущиеДанные;
	Если ДанныеТекущейСтроки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ТекстВставки = ДанныеТекущейСтроки.Оператор;
	Если СокрЛП(ТекстВставки) = "%1"
		ИЛИ СокрЛП(ТекстВставки) = "%5"
		ИЛИ СокрЛП(ТекстВставки) = "%20"
		ИЛИ СокрЛП(ТекстВставки) = "%50"
		Тогда
		
		ДанныеОперанда = Элементы.ДеревоОперандов.ТекущиеДанные;
		Если ДанныеОперанда = Неопределено 
			ИЛИ ПустаяСтрока(ДанныеОперанда.Операнд) Тогда
			
			ТекстСообщения = НСтр("ru ='Укажите вид цен, от которого необходимо вычислить процент'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ДеревоОперандов");
			
			Возврат;
			
		Иначе
			
			ПредставлениеЧисло = СтрЗаменить(ТекстВставки, "%", "");
			ТекстВставки = " + ([" + ДанныеОперанда.Операнд + "] / 100 * " + ПредставлениеЧисло + ".0)";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВставитьТекстВФормулу(ТекстВставки);
	
КонецПроцедуры

&НаКлиенте
Процедура ФормулаПриИзменении(Элемент)
	
	Если Найти(Формула, ",") > 0 Тогда
		
		ТекстСообщения = НСтр("ru ='Для указания дробной части необходимо использовать точку, а не запятую.'");
		
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Формула");
		
	КонецЕсли;
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ДополнительнаяОбработкаФормулы();
	
	Закрыть(Новый Структура("Результат, Формула", КодВозвратаДиалога.Да, Формула));
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьФормулу(Команда)
	Перем Ошибки;
	
	ОчиститьСообщения();
	ПроверитьФормулуНаСервере(Ошибки);
	
	Если Ошибки = Неопределено Тогда
		
		ТекстПредупреждения = НСтр("ru ='Формула пригодна для расчетов.'");
		ПоказатьПредупреждение(, ТекстПредупреждения, , НСтр("ru ='Проверка формулы'"));
		
	Иначе
		
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОператорыОкончаниеПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка)
	
	Если ПараметрыПеретаскивания.Значение.Количество() < 0 Тогда
		
		Возврат;
		
	КонецЕсли;
	
	ДанныеТекущейСтроки = ПараметрыПеретаскивания.Значение[0];
	Если ДанныеТекущейСтроки = Неопределено Тогда
		
		Возврат;
		
	КонецЕсли;
	
	СтандартнаяОбработка = Ложь;
	
	ТекстВставки = ДанныеТекущейСтроки.Оператор;
	Если СокрЛП(ТекстВставки) = "%1"
		ИЛИ СокрЛП(ТекстВставки) = "%5"
		ИЛИ СокрЛП(ТекстВставки) = "%20"
		ИЛИ СокрЛП(ТекстВставки) = "%50"
		Тогда
		
		ДанныеОперанда = Элементы.ДеревоОперандов.ТекущиеДанные;
		Если ДанныеОперанда = Неопределено 
			ИЛИ ПустаяСтрока(ДанныеОперанда.Операнд) Тогда
			
			ТекстСообщения = НСтр("ru ='Укажите вид цен, от которого необходимо вычислить процент'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "ДеревоОперандов");
			
			Возврат;
			
		Иначе
			
			ПредставлениеЧисло = СтрЗаменить(ТекстВставки, "%", "");
			ТекстВставки = " + ([" + ДанныеОперанда.Операнд + "] / 100 * " + ПредставлениеЧисло + ".0)";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ВставитьТекстВФормулу(ТекстВставки);
	
КонецПроцедуры

#КонецОбласти