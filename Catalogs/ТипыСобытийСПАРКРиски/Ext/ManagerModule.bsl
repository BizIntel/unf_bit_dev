
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияПолейПредставления(Поля, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Поля.Добавить("Описание");
	
КонецПроцедуры

Процедура ОбработкаПолученияПредставления(Данные, Представление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Представление = Данные.Описание;
	
КонецПроцедуры

Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	Спр.Описание КАК Название,
	|	Спр.Критичное,
	|	Спр.Ссылка
	|ИЗ
	|	Справочник.ТипыСобытийСПАРКРиски КАК Спр
	|ГДЕ
	|	НЕ Спр.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Название");
	
	ДанныеВыбора = Новый СписокЗначений;
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.Критичное Тогда
			ДанныеВыбора.Добавить(Выборка.Ссылка,
				ИнтернетПоддержкаПользователейКлиентСервер.ФорматированныйЗаголовок(
					"<body><font color = ""#FF0000"">" + Выборка.Название + "</span></body>"));
		Иначе
			ДанныеВыбора.Добавить(
				Выборка.Ссылка,
				Выборка.Название);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли