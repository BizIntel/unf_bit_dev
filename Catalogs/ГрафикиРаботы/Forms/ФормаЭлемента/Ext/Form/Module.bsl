
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВысотаШапки = 3;
	
	ШаблонЗаполнения = Объект.ТипГрафика;
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Цвет = Объект.Ссылка.Цвет.Получить();
	Иначе
		ЗначениеКопирования = Неопределено;
		Параметры.Свойство("ЗначениеКопирования", ЗначениеКопирования);
		Если ЗначениеКопирования <> Неопределено Тогда
			Цвет = ЗначениеКопирования.Цвет.Получить();
		Иначе
			Цвет = Новый Цвет(0, 0, 0);
		КонецЕсли;
	КонецЕсли;
	
	ЦветЗанятогоПериода = ЦветаСтиля.РабочееВремяЗанятоПолностью;
	ЦветСвободногоПериода = ЦветаСтиля.РабочееВремяСвободноДоступно;
	
	Если НЕ ЗначениеЗаполнено(Объект.ДатаОтсчета) Тогда
		Объект.ДатаОтсчета = ТекущаяДата();
	КонецЕсли;
		
	ВывестиГрафик();
	
КонецПроцедуры // ПриСозданииНаСервере()

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.Цвет = Новый ХранилищеЗначения(Цвет);
	
КонецПроцедуры // ПередЗаписьюНаСервере()

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

// Процедура - обработчик события ПриИзменении поля ШаблонЗаполнения.
//
&НаКлиенте
Процедура ШаблонЗаполненияПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(ШаблонЗаполнения) Тогда
		ЗаполнитьПоШаблону();
		ВывестиГрафик();
	ИначеЕсли ЗначениеЗаполнено(ШаблонЗаполнения)
	   И ШаблонЗаполнения <> Объект.ТипГрафика Тогда
		Режим = РежимДиалогаВопрос.ДаНет;
		Текст = "ru = 'Изменился тип графика. Шаблон будет перезаполнен.'";
		Оповещение = Новый ОписаниеОповещения("ШаблонЗаполненияПриИзмененииЗавершение",ЭтаФорма);
		ПоказатьВопрос(Оповещение,НСтр(Текст), Режим, 0);
	КонецЕсли;
	
	ШаблонЗаполнения = Объект.ТипГрафика;
	
КонецПроцедуры // ШаблонЗаполненияПриИзменении()

&НаКлиенте
Процедура ШаблонЗаполненияПриИзмененииЗавершение(Ответ,Параметры) Экспорт

	Если Ответ = КодВозвратаДиалога.Да Тогда
		ЗаполнитьПоШаблону();
		ВывестиГрафик();
	Иначе
		Объект.ТипГрафика = ШаблонЗаполнения;
	КонецЕсли;
	
	ШаблонЗаполнения = Объект.ТипГрафика;
	
КонецПроцедуры

// Процедура - обработчик события НачалоВыбора поля ШаблонЗаполнения.
//
&НаКлиенте
Процедура ШаблонЗаполненияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ШаблонЗаполнения = Объект.ТипГрафика;
	
КонецПроцедуры // ШаблонЗаполненияНачалоВыбора()

#КонецОбласти

#Область ОбработчикиКомандФормы

// Процедура - обработчик нажатия кнопки Заполнить.
//
&НаКлиенте
Процедура Заполнить(Команда)
	
	ЗаполнитьПоШаблону();
	
КонецПроцедуры // Заполнить()

// Процедура - обработчик нажатия кнопки ОтметитьВыбранноеКакРабочее.
//
&НаКлиенте
Процедура ОтметитьВыбранноеКакРабочее(Команда)
	
	КоличествоДней = ПолучитьКоличествоДнейГрафика();
	УстановитьЦветФонаВыбраннымОбластям(КоличествоДней, ЦветЗанятогоПериода);
	ЗаполнитьПериодыПоРасписаниюГрафика(КоличествоДней);
	ВывестиСуммуЧасовНаКлиенте(КоличествоДней);
	
КонецПроцедуры // ОтметитьВыбранноеКакРабочее()

// Процедура - обработчик нажатия кнопки ОтметитьВыбранноеКакНеРабочее.
//
&НаКлиенте
Процедура ОтметитьВыбранноеКакНеРабочее(Команда)
	
	КоличествоДней = ПолучитьКоличествоДнейГрафика();
	УстановитьЦветФонаВыбраннымОбластям(КоличествоДней, ЦветСвободногоПериода);
	ЗаполнитьПериодыПоРасписаниюГрафика(КоличествоДней);
	ВывестиСуммуЧасовНаКлиенте(КоличествоДней);
	
КонецПроцедуры // ОтметитьВыбранноеКакНеРабочее()

// Процедура - обработчик нажатия кнопки ДобавитьДеньВГрафик.
//
&НаКлиенте
Процедура ДобавитьДеньВГрафик(Команда)
	
	КоличествоДней = ПолучитьКоличествоДнейГрафика();
	НовПериод = Объект.Периоды.Добавить();
	НовПериод.НомерДня = КоличествоДней + 1;
	ВывестиГрафик();
	
КонецПроцедуры // ДобавитьДеньВГрафик()

// Процедура - обработчик нажатия кнопки УдалитьДеньИзГрафика.
//
&НаКлиенте
Процедура УдалитьДеньИзГрафика(Команда)
	
	КоличествоДней = ПолучитьКоличествоДнейГрафика();
	ВыделенныеОбласти = РасписаниеГрафика.ВыделенныеОбласти;
	
	Для каждого ТекОбласть Из ВыделенныеОбласти Цикл
		
		Если ТипЗнч(ТекОбласть) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			Продолжить;
		КонецЕсли;
		
		Для ТекСтрока = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			Если ТекСтрока >= ВысотаШапки + 1 И ТекСтрока <= КоличествоДней + ВысотаШапки Тогда
				ПараметрыОтбора = Новый Структура;
				ПараметрыОтбора.Вставить("НомерДня", ТекСтрока - ВысотаШапки);
				МассивУдаляемыхСтрок = Объект.Периоды.НайтиСтроки(ПараметрыОтбора);
				Для каждого УдаляемаяСтрока Из МассивУдаляемыхСтрок Цикл
					Объект.Периоды.Удалить(УдаляемаяСтрока);
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
		
	КонецЦикла;
	
	Если Объект.Периоды.Количество() = 0 Тогда
		ВывестиГрафик();
		Возврат;
	КонецЕсли;
	
	Объект.Периоды.Сортировать("НомерДня Возр");
	
	НомерПредыдущегоДня = Объект.Периоды[0].НомерДня;
	
	Если НомерПредыдущегоДня > 1 Тогда
		Для каждого ТекСтрока Из Объект.Периоды Цикл
			ТекСтрока.НомерДня = ТекСтрока.НомерДня - НомерПредыдущегоДня + 1;
		КонецЦикла;
	КонецЕсли;
	
	Если Объект.Периоды.Количество() > 1 Тогда
		Для Сч = 1 По Объект.Периоды.Количество() - 1 Цикл
			Если Объект.Периоды[Сч].НомерДня - НомерПредыдущегоДня > 1 Тогда
				Объект.Периоды[Сч].НомерДня = Объект.Периоды[Сч].НомерДня - НомерПредыдущегоДня + 1;
			Иначе
				НомерПредыдущегоДня = Объект.Периоды[Сч].НомерДня;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	ВывестиГрафик();
	
КонецПроцедуры // УдалитьДеньИзГрафика()

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Процедура заполняет расписание по шаблону.
//
&НаКлиенте
Процедура ЗаполнитьПоШаблону()
	
	Объект.Периоды.Очистить();
	
	Если Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафикаРаботы.Пятидневка") Тогда
		
		Объект.ДлительностьРабочейНедели = 40;
		Для ИндексДня = 1 По 5 Цикл
			Смена = Объект.Периоды.Добавить();
			Смена.ВремяНачала = Дата(1,1,1,8,0,0);
			Смена.ВремяОкончания = Дата(1,1,1,16,0,0);
			Смена.НомерДня = ИндексДня;
		КонецЦикла;
		
	ИначеЕсли Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафикаРаботы.Шестидневка") Тогда
		
		Объект.ДлительностьРабочейНедели = 40;
		Для ИндексДня = 1 По 6 Цикл
			Смена = Объект.Периоды.Добавить();
			Смена.ВремяНачала = Дата(1,1,1,8,0,0);
			Если ИндексДня = 6 Тогда
				Смена.ВремяОкончания = Дата(1,1,1,13,0,0);
			Иначе
				Смена.ВремяОкончания = Дата(1,1,1,15,0,0);
			КонецЕсли;
			Смена.НомерДня = ИндексДня;
		КонецЦикла;
		
	ИначеЕсли Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафикаРаботы.Сменный") Тогда
		
		Смена = Объект.Периоды.Добавить();
		Смена.НомерДня = 1;
		Смена.ВремяНачала = Дата(1,1,1,0,0,0);
		Смена.ВремяОкончания = Дата(1,1,1,23,59,59);
		Смена = Объект.Периоды.Добавить();
		Смена.НомерДня = 2;
		Смена = Объект.Периоды.Добавить();
		Смена.НомерДня = 3;
		
	ИначеЕсли Объект.ТипГрафика = ПредопределенноеЗначение("Перечисление.ТипыГрафикаРаботы.КалендарныеДни") Тогда
		
		Объект.ДлительностьРабочейНедели = 56;
		Для ИндексДня = 1 По 7 Цикл
			Смена = Объект.Периоды.Добавить();
			Смена.ВремяНачала = Дата(1,1,1,8,0,0);
			Смена.ВремяОкончания = Дата(1,1,1,16,0,0);
			Смена.НомерДня = ИндексДня;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры // ЗаполнитьПоШаблону()

// Процедура выводит график.
//
&НаСервере
Процедура ВывестиГрафик()
	
	Если Объект.ТипГрафика = Перечисления.ТипыГрафикаРаботы.Сменный Тогда
		Элементы.ГруппаРасписаниеГрафикаПанель.Видимость = Истина;
		ВывестиГрафикСменный();
		Элементы.ГруппаРасписаниеГрафика.Доступность = Истина;
	ИначеЕсли ЗначениеЗаполнено(Объект.ТипГрафика) Тогда
		Элементы.ГруппаРасписаниеГрафикаПанель.Видимость = Ложь;
		ВывестиГрафикПоДнямНедели();
		Элементы.ГруппаРасписаниеГрафика.Доступность = Истина;
	Иначе
		Элементы.ГруппаРасписаниеГрафика.Доступность = Ложь;
	КонецЕсли;
	
КонецПроцедуры // ВывестиГрафик()

// Процедура выводит график недельного типа.
//
&НаСервере
Процедура ВывестиГрафикПоДнямНедели()
	
	РасписаниеГрафика.Очистить();
	
	МакетРасписаниеПоГрафику = Справочники.ГрафикиРаботы.ПолучитьМакет("РасписаниеГрафика");
	ОбластьМакета = МакетРасписаниеПоГрафику.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ТекстКолонка = "Дни недели";
	РасписаниеГрафика.Вывести(ОбластьМакета);
	ОбластьМакета = МакетРасписаниеПоГрафику.ПолучитьОбласть("Календарь");
	РасписаниеГрафика.Вывести(ОбластьМакета);
	
	МассивСуммаЧасов = Новый Массив(7);
	Для Сч = 0 По 6 Цикл
		МассивСуммаЧасов[Сч] = 0;
	КонецЦикла;
	
	Для каждого Период Из Объект.Периоды Цикл
		Для ТекКолонка = 1 по 96 Цикл
			ТекПериодКонец = '00010101' + ТекКолонка * 900 - 1;
			ТекПериодНачало = ТекПериодКонец - 899;
			СтрокаТаблицы = Период.НомерДня + ВысотаШапки;
			КолонкаТаблицы = ТекКолонка + 2;
			Область = РасписаниеГрафика.Область("R" + СтрокаТаблицы + "C" + КолонкаТаблицы);
			Если ТекПериодНачало >= Период.ВремяНачала
			  И ТекПериодКонец <= Период.ВремяОкончания Тогда
				Область.ЦветФона = ЦветаСтиля.РабочееВремяЗанятоПолностью;
			КонецЕсли;
		КонецЦикла;
		СуммаЧасов = Период.ВремяОкончания - Период.ВремяНачала;
		МассивСуммаЧасов[Период.НомерДня - 1] = МассивСуммаЧасов[Период.НомерДня - 1] + СуммаЧасов;
	КонецЦикла;
	
	РасписаниеГрафика.ФиксацияСверху = ВысотаШапки;
	РасписаниеГрафика.ФиксацияСлева = 2;
	
	ВывестиСуммуЧасовНаСервере(7);
	
КонецПроцедуры // ВывестиГрафикПоДнямНедели()

// Процедура выводит график сменного типа.
//
&НаСервере
Процедура ВывестиГрафикСменный()
	
	РасписаниеГрафика.Очистить();
	
	МакетРасписаниеПоГрафику = Справочники.ГрафикиРаботы.ПолучитьМакет("РасписаниеГрафика");
	ОбластьМакета = МакетРасписаниеПоГрафику.ПолучитьОбласть("Шапка");
	ОбластьМакета.Параметры.ТекстКолонка = "День графика";
	РасписаниеГрафика.Вывести(ОбластьМакета);
	ОбластьСтрокаСменногоКалендаря = МакетРасписаниеПоГрафику.ПолучитьОбласть("СтрокаСменногоКалендаря");
	
	ТаблицаНомеровДней = Объект.Периоды.Выгрузить(, "НомерДня");
	ТаблицаНомеровДней.Свернуть("НомерДня");
	ТаблицаНомеровДней.Сортировать("НомерДня Возр");
	
	Для каждого ТекСтрокаНомерДня Из ТаблицаНомеровДней Цикл
		РасписаниеГрафика.Вывести(ОбластьСтрокаСменногоКалендаря);
		Область = РасписаниеГрафика.Область("R" + Строка(ТекСтрокаНомерДня.НомерДня + ВысотаШапки) + "C1");
		Область.Текст = "День " + Строка(ТекСтрокаНомерДня.НомерДня);
	КонецЦикла;
	
	Для каждого Период Из Объект.Периоды Цикл
		Для ТекКолонка = 1 по 96 Цикл
			ТекПериодКонец = '00010101' + ТекКолонка * 900 - 1;
			ТекПериодНачало = ТекПериодКонец - 899;
			СтрокаТаблицы = Период.НомерДня + ВысотаШапки;
			КолонкаТаблицы = ТекКолонка + 2;
			Область = РасписаниеГрафика.Область("R" + СтрокаТаблицы + "C" + КолонкаТаблицы);
			Если ТекПериодНачало >= Период.ВремяНачала
			  И ТекПериодКонец <= Период.ВремяОкончания Тогда
				Область.ЦветФона = ЦветаСтиля.РабочееВремяЗанятоПолностью;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	РасписаниеГрафика.ФиксацияСверху = ВысотаШапки;
	РасписаниеГрафика.ФиксацияСлева = 2;
	
	ВывестиСуммуЧасовНаСервере(ТаблицаНомеровДней.Количество());
	
КонецПроцедуры // ВывестиГрафикСменный()

// Процедура выводит сумму часов на сервере.
//
&НаСервере
Процедура ВывестиСуммуЧасовНаСервере(КоличествоДней)
	
	Объект.ДлительностьРабочейНедели = 0;
	ДлительностьРабочейНедели = 0;
	
	Если КоличествоДней = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСуммаЧасов = Новый Массив(КоличествоДней);
	Для Сч = 0 По КоличествоДней - 1 Цикл
		МассивСуммаЧасов[Сч] = 0;
	КонецЦикла;
	
	Для каждого Период Из Объект.Периоды Цикл
		СуммаЧасов = ?(Период.ВремяОкончания = Дата(1, 1, 1, 23, 59, 59), Период.ВремяОкончания + 1, Период.ВремяОкончания) - Период.ВремяНачала;
		СуммаЧасов = ?(СуммаЧасов < 0, 0, СуммаЧасов);
		Если МассивСуммаЧасов.Количество() >= Период.НомерДня Тогда
			МассивСуммаЧасов[Период.НомерДня - 1] = МассивСуммаЧасов[Период.НомерДня - 1] + СуммаЧасов;
		КонецЕсли;
		ДлительностьРабочейНедели = ДлительностьРабочейНедели + СуммаЧасов;
	КонецЦикла;
	
	Для Сч = 1 По КоличествоДней Цикл
		Если МассивСуммаЧасов[Сч-1] >= 86399 Тогда
			СуммаЧасовСтрока = "24:00";
		Иначе
			СуммаЧасовСтрока = Формат(Дата(1, 1, 1, Цел(МассивСуммаЧасов[Сч-1] / 3600), (МассивСуммаЧасов[Сч-1] / 3600 - Цел(МассивСуммаЧасов[Сч-1] / 3600)) * 60, 0), "ДФ=ЧЧ:мм");
		КонецЕсли;
		Область = РасписаниеГрафика.Область("R" + Строка(Сч + ВысотаШапки) + "C2");
		Область.Текст = ?(ЗначениеЗаполнено(СуммаЧасовСтрока), СуммаЧасовСтрока, "00:00") + " ч.";
	КонецЦикла;
	
	Объект.ДлительностьРабочейНедели = ДлительностьРабочейНедели / 3600;
	
КонецПроцедуры // ВывестиСуммуЧасовНаСервере()

// Процедура выводит сумму часов на клиенте.
//
&НаКлиенте
Процедура ВывестиСуммуЧасовНаКлиенте(КоличествоДней)
	
	Объект.ДлительностьРабочейНедели = 0;
	ДлительностьРабочейНедели = 0;
	
	Если КоличествоДней = 0 Тогда
		Возврат;
	КонецЕсли;
	
	МассивСуммаЧасов = Новый Массив(КоличествоДней);
	Для Сч = 0 По КоличествоДней - 1 Цикл
		МассивСуммаЧасов[Сч] = 0;
	КонецЦикла;
	
	Для каждого Период Из Объект.Периоды Цикл
		СуммаЧасов = ?(Период.ВремяОкончания = Дата(1, 1, 1, 23, 59, 59), Период.ВремяОкончания + 1, Период.ВремяОкончания) - Период.ВремяНачала;
		СуммаЧасов = ?(СуммаЧасов < 0, 0, СуммаЧасов);
		Если МассивСуммаЧасов.Количество() >= Период.НомерДня Тогда
			МассивСуммаЧасов[Период.НомерДня - 1] = МассивСуммаЧасов[Период.НомерДня - 1] + СуммаЧасов;
		КонецЕсли;
		ДлительностьРабочейНедели = ДлительностьРабочейНедели + СуммаЧасов;
	КонецЦикла;
	
	Для Сч = 1 По КоличествоДней Цикл
		Если МассивСуммаЧасов[Сч-1] >= 86399 Тогда
			СуммаЧасовСтрока = "24:00";
		Иначе
			СуммаЧасовСтрока = Формат(Дата(1, 1, 1,Цел(МассивСуммаЧасов[Сч-1] / 3600), (МассивСуммаЧасов[Сч-1] / 3600 - Цел(МассивСуммаЧасов[Сч-1] / 3600)) * 60, 0), "ДФ=ЧЧ:мм");
		КонецЕсли;
		Область = РасписаниеГрафика.Область("R" + Строка(Сч + ВысотаШапки) + "C2");
		Область.Текст = ?(ЗначениеЗаполнено(СуммаЧасовСтрока), СуммаЧасовСтрока, "00:00") + " ч.";
	КонецЦикла;
	
	Объект.ДлительностьРабочейНедели = ДлительностьРабочейНедели / 3600;
	
КонецПроцедуры // ВывестиСуммуЧасовНаКлиенте()

// Функция возвращает количество дней в графике.
&НаКлиенте
Функция ПолучитьКоличествоДнейГрафика()
	
	Если Строка(Объект.ТипГрафика) = "Сменный" Тогда
		КоличествоЭлементовМассива = Объект.Периоды.Количество();
		Если КоличествоЭлементовМассива = 0 Тогда
			Возврат 0;
		КонецЕсли;
		КоличествоРазличныхЭлементовМассива = 1;
		Для НомерДня = 1 По КоличествоЭлементовМассива - 1 Цикл
			Сч = НомерДня + 1;
			Пока Сч <= КоличествоЭлементовМассива И Объект.Периоды[НомерДня - 1].НомерДня <> Объект.Периоды[Сч - 1].НомерДня Цикл
				Сч = Сч + 1;
				Если Сч = КоличествоЭлементовМассива + 1 Тогда
					КоличествоРазличныхЭлементовМассива = КоличествоРазличныхЭлементовМассива + 1;
				КонецЕсли;
			КонецЦикла
		КонецЦикла;
		Возврат КоличествоРазличныхЭлементовМассива;
	ИначеЕсли ЗначениеЗаполнено(Объект.ТипГрафика) Тогда
		Возврат 7;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции // ПолучитьКоличествоДнейГрафика()

// Процедура заполняет периоды по расписанию графика.
//
&НаКлиенте
Процедура ЗаполнитьПериодыПоРасписаниюГрафика(КоличествоДней)
	
	Объект.Периоды.Очистить();
	ОткрытИнтервал = Ложь;
	
	Для ТекСтрока = ВысотаШапки + 1 По КоличествоДней + ВысотаШапки Цикл
		
		ПустойДень = Истина;
		
		Для ТекКолонка = 3 По 98 Цикл
			
			Область = РасписаниеГрафика.Область("R" + ТекСтрока + "C" + ТекКолонка);
			Если Область.ЦветФона = ЦветЗанятогоПериода Тогда
				Если НЕ ОткрытИнтервал Тогда
					ОткрытИнтервал = Истина;
					НоваяСтрока = Объект.Периоды.Добавить();
					НоваяСтрока.НомерДня = ТекСтрока - ВысотаШапки;
					НоваяСтрока.ВремяНачала = '00010101' + (ТекКолонка - 3) * 900;
					ПустойДень = Ложь;
				КонецЕсли;
			Иначе
				Если ОткрытИнтервал Тогда
					ОткрытИнтервал = Ложь;
					НоваяСтрока.ВремяОкончания = '00010101' + (ТекКолонка - 3) * 900;
				КонецЕсли;
			КонецЕсли;
			
			Если ТекКолонка = 98 И ОткрытИнтервал Тогда
				ОткрытИнтервал = Ложь;
				НоваяСтрока.ВремяОкончания = '00010101' + 86399;
			КонецЕсли;
			
		КонецЦикла;
		
		Если ПустойДень Тогда
			
			НоваяСтрока = Объект.Периоды.Добавить();
			НоваяСтрока.НомерДня = ТекСтрока - ВысотаШапки;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПериодыПоРасписаниюГрафика()

// Процедура устанавливает цвет выбранным областям.
//
&НаКлиенте
Процедура УстановитьЦветФонаВыбраннымОбластям(КоличествоДней, Цвет)
	
	ВыделенныеОбласти = РасписаниеГрафика.ВыделенныеОбласти;
	
	Для каждого ТекОбласть Из ВыделенныеОбласти Цикл
		
		Если ТипЗнч(ТекОбласть) <> Тип("ОбластьЯчеекТабличногоДокумента") Тогда
			Продолжить;
		КонецЕсли;
		
		Для ТекСтрока = ТекОбласть.Верх По ТекОбласть.Низ Цикл
			Для ТекКолонка = ТекОбласть.Лево По ТекОбласть.Право Цикл
				Если ТекКолонка >= 3 И ТекКолонка <= 98 И ТекСтрока >= ВысотаШапки + 1 И ТекСтрока <= КоличествоДней + ВысотаШапки Тогда
					Область = РасписаниеГрафика.Область("R" + ТекСтрока + "C" + ТекКолонка);
					Область.ЦветФона = Цвет;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры // УстановитьЦветФонаВыбраннымОбластям()

#КонецОбласти
