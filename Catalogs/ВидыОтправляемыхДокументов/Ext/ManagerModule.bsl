#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьЦеликомПредопределенныеВидыОтправляемыхДокументов() Экспорт
	
	ЗаполнитьПредопределенныеВидыОтправляемыхДокументов(Истина);
	
КонецПроцедуры 

Процедура ОбновитьНаименованияПредопределенныхЭлементов() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВидыОтправляемыхДокументов.Ссылка,
	|	ВидыОтправляемыхДокументов.Наименование,
	|	ВидыОтправляемыхДокументов.ИмяПредопределенныхДанных
	|ИЗ
	|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
	|ГДЕ
	|	ВидыОтправляемыхДокументов.Предопределенный = ИСТИНА
	|	И ВидыОтправляемыхДокументов.ЭтоГруппа = ЛОЖЬ";	
	
	Результат = Запрос.Выполнить();
	
	ВыборкаРезультата = Результат.Выбрать();
	
	СоответствиеСвойств = ПолучитьСоответствиеСвойствВидовОтправляемыхДокументов();
	
	ВыборкаРезультата = Результат.Выбрать();
	
	//заполняем свойства, перебирая выборку результата запроса
	Пока ВыборкаРезультата.Следующий() Цикл
		СпрСсылка = ВыборкаРезультата.Ссылка;
		СпрИмя = ВыборкаРезультата.ИмяПредопределенныхДанных;
		ЭталонныеСвойства = СоответствиеСвойств[СпрИмя];
		
		Если ЭталонныеСвойства = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭталонНаименование 	= ЭталонныеСвойства.Наименование;
		
		Если ВыборкаРезультата.Наименование <> ЭталонНаименование Тогда
			
			СпрОбъект = СпрСсылка.ПолучитьОбъект();
			
			СпрОбъект.Наименование = ЭталонНаименование;
			
			СпрОбъект.ОбменДанными.Загрузка = Истина;
			
			СпрОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры 

// Получает список видов отправляемых документов по шаблону, представленному
// в макете ОписаниеВидовОтправляемыхДокументов объекта Справочник.ВидыОтправляемыхДокументов
//
Функция ПолучитьСоответствиеСвойствВидовОтправляемыхДокументов(МассивВыборочныхИмен = Неопределено) 

	ВыборочноеЗаполнение = (МассивВыборочныхИмен <> Неопределено);
	
	РезультатСоответствие = Новый Соответствие;
	
	// Получим макет со списком документов.
	МакетСписокДокументов = Справочники.ВидыОтправляемыхДокументов.ПолучитьМакет("ОписаниеВидовОтправляемыхДокументов");

	Для Инд = 0 По МакетСписокДокументов.Области.Количество() - 1 Цикл

		ТекОбласть = МакетСписокДокументов.Области[Инд];

		ИмяГруппы 			= СокрЛП(МакетСписокДокументов.Область(ТекОбласть.Верх, 1).Текст);
		НаименованиеГруппы 	= СокрЛП(МакетСписокДокументов.Область(ТекОбласть.Верх, 2).Текст);
		ОписаниеГруппы		= СокрЛП(МакетСписокДокументов.Область(ТекОбласть.Верх, 3).Текст);
		
		Если Не ПустаяСтрока(ИмяГруппы) Тогда

			Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
				
				// перебираем элементы второго уровня
				ИмяЭлемента = СокрЛП(МакетСписокДокументов.Область(Ном, 1).Текст);
				
				Если ВыборочноеЗаполнение Тогда
					Если МассивВыборочныхИмен.Найти(ИмяЭлемента) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;
				
				СтруктураСвойствЭлемента = Новый Структура();
				СтруктураСвойствЭлемента.Вставить("Наименование", 	СокрЛП(МакетСписокДокументов.Область(Ном, 2).Текст));
				СтруктураСвойствЭлемента.Вставить("Описание", 		СокрЛП(МакетСписокДокументов.Область(Ном, 3).Текст));
				СтруктураСвойствЭлемента.Вставить("Источник", 		СокрЛП(МакетСписокДокументов.Область(Ном, 4).Текст));
				СтруктураСвойствЭлемента.Вставить("ТипПолучателя", 	СокрЛП(МакетСписокДокументов.Область(Ном, 5).Текст));
				СтруктураСвойствЭлемента.Вставить("ТипДокумента", 	СокрЛП(МакетСписокДокументов.Область(Ном, 6).Текст));
				
				РезультатСоответствие.Вставить(ИмяЭлемента, СтруктураСвойствЭлемента);
			КонецЦикла;

		Иначе
			// для элемента корневого (0-уровня)
			Для Ном = ТекОбласть.Верх По ТекОбласть.Низ Цикл
				
				// перебираем элементы второго уровня
				ИмяЭлемента = СокрЛП(МакетСписокДокументов.Область(Ном, 1).Текст);
				
				Если ВыборочноеЗаполнение Тогда
					Если МассивВыборочныхИмен.Найти(ИмяЭлемента) = Неопределено Тогда
						Продолжить;
					КонецЕсли;
				КонецЕсли;

				СтруктураСвойствЭлемента = Новый Структура();
				СтруктураСвойствЭлемента.Вставить("Наименование", 	СокрЛП(МакетСписокДокументов.Область(Ном, 2).Текст));
				СтруктураСвойствЭлемента.Вставить("Описание", 		СокрЛП(МакетСписокДокументов.Область(Ном, 3).Текст));
				СтруктураСвойствЭлемента.Вставить("Источник", 		СокрЛП(МакетСписокДокументов.Область(Ном, 4).Текст));
				СтруктураСвойствЭлемента.Вставить("ТипПолучателя", 	СокрЛП(МакетСписокДокументов.Область(Ном, 5).Текст));
				СтруктураСвойствЭлемента.Вставить("ТипДокумента", 	СокрЛП(МакетСписокДокументов.Область(Ном, 6).Текст));
				
				РезультатСоответствие.Вставить(ИмяЭлемента, СтруктураСвойствЭлемента);

			КонецЦикла;

		КонецЕсли;

	КонецЦикла;
	
	Возврат РезультатСоответствие;

КонецФункции

// Обработчик обновления БРО.
//
// Вызывается при каждом обновлении ИБ.
// Заполняет реквизиты предопределенных элементов справочника "ВидыОтправляемыхДокументов".
Процедура ЗаполнитьПредопределенныеВидыОтправляемыхДокументов(СправочникЦеликом = Ложь) Экспорт
	
	Если СправочникЦеликом Тогда
		
		// полное заполнение при интерактивном выборе восстановления
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыОтправляемыхДокументов.Ссылка,
		|	ВидыОтправляемыхДокументов.Наименование,
		|	ВидыОтправляемыхДокументов.Описание,
		|	ВидыОтправляемыхДокументов.Источник,
		|	ВидыОтправляемыхДокументов.ТипПолучателя,
		|	ВидыОтправляемыхДокументов.ТипДокумента,
		|	ВидыОтправляемыхДокументов.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|ГДЕ
		|	ВидыОтправляемыхДокументов.Предопределенный = ИСТИНА
		|	И ВидыОтправляемыхДокументов.ЭтоГруппа = ЛОЖЬ";	
		
		Результат = Запрос.Выполнить();
		
		ВыборкаРезультата = Результат.Выбрать();
		
		СоответствиеСвойств = ПолучитьСоответствиеСвойствВидовОтправляемыхДокументов();
		
	Иначе
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	ВидыОтправляемыхДокументов.Ссылка,
		|	ВидыОтправляемыхДокументов.Наименование,
		|	ВидыОтправляемыхДокументов.Описание,
		|	ВидыОтправляемыхДокументов.Источник,
		|	ВидыОтправляемыхДокументов.ТипПолучателя,
		|	ВидыОтправляемыхДокументов.ТипДокумента,
		|	ВидыОтправляемыхДокументов.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|ГДЕ
		|	ВидыОтправляемыхДокументов.Предопределенный = ИСТИНА
		|	И ВидыОтправляемыхДокументов.ЭтоГруппа = ЛОЖЬ
		|	И (ВидыОтправляемыхДокументов.Источник = """"
		|			ИЛИ ВидыОтправляемыхДокументов.ТипПолучателя = ЗНАЧЕНИЕ(Перечисление.ТипыКонтролирующихОрганов.ПустаяСсылка)
		|			ИЛИ ВидыОтправляемыхДокументов.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыОтправляемыхДокументов.ПустаяСсылка))";	
		
		Результат = Запрос.Выполнить();
		
		Если Результат.Пустой() Тогда
			//свойства всех предопределенных элементов заполнены
			Возврат;	
		КонецЕсли;
		
		ВыборкаРезультата = Результат.Выбрать();
		
		МассивНезаполненныхЭлементов = Новый Массив;
		
		Пока ВыборкаРезультата.Следующий() Цикл
			ИмяЭлемента = ВыборкаРезультата.ИмяПредопределенныхДанных;
			МассивНезаполненныхЭлементов.Добавить(ИмяЭлемента);	
		КонецЦикла;
		
		СоответствиеСвойств = ПолучитьСоответствиеСвойствВидовОтправляемыхДокументов(МассивНезаполненныхЭлементов);
		
	КонецЕсли;
	
	ВыборкаРезультата = Результат.Выбрать();
	
	//заполняем свойства, перебирая выборку результата запроса
	Пока ВыборкаРезультата.Следующий() Цикл
		СпрСсылка = ВыборкаРезультата.Ссылка;
		СпрИмя = ВыборкаРезультата.ИмяПредопределенныхДанных;
		ЭталонныеСвойства = СоответствиеСвойств[СпрИмя];
		
		Если ЭталонныеСвойства = Неопределено Тогда
			Продолжить;
		КонецЕсли;
		
		ЭталонНаименование 	= ЭталонныеСвойства.Наименование;
		ЭталонОписание 		= ЭталонныеСвойства.Описание;
		ЭталонИсточник 		= ЭталонныеСвойства.Источник;
		ЭталонТипПолучателя = Перечисления.ТипыКонтролирующихОрганов[ЭталонныеСвойства.ТипПолучателя];
		ЭталонТипДокумента 	= Перечисления.ТипыОтправляемыхДокументов[ЭталонныеСвойства.ТипДокумента];
		
		Если ВыборкаРезультата.Наименование <> ЭталонНаименование
			ИЛИ ВыборкаРезультата.Описание <> ЭталонОписание
			ИЛИ ВыборкаРезультата.Источник <> ЭталонИсточник
			ИЛИ ВыборкаРезультата.ТипПолучателя <> ЭталонТипПолучателя
			ИЛИ ВыборкаРезультата.ТипДокумента <> ЭталонТипДокумента Тогда
			
			СпрОбъект = СпрСсылка.ПолучитьОбъект();
			
			СпрОбъект.Наименование = ЭталонНаименование;
			СпрОбъект.Описание = ЭталонОписание;
			СпрОбъект.Источник = ЭталонИсточник;
			СпрОбъект.ТипПолучателя = ЭталонТипПолучателя;
			СпрОбъект.ТипДокумента = ЭталонТипДокумента;
			
			СпрОбъект.ОбменДанными.Загрузка = Истина;
			
			СпрОбъект.Записать();
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры // ЗаполнитьПредопределенныеВидыОтправляемыхДокументов()

// Обработчик обновления БРО 1.1.9.5
//
Процедура ИсправитьВидыОтправляемыхДокументовСЗВ_МДоходыОтОбрМедДеятельностиНДФЛ() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыОтправляемыхДокументов.Ссылка,
		|	ВидыОтправляемыхДокументов.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|ГДЕ
		|	ВидыОтправляемыхДокументов.Предопределенный = ИСТИНА
		|	И ВидыОтправляемыхДокументов.ЭтоГруппа = ЛОЖЬ
		|	И (ВидыОтправляемыхДокументов.Источник = ""ПачкаДокументовСЗВ_М""
		|		И ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.СведенияОЗастрахованныхЛицахСЗВ_М)
		|		ИЛИ ВидыОтправляемыхДокументов.Источник = ""РегламентированныйОтчетСведенияОДолеДоходовОтОбразовательнойИлиМедицинскойДеятельности""
		|		И ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.СведенияОДолеДоходовОтОбразовательнойИлиМедицинскойДеятельности)
		|		ИЛИ ВидыОтправляемыхДокументов.Источник = ""СправкаПоНДФЛВНалоговыйОрган""
		|		И ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.СправкаПоНДФЛВНалоговыйОрган))";
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаЗапроса.Количество() > 0 Тогда
		МассивКорректируемыхЭлементов = Новый Массив;
		Для каждого СтрокаЗапроса Из ТаблицаЗапроса Цикл
			МассивКорректируемыхЭлементов.Добавить(СтрокаЗапроса.ИмяПредопределенныхДанных);
		КонецЦикла;
		
		СоответствиеСвойств = ПолучитьСоответствиеСвойствВидовОтправляемыхДокументов(МассивКорректируемыхЭлементов);
		
		Для каждого СтрокаЗапроса Из ТаблицаЗапроса Цикл
			ЭталонныеСвойства = СоответствиеСвойств[СтрокаЗапроса.ИмяПредопределенныхДанных];
			Если ЭталонныеСвойства = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ВидыОтправляемыхДокументовОбъект = СтрокаЗапроса.Ссылка.ПолучитьОбъект();
			
			ВидыОтправляемыхДокументовОбъект.Наименование = ЭталонныеСвойства.Наименование;
			ВидыОтправляемыхДокументовОбъект.Описание = ЭталонныеСвойства.Описание;
			ВидыОтправляемыхДокументовОбъект.Источник = ЭталонныеСвойства.Источник;
			ВидыОтправляемыхДокументовОбъект.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов[ЭталонныеСвойства.ТипПолучателя];
			ВидыОтправляемыхДокументовОбъект.ТипДокумента = Перечисления.ТипыОтправляемыхДокументов[ЭталонныеСвойства.ТипДокумента];
			
			ВидыОтправляемыхДокументовОбъект.ОбменДанными.Загрузка = Истина;
			
			ВидыОтправляемыхДокументовОбъект.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

// Обработчик обновления БРО 1.1.9.12
//
Процедура ИсправитьВидыОтправляемыхДокументовОбъемВинограда() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыОтправляемыхДокументов.Ссылка
		|ИЗ
		|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|ГДЕ
		|	ВидыОтправляемыхДокументов.Предопределенный = ИСТИНА
		|	И ВидыОтправляемыхДокументов.ЭтоГруппа = ЛОЖЬ
		|	И (ВидыОтправляемыхДокументов.Источник = ""РегламентированныйОтчетАлкоПриложение14""
		|		ИЛИ ВидыОтправляемыхДокументов.Источник = ""РегламентированныйОтчетАлкоПриложение15"")
		|	И ВидыОтправляемыхДокументов.ТипДокумента = ЗНАЧЕНИЕ(Перечисление.ТипыОтправляемыхДокументов.ОтчетФСРАРРозница)";
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Для каждого СтрокаЗапроса Из ТаблицаЗапроса Цикл
		ВидыОтправляемыхДокументовОбъект = СтрокаЗапроса.Ссылка.ПолучитьОбъект();
		
		ВидыОтправляемыхДокументовОбъект.ТипДокумента = Перечисления.ТипыОтправляемыхДокументов.ОтчетФСРАРОпт;
		ВидыОтправляемыхДокументовОбъект.ОбменДанными.Загрузка = Истина;
		
		ВидыОтправляемыхДокументовОбъект.Записать();
	КонецЦикла;
	
КонецПроцедуры

// Обработчик обновления БРО 1.1.9.14
//
Процедура ИсправитьНаименованияДекларацийПоАлкоголюРеестровПоНДС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ВидыОтправляемыхДокументов.Ссылка,
		|	ВидыОтправляемыхДокументов.ИмяПредопределенныхДанных
		|ИЗ
		|	Справочник.ВидыОтправляемыхДокументов КАК ВидыОтправляемыхДокументов
		|ГДЕ
		|	ВидыОтправляемыхДокументов.Предопределенный = ИСТИНА
		|	И ВидыОтправляемыхДокументов.ЭтоГруппа = ЛОЖЬ
		|	И (ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ПроизводствоИОборотЭтиловогоСпирта)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ИспользованиеЭтиловогоСпирта)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ПроизводствоИОборотАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ИспользованиеАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ОборотЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ПоставкаЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ЗакупкаЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ОбъемПеревозкиЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ПеревозкаЭтиловогоСпиртаАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ИспользованиеМощностейПоПроизводствуЭтиловогоСпиртаИАлкогольнойПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РозничнаяПродажаАлкогольнойИСпиртосодержащейПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РозничнаяПродажаПиваИПивныхНапитков)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ОбъемСобранногоВиноградаДляПроизводстваВинодельческойПродукции)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ОбъемВиноградаИспользованногоДляПроизводстваВинаИгристогоВинаШампанского)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.ОбъемВиноградаИспользованногоДляПроизводстваЗащищенныхВин)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение1)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение2)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение3)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение4)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение5)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение6)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение7)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение8)
		|		ИЛИ ВидыОтправляемыхДокументов.Ссылка = ЗНАЧЕНИЕ(Справочник.ВидыОтправляемыхДокументов.РеестрНДСПриложение9))";
	
	ТаблицаЗапроса = Запрос.Выполнить().Выгрузить();
	
	Если ТаблицаЗапроса.Количество() > 0 Тогда
		МассивКорректируемыхЭлементов = Новый Массив;
		Для каждого СтрокаЗапроса Из ТаблицаЗапроса Цикл
			МассивКорректируемыхЭлементов.Добавить(СтрокаЗапроса.ИмяПредопределенныхДанных);
		КонецЦикла;
		
		СоответствиеСвойств = ПолучитьСоответствиеСвойствВидовОтправляемыхДокументов(МассивКорректируемыхЭлементов);
		
		Для каждого СтрокаЗапроса Из ТаблицаЗапроса Цикл
			ЭталонныеСвойства = СоответствиеСвойств[СтрокаЗапроса.ИмяПредопределенныхДанных];
			Если ЭталонныеСвойства = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			
			ВидыОтправляемыхДокументовОбъект = СтрокаЗапроса.Ссылка.ПолучитьОбъект();
			
			ВидыОтправляемыхДокументовОбъект.Наименование = ЭталонныеСвойства.Наименование;
			ВидыОтправляемыхДокументовОбъект.Описание = ЭталонныеСвойства.Описание;
			ВидыОтправляемыхДокументовОбъект.Источник = ЭталонныеСвойства.Источник;
			ВидыОтправляемыхДокументовОбъект.ТипПолучателя = Перечисления.ТипыКонтролирующихОрганов[ЭталонныеСвойства.ТипПолучателя];
			ВидыОтправляемыхДокументовОбъект.ТипДокумента = Перечисления.ТипыОтправляемыхДокументов[ЭталонныеСвойства.ТипДокумента];
			
			ВидыОтправляемыхДокументовОбъект.ОбменДанными.Загрузка = Истина;
			
			ВидыОтправляемыхДокументовОбъект.Записать();
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли