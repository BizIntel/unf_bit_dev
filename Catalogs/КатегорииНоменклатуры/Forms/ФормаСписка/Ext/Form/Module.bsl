
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АвтоЗаголовок = Ложь;
	Заголовок = НСтр("ru='Подбор категорий'");
	
	УстановитьВидимостьПодсказки();
	
	Если Параметры.Свойство("РежимВыбора") И Параметры.РежимВыбора Тогда
		РежимВыбора = Истина;
		Элементы.Список.РежимВыбора = Истина;
	КонецЕсли;
	
	Элементы.Классификатор.РежимВыбора = Истина;
	
	// СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	ДополнительныеОтчетыИОбработки.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ДополнительныеОтчетыИОбработки
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ДекорацияЯндексЛоготипНажатие(Элемент)
	
	АдресСтраницы = "http://market.yandex.ru/";
	ПерейтиПоНавигационнойСсылке(АдресСтраницы);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПроверкаПеретаскивания(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	СтандартнаяОбработка = Ложь;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПеретаскивание(Элемент, ПараметрыПеретаскивания, СтандартнаяОбработка, Строка, Поле)
	
	Если ТипЗнч(ПараметрыПеретаскивания.Значение) = Тип("СправочникСсылка.КлассификаторКатегорийНоменклатуры") Тогда
		
		СтандартнаяОбработка = Ложь;
		ОбработкаВыборкаКатегорииИзКлассификатора(ПараметрыПеретаскивания.Значение, Строка);
		
	Иначе
		
		СтандартнаяОбработка = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СписокОбработкаЗаписиНового(НовыйОбъект, Источник, СтандартнаяОбработка)
	
	ПоказатьВыбратьНовуюКатегорию(НовыйОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПеренестиКатегориюИзКлассификатора(Команда)
	
	ОбработкаВыборкаКатегорииИзКлассификатора(
		Элементы.Классификатор.ТекущаяСтрока,
		Элементы.Список.ТекущийРодитель);
	
КонецПроцедуры

#КонецОбласти

#Область ЗамерыПроизводительности

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "СозданиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломИзменения(Элемент, Отказ)
	
	ОценкаПроизводительностиКлиент.НачатьЗамерВремени(Истина, "ОткрытиеФормы" + РаботаСФормойДокументаКлиентСервер.ПолучитьИмяФормыСтрокой(ЭтотОбъект.ИмяФормы));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция СправочникПуст()
	
	КоличествоКатегорий = 0;
	Выборка = Справочники.КатегорииНоменклатуры.Выбрать();
	Пока Выборка.Следующий() И КоличествоКатегорий <= 1 Цикл
		КоличествоКатегорий = КоличествоКатегорий + 1;
	КонецЦикла;
	
	Возврат КоличествоКатегорий <= 1;
	
КонецФункции

&НаСервере
Процедура УстановитьВидимостьПодсказки(ПоказатьПодсказку = Неопределено)
	
	Если ПоказатьПодсказку = Неопределено Тогда
		ПоказатьПодсказку = СправочникПуст();
	КонецЕсли;
	
	Если ПоказатьПодсказку Тогда
		Элементы.ГруппаПодсказкаИспользования.Видимость = Истина;
		Элементы.Список.Высота = 7;
		Элементы.Список.РастягиватьПоВертикали = Ложь;
	Иначе
		Элементы.ГруппаПодсказкаИспользования.Видимость = Ложь;
		Элементы.Список.Высота = 0;
		Элементы.Список.РастягиватьПоВертикали = Истина;
	КонецЕсли;
	
	ПодсказкаНаФорме = ПоказатьПодсказку;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыборкаКатегорииИзКлассификатора(Знач КатегорияНоменклатуры, Родитель = Неопределено)
	
	КатегорииНоменклатурыКлиент.КатегорияНоменклатурыОбработкаВыбора(КатегорияНоменклатуры, Родитель);
	ПоказатьВыбратьНовуюКатегорию(КатегорияНоменклатуры);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьВыбратьНовуюКатегорию(НоваяКатегория)
	
	Если НоваяКатегория = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ПодсказкаНаФорме Тогда
		УстановитьВидимостьПодсказки(Ложь);
	КонецЕсли;
	
	Если ОбъектЯвляетсяГруппой(НоваяКатегория) Тогда
		
		Элементы.Список.ТекущаяСтрока = НоваяКатегория;
		Возврат;
		
	КонецЕсли;
	
	Элементы.Список.ТекущаяСтрока = НоваяКатегория;
	ПараметрыОткрытия = Новый Структура("Ключ", НоваяКатегория);
	ОткрытьФорму(
		"Справочник.КатегорииНоменклатуры.ФормаОбъекта",
		ПараметрыОткрытия,
		Элементы.Список.ТекущийЭлемент,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца
	);
	
КонецПроцедуры

&НаСервере
Функция ОбъектЯвляетсяГруппой(Объект)
	
	Возврат ОбщегоНазначения.ОбъектЯвляетсяГруппой(Объект);
	
КонецФункции

#КонецОбласти




