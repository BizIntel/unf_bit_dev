
#Область ОписаниеПеременных

&НаКлиенте
Перем ВнутренниеДанные, СвойстваПароля, ПараметрыОбработчикаОжидания, ФормаДлительнойОперации;

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЕстьПравоНастройкиЭДО = ПравоДоступа("Изменение", Метаданные.Справочники.ПрофилиНастроекЭДО);
	
	УстановитьУсловноеОформление();
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		ПодготовитьФорму();
	КонецЕсли;
	
	// Управление уведомлениями о событиях 1С-ЭДО.
	Если Объект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО Тогда
		УведомлятьОСобытиях = ОбменСКонтрагентамиСлужебный.УведомленияПодключены(Объект.ИдентификаторОрганизации);
		УстановитьНастройкуУведомлений();
	КонецЕсли;
	
	ЗаполнитьПредставлениеНастроек(Объект);
	
	Элементы.СписокСертификатовДобавитьСертификат.Доступность = ПравоДоступа(
		"Изменение", Метаданные.Справочники.СертификатыКлючейЭлектроннойПодписиИШифрования);
	
	Если Не ЕстьПравоНастройкиЭДО Тогда
		Элементы.СписокСертификатовДобавитьСертификат.Доступность = Ложь;
		Элементы.ТекстПриглашений.Доступность = Ложь;
		Элементы.НачальнаяДатаЗапросаДанныхУОператора.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Элементы.КаталогВходящихДокументов.КнопкаВыбора = Ложь;
	#Иначе
		Если Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском")
		И Не ОбменСКонтрагентамиСлужебныйКлиент.ИспользуетсяИнтернетПоддержкаПользователей() Тогда
			Элементы.НадписьЛичныйКабинетУчастникаОбменаЭД.Видимость = Ложь;
		КонецЕсли;
	#КонецЕсли

КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если КонтролироватьСодержимоеНастроекЭД Тогда
		ТекущийОбъект.ИзменитьДанныеВСвязанныхНастройкахЭДО(ТекущийОбъект, Отказ)
	КонецЕсли;
	
	Если ТекущийОбъект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
		ТекущийОбъект.РесурсВходящихДокументов  = FTPКаталогВходящихДокументов;
	ИначеЕсли ТекущийОбъект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезКаталог Тогда
		ТекущийОбъект.РесурсВходящихДокументов  = КаталогВходящихДокументов;
	ИначеЕсли ТекущийОбъект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
		ТекущийОбъект.РесурсВходящихДокументов  = ЭлектроннаяПочтаОрганизации;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	ПометитьНеДействующиеСертификатыВСписке();
	Оповестить("ОбновитьСостояниеЭД");
	
	Если КонтролироватьСодержимоеНастроекЭД Тогда
		Оповестить("ОбновитьИсходящиеЭД",Объект.Ссылка);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ Объект.ПометкаУдаления И Не ПропускатьПроверкуНастроекЭДО И ОтсутствуютПодчиненныеНастройкиЭДО() Тогда
		ТекстВопроса = НСтр("ru = 'Подключить контрагента к обмену электронными документами?'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемПродолжить", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ИмяСправочникаКонтрагенты = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ИмяПрикладногоСправочника("Контрагенты");
	
	Если ТипЗнч(ВыбранноеЗначение) = Тип("СправочникСсылка." + ИмяСправочникаКонтрагенты) Тогда
		
		ПараметрыФормы = Новый Структура;
		ЗначенияЗаполнения = Новый Структура;
		ЗначенияЗаполнения.Вставить("Контрагент",         ВыбранноеЗначение);
		ЗначенияЗаполнения.Вставить("ПрофильНастроекЭДО", Объект.Ссылка);
		
		ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
		ОткрытьФорму("Справочник.СоглашенияОбИспользованииЭД.Форма.ФормаЭлемента", ПараметрыФормы, ЭтотОбъект);

	Иначе
	
		Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			Если ВыбранноеЗначение = Истина Тогда
				Сертификат = ВнутренниеДанные["ВыбранныйСертификат"];
				СвойстваПароля = Новый Структура("Значение", ВнутренниеДанные["ВыбранныйСертификатПароль"]);
				ВнутренниеДанные.Удалить("ВыбранныйСертификат");
				ВнутренниеДанные.Удалить("ВыбранныйСертификатПароль");
			Иначе
				Сертификат = ВыбранноеЗначение;
			КонецЕсли;
			ТипВыбранногоЗначения = ТипЗнч(Сертификат);
			Если ТипВыбранногоЗначения = Тип("СправочникСсылка.СертификатыКлючейЭлектроннойПодписиИШифрования") Тогда
				
				// Сначала нужно проверить, что выбранный сертификат уже не содержится в списке
				МассивСтрок = Объект.СертификатыПодписейОрганизации.НайтиСтроки(Новый Структура("Сертификат", Сертификат));
				Если МассивСтрок.Количество() > 0 Тогда
					ТекстПредупреждения = НСтр("ru = 'Выбранный сертификат уже зарегистрирован в профиле настроек ЭДО.'");
					ПоказатьПредупреждение(, ТекстПредупреждения, 30);
					Возврат;
				КонецЕсли;
				
				Если ОбменСКонтрагентамиСлужебныйКлиент.ИспользуетсяИнтернетПоддержкаПользователей()
					И Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
					
					ДобавляемыйСертификат = Сертификат;
					ЗарегистрироватьСертификатТакском();
					// Добавление сертификата срабатывает в обработчике оповещения
				ИначеЕсли Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
					
					ДобавляемыйСертификат = Сертификат;
					ЗарегистрироватьСертификат1СЭДО();
					// Добавление сертификата срабатывает в обработчике оповещения
				Иначе
					ДобавитьСертификатПодписиВСписок(Сертификат);
					Элементы.СертификатыПодписейОрганизации.Обновить();
				КонецЕсли;
			КонецЕсли;
			СвойстваПароля = Неопределено;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД" Тогда
		
		ДанныеИзменены = Ложь;
		
		// ДобавляемыйСертификат инициализируется в ОбработкаВыбора(...)
		Если ЗначениеЗаполнено(ДобавляемыйСертификат) Тогда
			ДанныеИзменены = Истина;
			ДобавитьСертификатПодписиВСписок(ДобавляемыйСертификат);
			Элементы.СертификатыПодписейОрганизации.Обновить();
			ДобавляемыйСертификат = Неопределено;
		КонецЕсли;
		
		ЭтаФорма.Модифицированность = ДанныеИзменены;
		УправлениеДоступностьюЛичногоКабинета();
	КонецЕсли;
	
	Если ИмяСобытия = "ОповещениеОбУспешномИзмененииДанныхУчастникаОбменаЭД" Тогда
		
		ДанныеИзменены = Ложь;
		
		// ДобавляемыйСертификат инициализируется в ОбработкаВыбора(...)
		Если ЗначениеЗаполнено(ДобавляемыйСертификат) Тогда
			ДанныеИзменены = Истина;
			ДобавитьСертификатПодписиВСписок(ДобавляемыйСертификат);
			Элементы.СертификатыПодписейОрганизации.Обновить();
			ДобавляемыйСертификат = Неопределено;
		КонецЕсли;
		
		ЭтаФорма.Модифицированность = ДанныеИзменены;
	КонецЕсли;
	
	УстановитьВидимостьЭлементов(Истина);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейФормы

&НаКлиенте
Процедура СпособОбменаЭДПриИзменении(Элемент)
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторОрганизацииПриИзменении(Элемент)
	
	Объект.ИдентификаторОрганизации = СокрЛП(Объект.ИдентификаторОрганизации);
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогВходящихДокументовНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
#Если НЕ ВебКлиент Тогда
	КаталогОбмена(КаталогВходящихДокументов);
#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура НадписьЛичныйКабинетУчастникаОбменаЭДНажатие(Элемент)
	
	// Если соглашение содержит более одного сертификата, то считается,
	// что все сертификаты были зарегистрированы у оператора ЭДО ранее и
	// обращение в личный кабинет выполняется по текущему или по первому сертификату.
	
	Если Объект.СертификатыПодписейОрганизации.Количество() = 0 Тогда
		ТекстПредупреждения = НСтр("ru = 'Для входа в личный кабинет должен быть зарегистрирован хотя бы один сертификат'");
		ПоказатьПредупреждение(, ТекстПредупреждения, 30);
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПерейтиВЛичныйКабинет", ЭтотОбъект);
	Если Модифицированность Тогда
		ТекстВопроса = НСтр("ru = 'Выполнить действие можно только в записанном профиле настроек ЭДО.
			|Записать?'");
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТекстПриглашенийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Оповещение = Новый ОписаниеОповещения("ЗавершитьРедактированиеТекстаПриглашения", ЭтотОбъект);
	ЗаголовокФормы = НСтр("ru = 'Шаблон текста для приглашений контрагентов'");
	
	ПоказатьВводСтроки(Оповещение, Элементы.ТекстПриглашений.ТекстРедактирования,ЗаголовокФормы, 255, Истина);

КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторОрганизацииОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Не КонтролироватьСодержимоеНастроекЭД Тогда
		ТекстВопроса = НСтр("ru = 'Изменения ""Профиля настроек ЭДО"" будут применены для связанных ""Настроек ЭДО"" с отключенным расширенным режимом.
								|Продолжить?'");
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("СтарыйИдентификаторОрганизации", Объект.ИдентификаторОрганизации);
		ОписаниеОповещения = Новый ОписаниеОповещения("РазрешитьРедактированиеИдентификатораОрганизацииЗавершить", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУКДПриИзменении(Элемент)
	
	Если Не КонтролироватьСодержимоеНастроекЭД Тогда
		ТекстВопроса = НСтр("ru = 'Изменения ""Профиля настроек ЭДО"" будут применены для связанных ""Настроек ЭДО"" с отключенным расширенным режимом.
								|Продолжить?'");
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ИспользоватьУКД", Не Объект.ИспользоватьУКД);
		ОписаниеОповещения = Новый ОписаниеОповещения("РазрешитьРедактированиеИсходящиеДокументыЗавершить", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ИзменитьФлагУПДУКД();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьУПДПриИзменении(Элемент)
	
	Если Не КонтролироватьСодержимоеНастроекЭД Тогда
		ТекстВопроса = НСтр("ru = 'Изменения ""Профиля настроек ЭДО"" будут применены для связанных ""Настроек ЭДО"" с отключенным расширенным режимом.
								|Продолжить?'");
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ИспользоватьУПД", Объект.ИспользоватьУПД);
		ОписаниеОповещения = Новый ОписаниеОповещения("РазрешитьРедактированиеИсходящиеДокументыЗавершить", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	Иначе
		ИзменитьФлагУПДУКД();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ТестПрофиляНастроек(Команда)
	
	ОчиститьСообщения();
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	Если Модифицированность Тогда
		ОписаниеОповещения = Новый ОписаниеОповещения("ТестПрофиляНастроекПродолжить", ЭтотОбъект);
		ТекстВопроса = НСтр("ru = 'В текущий профиль настроек ЭДО внесены изменения. Продолжить выполнение теста?'");
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Выполнить", НСтр("ru = 'Сохранить и выполнить тест'"));
		СписокКнопок.Добавить("Отменить", НСтр("ru = 'Отменить тест'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок, , "Выполнить", НСтр("ru = 'Тест настроек'"));
	Иначе
		ТестПрофиляНастроекПродолжить("Выполнить", Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификат(Команда)
	
	Если НЕ ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеПодписиЭД") Тогда
		
		ТекстСообщения = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ТекстСообщенияОНеобходимостиНастройкиСистемы("ПодписаниеЭД");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ТекстВопроса = НСтр("ru = 'Операция добавления сертификата в профиль настроек ЭДО займет несколько минут.
							|Продолжить?'");
	ОписаниеОповещения = Новый ОписаниеОповещения("ДобавитьСертификатПродолжение", ЭтотОбъект);
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	
КонецПроцедуры

&НаКлиенте
Процедура ДобавитьСертификатПродолжение(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	Если РезультатВыполнения = КодВозвратаДиалога.Да Тогда
		ОткрытьФормуВыбораСертификатаЭП();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаУведомлений(Команда)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("Профиль", Объект.Ссылка);
	ДополнительныеПараметры.Вставить("Идентификатор", Объект.ИдентификаторОрганизации);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаУведомленийПродолжить", ЭтотОбъект, ДополнительныеПараметры);
	
	ПредставлениеОперации = НСтр("ru = 'Настройка уведомлений о событиях ЭДО'");
	ОбменСКонтрагентамиСлужебныйКлиент.ПолучитьМаркерПрофиляЭДО(ОписаниеОповещения, ДополнительныеПараметры, ПредставлениеОперации);
	
КонецПроцедуры

&НаКлиенте
Процедура НачальнаяДатаЗапросаДанныхУОператора(Команда)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ПрофильНастроекЭДО", Объект.Ссылка);
	ПараметрыФормы.Вставить("РежимОткрытияОкна", РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	ОткрытьФорму("РегистрСведений.СостоянияОбменовЭДЧерезОператоровЭДО.Форма.ФормаРедактирования",
		ПараметрыФормы, ЭтотОбъект, УникальныйИдентификатор);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейТаблицыПодписиОрганизации

&НаКлиенте
Процедура СертификатыПодписейОрганизацииПередНачаломИзменения(Элемент, Отказ)
	
	Отказ = Истина;
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Сертификат);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыПодписейОрганизацииВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПоказатьЗначение(, Элемент.ТекущиеДанные.Сертификат);
	
КонецПроцедуры

&НаКлиенте
Процедура СертификатыПодписейОрганизацииПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыВерсияФорматаПриИзменении(Элемент)
	
	// Изменение представления регламента ЭДО при изменении формата документа Акт об оказании услуг.
	
	ТекущиеДанные = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИзменитьРегламентЭДО(ТекущиеДанные.ПолучитьИдентификатор());
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийПолейТаблицыИсходящиеДокументы

&НаКлиенте
Процедура ИсходящиеДокументыПередНачаломИзменения(Элемент, Отказ)
	
	Если Не КонтролироватьСодержимоеНастроекЭД Тогда
		ТекстВопроса = НСтр("ru = 'Изменения ""Профиля настроек ЭДО"" будут применены для связанных ""Настроек ЭДО"" с отключенным расширенным режимом.
									|Продолжить?'");
		ПараметрыОповещения = Новый Структура;
		ПараметрыОповещения.Вставить("ИдентификаторСтроки", Элемент.ТекущиеДанные.ПолучитьИдентификатор());
		ПараметрыОповещения.Вставить("ВерсияФормата",       Элемент.ТекущиеДанные.ВерсияФормата);
		ПараметрыОповещения.Вставить("ИспользоватьЭП",      Элемент.ТекущиеДанные.ИспользоватьЭП);
		ПараметрыОповещения.Вставить("ИсходящийДокумент",   Элемент.ТекущиеДанные.ИсходящийДокумент);
		ПараметрыОповещения.Вставить("Формировать",         Элемент.ТекущиеДанные.Формировать);
		
		ОписаниеОповещения = Новый ОписаниеОповещения("РазрешитьРедактированиеРеквизитовСпискаВидыЭДЗавершить", ЭтотОбъект, ПараметрыОповещения);
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыПриИзменении(Элемент)
	
	ОчиститьСообщения();
	
	Если Элемент.ТекущийЭлемент.Имя = "ИсходящиеДокументыФормировать" Тогда
		ЗначениеЭлемента = Элемент.ТекущиеДанные.Формировать;
		Если Не ЗначениеЭлемента Тогда
			Элемент.ТекущиеДанные.ИспользоватьЭП = ЗначениеЭлемента;
		КонецЕсли;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.ИспользоватьЭП И НЕ Элемент.ТекущиеДанные.Формировать Тогда
		Если Элемент.ТекущийЭлемент.Имя = "ИсходящиеДокументыИспользоватьЭП" Тогда
			Элемент.ТекущиеДанные.Формировать = Истина;
		Иначе
			Элемент.ТекущиеДанные.ИспользоватьЭП = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.ПроизвольныйЭД")
		ИЛИ НЕ ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
		
		Если Элемент.ТекущийЭлемент.Имя = "ИсходящиеДокументыИспользоватьЭП" И НЕ Элемент.ТекущиеДанные.ИспользоватьЭП Тогда
			ШаблонСообщения = НСтр("ru='Отправка документа %1 возможна только с использованием электронной подписи.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
				Элемент.ТекущиеДанные.ИсходящийДокумент);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Элемент.ТекущиеДанные.ИспользоватьЭП = Элемент.ТекущиеДанные.Формировать;
		Если Не Элемент.ТекущиеДанные.Формировать Тогда 
			Элемент.ТекущиеДанные.ТребуетсяОтветнаяПодпись = Элемент.ТекущиеДанные.Формировать;
		КонецЕсли;
	КонецЕсли;
	
	Если (Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.СчетФактура")
		ИЛИ Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.КорректировочныйСчетФактура"))
		И ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
		
		Элемент.ТекущиеДанные.Формировать = Ложь;
		Элемент.ТекущиеДанные.ИспользоватьЭП = Ложь;
		
		ШаблонСообщения = НСтр("ru='Отправка документа %1 возможна только через оператора ЭДО.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			Элемент.ТекущиеДанные.ИсходящийДокумент);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
		Если (Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.АктНаПередачуПрав")
		ИЛИ Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.АктИсполнитель")
		ИЛИ Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.ТОРГ12Продавец"))
		И Объект.ИспользоватьУПД Тогда
		
		Элемент.ТекущиеДанные.Формировать = Ложь;
		
		ШаблонСообщения = НСтр("ru='Данные документ %1 передаются в составе счет-фактуры(универсального передаточного документа).'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			Элемент.ТекущиеДанные.ИсходящийДокумент);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель")
		И Объект.ИспользоватьУКД Тогда
		
		Элемент.ТекущиеДанные.Формировать = Ложь;
		
		ШаблонСообщения = НСтр("ru='Данные документ %1 передаются в составе корректировочного счет-фактуры(универсального корректировочного документа).'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения,
			Элемент.ТекущиеДанные.ИсходящийДокумент);
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.СчетФактура")
		И Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
		
		Если Элемент.ТекущиеДанные.ВерсияФормата = "ФНС 5.01 (УПД:Счет-фактура и первичный документ)" Тогда
			Объект.ИспользоватьУПД = Истина;
			ИзменитьФлагУПДУКД();
		КонецЕсли;
	КонецЕсли;
	Если Элемент.ТекущиеДанные.ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.КорректировочныйСчетФактура")
		И Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
		
		Если Элемент.ТекущиеДанные.ВерсияФормата = "ФНС 5.01 (УКД:Корректировочный счет-фактура и первичный документ)" Тогда
			Объект.ИспользоватьУКД = Истина;
			ИзменитьФлагУПДУКД();
		КонецЕсли;
	КонецЕсли;
	
	Если ВРег(Элемент.ТекущийЭлемент.Имя) = ВРег("ИсходящиеДокументыДокументУчета") Тогда
		Если Не Элемент.ТекущиеДанные.ИспользоватьЭП Тогда
			Элемент.ТекущиеДанные.ТребуетсяОтветнаяПодпись = Ложь;
		КонецЕсли;
		
		ЗаполнитьПредставлениеРегламентаЭДО(Элемент.ТекущиеДанные);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыВерсияФорматаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если Не КонтролироватьСодержимоеНастроекЭД Тогда
		СтандартнаяОбработка = Ложь;
		Возврат;
	КонецЕсли;
	
	СписокВыбора = Элемент.СписокВыбора;
	СписокВыбора.Очистить();
	
	ИсходящийДокумент = Элементы.ИсходящиеДокументы.ТекущиеДанные.ИсходящийДокумент;
	Если ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.СчетФактура") Тогда
		Если Не Объект.ИспользоватьУПД Тогда
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.02'"), НСтр("ru = 'ФНС 5.02'"));
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УПД:Счет-фактура)'"), НСтр("ru = 'ФНС 5.01 (УПД:Счет-фактура)'"));
		КонецЕсли;
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УПД:Счет-фактура и первичный документ)'"), НСтр("ru = 'ФНС 5.01 (УПД:Счет-фактура и первичный документ)'"));
	ИначеЕсли ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.КорректировочныйСчетФактура") Тогда
		Если Не Объект.ИспользоватьУКД Тогда
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.02'"), НСтр("ru = 'ФНС 5.02'"));
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УКД:Корректировочный счет-фактура)'"), НСтр("ru = 'ФНС 5.01 (УКД:Корректировочный счет-фактура)'"));
		КонецЕсли;
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УКД:Корректировочный счет-фактура и первичный документ)'"), НСтр("ru = 'ФНС 5.01 (УКД:Корректировочный счет-фактура и первичный документ)'"));
	ИначеЕсли ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.АктНаПередачуПрав") Тогда
		СписокВыбора.Добавить(НСтр("ru = 'CML 2.08'"), НСтр("ru = 'CML 2.08'"));
		Если НЕ ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УПД:Первичный документ)'"), НСтр("ru = 'ФНС 5.01 (УПД:Первичный документ)'"));
		КонецЕсли;

	ИначеЕсли ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.АктИсполнитель") Тогда
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01'"), НСтр("ru = 'ФНС 5.01 (до 01.07.2017г.)'"));
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (с 2016г.)'"), НСтр("ru = 'ФНС 5.01 (с 10.01.2016г.)'"));
		Если НЕ ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УПД:Первичный документ)'"), НСтр("ru = 'ФНС 5.01 (УПД:Первичный документ)'"));
		КонецЕсли;

	ИначеЕсли ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.ТОРГ12Продавец") Тогда
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01'"), НСтр("ru = 'ФНС 5.01 (до 01.07.2017г.)'"));
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (с 2016г.)'"), НСтр("ru = 'ФНС 5.01 (с 10.01.2016г.)'"));
		Если НЕ ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УПД:Первичный документ)'"), НСтр("ru = 'ФНС 5.01 (УПД:Первичный документ)'"));
		КонецЕсли;

	ИначеЕсли ИсходящийДокумент = ПредопределенноеЗначение("Перечисление.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель") Тогда
		СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01'"), НСтр("ru = 'ФНС 5.01'"));
		Если НЕ ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
			СписокВыбора.Добавить(НСтр("ru = 'ФНС 5.01 (УКД:Первичный документ)'"), НСтр("ru = 'ФНС 5.01 (УКД:Первичный документ)'"));
		КонецЕсли;
	Иначе
		СписокВыбора.Добавить(НСтр("ru = 'CML 2.08'"), НСтр("ru = 'CML 2.08'"));
		СписокВыбора.Добавить(НСтр("ru = 'CML 4.02'"), НСтр("ru = 'CML 4.02'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИсходящиеДокументыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЕстьПравоНастройкиЭДО И Поле.Имя = "ИсходящиеДокументыДополнительныеНастройки" Тогда
	
		Если Не КонтролироватьСодержимоеНастроекЭД Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ТекстВопроса = НСтр("ru = 'Изменения ""Профиля настроек ЭДО"" будут применены для связанных ""Настроек ЭДО"" с отключенным расширенным режимом.
										|Продолжить?'");
				
			ОписаниеОповещения = Новый ОписаниеОповещения("РазрешитьРедактированиеДополнительныхНастроекЗавершить", ЭтотОбъект);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);
		Иначе
			
			РазрешитьРедактированиеДополнительныхНастроекЗавершить(КодВозвратаДиалога.Да);
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ТестПрофиляНастроекПродолжить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Если Результат = "Отменить" Тогда
			Возврат;
		ИначеЕсли Модифицированность Тогда
			Записать();
		КонецЕсли;
	КонецЕсли;
	
	Если ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД) Тогда
		Если Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту") Тогда
			Состояние(НСтр("ru = 'Тест настроек.'"),
			,
			НСтр("ru = 'Выполняется тестирование обмена ЭД через электронную почту. Подождите...'"));
			
			ПроверитьУчетнуюЗаписьЭлектроннойПочты(Объект.РесурсВходящихДокументов);
		КонецЕсли;
		
		Если Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог") Тогда
			Состояние(НСтр("ru = 'Тест настроек.'"),
			,
			НСтр("ru = 'Выполняется тестирование обмена ЭД через каталог. Подождите...'"));
			
			ПутьКРодительскомуКаталогуПрофильНастроекЭДО = Объект.РесурсВходящихДокументов;
			
			// Блок проверки доступа к каталогам.
			ШаблонСообщения = НСтр("ru = 'Тест. Проверка доступа к общему каталогу для обмена ЭД.
			|%1'");
			Попытка
				Если ОбменСКонтрагентамиСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(
					ПутьКРодительскомуКаталогуПрофильНастроекЭДО) Тогда
					
					РезультатТеста = НСтр("ru = 'Пройден успешно.'");
				Иначе
					РезультатТеста = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ПолучитьСообщениеОбОшибке("107");
				КонецЕсли;
			Исключение
				ШаблонРезультата = НСтр("ru = '%1
				|%2'");
				ТекстОшибки = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ПолучитьСообщениеОбОшибке("107");
				РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатТеста);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		Если Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP") Тогда
			Состояние(НСтр("ru = 'Тест настроек.'"),
			,
			НСтр("ru = 'Выполняется тестирование обмена ЭД через FTP. Подождите...'"));
			
			// Блок проверки доступа к каталогам.
			ШаблонСообщения = НСтр("ru = 'Тест. Проверка доступа к серверу FTP для обмена ЭД.
			|%1'");
			Попытка
				ПараметрыСоединения = Новый Структура;
				ПараметрыСоединения.Вставить("FTPКаталогВходящихДокументов", FTPКаталогВходящихДокументов);
				ПараметрыСоединения.Вставить("АдресСервераFTP", Объект.АдресСервера);
				ПараметрыСоединения.Вставить("ПортFTP", Объект.Порт);
				ПараметрыСоединения.Вставить("ПользовательFTP", Объект.Логин);
				ПараметрыСоединения.Вставить("ПарольFTP", Объект.Пароль);
				ПараметрыСоединения.Вставить("ПассивноеСоединениеFTP", Объект.ПассивноеСоединение);
				Если ОбменСКонтрагентамиСлужебныйВызовСервера.ПроверитьДоступностьКаталогаFTP(ПараметрыСоединения) Тогда
					РезультатТеста = НСтр("ru = 'Пройден успешно.'");
				Иначе
					РезультатТеста = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ПолучитьСообщениеОбОшибке("107");
				КонецЕсли;
			Исключение
				ШаблонРезультата = НСтр("ru = '%1
				|%2'");
				ТекстОшибки = ЭлектронноеВзаимодействиеСлужебныйКлиентПовтИсп.ПолучитьСообщениеОбОшибке("107");
				РезультатТеста = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонРезультата, ТекстОшибки,
				КраткоеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, РезультатТеста);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
		
		ТестОтпечатковСертификатов();
		
	Иначе
		// Блок проверки связи с оператором.
		Состояние(НСтр("ru = 'Тест настроек.'"),
			,
			НСтр("ru = 'Выполняется тестирование связи с сервисом ЭДО. Подождите...'"));
		
		Если Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезСервис1СЭДО") Тогда
			ТестСвязиСервисаЭДОНачало();
		КонецЕсли;
		
		Если Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском") Тогда
			
			Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковПроверитьСвязьЭДО", ЭтотОбъект);
			ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Истина);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьУчетнуюЗаписьЭлектроннойПочты(УчетнаяЗапись)
	
	СообщениеОбОшибке = "";
	ДополнительноеСообщение = "";
	РаботаСПочтовымиСообщениямиВызовСервера.ПроверитьВозможностьОтправкиИПолученияЭлектроннойПочты(УчетнаяЗапись, СообщениеОбОшибке, ДополнительноеСообщение);
	
	Если ЗначениеЗаполнено(СообщениеОбОшибке) Тогда
		ПоказатьОповещениеПользователя(НСтр("ru = 'Проверка учетной записи эл. почты'"),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Проверка параметров учетной записи завершилась с ошибками:
					   |%1'"), СообщениеОбОшибке ));
	Иначе
		ПоказатьОповещениеПользователя(НСтр("ru = 'Проверка учетной записи эл. почты'"),,
		СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			НСтр("ru = 'Проверка параметров учетной записи завершилась успешно. %1'"),
			ДополнительноеСообщение));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестСвязиСервисаЭДОНачало()
	
	Если (ТипЗнч(ПараметрыАутентификацииНаСайте) <> Тип("Структура")
		ИЛИ Не ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль))
		И ИнтернетПоддержкаПользователейКлиентСервер.ДоступноПодключениеИнтернетПоддержки() Тогда
		
		ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
			Новый ОписаниеОповещения("ТестСвязиСервисаЭДОАвторизация", ЭтотОбъект), ЭтотОбъект);
	Иначе
		ТестСвязиСервисаЭДОАвторизация(ПараметрыАутентификацииНаСайте, Неопределено);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТестСвязиСервисаЭДОАвторизация(ПараметрыАутентификации, Контекст) Экспорт
	
	Если ТипЗнч(ПараметрыАутентификации) <> Тип("Структура")
		И ИнтернетПоддержкаПользователейКлиентСервер.ДоступноПодключениеИнтернетПоддержки() Тогда
		
		ПараметрыАутентификацииНаСайте = Неопределено;
		// Пользователь отказался от ввода логина и пароля.
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификацииНаСайте = ПараметрыАутентификации;
	
	Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковПроверитьСвязьЭДО", ЭтотОбъект);
	ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтпечатковПроверитьСертификаты(Отпечатки, Параметры = Неопределено) Экспорт
	
	Если Не ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	МассивОтпечатковСертификатов = Новый Массив;
	Для Каждого КлючЗначение Из Отпечатки Цикл
		МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	Соответствие = ОбменСКонтрагентамиСлужебныйВызовСервера.СоответствиеДоступныхСертификатовИПараметров(
		МассивОтпечатковСертификатов, Объект.Ссылка);
	
	Сертификат = Неопределено;
	ПарольПолучен = Ложь;
	Для Каждого КлючЗначение Из Соответствие Цикл
		ТекСертификат = КлючЗначение.Ключ;
		Если Сертификат = Неопределено Тогда
			// Если в соответствии нет сертификатов с сохраненным паролем, то для теста возьмем первый из списка сертификат.
			Сертификат = ТекСертификат;
		КонецЕсли;
		Если КлючЗначение.Значение.Свойство("ПарольПолучен", ПарольПолучен) И ПарольПолучен = Истина Тогда
			Сертификат = ТекСертификат;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Сертификат) Тогда
		ОбменСКонтрагентамиСлужебныйКлиент.ТестНастроекСПроверкойСертификата(Сертификат, ЭтаФорма, Ложь, Ложь, Ложь);
	Иначе
		ТекстСообщения = НСтр("ru = 'Нет доступных сертификатов. Тест не выполнен.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеПолученияОтпечатковПроверитьСвязьЭДО(Отпечатки, Параметры = Неопределено) Экспорт
	
	Если Не ТипЗнч(Отпечатки) = Тип("Соответствие") Тогда
		Возврат;
	КонецЕсли;
	
	МассивОтпечатковСертификатов = Новый Массив;
	Для Каждого КлючЗначение Из Отпечатки Цикл
		МассивОтпечатковСертификатов.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	Соответствие = ОбменСКонтрагентамиСлужебныйВызовСервера.СоответствиеДоступныхСертификатовИПараметров(
												МассивОтпечатковСертификатов, Объект.Ссылка);
	
	МассивСертификатов = Новый Массив;
	ЕстьСвойство = Ложь;
	Для Каждого КлючЗначение Из Соответствие Цикл
		СвойствоСертификата = Неопределено;
		Если ТипЗнч(КлючЗначение.Значение) = Тип("Структура")
			И КлючЗначение.Значение.Свойство("ПарольПолучен", ЕстьСвойство) И ЕстьСвойство = Истина
			И КлючЗначение.Значение.Свойство("ПарольПользователя", СвойствоСертификата) Тогда
			ЭлектроннаяПодписьКлиент.УстановитьПарольСертификата(КлючЗначение.Ключ, СвойствоСертификата);
		КонецЕсли;
		МассивСертификатов.Добавить(КлючЗначение.Ключ);
	КонецЦикла;
	
	Если Не ЗначениеЗаполнено(МассивСертификатов) Тогда
		ТекстСообщения = НСтр("ru = 'Нет доступных сертификатов. Тест не выполнен.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ДополнительныеПараметры = Новый Структура;
	ОбработкаПолученияМаркера = Новый ОписаниеОповещения("ПолучениеМаркераДляВыбранногоСертификата", ЭтотОбъект, ДополнительныеПараметры);
	ОбработкаЗавершения = Новый ОписаниеОповещения("ДействияПослеТестаСвязиЭДО", ЭтотОбъект, ДополнительныеПараметры);
	
	ОписаниеДанных = Новый Структура;
	
	ШаблонОперация = НСтр("ru = 'Тест аутентификации: %1'");
	Операция = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонОперация,
		Объект.Наименование);
	ОписаниеДанных.Вставить("Операция",            Операция);

	ОписаниеДанных.Вставить("ОтборСертификатов",   МассивСертификатов);
	ОписаниеДанных.Вставить("ИзменятьНабор",       Ложь);
	
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("ЭтоАутентификация",   Истина);
	ОписаниеДанных.Вставить("Данные",              ОбработкаПолученияМаркера);
	
	ОписаниеДанных.Вставить("БезПодтверждения",    ЕстьСвойство И МассивСертификатов.Количество() = 1);
	
	ЭлектроннаяПодписьКлиент.Расшифровать(ОписаниеДанных, , ОбработкаЗавершения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучениеМаркераДляВыбранногоСертификата(Знач РезультатВыполнения, Знач ДополнительныеПараметры) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура")
		И Не РезультатВыполнения.ОписаниеДанных.Свойство("ВыбранныйСертификат") Тогда
		
		Возврат;
	КонецЕсли;
	
	СтруктураПараметровЗапросаМаркера = Новый Структура;
	СтруктураПараметровЗапросаМаркера.Вставить("СертификатПодписи", РезультатВыполнения.ОписаниеДанных.ВыбранныйСертификат.Ссылка);
	СтруктураПараметровЗапросаМаркера.Вставить("СпособОбменаЭД", Объект.СпособОбменаЭД);
	СтруктураПараметровЗапросаМаркера.Вставить("ИдентификаторОрганизации", Объект.ИдентификаторОрганизации);
	
	ЗашифрованныеДанные = ОбменСКонтрагентамиСлужебныйВызовСервера.ЗашифрованныйМаркер(СтруктураПараметровЗапросаМаркера,
		ПараметрыАутентификацииНаСайте);
	Если Не ЗначениеЗаполнено(ЗашифрованныеДанные) Тогда
		Возврат;
	КонецЕсли;
	
	РезультатВыполнения.ОписаниеДанных.Данные = ЗашифрованныеДанные;
	ВыполнитьОбработкуОповещения(РезультатВыполнения.Оповещение, РезультатВыполнения.ОписаниеДанных);
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияПослеТестаСвязиЭДО(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Если РезультатВыполнения.Успех = Истина Тогда
			ТекстСообщения = НСтр("ru = 'Тест профиля настроек выполнен успешно.'");
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемПродолжить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	ИмяСправочникаКонтрагенты = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ИмяПрикладногоСправочника("Контрагенты");
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыФормыВыбора = Новый Структура;
		ПараметрыФормыВыбора.Вставить("РежимВыбора",        Истина);
		ПараметрыФормыВыбора.Вставить("ЗакрыватьПриВыборе", Истина);
		ПараметрыФормыВыбора.Вставить("МножественныйВыбор", Ложь);
		
		ОткрытьФорму("Справочник." + ИмяСправочникаКонтрагенты + ".ФормаВыбора",
			ПараметрыФормыВыбора, ЭтотОбъект, УникальныйИдентификатор);
	Иначе
		ПропускатьПроверкуНастроекЭДО = Истина;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЛичныйКабинет(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат = КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Записать();
	Массив = Новый Массив;
	Массив.Добавить(Объект.Ссылка);
	
	ОбработкаОповещения = Новый ОписаниеОповещения("ПерейтиВЛичныйКабинетОповещение", ЭтотОбъект);
	
	ОбменСКонтрагентамиСлужебныйКлиент.ПолучитьНастройкиЭДОИПараметрыСертификатов(ОбработкаОповещения, Массив);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВЛичныйКабинетОповещение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	СоответствиеПрофилейИПараметровСертификатов = "";
	СертификатПодписи  = Неопределено;
	ПарольПользователя = Неопределено;
	Если Результат.Свойство("СоответствиеПрофилейИПараметровСертификатов", СоответствиеПрофилейИПараметровСертификатов)
		И Не ЗначениеЗаполнено(СоответствиеПрофилейИПараметровСертификатов) Тогда
		
		ТекстСообщения = НСтр("ru='Нет доступных сертификатов, среди зарегистрированных по данному профилю настроек ЭДО.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,
			,
			"СертификатыПодписейОрганизации",
			"Объект.СертификатыПодписейОрганизации");
	Иначе
		Для Каждого ЭлементСтруктуры Из СоответствиеПрофилейИПараметровСертификатов Цикл
			СтруктураСертификата = ЭлементСтруктуры.Значение;
			Если ТипЗнч(СтруктураСертификата) = Тип("Структура") Тогда
				СтруктураСертификата.Свойство("СертификатПодписи", СертификатПодписи);
				СтруктураСертификата.Свойство("ПарольПользователя", ПарольПользователя);
			КонецЕсли;
			Прервать;
		КонецЦикла;
	КонецЕсли;
	Если ЗначениеЗаполнено(СертификатПодписи) Тогда
		Подключение1СТакскомКлиент.ОткрытьЛичныйКабинетАбонента(СертификатПодписи);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеРеквизитовСпискаВидыЭДЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		КонтролироватьСодержимоеНастроекЭД = Истина;
	Иначе
		
		ЗаполнитьЗначенияСвойств(Объект.ИсходящиеДокументы.НайтиПоИдентификатору(ДополнительныеПараметры.ИдентификаторСтроки),
			ДополнительныеПараметры);
		ЭтаФорма.ТекущийЭлемент = Элементы.ИдентификаторОрганизации;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеДополнительныхНастроекЗавершить(Результат, ДополнительныеПараметры = Неопределено) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		КонтролироватьСодержимоеНастроекЭД = Истина;
	Иначе
		Возврат;
	КонецЕсли;
	
	Элемент = Элементы.ИсходящиеДокументы;
	ДанныеСтроки = Элемент.ТекущиеДанные;
	
	Если ДанныеСтроки = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ДанныеСтроки.Формировать Тогда
		Возврат;
	КонецЕсли;
	
	СвойстваФормы = ОбменСКонтрагентамиСлужебныйКлиент.СвойстваДокументооборотаЭД();
	ЗаполнитьЗначенияСвойств(СвойстваФормы, ДанныеСтроки);
	
	НастройкиРегламентаЭДО = ОбменСКонтрагентамиСлужебныйКлиент.НастройкиРегламентаЭДО(ДанныеСтроки.ИсходящийДокумент, ДанныеСтроки.ВерсияФормата, Объект.СпособОбменаЭД);
	СвойстваФормы.Вставить("НастройкиРегламентаЭДО", НастройкиРегламентаЭДО);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("НастройкаРегламентаЭДОПродолжить", ЭтотОбъект, СвойстваФормы);
	РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	
	ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.НастройкаРегламентаЭДО", СвойстваФормы
		,Элемент
		,
		,
		,
		,ОписаниеОповещения
		, РежимОткрытияОкна);
	
КонецПроцедуры

&НаКлиенте
Процедура РазрешитьРедактированиеИдентификатораОрганизацииЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		КонтролироватьСодержимоеНастроекЭД = Истина;
	Иначе
		Объект.ИдентификаторОрганизации = СокрЛП(ДополнительныеПараметры.СтарыйИдентификаторОрганизации);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗавершитьРедактированиеТекстаПриглашения(Результат, ДополнительныеПараметры) Экспорт
	
	Если Не Результат = Неопределено Тогда
		Объект.ШаблонТекстаПриглашений = Результат;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ТестОтпечатковСертификатов()
	
	Если ИспользуютсяЭлектронныеПодписи(Объект.Ссылка) Тогда
		Оповещение = Новый ОписаниеОповещения("ПослеПолученияОтпечатковПроверитьСертификаты", ЭтотОбъект);
		ЭлектроннаяПодписьКлиент.ПолучитьОтпечаткиСертификатов(Оповещение, Истина, Ложь);
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуютсяЭлектронныеПодписи(ПрофильНастроек)
	
	ВключенаФО = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции(
		"ИспользоватьЭлектронныеПодписиЭД");
	
	Если НЕ ВключенаФО Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ ПЕРВЫЕ 1
	               |	ИСТИНА КАК Поле1
	               |ИЗ
	               |	Справочник.ПрофилиНастроекЭДО.ИсходящиеДокументы КАК ПрофилиНастроекЭДОИсходящиеДокументы
	               |ГДЕ
	               |	ПрофилиНастроекЭДОИсходящиеДокументы.Ссылка = &Ссылка
	               |	И ПрофилиНастроекЭДОИсходящиеДокументы.Формировать
	               |	И ПрофилиНастроекЭДОИсходящиеДокументы.ИспользоватьЭП";
	Запрос.УстановитьПараметр("Ссылка", ПрофильНастроек);
	Результат = Запрос.Выполнить();
	Возврат НЕ Результат.Пустой();
	
КонецФункции

&НаСервере
Процедура ПодготовитьФорму()
	
	Если Объект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезFTP Тогда
		FTPКаталогВходящихДокументов = Объект.РесурсВходящихДокументов;
	ИначеЕсли Объект.СпособОбменаЭД  = Перечисления.СпособыОбменаЭД.ЧерезКаталог Тогда
		КаталогВходящихДокументов    = Объект.РесурсВходящихДокументов;
	ИначеЕсли Объект.СпособОбменаЭД  = Перечисления.СпособыОбменаЭД.ЧерезЭлектроннуюПочту Тогда
		ЭлектроннаяПочтаОрганизации  = Объект.РесурсВходящихДокументов;
	ИначеЕсли Объект.СпособОбменаЭД  = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО
		ИЛИ Объект.СпособОбменаЭД  = Перечисления.СпособыОбменаЭД.ЧерезОператораЭДОТакском Тогда
		
		ПараметрыАутентификацииНаСайте = ИнтернетПоддержкаПользователей.ДанныеАутентификацииПользователяИнтернетПоддержки();
	КонецЕсли;
	
	ПометитьНеДействующиеСертификатыВСписке();
	
	УстановитьВидимостьЭлементов();
	
КонецПроцедуры

&НаСервере
Функция ОтсутствуютПодчиненныеНастройкиЭДО()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка
	|ИЗ
	|	Справочник.СоглашенияОбИспользованииЭД.ИсходящиеДокументы КАК СоглашенияОбИспользованииЭДИсходящиеДокументы
	|ГДЕ
	|	СоглашенияОбИспользованииЭДИсходящиеДокументы.СпособОбменаЭД = &СпособОбменаЭД
	|	И СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.Организация = &Организация
	|	И НЕ СоглашенияОбИспользованииЭДИсходящиеДокументы.Ссылка.ПометкаУдаления";
	
	Запрос.УстановитьПараметр("СпособОбменаЭД", Объект.СпособОбменаЭД);
	Запрос.УстановитьПараметр("Организация", Объект.Организация);
	
	Возврат Запрос.Выполнить().Пустой();

КонецФункции

&НаСервере
Процедура УстановитьВидимостьЭлементов(ОбновитьРегламентЭДО = Ложь)
	
	Элементы.ГруппаОбменЧерезОператораИнформация.Видимость = Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД);
	Элементы.ГруппаУПДУКД.Видимость = Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД);
	Элементы.ТекстПриглашений.Видимость = Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД);
	
	Элементы.ГруппаПрямойОбменЧерезЭлектроннуюПочтуИнформация.Видимость = Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезЭлектроннуюПочту");
	Элементы.ГруппаПрямойОбменЧерезКаталогИнформация.Видимость = Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезКаталог");
	Элементы.ГруппаПрямойОбменЧерезFTPИнформация.Видимость = Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезFTP");
	
	Элементы.НачальнаяДатаЗапросаДанныхУОператора.Видимость = Не ОбменСКонтрагентамиСлужебныйВызовСервера.ЭтоПрямойОбмен(Объект.СпособОбменаЭД);
	Элементы.НадписьЛичныйКабинетУчастникаОбменаЭД.Видимость = ЗначениеЗаполнено(Объект.ИдентификаторОрганизации)
		И Объект.СпособОбменаЭД = ПредопределенноеЗначение("Перечисление.СпособыОбменаЭД.ЧерезОператораЭДОТакском");
	
	// Настройка по использованию ЭП
	ИспользоватьЭП = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьЭлектронныеПодписиЭД");
	Элементы.ГруппаСертификатыПодписейОрганизации.Видимость = ИспользоватьЭП;
	Элементы.СтраницыПрофиляНастроекЭДО.ОтображениеСтраниц = ?(ИспользоватьЭП, ОтображениеСтраницФормы.ЗакладкиСверху, ОтображениеСтраницФормы.Нет);

	ВидимостьУведомления = (Объект.СпособОбменаЭД = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО);
	Элементы.ГруппаУведомления.Видимость = ВидимостьУведомления;
	
	Если ОбновитьРегламентЭДО Тогда
		ЗаполнитьПредставлениеНастроек(Объект);
	КонецЕсли;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификатТакском()
	
	// Проводим тест настроек сертификата подписи с подавлением вывода успешных результатов.
	ОчиститьСообщения();
	
	Отказ = Ложь;
	Если Не ЗначениеЗаполнено(ДобавляемыйСертификат) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
											ЭлектронноеВзаимодействиеКлиентСервер.ТекстСообщения("Поле", "Заполнение", "Сертификат"),
											Объект.Ссылка,
											"СертификатыПодписейОрганизации",
											,
											Отказ);
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
											ЭлектронноеВзаимодействиеКлиентСервер.ТекстСообщения("Поле", "Заполнение", "Организация"),
											Объект.Ссылка,
											"Организация",
											,
											Отказ);
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Контекст = Новый Структура;
	ОбработкаОповещения = Новый ОписаниеОповещения("ЗарегистрироватьСертификатТакскомАутентификация", ЭтотОбъект, Контекст);
	
	ОбменСКонтрагентамиСлужебныйКлиент.ТестНастроекСПроверкойСертификата(ДобавляемыйСертификат, ЭтаФорма,
		Ложь, Истина, Истина, ОбработкаОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификатТакскомАутентификация(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	Если РезультатВыполнения = Истина Тогда
		
		Если ТипЗнч(ПараметрыАутентификацииНаСайте) <> Тип("Структура")
			ИЛИ Не ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль) Тогда
			
			ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
				Новый ОписаниеОповещения("ЗарегистрироватьСертификатТакскомОповещение", ЭтотОбъект),
				ЭтотОбъект);
		Иначе
			ЗарегистрироватьСертификатТакскомОповещение(ПараметрыАутентификацииНаСайте, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификатТакскомОповещение(ПараметрыАутентификации, Контекст = Неопределено) Экспорт
	
	Если ТипЗнч(ПараметрыАутентификации) <> Тип("Структура") Тогда
		ПараметрыАутентификацииНаСайте = Неопределено;
		// Пользователь отказался от ввода логина и пароля.
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификацииНаСайте = ПараметрыАутентификации;
	
	Подключение1СТакскомКлиент.ЗарегистрироватьСертификатАбонента(ДобавляемыйСертификат,
		Объект.ИдентификаторОрганизации,
		Объект.Организация,
		Новый ОписаниеОповещения("ЗарегистрироватьСертификатТакскомЗавершение", ЭтотОбъект));
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификатТакскомЗавершение(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	Если РезультатВыполнения = Истина Тогда
		Оповестить("ОповещениеОПолученииУникальногоИдентификатораУчастникаОбменаЭД");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификат1СЭДО()
	
	// Проводим тест настроек сертификата подписи с подавлением вывода успешных результатов.
	ОчиститьСообщения();
	
	Если Не ЗначениеЗаполнено(Объект.Организация) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
											ЭлектронноеВзаимодействиеКлиентСервер.ТекстСообщения("Поле", "Заполнение", "Организация"),
											Объект.Ссылка,
											"Организация");
		Возврат;
	КонецЕсли;
	

	// Подготовка данных и открытие формы для ввода адреса.
	ВыбратьАдрес(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификат1СЭДОПродолжить(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	ОбработкаОповещения = Новый ОписаниеОповещения("ЗарегистрироватьСертификат1СЭДОАутентификация", ЭтотОбъект);
	ОбменСКонтрагентамиСлужебныйКлиент.ТестНастроекСПроверкойСертификата(ДобавляемыйСертификат, ЭтаФорма,
		Ложь, Истина, Истина, ОбработкаОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификат1СЭДОАутентификация(РезультатВыполнения, Контекст = Неопределено) Экспорт
	
	Если РезультатВыполнения = Истина Тогда
		
		Если (ТипЗнч(ПараметрыАутентификацииНаСайте) <> Тип("Структура")
			ИЛИ Не ЗначениеЗаполнено(ПараметрыАутентификацииНаСайте.Пароль))
			И ИнтернетПоддержкаПользователейКлиентСервер.ДоступноПодключениеИнтернетПоддержки() Тогда
			
			ИнтернетПоддержкаПользователейКлиент.ПодключитьИнтернетПоддержкуПользователей(
				Новый ОписаниеОповещения("ЗарегистрироватьСертификат1СЭДОЗавершение", ЭтотОбъект),
				ЭтотОбъект);
		Иначе
			ЗарегистрироватьСертификат1СЭДОЗавершение(ПараметрыАутентификацииНаСайте, Неопределено);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗарегистрироватьСертификат1СЭДОЗавершение(ПараметрыАутентификации, Контекст = Неопределено) Экспорт
	
	Если ТипЗнч(ПараметрыАутентификации) <> Тип("Структура") Тогда
		ПараметрыАутентификацииНаСайте = Неопределено;
		// Пользователь отказался от ввода логина и пароля.
		Возврат;
	КонецЕсли;
	
	ПараметрыАутентификацииНаСайте = ПараметрыАутентификации;
	
	// Обновим данные по оператору ЭДО
	ПолучитьАктуальныеДанныеОператораЭДО();
	
	ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНачало();
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьАктуальныеДанныеОператораЭДО()
	
	ИмяФайлаОтвета = ОбменСКонтрагентамиСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetOperators");
	Если ИмяФайлаОтвета <> Неопределено Тогда
		ДанныеФайла = ОбменСКонтрагентамиСлужебный.ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
		
		ДеревоXML = ОбменСКонтрагентамиСлужебный.ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
		Если ДеревоXML <> Неопределено Тогда
			
			УзелОператорыЭДО = ДеревоXML.Строки.Найти("Operators", "Имя");
			Если УзелОператорыЭДО <> Неопределено Тогда
				Для Каждого УзелОператорЭДО Из УзелОператорыЭДО.Строки Цикл
					
					УзелОператорЭДОИд = УзелОператорЭДО.Строки.Найти("ИдОперЭДО", "Имя");
					Если ВРег(УзелОператорЭДОИд.Значение) = ВРег(Лев(Объект.ИдентификаторОрганизации, 3)) Тогда
						
						УзелОператорЭДОНаимОрг = УзелОператорЭДО.Строки.Найти("НаимОрг", "Имя");
						Объект.ОператорЭДО = УзелОператорЭДОНаимОрг.Значение;
						
						УзелОператорЭДОИд = УзелОператорЭДО.Строки.Найти("ИдОперЭДО", "Имя");
						Объект.ОператорЭДОИд = УзелОператорЭДОИд.Значение;
						
						УзелОператорЭДОИНН = УзелОператорЭДО.Строки.Найти("ИННЮЛ", "Имя");
						Объект.ОператорЭДОИНН = УзелОператорЭДОИНН.Значение;
						
						УзелОператорЭДОКПП = УзелОператорЭДО.Строки.Найти("КПП", "Имя");
						Объект.ОператорЭДОКПП = УзелОператорЭДОКПП.Значение;
						
						УзелОператорЭДООГРН = УзелОператорЭДО.Строки.Найти("ОГРН", "Имя");
						Если УзелОператорЭДООГРН <> Неопределено Тогда
							Объект.ОператорЭДООГРН = УзелОператорЭДООГРН.Значение;
						КонецЕсли;
						
						УзелОператорЭДОСертификат = УзелОператорЭДО.Строки.Найти("Сертификат", "Имя");
						Если УзелОператорЭДОСертификат <> Неопределено Тогда
							ОператорЭДОСертификат = УзелОператорЭДОСертификат.Значение;
						КонецЕсли;
						
						Прервать;
					КонецЕсли;
					
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Методы создания и отправки рег. пакета 1СЭДО.

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНачало()
	
	Контекст = Новый Структура;
	
	РеквизитыПакета = Новый Структура;
	ДвоичныеДанныеДляОператораЭДО = Неопределено;
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Неопределено;
	
	ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНачалоНаСервере(ДвоичныеДанныеСоглашенияНаПодключениеЭДО, РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО);
	
	Если Не ЗначениеЗаполнено(ДвоичныеДанныеДляОператораЭДО) ИЛИ Не ЗначениеЗаполнено(ДвоичныеДанныеДляОператораЭДО) Тогда
		Возврат;
	КонецЕсли;
	
	Контекст.Вставить("ДвоичныеДанныеСоглашенияНаПодключениеЭДО", ДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	Контекст.Вставить("РеквизитыПакета", РеквизитыПакета);
	Контекст.Вставить("ДвоичныеДанныеДляОператораЭДО", ДвоичныеДанныеДляОператораЭДО);
	Контекст.Вставить("УникальныйИдентификаторЗаявки1СЭДО", УникальныйИдентификаторЗаявки1СЭДО);
	Контекст.Вставить("ДвоичныеДанныеДляОператораЭДО", ДвоичныеДанныеДляОператораЭДО);
	Контекст.Вставить("УникальныйИдентификатор", УникальныйИдентификатор);
	ОписаниеПодписатьЭД = Новый ОписаниеОповещения("ПодготовитьДанныеДляРегистрационногоПакета1СЭДОПродолжение2", ЭтотОбъект, Контекст);
	
	ОписаниеДанных = Новый Структура;
	
	Операция = НСтр("ru = 'Подписание регистрационных данных на подключение к сервису 1С-ЭДО'");
	ОписаниеДанных.Вставить("Операция",            Операция);
	
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(ДобавляемыйСертификат);
	ОписаниеДанных.Вставить("ОтборСертификатов",   МассивСертификатов);
	
	
	ОписаниеДанных.Вставить("НаборДанных",         Новый Массив);
	ОписаниеДанных.НаборДанных.Добавить(Новый Структура("Данные", ДвоичныеДанныеСоглашенияНаПодключениеЭДО));
	ОписаниеДанных.НаборДанных.Добавить(Новый Структура("Данные", ДвоичныеДанныеДляОператораЭДО));
	
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("ЗаголовокДанных",     "");
	ОписаниеДанных.Вставить("БезПодтверждения",    Истина);
	
	СписокПредставлений = Новый СписокЗначений;
	
	Представление = НСтр("ru = 'Пользовательское соглашение на подключение к сервису 1С-ЭДО'");
	ОписаниеПредставленияЗаявления = Новый ОписаниеОповещения("ОбработатьПредставлениеЗаявленияНаПодключениеКЭДО", ОбменСКонтрагентамиСлужебныйКлиент, Контекст);
	СписокПредставлений.Добавить(ОписаниеПредставленияЗаявления, Представление);
	
	Представление = НСтр("ru = 'Данные для регистрации учетной записи у оператора ЭДО'");
	ОписаниеПредставленияДанных = Новый ОписаниеОповещения("ОбработатьПредставлениеДанныхДляОператораЭДО", ОбменСКонтрагентамиСлужебныйКлиент, Контекст);
	СписокПредставлений.Добавить(ОписаниеПредставленияДанных, Представление);
	
	ОписаниеДанных.Вставить("СписокПредставлений", СписокПредставлений);
	ОписаниеДанных.Вставить("ПредставлениеНабора", НСтр("ru = 'Пользовательское соглашение, Данные для регистрации у оператора ЭДО'"));
	
	ЭлектроннаяПодписьКлиент.Подписать(ОписаниеДанных, , ОписаниеПодписатьЭД);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОНачалоНаСервере(ДвоичныеДанныеСоглашенияНаПодключениеЭДО, РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО)
	
	ПодготовитьДанныеПоСоглашениюНаПодключениеКЭДО(ДвоичныеДанныеСоглашенияНаПодключениеЭДО);
	ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОПродолжение2(РезультатВыполнения, Контекст) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		// Оповещение произошло из процедуры БСП
		Если РезультатВыполнения.Свойство("НаборДанных") Тогда
			// Если Успех, необходимо перебрать элементы массива Набор данных
			// в подписанных эд в элементе массива являющимся структурой будет свойство "Свойства подписи"
			// такие ЭД надо добавить в массив "МассивЭД" для обновления их статусов.
			Для Каждого ПодписываемыеДанные Из РезультатВыполнения.НаборДанных Цикл
				Если Не ПодписываемыеДанные.Свойство("СвойстваПодписи") Тогда
					Возврат;
				КонецЕсли;
				
				СтруктураПодписи = ПодписываемыеДанные.СвойстваПодписи;
				Если ЭтоАдресВременногоХранилища(ПодписываемыеДанные.СвойстваПодписи) Тогда
					СтруктураПодписи = ПолучитьИзВременногоХранилища(СтруктураПодписи);
				КонецЕсли;
				Если Контекст.ДвоичныеДанныеСоглашенияНаПодключениеЭДО = ПодписываемыеДанные.Данные Тогда
				
					Контекст.Вставить("ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО", СтруктураПодписи.Подпись);
				КонецЕсли;
				Если Контекст.ДвоичныеДанныеДляОператораЭДО = ПодписываемыеДанные.Данные Тогда
				
					Контекст.Вставить("ПодписанныеДвоичныеДанныеДляОператораЭДО", СтруктураПодписи.Подпись);
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		
	КонецЕсли;
	
	// Подготовим сертификат оператора ЭДО для шифрования информации
	Если Не ЗначениеЗаполнено(ОператорЭДОСертификат) Тогда
		
		ТекстСообщения = НСтр("ru = 'Ошибка получения сертификата оператора ЭДО.
									|Необходимо повторить добавление сертификата.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;

	// Зашифровать
	ОписаниеПодписатьЭД = Новый ОписаниеОповещения("ПодготовитьДанныеДляРегистрационногоПакета1СЭДОЗавершение", ЭтотОбъект, Контекст);
	
	ТекстСертификата = ОператорЭДОСертификат;
	Если СтрНайти(ТекстСертификата, "-----BEGIN CERTIFICATE-----") > 0 Тогда
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----BEGIN CERTIFICATE-----", "");
		ТекстСертификата = СтрЗаменить(ТекстСертификата, "-----END CERTIFICATE-----", "");
	КонецЕсли;
	ДвоичныеДанныеСертификатаОператораЭДО = Base64Значение(ТекстСертификата);
	
	ОписаниеДанных = Новый Структура;
	ОписаниеДанных.Вставить("Операция",            НСтр("ru = 'Шифрование данных для регистрации у оператора ЭДО'"));
	
	МассивСертификатов = Новый Массив;
	МассивСертификатов.Добавить(ДвоичныеДанныеСертификатаОператораЭДО);
	ОписаниеДанных.Вставить("НаборСертификатов",   МассивСертификатов);
	
	ОписаниеДанных.Вставить("ПоказатьКомментарий", Ложь);
	ОписаниеДанных.Вставить("ИзменятьНабор",       Ложь);
	ОписаниеДанных.Вставить("Данные",              Контекст.ДвоичныеДанныеДляОператораЭДО);
	ОписаниеДанных.Вставить("ЗаголовокДанных",     НСтр("ru = 'Файл'"));
	ОписаниеДанных.Вставить("БезПодтверждения",    Ложь);
	
	Представление = Новый Структура;
	Представление.Вставить("Представление", НСтр("ru = 'Данные для регистрации у оператора ЭДО'"));
	ОписаниеПредставленияЗаявления = Новый ОписаниеОповещения("ОбработатьПредставлениеДанныхДляОператораЭДО", ОбменСКонтрагентамиСлужебныйКлиент, Контекст);
	Представление.Вставить("Значение",      ОписаниеПредставленияЗаявления);
	
	ОписаниеДанных.Вставить("Представление",       Представление);
	
	ЭлектроннаяПодписьКлиент.Зашифровать(ОписаниеДанных, , ОписаниеПодписатьЭД);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодготовитьДанныеДляРегистрационногоПакета1СЭДОЗавершение(РезультатВыполнения, Контекст) Экспорт
	
	Если ТипЗнч(РезультатВыполнения) = Тип("Структура") Тогда
		Если Не РезультатВыполнения.Свойство("ЗашифрованныеДанные") Тогда
			Возврат;
		КонецЕсли;
	КонецЕсли;
	
	ЗашифрованныеДанныеОператораЭДО = РезультатВыполнения.ЗашифрованныеДанные;
	
	ЕстьОшибки = Ложь;
	СформироватьИОтправитьРегистрационныйПакет1СЭДО(Контекст.РеквизитыПакета, ЗашифрованныеДанныеОператораЭДО,
		Контекст.ПодписанныеДвоичныеДанныеДляОператораЭДО, Контекст.ДвоичныеДанныеСоглашенияНаПодключениеЭДО,
		Контекст.ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификацииНаСайте, ЕстьОшибки);
		
	Если Не ЕстьОшибки Тогда
		// Запустим обработчик ожидания результата регистрации организации у оператора.
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД", ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
		ФормаДлительнойОперации = ДлительныеОперацииКлиент.ОткрытьФормуДлительнойОперации(ЭтаФорма, УникальныйИдентификаторЗаявки1СЭДО);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеПоСоглашениюНаПодключениеКЭДО(ДвоичныеДанныеСоглашенияНаПодключениеЭДО)
	
	// Подготовим данные для соглашения с оператором ЭДО
	ИмяВременногоФайлаСоглашениеЭДО = ОбменСКонтрагентамиСлужебный.ТекущееИмяВременногоФайла("xml");
	
	// LicenseCard
	СоглашениеЭДО = Новый ЗаписьXML;
	СоглашениеЭДО.ОткрытьФайл(ИмяВременногоФайлаСоглашениеЭДО, "windows-1251");
	СоглашениеЭДО.ЗаписатьОбъявлениеXML();
	
	СоглашениеЭДО.ЗаписатьНачалоЭлемента("LicenseCard");
	СоглашениеЭДО.ЗаписатьНачалоЭлемента("info");
	СоглашениеЭДО.ЗаписатьАтрибут("EDOAgreed", XMLСтрока(Истина));
	СоглашениеЭДО.ЗаписатьКонецЭлемента(); // info
	СоглашениеЭДО.ЗаписатьКонецЭлемента(); // LicenseCard
	
	СоглашениеЭДО.Закрыть();
	
	ДвоичныеДанныеСоглашенияНаПодключениеЭДО = Новый ДвоичныеДанные(ИмяВременногоФайлаСоглашениеЭДО);
	ЭлектронноеВзаимодействиеСлужебный.УдалитьВременныеФайлы(ИмяВременногоФайлаСоглашениеЭДО);
	
КонецПроцедуры

&НаСервере
Процедура ПодготовитьДанныеПоРегистрационнойИнформации(РеквизитыПакета, ДвоичныеДанныеДляОператораЭДО)
	
	УникальныйИдентификаторЗаявки1СЭДО = ОбменСКонтрагентамиСлужебный.НовыйUUID();
	
	// Подготовим данные для 1СЭДО
	ИмяРеквизитаИННОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ИННОрганизации");
	ИмяРеквизитаКППОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("КППОрганизации");
	ИмяРеквизитаОГРНОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("ОГРНОрганизации");
	ИмяРеквизитаНаименованиеОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяНаличиеОбъектаРеквизитаВПрикладномРешении("НаименованиеОрганизации");
	
	ПараметрыОрганизации = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Организация,
		ИмяРеквизитаИННОрганизации + ", " + ИмяРеквизитаКППОрганизации + ", " + ИмяРеквизитаНаименованиеОрганизации + ", " + ИмяРеквизитаОГРНОрганизации);

	ПараметрыСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(ДобавляемыйСертификат);
	
	РеквизитыПакета.Вставить("УникальныйИдентификаторЗаявки1СЭДО", УникальныйИдентификаторЗаявки1СЭДО);
	РеквизитыПакета.Вставить("НаименованиеОрганизации", ПараметрыОрганизации[ИмяРеквизитаНаименованиеОрганизации]);
	РеквизитыПакета.Вставить("ИННОрганизации",  СокрЛП(ПараметрыОрганизации[ИмяРеквизитаИННОрганизации]));
	РеквизитыПакета.Вставить("КППОрганизации",  СокрЛП(ПараметрыОрганизации[ИмяРеквизитаКППОрганизации]));
	РеквизитыПакета.Вставить("ИдентификаторОрганизации",  Объект.ИдентификаторОрганизации);
	РеквизитыПакета.Вставить("ОператорЭДО",     Объект.ОператорЭДО);
	РеквизитыПакета.Вставить("ОператорЭДОИд",   Объект.ОператорЭДОИд);
	РеквизитыПакета.Вставить("Сертификат", Новый Массив);
	РеквизитыПакета.Вставить("ЭлектроннаяПочта", Объект.ЭлектроннаяПочтаДляУведомлений);
	
	Макет = Обработки.ОбменСКонтрагентами.ПолучитьМакет("РегистрационнаяИнформация");

	// Подготовим данные для оператора ЭДО
	ДеревоРегистрационнойИнформации = ЭлектронноеВзаимодействие.ДеревоДокумента(Макет);
	
	// Служебные данные
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ИдФайл",   "registration_" + УникальныйИдентификаторЗаявки1СЭДО);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ВерсПрог", "1С:Предприятие 8");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ВерсФорм", "5.02");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "КНД",      "1115109");
	
	// Данные по налоговому органу
	Если Не ЗначениеЗаполнено(Объект.КодНалоговогоОргана) И ЗначениеЗаполнено(ПараметрыОрганизации[ИмяРеквизитаКППОрганизации]) Тогда
		Объект.КодНалоговогоОргана = Лев(ПараметрыОрганизации[ИмяРеквизитаКППОрганизации], 4);
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "КодНО", Объект.КодНалоговогоОргана);
	
	// Данные по оператору ЭДО
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.НаимОрг",   Объект.ОператорЭДО);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ИдОперЭДО", Объект.ОператорЭДОИд);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ИННЮЛ",     Объект.ОператорЭДОИНН);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.КПП",       Объект.ОператорЭДОКПП);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "ОперЭДО.ОГРН",      Объект.ОператорЭДООГРН);
	
	// Данные по абоненту
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипЗаявления", "2");
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ИдУчастЭДО", Объект.ИдентификаторОрганизации);
		
	Если Не ОбменСКонтрагентамиПереопределяемый.ЭтоФизЛицо(Объект.Организация) Тогда
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.НаимОрг", ПараметрыОрганизации[ИмяРеквизитаНаименованиеОрганизации]);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ИННЮЛ",   СокрЛП(ПараметрыОрганизации[ИмяРеквизитаИННОрганизации]));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.КПП",     СокрЛП(ПараметрыОрганизации[ИмяРеквизитаКППОрганизации]));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ОГРН",    СокрЛП(ПараметрыОрганизации[ИмяРеквизитаОГРНОрганизации]));
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ДатаПодклЭДО", ТекущаяДатаСеанса());
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Индекс",     Объект.Индекс);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.КодРегион",  Объект.КодРегиона);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Район",      Объект.Район);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Город",      Объект.Город);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.НаселПункт", Объект.НаселенныйПункт);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Улица",      Объект.Улица);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Дом",        Объект.Дом);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Корпус",     Объект.Корпус);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.АдрРФ.Кварт",      Объект.Квартира);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Фамилия",  ПараметрыСертификата.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Имя",      ПараметрыСертификата.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ЮЛ.ФИООтв.Отчество", ПараметрыСертификата.Отчество);
		
	Иначе
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ИННФЛ",  СокрЛП(ПараметрыОрганизации[ИмяРеквизитаИННОрганизации]));
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ОГРНИП", СокрЛП(ПараметрыОрганизации[ИмяРеквизитаОГРНОрганизации]));
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ДатаПодклЭДО", ТекущаяДатаСеанса());
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Индекс",     Объект.Индекс);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.КодРегион",  Объект.КодРегиона);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Район",      Объект.Район);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Город",      Объект.Город);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.НаселПункт", Объект.НаселенныйПункт);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Улица",      Объект.Улица);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Дом",        Объект.Дом);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Корпус",     Объект.Корпус);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.АдрРФ.Кварт",      Объект.Квартира);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Фамилия",  ДобавляемыйСертификат.Фамилия);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Имя",      ДобавляемыйСертификат.Имя);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "УчастЭДО.ТипУчастника.ИП.ФИО.Отчество", ДобавляемыйСертификат.Отчество);
		
	КонецЕсли;
	
	СертификатыПользователя = Новый ТаблицаЗначений;
	СертификатыПользователя.Колонки.Добавить("ДатаНачСертиф");
	СертификатыПользователя.Колонки.Добавить("ДатаКонСертиф");
	СертификатыПользователя.Колонки.Добавить("ОтпСертиф");
	СертификатыПользователя.Колонки.Добавить("Сертификат");
	СертификатыПользователя.Колонки.Добавить("Фамилия");
	СертификатыПользователя.Колонки.Добавить("Имя");
	СертификатыПользователя.Колонки.Добавить("Отчество");
	
	// Добавим текущие сертификаты
	Для каждого СтрокаТаблицы Из Объект.СертификатыПодписейОрганизации Цикл
		ПараметрыТекущегоСертификата = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.РеквизитыСертификата(СтрокаТаблицы.Сертификат);
		
		НоваяСтрока = СертификатыПользователя.Добавить();
		НоваяСтрока.ДатаКонСертиф = ПараметрыТекущегоСертификата.ДействителенДо;
		
		Отпечаток = НРег(СТРЗаменить(Строка(Base64Значение(ПараметрыТекущегоСертификата.Отпечаток))," ",""));
		НоваяСтрока.ОтпСертиф = Отпечаток;
		
		// Подготовим пользовательский сертификат
		ДвоичныеДанныеСертификата = ПараметрыТекущегоСертификата.ДвоичныеДанныеСертификата;
		
		СертификатТекст = Base64Строка(ДвоичныеДанныеСертификата);
		НоваяСтрока.Сертификат = "-----BEGIN CERTIFICATE-----" + Символы.ПС
								+ СертификатТекст + Символы.ПС
								+ "-----END CERTIFICATE-----";
		
		РеквизитыПакета.Сертификат.Добавить(СертификатТекст);
		
		НоваяСтрока.Фамилия  = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", ПараметрыТекущегоСертификата.Фамилия, " ");
		НоваяСтрока.Имя      = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", ПараметрыТекущегоСертификата.Имя, " ");
		НоваяСтрока.Отчество = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", ПараметрыТекущегоСертификата.Отчество, " ");
		
		Сертификат = Новый СертификатКриптографии(ДвоичныеДанныеСертификата);
		НоваяСтрока.ДатаНачСертиф = Сертификат.ДатаНачала;
	КонецЦикла;
	
	НоваяСтрока = СертификатыПользователя.Добавить();
	
	ТекущийСертификат = Новый СертификатКриптографии(ПараметрыСертификата.ДвоичныеДанныеСертификата);
	НоваяСтрока.ДатаНачСертиф = ТекущийСертификат.ДатаНачала;
	НоваяСтрока.ДатаКонСертиф = ДобавляемыйСертификат.ДействителенДо;
	
	Отпечаток = НРег(СТРЗаменить(Строка(Base64Значение(ДобавляемыйСертификат.Отпечаток))," ",""));
	НоваяСтрока.ОтпСертиф = Отпечаток;
	
	СертификатТекст = Base64Строка(ПараметрыСертификата.ДвоичныеДанныеСертификата);
	НоваяСтрока.Сертификат = "-----BEGIN CERTIFICATE-----" + Символы.ПС
							+ СертификатТекст + Символы.ПС
							+ "-----END CERTIFICATE-----";
	РеквизитыПакета.Сертификат.Добавить(СертификатТекст);

	НоваяСтрока.Фамилия  = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", ПараметрыСертификата.Фамилия, " ");
	НоваяСтрока.Имя      = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", ПараметрыСертификата.Имя, " ");
	НоваяСтрока.Отчество = СтроковыеФункцииКлиентСервер.ЗаменитьОдниСимволыДругими("_", ПараметрыСертификата.Отчество, " ");
	
	ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоРегистрационнойИнформации, СертификатыПользователя, "УчастЭДО.СертифДолжн");
	
	// Данные по подписанту
	ДолжностьПоСертификату = 0;
	Если ЗначениеЗаполнено(ПараметрыСертификата.Должность) Тогда
		ДолжностьПоСертификату = ПараметрыСертификата.Должность;
	КонецЕсли;
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.Должность",    ДолжностьПоСертификату);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Фамилия",  ПараметрыСертификата.Фамилия);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Имя",      ПараметрыСертификата.Имя);
	ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоРегистрационнойИнформации, "Подписант.ФИО.Отчество", ПараметрыСертификата.Отчество);
	
	ДвоичныеДанныеДляОператораЭДО = ОбменСКонтрагентамиВнутренний.РегистрационныеДанныеДляОператораЭДО(ДеревоРегистрационнойИнформации);
	
КонецПроцедуры

&НаСервере
Процедура СформироватьИОтправитьРегистрационныйПакет1СЭДО(РеквизитыПакета, ЗашифрованныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПараметрыАутентификации, ЕстьОшибки)
	
	ИмяФайлаПакетаДля1СЭДО = ОбменСКонтрагентамиСлужебный.РегистрационныйПакетДляОператораЭДО(РеквизитыПакета,
		ЗашифрованныеДанныеОператораЭДО, ПодписанныеДвоичныеДанныеДляОператораЭДО,
		ДвоичныеДанныеСоглашенияНаПодключениеЭДО, ПодписанныеДвоичныеДанныеСоглашенияНаПодключениеЭДО);
		
	// Отправка заявления на сервис 1С-ЭДО
	ТекстЗаголовкаСообщения = НСтр("ru = 'Отправка заявления на изменение данных на сервисе 1С-ЭДО'");
	
	ШаблонСообщения = НСтр("ru = 'Выполнение операции: %1
								|Заявление № %2'");
	ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонСообщения, ТекстЗаголовкаСообщения,
		УникальныйИдентификаторЗаявки1СЭДО);
	ЭлектронноеВзаимодействиеСлужебный.ВыполнитьЗаписьСобытияПоЭДВЖурналРегистрации(ТекстСообщения, 2, УровеньЖурналаРегистрации.Информация);
	
	ОбменСКонтрагентамиСлужебный.ОтправитьРегистрационныйПакет1СЭДО(ИмяФайлаПакетаДля1СЭДО, ПараметрыАутентификации, ЕстьОшибки);
	
	ЭлектронноеВзаимодействиеСлужебный.УдалитьВременныеФайлы(ИмяФайлаПакетаДля1СЭДО);
	
КонецПроцедуры

// Открывает форму выбора адреса в модальном режиме и возвращает
// реквизиты адреса в виде структуры с соответствующими полями.
//
// Параметры:
//  ТолькоДляПросмотра (Булево): Истина - открыть форму выбора адреса только для просмотра.
//
// Возвращаемое значение:
//  Структура с полями - реквизитами адреса или Неопределено, если на форме адреса при закрытии не была нажата кнопка "ОК".
//
&НаКлиенте
Процедура ВыбратьАдрес(ТолькоДляПросмотра = Ложь)
	
	ПараметрыФормы = Новый Структура("ТолькоПросмотр", ТолькоДляПросмотра);
	
	Если ТолькоДляПросмотра Тогда
		ОповещениеОЗакрытии = Неопределено;
	Иначе
		ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПриВыбореАдреса", ЭтотОбъект);
	КонецЕсли;
	
	ПараметрыФормы.Вставить("Индекс"             , Объект.Индекс);
	ПараметрыФормы.Вставить("Регион"             , Объект.Регион);
	ПараметрыФормы.Вставить("Район"              , Объект.Район);
	ПараметрыФормы.Вставить("Город"              , Объект.Город);
	ПараметрыФормы.Вставить("НасПункт"           , Объект.НаселенныйПункт);
	ПараметрыФормы.Вставить("Улица"              , Объект.Улица);
	ПараметрыФормы.Вставить("Дом"                , Объект.Дом);
	ПараметрыФормы.Вставить("Корпус"             , Объект.Корпус);
	ПараметрыФормы.Вставить("Квартира"           , Объект.Квартира);
	ПараметрыФормы.Вставить("КодРегиона"         , Объект.КодРегиона);
	ПараметрыФормы.Вставить("КодНалоговогоОргана", Объект.КодНалоговогоОргана);
	
	ОткрытьФорму("ОбщаяФорма.АдресУчастникаОбменаЭД",
		ПараметрыФормы,
		,
		,
		,
		,
		ОповещениеОЗакрытии);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриВыбореАдреса(ПараметрыАдреса, ДопПараметры) Экспорт
	
	Если ТипЗнч(ПараметрыАдреса) <> Тип("Структура") Тогда
		Возврат;
	КонецЕсли;
	
	// Если адрес изменен, то применение изменений
	Объект.Индекс              = ПараметрыАдреса.Индекс;
	Объект.Регион              = ПараметрыАдреса.Регион;
	Объект.Район               = ПараметрыАдреса.Район;
	Объект.Город               = ПараметрыАдреса.Город;
	Объект.НаселенныйПункт     = ПараметрыАдреса.НаселенныйПункт;
	Объект.Улица               = ПараметрыАдреса.Улица;
	Объект.Дом                 = ПараметрыАдреса.Дом;
	Объект.Корпус              = ПараметрыАдреса.Корпус;
	Объект.Квартира            = ПараметрыАдреса.Квартира;
	Объект.КодРегиона          = ПараметрыАдреса.КодРегиона;
	Объект.КодНалоговогоОргана = ПараметрыАдреса.КодНалоговогоОргана;
	
	ОповещениеПродолжитьДобавлениеСертификата = Новый ОписаниеОповещения("ЗарегистрироватьСертификат1СЭДОПродолжить", ЭтотОбъект);
	ВыполнитьОбработкуОповещения(ОповещениеПродолжитьДобавлениеСертификата);
	
КонецПроцедуры

// Методы работы с сервисом 1СЭДО

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗаданияДляЭД()

	Попытка
		Если ФормаДлительнойОперации.Открыта() Тогда
			МассивИдентификаторов = ОбменССерверомПолучитьОтветЭДО(УникальныйИдентификаторЗаявки1СЭДО);
			Если ЗначениеЗаполнено(МассивИдентификаторов) Тогда
				Оповестить("ОповещениеОбУспешномИзмененииДанныхУчастникаОбменаЭД");
				ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
			Иначе
				ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
				ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗаданияДляЭД",
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, Истина);
			КонецЕсли;
		КонецЕсли;
	Исключение
		ДлительныеОперацииКлиент.ЗакрытьФормуДлительнойОперации(ФормаДлительнойОперации);
		ВызватьИсключение ПодробноеПредставлениеОшибки(ИнформацияОбОшибке());
	КонецПопытки;
	
КонецПроцедуры

&НаСервере
Функция ОбменССерверомПолучитьОтветЭДО(ИдентификаторЗапросаНаРегистрацию)
	
	МассивИдентификаторов = Новый Массив;
	
	ИмяФайлаОтвета = ОбменСКонтрагентамиСлужебный.ПолучитьФайлОтветаОтСервераЭДО("GetInfo", ИдентификаторЗапросаНаРегистрацию);
	Если ИмяФайлаОтвета <> Неопределено Тогда
		ДанныеФайла = ОбменСКонтрагентамиСлужебный.ПрочитатьТекстИзФайла(ИмяФайлаОтвета, , Истина);
		ПолучитьИдентификаторАбонента(ДанныеФайла, МассивИдентификаторов);
	КонецЕсли;
	
	Возврат МассивИдентификаторов;

КонецФункции

&НаСервереБезКонтекста
Процедура ПолучитьИдентификаторАбонента(ДанныеФайла, МассивИдентификаторов)
	
	Если Не ПустаяСтрока(ДанныеФайла) Тогда
		ДеревоXML = ОбменСКонтрагентамиСлужебный.ЗагрузитьСтрокуXMLВДеревоЗначений(ДанныеФайла);
		Если ДеревоXML <> Неопределено Тогда
			УзелАбоненты = ДеревоXML.Строки.Найти("Abonents", "Имя");
			Если УзелАбоненты <> Неопределено Тогда
				Для каждого УзелАбонент Из УзелАбоненты.Строки Цикл
					УзелИдентификаторАбонента = УзелАбонент.Строки.Найти("Identifier", "Имя");
					МассивИдентификаторов.Добавить(УзелИдентификаторАбонента.Значение);
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеДоступностьюЛичногоКабинета()
	
	Элементы.НадписьЛичныйКабинетУчастникаОбменаЭД.Доступность = НЕ ПустаяСтрока(Объект.ИдентификаторОрганизации);
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСертификатПодписиВСписок(СертификатЭП)
	
	Строка = Объект.СертификатыПодписейОрганизации.Добавить();
	Строка.Сертификат = СертификатЭП;
	Модифицированность = Истина;
	ПометитьНеДействующиеСертификатыВСписке();
	
КонецПроцедуры

&НаСервере
Процедура ПометитьНеДействующиеСертификатыВСписке()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка,
	|	ВЫБОР
	|		КОГДА НЕ СертификатыКлючейЭлектроннойПодписиИШифрования.ПометкаУдаления
	|				И НЕ СертификатыКлючейЭлектроннойПодписиИШифрования.Отозван
	|				И СертификатыКлючейЭлектроннойПодписиИШифрования.ДействителенДо >= &ТекущаяДата
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК Действует
	|ИЗ
	|	Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования КАК СертификатыКлючейЭлектроннойПодписиИШифрования
	|ГДЕ
	|	СертификатыКлючейЭлектроннойПодписиИШифрования.Ссылка В(&СписокСертификатов)";
	
	Запрос.УстановитьПараметр("СписокСертификатов", Объект.СертификатыПодписейОрганизации.Выгрузить().ВыгрузитьКолонку("Сертификат"));
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());
	ТаблицаРезультата = Запрос.Выполнить().Выгрузить();
	
	Для Каждого Строка Из Объект.СертификатыПодписейОрганизации Цикл
		СтрокиРезультата = ТаблицаРезультата.НайтиСтроки(Новый Структура("Ссылка", Строка.Сертификат));
		Если СтрокиРезультата.Количество() = 1 Тогда
			Строка.Действует = СтрокиРезультата[0].Действует;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогОбмена(ПутьККаталогу)
	
#Если НЕ ВебКлиент Тогда
	ДиалогКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогКаталога.Заголовок = НСтр("ru = 'Выберите сетевой каталог для обмена'");
	ДиалогКаталога.Каталог   = ПутьККаталогу;
	Если ДиалогКаталога.Выбрать() Тогда
		Модифицированность = Не ПутьККаталогу = ДиалогКаталога.Каталог;
		ПутьККаталогу = ДиалогКаталога.Каталог;
		ОбменСКонтрагентамиСлужебныйВызовСервера.ПроверитьДоступностьКаталогаДляПрямогоОбмена(ПутьККаталогу);
	КонецЕсли;
#КонецЕсли
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуВыбораСертификатаЭП()
	
	Если Объект.ПометкаУдаления Тогда
		ТекстСообщения = НСтр("ru='Для выполнения действия необходимо снять пометку удаления.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	Если ОбменСКонтрагентамиСлужебныйКлиент.ИспользуетсяИнтернетПоддержкаПользователей() Тогда
		// Обработчик библиотеки ИПП
		
		Если ПустаяСтрока(Объект.ИдентификаторОрганизации)
			И Объект.СертификатыПодписейОрганизации.Количество() > 0 Тогда
			// Эта операция запрещена. Так как в соглашении уже имеется хотя бы один
			// сертификат, но еще не получен идентификатор.
			ТекстПредупреждения = НСтр("ru = 'Перед добавлением новых сертификатов в профиль настроек ЭДО
											|необходимо получить идентификатор участника обмена ЭД'");
			ПоказатьПредупреждение(, ТекстПредупреждения, 30);
			Возврат;
			
			// Иначе
			// Остались случаи, когда добавляется первый сертификат
			// и когда добавляемый сертификат нужно зарегистрировать в 1С-Такском
			// оба этих случая отрабатываются в обработчике события формы ОбработкаВыбора.
			
		КонецЕсли;
		
		// Конец обработчика библиотеки ИПП
	КонецЕсли;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор", Новый Структура("Организация", Объект.Организация));
	ОткрытьФорму("Справочник.СертификатыКлючейЭлектроннойПодписиИШифрования.ФормаВыбора", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаУведомленийПродолжить(РасшифрованныйМаркер, ДополнительныеПараметры) Экспорт
	
	Если РасшифрованныйМаркер = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУведомления = СвойстваПодписок(РасшифрованныйМаркер);
	
	Если НастройкиУведомления = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	НастройкиУведомления.Вставить("ИдентификаторОрганизации", ДополнительныеПараметры.Идентификатор);
	НастройкиУведомления.Вставить("ПрофильНастроек", ДополнительныеПараметры.Профиль);
	
	ОповещениеФормы = Новый ОписаниеОповещения("НастройкаУведомленийЗакрытие", ЭтотОбъект);
	ОткрытьФорму("Справочник.ПрофилиНастроекЭДО.Форма.НастройкаУведомленийЭДО", НастройкиУведомления, ЭтотОбъект,,,,
			ОповещениеФормы, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаСервере
Функция СвойстваПодписок(РасшифрованныйМаркер)
	
	СпособОбмена = Перечисления.СпособыОбменаЭД.ЧерезСервис1СЭДО;
	АдресРесурса = "GetSubscriptions/?query=" + Объект.ИдентификаторОрганизации;
	ВидОперации = НСтр("ru = 'Получение информации о свойствах подписки ЭДО'");
	
	Настройка = ОбменСКонтрагентамиВнутренний.РезультатОперацииЭДОПолучить(РасшифрованныйМаркер, СпособОбмена, АдресРесурса, ВидОперации);
	
	Если Настройка = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	СвойстваПодписок = ПустыеРеквизитыПодписки();
	
	СвойстваПодписок.ВключитьПодписку = ОбменСКонтрагентамиСлужебный.ЗначениеСтатуса(Настройка.Total.Status);
	СвойстваПодписок.ЭлектроннаяПочта = Настройка.Mail;
	СвойстваПодписок.Приглашения = ОбменСКонтрагентамиСлужебный.ЗначениеСтатуса(Настройка.NewInvitations.Status);
	СвойстваПодписок.Ответы = ОбменСКонтрагентамиСлужебный.ЗначениеСтатуса(Настройка.NewInvitationResults.Status);
	СвойстваПодписок.НовыеЭД = ОбменСКонтрагентамиСлужебный.ЗначениеСтатуса(Настройка.NewMessages.Status);
	СвойстваПодписок.СтарыеЭД = ОбменСКонтрагентамиСлужебный.ЗначениеСтатуса(Настройка.UnfinishedMessages.Status);
	СвойстваПодписок.Сертификаты = ОбменСКонтрагентамиСлужебный.ЗначениеСтатуса(Настройка.CertExpiration.Status);
	
	Возврат СвойстваПодписок;
	
КонецФункции

&НаКлиенте
Процедура НастройкаУведомленийЗакрытие(ПараметрыОповещения, ДополнительныеПараметры) Экспорт
	
	Если ПараметрыОповещения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗаписатьПрофиль = Ложь;
	ОбновитьОтображениеПочты = Ложь;
	Если Не ПараметрыОповещения.УведомлятьОСобытиях = УведомлятьОСобытиях 
		Или Не ПараметрыОповещения.АдресУведомлений = Объект.ЭлектроннаяПочтаДляУведомлений Тогда
		
		УведомлятьОСобытиях = ПараметрыОповещения.УведомлятьОСобытиях;
		Объект.ЭлектроннаяПочтаДляУведомлений = ПараметрыОповещения.АдресУведомлений;
		
		ОбновитьОтображениеПочты = Истина;
	КонецЕсли;
	
	Если ОбновитьОтображениеПочты Тогда
		УстановитьНастройкуУведомлений();
	КонецЕсли;
	
	Если ЗаписатьПрофиль Тогда
		ЭтотОбъект.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПустыеРеквизитыПодписки()
	
	Реквизиты = Новый Структура;
	Реквизиты.Вставить("ВключитьПодписку", Неопределено);
	Реквизиты.Вставить("ЭлектроннаяПочта", Неопределено);
	Реквизиты.Вставить("Приглашения", Неопределено);
	Реквизиты.Вставить("Ответы", Неопределено);
	Реквизиты.Вставить("НовыеЭД", Неопределено);
	Реквизиты.Вставить("СтарыеЭД", Неопределено);
	Реквизиты.Вставить("Сертификаты", Неопределено);
	
	Возврат Реквизиты;
	
КонецФункции

&НаСервере
Процедура УстановитьНастройкуУведомлений()
	
	Если УведомлятьОСобытиях Тогда
		ЗаголовокШаблон = НСтр("ru ='Email для уведомлений: %1'");
	Иначе
		ЗаголовокШаблон = НСтр("ru ='Уведомления не подключены'");
	КонецЕсли;

	Если ЗначениеЗаполнено(Объект.ЭлектроннаяПочтаДляУведомлений) Тогда
		АдресУведомлений = Объект.ЭлектроннаяПочтаДляУведомлений;
	ИначеЕсли УведомлятьОСобытиях Тогда
		АдресУведомлений = НСтр("ru = '<не указан>'");
	КонецЕсли;
	
	ЗаголовокПочты = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ЗаголовокШаблон, АдресУведомлений);
	
	Элементы.НастройкаУведомлений.Заголовок = ЗаголовокПочты;
	
КонецПроцедуры

&НаКлиенте
Процедура НастройкаРегламентаЭДОПродолжить(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ДанныеСтроки = Элементы.ИсходящиеДокументы.ТекущиеДанные;
	ЗаполнитьЗначенияСвойств(ДанныеСтроки, Результат);
	
	Модифицированность = Макс(Модифицированность, Результат.Модифицированность); 
	
	ЗаполнитьПредставлениеНастроек(Объект);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ЗаполнитьПредставлениеНастроек(Объект)
	
	Для Каждого ТекСтрока Из Объект.ИсходящиеДокументы Цикл
		
		ЗаполнитьПредставлениеРегламентаЭДО(ТекСтрока);
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста 
Процедура ЗаполнитьПредставлениеРегламентаЭДО(ТекСтрока)
	
	ИспользоватьЭП = ЭлектронноеВзаимодействиеСлужебныйВызовСервера.ЗначениеФункциональнойОпции("ИспользоватьЭлектронныеПодписиЭД");
	
	Если ТекСтрока.ИспользоватьЭП И ИспользоватьЭП Тогда
		Подпись = НСтр("ru = 'Подпись'");
	Иначе
		Подпись = НСтр("ru = 'Без подписи'");
	КонецЕсли;
	
	ОтветнаяПодпись = "";
	Если ТекСтрока.ТребуетсяОтветнаяПодпись И ИспользоватьЭП Тогда
		ОтветнаяПодпись = НСтр("ru = 'ответная подпись'");
	КонецЕсли;
	
	ИзвещениеОПолучении = "";
	Если ТекСтрока.ТребуетсяИзвещениеОПолучении Тогда
		ИзвещениеОПолучении = НСтр("ru = 'извещение о получении'");
	КонецЕсли;
	
	ДополнительныеНастройки = "";
	ДополнительныеНастройки = ДополнительныеНастройки
		+ Подпись
		+ ?(Не ИзвещениеОПолучении = "",","+ " ", "")
		+ ИзвещениеОПолучении
		+ ?(Не ОтветнаяПодпись = "",","+ " ", "")
		+ ОтветнаяПодпись;
		
	ТекСтрока.ДополнительныеНастройки = ДополнительныеНастройки;

КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	УсловноеОформление.Элементы.Очистить();

	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.СертификатыПодписейОрганизации.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.СертификатыПодписейОрганизации.Действует");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);

	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументы.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("КонтролироватьСодержимоеНастроекЭД");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);

	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыВерсияФормата.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ВерсияФормата");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = НСтр("ru = 'ФНС 5.01 (с 2016г.)'");


	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ИсходящийДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ВидыЭД.АктИсполнитель);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ТОРГ12Продавец);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'ФНС 5.01 (с 10.01.2016)'"));
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыВерсияФормата.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ВерсияФормата");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = НСтр("ru = 'ФНС 5.01'");


	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ИсходящийДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ВидыЭД.АктИсполнитель);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ТОРГ12Продавец);
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;
	
	Элемент.Оформление.УстановитьЗначениеПараметра("Текст", НСтр("ru = 'ФНС 5.01 (до 01.07.2017)'"));

	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыДополнительныеНастройки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.Формировать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);
	
	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыДополнительныеНастройки.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.Формировать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Если ЕстьПравоНастройкиЭДО Тогда
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.ЦветГиперссылкиБЭД);
	Иначе
		Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);
	КонецЕсли;

	//
	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыВерсияФормата.Имя);

	ОтборЭлемента = Элемент.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.Формировать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Ложь;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);

	Элемент = УсловноеОформление.Элементы.Добавить();

	ПолеЭлемента = Элемент.Поля.Элементы.Добавить();
	ПолеЭлемента.Поле = Новый ПолеКомпоновкиДанных(Элементы.ИсходящиеДокументыВерсияФормата.Имя);

	ГруппаОтбора1 = Элемент.Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора1.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаНе;

	ГруппаОтбора2 = ГруппаОтбора1.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
	ГруппаОтбора2.ТипГруппы = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИ;

	ОтборЭлемента = ГруппаОтбора2.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.ИсходящийДокумент");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.ВСписке;
	
	СписокЗначений = Новый СписокЗначений;
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ЗаказТовара);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ОтветНаЗаказ);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ОтчетОПродажахКомиссионногоТовара);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ОтчетОСписанииКомиссионногоТовара);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ПрайсЛист);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.СчетНаОплату);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.КаталогТоваров);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.ТОРГ12Продавец);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.АктИсполнитель);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.СчетФактура);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.КорректировочныйСчетФактура);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель);
	СписокЗначений.Добавить(Перечисления.ВидыЭД.АктНаПередачуПрав);
	
	ОтборЭлемента.ПравоеЗначение = СписокЗначений;

	ОтборЭлемента = ГруппаОтбора2.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ОтборЭлемента.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ИсходящиеДокументы.Формировать");
	ОтборЭлемента.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
	ОтборЭлемента.ПравоеЗначение = Истина;

	Элемент.Оформление.УстановитьЗначениеПараметра("ЦветТекста", ЦветаСтиля.НедоступныеДанныеЭДЦвет);
	Элемент.Оформление.УстановитьЗначениеПараметра("ТолькоПросмотр", Истина);
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьРегламентЭДО(Идентификатор)
	
	// Установка флагов "Подписывать, Извещения, Ответная подпись"
	// и обновление заголовка гиперссылки.
	
	СтрокаДляРедактирования = Объект.ИсходящиеДокументы.НайтиПоИдентификатору(Идентификатор);
	Если СтрокаДляРедактирования.ИсходящийДокумент = Перечисления.ВидыЭД.АктИсполнитель Тогда
	
		Если ВРег(СтрокаДляРедактирования.ВерсияФормата) = ВРег("ФНС 5.01 (С 2016Г.)")
			Или ВРег(СтрокаДляРедактирования.ВерсияФормата) = ВРег("ФНС 5.01 (УПД:Первичный документ)") Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаДляРедактирования.ТребуетсяОтветнаяПодпись = Истина;
		СтрокаДляРедактирования.ТребуетсяИзвещениеОПолучении = Истина;
		
		ЗаполнитьПредставлениеРегламентаЭДО(СтрокаДляРедактирования);
		
	ИначеЕсли СтрокаДляРедактирования.ИсходящийДокумент = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
	
		Если ВРег(СтрокаДляРедактирования.ВерсияФормата) = ВРег("ФНС 5.01 (С 2016Г.)") Тогда
			Возврат;
		КонецЕсли;
		
		СтрокаДляРедактирования.ТребуетсяОтветнаяПодпись = Истина;
		СтрокаДляРедактирования.ТребуетсяИзвещениеОПолучении = Истина;
		
		ЗаполнитьПредставлениеРегламентаЭДО(СтрокаДляРедактирования);

		
	ИначеЕсли СтрокаДляРедактирования.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура Тогда
		
		Если Не ВРег(СтрокаДляРедактирования.ВерсияФормата) = ВРег("ФНС 5.01 (УПД:Счет-фактура и первичный документ)") Тогда
			СтрокаДляРедактирования.ТребуетсяОтветнаяПодпись = Ложь;
			ЗаполнитьПредставлениеРегламентаЭДО(СтрокаДляРедактирования);
		КонецЕсли;
		
	ИначеЕсли СтрокаДляРедактирования.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
		
		Если Не ВРег(СтрокаДляРедактирования.ВерсияФормата) = ВРег("ФНС 5.01 (УКД:Корректировочный счет-фактура и первичный документ)") Тогда
			СтрокаДляРедактирования.ТребуетсяОтветнаяПодпись = Ложь;
			ЗаполнитьПредставлениеРегламентаЭДО(СтрокаДляРедактирования);
		КонецЕсли;
		
	ИначеЕсли СтрокаДляРедактирования.ИсходящийДокумент = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
		
		Если Не ВРег(СтрокаДляРедактирования.ВерсияФормата) = ВРег("ФНС 5.01 (УКД:Первичный документ)") Тогда
			СтрокаДляРедактирования.ТребуетсяОтветнаяПодпись = Истина;
			ЗаполнитьПредставлениеРегламентаЭДО(СтрокаДляРедактирования);
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиАсинхронныхВызовов

&НаКлиенте
Процедура РазрешитьРедактированиеИсходящиеДокументыЗавершить(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		КонтролироватьСодержимоеНастроекЭД = Истина;
		ИзменитьФлагУПДУКД();
	Иначе
		Если ДополнительныеПараметры.Свойство("ИспользоватьУПД") Тогда
			Объект.ИспользоватьУПД = ДополнительныеПараметры.ИспользоватьУПД;
		КонецЕсли;
		Если ДополнительныеПараметры.Свойство("ИспользоватьУКД") Тогда
			Объект.ИспользоватьУКД = ДополнительныеПараметры.ИспользоватьУКД;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ИзменитьФлагУПДУКД()
	
	Для каждого Строка Из Объект.ИсходящиеДокументы Цикл
		
		Если Строка.ИсходящийДокумент = Перечисления.ВидыЭД.АктИсполнитель Тогда
			Строка.Формировать = Не Объект.ИспользоватьУПД;
		КонецЕсли;
		
		Если Строка.ИсходящийДокумент = Перечисления.ВидыЭД.ТОРГ12Продавец Тогда
			Строка.Формировать = Не Объект.ИспользоватьУПД;
		КонецЕсли;
		
		Если Строка.ИсходящийДокумент = Перечисления.ВидыЭД.АктНаПередачуПрав Тогда
			Строка.Формировать = Не Объект.ИспользоватьУПД;
		КонецЕсли;
		
		Если Строка.ИсходящийДокумент = Перечисления.ВидыЭД.СоглашениеОбИзмененииСтоимостиОтправитель Тогда
			Строка.Формировать = Не Объект.ИспользоватьУКД;
		КонецЕсли;
		
		Если Строка.ИсходящийДокумент = Перечисления.ВидыЭД.СчетФактура Тогда
			
			Если Объект.ИспользоватьУПД Тогда
				ВерсияФормата = "ФНС 5.01 (УПД:Счет-фактура и первичный документ)";
				Строка.ТребуетсяОтветнаяПодпись = Истина;
			Иначе
				ВерсияФормата = "ФНС 5.02";
				Строка.ТребуетсяОтветнаяПодпись = Ложь;
			КонецЕсли;
			Строка.ВерсияФормата = ВерсияФормата;
			ЗаполнитьПредставлениеРегламентаЭДО(Строка);
		КонецЕсли;
		
		Если Строка.ИсходящийДокумент = Перечисления.ВидыЭД.КорректировочныйСчетФактура Тогда
			
			Если Объект.ИспользоватьУКД Тогда
				ВерсияФормата = "ФНС 5.01 (УКД:Корректировочный счет-фактура и первичный документ)";
				Строка.ТребуетсяОтветнаяПодпись = Истина;
			Иначе
				ВерсияФормата = "ФНС 5.02";
				Строка.ТребуетсяОтветнаяПодпись = Ложь;
			КонецЕсли;
			Строка.ВерсияФормата = ВерсияФормата;
			
			ЗаполнитьПредставлениеРегламентаЭДО(Строка);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти
