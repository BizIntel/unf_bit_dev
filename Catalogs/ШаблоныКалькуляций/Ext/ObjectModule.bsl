
Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УчетПоВалютам = ПолучитьФункциональнуюОпцию("УчетВалютныхОпераций");
	ВалютаУчета = Константы.ВалютаУчета.Получить();
	Ошибки = Неопределено;
	
	Для каждого Стр Из Расходы Цикл
		Если Стр.СпособРасчета=Перечисления.СпособыРасчетаСуммыЗатрат.ФиксированнаяСумма И НЕ ЗначениеЗаполнено(Стр.Валюта) Тогда
			Если УчетПоВалютам Тогда
				Индекс = Расходы.Индекс(Стр);
				ОбщегоНазначенияКлиентСервер.ДобавитьОшибкуПользователю(
				Ошибки, 
				"Расходы.Валюта",,
				"Расходы.Валюта",
				Индекс,
				НСтр("ru = 'Не указана валюта в строке %1'"),
				Индекс);
			Иначе
				Стр.Валюта = ВалютаУчета;
			КонецЕсли; 
		КонецЕсли; 
	КонецЦикла;
	
	Если НЕ Ошибки=Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьОшибкиПользователю(Ошибки, Отказ);
	КонецЕсли; 
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	// Обновление реквизита КлючСвязи
	
	КлючСвязи = 0;
	Для каждого Стр Из Запасы Цикл
		КлючСвязи = Макс(КлючСвязи, Стр.КлючСвязи); 
	КонецЦикла; 
	Для каждого Стр Из Расходы Цикл
		КлючСвязи = Макс(КлючСвязи, Стр.КлючСвязи); 
	КонецЦикла;
	
	Для каждого Стр Из Запасы Цикл
		Если Стр.КлючСвязи=0 Тогда
			КлючСвязи = КлючСвязи+1;
			Стр.КлючСвязи = КлючСвязи;
		КонецЕсли; 
	КонецЦикла; 
	Для каждого Стр Из Расходы Цикл
		Если Стр.КлючСвязи=0 Тогда
			КлючСвязи = КлючСвязи+1;
			Стр.КлючСвязи = КлючСвязи;
		КонецЕсли; 
	КонецЦикла; 
	
КонецПроцедуры
