
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.УсловиеСтарта) Тогда
		
		Если Параметры.УсловиеСтарта = Перечисления.СобытияРабочегоПроцесса.ИзменениеСостоянияЗаказНаряда Тогда
			Параметры.Отбор.Вставить("ВидОснования", "ЗаказНаряд");
		КонецЕсли;
		
		МетаданныеОснования = Перечисления.СобытияРабочегоПроцесса.СоответствиеСобытияМетаданнымОснования()[Параметры.УсловиеСтарта];
		Если МетаданныеОснования <> Неопределено Тогда
			Параметры.Отбор.Вставить("ПолноеИмяОснования", МетаданныеОснования.ПолноеИмя());
		КонецЕсли;
		
	КонецЕсли;
	
	Если Параметры.Отбор.Свойство("ПолноеИмяОснования") И ЗначениеЗаполнено(Параметры.Отбор.ПолноеИмяОснования) Тогда
		
		МетаданныеОснования = Метаданные.НайтиПоПолномуИмени(Параметры.Отбор.ПолноеИмяОснования);
		Если МетаданныеОснования <> Неопределено Тогда
			
			АвтоЗаголовок = Ложь;
			Заголовок = НСтр("ru='Действия рабочего процесса по основанию:'") + " ";
			Если Параметры.Отбор.Свойство("ВидОснования") И Параметры.Отбор.ВидОснования = "ЗаказНаряд" Тогда
				Заголовок = Заголовок + НСтр("ru='Заказ-наряд'");
			Иначе
				Заголовок = Заголовок + МетаданныеОснования.Представление();
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
	// Установим настройки формы для случая открытия в режиме выбора
	Элементы.Список.РежимВыбора = Параметры.РежимВыбора;
	Элементы.Список.МножественныйВыбор = ?(Параметры.ЗакрыватьПриВыборе = Неопределено, Ложь, Не Параметры.ЗакрыватьПриВыборе);
	Если Параметры.РежимВыбора Тогда
		КлючНазначенияИспользования = "ВыборПодбор";
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;
	Иначе
		КлючНазначенияИспользования = "Список";
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
