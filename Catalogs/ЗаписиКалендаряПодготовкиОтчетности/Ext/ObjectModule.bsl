#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ИнтерфейсКалендаряСотрудника

// Процедура актуализирует записи календаря сотрудника, при изменении записи календаря подготовки отчетности
// Вызывается из обработчика подписки на событие "АктуализироватьЗаписиКалендаряПоИсточнику"
//
// Параметры:
//  ЗаписиПоИсточнику	 - массив из СправочникСсылка.ЗаписиКалендаряСотрудника	- записи, в которых в качестве источника указан текущий объект
//
Процедура ОбновитьЗаписьКалендаряПриЗаписиИсточника(ЗаписиПоИсточнику) Экспорт
	
	АрхивнаяЗапись = ПометкаУдаления
		Или Завершено
		Или (Состояние = Перечисления.СостоянияСобытийКалендаря.НеНачато
			И ОбщегоНазначения.ЗначениеРеквизитаОбъекта(СобытиеКалендаря, "ДатаОкончанияСобытия") < Константы.ДатаПервогоВходаВСистему.Получить());
	
	// Отмена всех существующих записей календарей сотрудника
	Если АрхивнаяЗапись Тогда
	
		Для Каждого ЗаписьКалендаря Из ЗаписиПоИсточнику Цикл
			ЗаписьКалендаря = ЗаписьКалендаря.ПолучитьОбъект();
			ЗаписьКалендаря.УстановитьПометкуУдаления(Истина);
		КонецЦикла;
		
		Возврат;
		
	КонецЕсли;
	
	РеквизитыСобытияКалендаря = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(
		СобытиеКалендаря,
		"ПредставлениеВКалендаре,
		|ДатаНачалаСобытия,
		|ДатаОкончанияСобытия,
		|Комментарий");
	
	Если ЗаписиПоИсточнику.Количество() = 0 Тогда
		
		ЗаписьКалендаря = Справочники.ЗаписиКалендаряСотрудника.СоздатьЭлемент();
		ЗаписьКалендаря.УстановитьНовыйКод();
		
	Иначе
		
		ЗаписьКалендаря = ЗаписиПоИсточнику[0].ПолучитьОбъект();
		
		Если ЗаписьКалендаря.ПометкаУдаления Тогда
			ЗаписьКалендаря.УстановитьПометкуУдаления(Ложь);
		КонецЕсли;
		
	КонецЕсли;
	
	ЗаписьКалендаря.Наименование	= СтрШаблон(НСтр("ru='%1 (по %2)'"), РеквизитыСобытияКалендаря.ПредставлениеВКалендаре, Организация);
	ЗаписьКалендаря.Источник		= Ссылка;
	ЗаписьКалендаря.Календарь		= ОбщегоНазначенияКлиентСервер.ПредопределенныйЭлемент("Справочник.КалендариСотрудников.КалендарьНалогов");
	ЗаписьКалендаря.Начало			= РеквизитыСобытияКалендаря.ДатаНачалаСобытия;
	ЗаписьКалендаря.Окончание		= КонецДня(РеквизитыСобытияКалендаря.ДатаОкончанияСобытия);
	ЗаписьКалендаря.Описание		= РеквизитыСобытияКалендаря.Комментарий;
	ЗаписьКалендаря.РедактированиеЗапрещено	= Истина;
	
	ЗаписьКалендаря.Записать();
	
КонецПроцедуры

Процедура ОбновитьИсточникПриИзмененииЗаписиКалендаря(ЭлементыПланировщика) Экспорт
	
	// Запись календаря подготовки отчетности не редактируется пользователем
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли