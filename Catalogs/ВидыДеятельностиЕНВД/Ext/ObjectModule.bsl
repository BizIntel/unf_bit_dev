#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если ПометкаУдаления Тогда
		Актуально = Ложь;
	КонецЕсли;
	
	Если Актуально И ДатаНачала > ДатаОкончания Тогда
		ДатаОкончания = ДатаНачала;
	КонецЕсли;
	
	Если (ВидПредпринимательскойДеятельности.Код = "01"
		ИЛИ ВидПредпринимательскойДеятельности.Код = "02"
		ИЛИ ВидПредпринимательскойДеятельности.Код = "03") Тогда
		ОКУНСтрокой = "";
		
		Для Каждого Строка Из ЗначенияОКУН Цикл
			ОКУНСтрокой = ОКУНСтрокой + Строка.ОКУН.Код+";";
		КонецЦикла;
	Иначе
		ОКУНСтрокой = "";
		ЗначенияОКУН.Очистить();
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ДатаНачала > ДатаОкончания И ЗначениеЗаполнено(ДатаОкончания) И НЕ Актуально Тогда
		Отказ = Истина;
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(
			НСтр("ru='Дата окончания деятельности больше дата начала, так не бывает'"),
			ЭтотОбъект,
			"ДатаОкончания",
			,
			Отказ);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#КонецЕсли