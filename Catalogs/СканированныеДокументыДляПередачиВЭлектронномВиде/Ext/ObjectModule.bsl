#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Перем ОшибкаЗаполнения Экспорт;

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)

	Если РегламентированнаяОтчетностьВызовСервера.ИспользуетсяОднаОрганизация() Тогда
		Модуль = ОбщегоНазначения.ОбщийМодуль("Справочники.Организации");
        Организация = Модуль.ОрганизацияПоУмолчанию();
	КонецЕсли;

	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.ОбработкаЗаполненияОбъекта(ЭтотОбъект, ДанныеЗаполнения);

КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.ПриЗаписиОбъекта(ЭтотОбъект, Отказ);

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДОСервер = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО();
	КонтекстЭДОСервер.ПередЗаписьюОбъекта(ЭтотОбъект, Отказ, РежимЗаписи, РежимПроведения);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения (Отказ, ПроверяемыеРеквизиты)
	// формируем соответствие для колучения кода вида документа
	КодПоВидуДокумента = ПолучитьСоответствиеКодПоВидуДокумента();
	КодДок = КодПоВидуДокумента[ВидДокумента];
	
	Если КодДок <> "0924" И КодДок <> "1665" И КодДок <> "2181" 
		И КодДок <> "2234" И КодДок <> "2772" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("Направление")); 
	КонецЕсли; 
	
	Если КодДок <> "0924" И КодДок <> "1665" И КодДок <> "2181" 
		И КодДок <> "2330" И КодДок <> "2234" И КодДок <> "2745"
		И КодДок <> "2766" И КодДок <> "2772" И КодДок <> "2799" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ДатаДокумента")); 
	КонецЕсли; 
	
	Если КодДок <> "0924" И КодДок <> "1665" И КодДок <> "2215" 
		И КодДок <> "2216" И КодДок <> "2330" И КодДок <> "2745"
		И КодДок <> "2766" И КодДок <> "2772" И КодДок <> "2799" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("НомерДокумента")); 
	КонецЕсли;
	
	Если КодДок <> "1665" И КодДок <> "2181" И КодДок <> "2216" 
		И КодДок <> "2330" И КодДок <> "2745" И КодДок <> "2772" И КодДок <> "2799" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("НомерОснования")); 
	КонецЕсли;
	
	Если КодДок <> "1665" И КодДок <> "2181" И КодДок <> "2330" 
		И КодДок <> "2745" И КодДок <> "2772" И КодДок <> "2799" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ДатаОснования")); 
	КонецЕсли;
	
	Если КодДок <> "2745" И КодДок <> "2330" И КодДок <> "2766" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("ПредметДокумента")); 
	КонецЕсли; 
	
	Если КодДок <> "0924" И КодДок <> "1665" И КодДок <> "2181" 
		И КодДок <> "2330" И КодДок <> "2234" И КодДок <> "2745"
		И КодДок <> "2766" И КодДок <> "2772" И КодДок <> "2799" Тогда
		ПроверяемыеРеквизиты.Удалить(ПроверяемыеРеквизиты.Найти("РеквизитыУчастников")); 
	КонецЕсли;
	
	// Обходим строки и проверяем заполнение реквизита
	Для Индекс = 1 по РеквизитыУчастников.Количество() Цикл
		Индекс0 = Индекс - 1;
		РеквизитыУчастника = РеквизитыУчастников.Получить(Индекс0);
		
		Если РеквизитыУчастника.ЯвляетсяЮрЛицом Тогда
			//для юридического лица
			
			Если Не ЗначениеЗаполнено(РеквизитыУчастника.ЮрЛицоНаименование) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "В строке " + Индекс + " таблицы ""Участники сделки"" не заполнено значение ""Полное наименование организации""";
				Сообщение.Поле = "ТаблицаУчастников[" + Индекс0 + "].СоставноеПоле";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();
				Отказ = Истина;
			КонецЕсли;
			
		Иначе
			//для физического лица
			
			Если НЕ ЗначениеЗаполнено(РеквизитыУчастника.ФизЛицоФамилия) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "В строке " + Индекс + " таблицы ""Участники сделки"" не заполнено значение ""Фамилия физического лица""";
				Сообщение.Поле = "ТаблицаУчастников[" + Индекс0 + "].СоставноеПоле";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();
				Отказ = Истина;
			КонецЕсли;
			
			Если НЕ ЗначениеЗаполнено(РеквизитыУчастника.ФизЛицоИмя) Тогда
				Сообщение = Новый СообщениеПользователю();
				Сообщение.Текст = "В строке " + Индекс + " таблицы ""Участники сделки"" не заполнено значение ""Имя физического лица""";
				Сообщение.Поле = "ТаблицаУчастников[" + Индекс0 + "].СоставноеПоле";
				Сообщение.УстановитьДанные(ЭтотОбъект);
				Сообщение.Сообщить();
				Отказ = Истина;
			КонецЕсли;
			
			Если КодДок = "2181" ИЛИ КодДок = "2330" ИЛИ КодДок = "2745" ИЛИ КодДок = "2766" Тогда
				
				Если Не ЗначениеЗаполнено(РеквизитыУчастника.ФизЛицоИНН) Тогда
					Сообщение = Новый СообщениеПользователю();
					Сообщение.Текст = "В строке " + Индекс + " таблицы ""Участники сделки"" не заполнено значение ""ИНН физического лица""";
					Сообщение.Поле = "ТаблицаУчастников[" + Индекс0 + "].СоставноеПоле";
					Сообщение.УстановитьДанные(ЭтотОбъект);
					Сообщение.Сообщить();
					Отказ = Истина;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

//вспомогательная для ОбработкаПроверкиЗаполнения 
Функция ПолучитьСоответствиеКодПоВидуДокумента()
	
	Соответствие = Новый Соответствие;
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.СчетФактура,								"0924");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ТоварноТранспортнаяНакладная, 				"1665");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот, 						"2181");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ГрузоваяТаможеннаяДекларация, 				"2215");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации, "2216");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ТоварнаяНакладнаяТОРГ12, 					"2234");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.СпецификацияЦены, 							"2330");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ДополнениеКДоговору, 						"2745");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.Договор, 									"2766");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.КорректировочныйСчетФактура,				"2772");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ОтчетНИОКР,									"2799");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ПередачаТоваров,							"2937");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.ПередачаУслуг,								"2939");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.УПД,										"2941");
	Соответствие.Вставить(Перечисления.ВидыПредставляемыхДокументов.УКД,										"2943");
	
	Возврат Соответствие;
	
КонецФункции

#КонецОбласти

#КонецЕсли