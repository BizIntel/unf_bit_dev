
&НаКлиенте
Перем КонтекстЭДОКлиент;

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
		
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураПараметров = Параметры.СтруктураПараметров;
	
	ВидДокумента 		= СтруктураПараметров.ВидДокумента;
	НомерДокумента		= СтруктураПараметров.НомерДокумента;
	ДатаДокумента		= СтруктураПараметров.ДатаДокумента;
	НомерОснования 		= СтруктураПараметров.НомерОснования;
	ДатаОснования 		= СтруктураПараметров.ДатаОснования;
	КонецПериода 		= СтруктураПараметров.КонецПериода;
	НачалоПериода 		= СтруктураПараметров.НачалоПериода;
	ПредметДокумента 	= СтруктураПараметров.ПредметДокумента;
	СуммаВсего 			= СтруктураПараметров.СуммаВсего;
	СуммаНалога 		= СтруктураПараметров.СуммаНалога;
	
	Организация 		= СтруктураПараметров.Организация;
	
	ТЗУчастникиДокумента 	= ПолучитьИзВременногоХранилища(СтруктураПараметров.АдресТЗУчастникиДокумента);
	ТЗФайлыДокумента 		= ПолучитьИзВременногоХранилища(СтруктураПараметров.АдресТЗФайлыДокумента);
	
	Для каждого СтрокаУчастникДокумента Из ТЗУчастникиДокумента Цикл
		НоваяСтрока = РеквизитыУчастников.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаУчастникДокумента);
	КонецЦикла;
	
	Для каждого СтрокаФайлДокумента Из ТЗФайлыДокумента Цикл
		НоваяСтрока = Файлы.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаФайлДокумента);
		
		ПредставлениеФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
		НСтр("ru = '%1 (%2)'"), НоваяСтрока.Имя, ЭлектронныйДокументооборотСКонтролирующимиОрганамиКлиентСервер.ТекстовоеПредставлениеРазмераФайла(НоваяСтрока.Размер));
		
		НоваяСтрока.СоставноеПоле = ПредставлениеФайла;
		
	КонецЦикла;
	
	Файлы.Сортировать("НомерСтраницы");
	
	УправлениеЭУ();
	
	ЭлектронныйДокументооборотСКонтролирующимиОрганамиВызовСервера.СкрытьЭлементыФормыПриИспользованииОднойОрганизации(ЭтаФорма, "ЗаголовокОргаинзация");
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// инициализируем контекст формы - контейнера клиентских методов
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
	
КонецПроцедуры


#КонецОбласти

#Область СлужебныеПроцедурыИФункции


&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	Если КонтекстЭДОКлиент = Неопределено Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УправлениеЭУ()
	
	//ПанельПредмет
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДополнениеКДоговору
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.СпецификацияЦены 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.Договор Тогда
		Элементы.ПредметДокумента.Видимость = Истина;
	Иначе
		Элементы.ПредметДокумента.Видимость = Ложь;
	КонецЕсли;
	
	//элемент ДатаДокумента
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ГрузоваяТаможеннаяДекларация
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации Тогда
		Элементы.ДатаДокумента.Видимость = Ложь;
	Иначе
		Элементы.ДатаДокумента.Видимость = Истина;
	КонецЕсли;

	
	//ПанельСумма
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ГрузоваяТаможеннаяДекларация 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации Тогда
		Элементы.ГруппаСумма.Видимость = Ложь;
	Иначе
		Элементы.ГруппаСумма.Видимость = Истина;
	КонецЕсли;
	
	//ПанельПериод
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот Тогда
		Элементы.ГруппаПериод.Видимость = Истина;
	Иначе
		Элементы.ГруппаПериод.Видимость = Ложь;
	КонецЕсли;
	
	//ПанельОснование
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ТоварноТранспортнаяНакладная
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.АктПриемкиСдачиРабот 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.СпецификацияЦены 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДополнениеКДоговору 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ТоварнаяНакладнаяТОРГ12 
		ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.КорректировочныйСчетФактура Тогда
		
		Элементы.ГруппаОснование.Видимость = Истина;
	Иначе
		Элементы.ГруппаОснование.Видимость = Ложь;
	КонецЕсли;
	
	//ПанельУчастники
	Если ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ГрузоваяТаможеннаяДекларация
	ИЛИ ВидДокумента = Перечисления.ВидыПредставляемыхДокументов.ДобавочныйЛистГрузовойТаможеннойДекларации Тогда
		Элементы.ГруппаРеквизитыУчастниковСделки.Видимость = Ложь;
	Иначе
		Элементы.ГруппаРеквизитыУчастниковСделки.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
