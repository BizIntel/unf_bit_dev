#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Обработчик обновления БРО 1.0.1.43
//
Процедура ЗаполнитьНовыеПоляОтправкиФСС() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтправкиФСС.Ссылка КАК ОтправкаСсылка,
		|	ВЫБОР
		|		КОГДА ОтправкиФСС.СтатусОтправки = ЗНАЧЕНИЕ(Перечисление.СтатусыОтправки.Отправлен)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК СтатусОтправлен,
		|	ВложенныйЗапрос.ДатаНачала,
		|	ВложенныйЗапрос.ДатаОкончания,
		|	ВложенныйЗапрос.Версия
		|ИЗ
		|	Справочник.ОтправкиФСС КАК ОтправкиФСС
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			РегламентированныйОтчет.Ссылка КАК Ссылка,
		|			РегламентированныйОтчет.ДатаНачала КАК ДатаНачала,
		|			РегламентированныйОтчет.ДатаОкончания КАК ДатаОкончания,
		|			РегламентированныйОтчет.Вид КАК Версия
		|		ИЗ
		|			Документ.РегламентированныйОтчет КАК РегламентированныйОтчет
		|		
		|		ОБЪЕДИНИТЬ ВСЕ
		|		
		|		ВЫБРАТЬ
		|			ЭлектронныеПредставленияРегламентированныхОтчетов.Ссылка,
		|			ЭлектронныеПредставленияРегламентированныхОтчетов.ДатаНачала,
		|			ЭлектронныеПредставленияРегламентированныхОтчетов.ДатаОкончания,
		|			ЭлектронныеПредставленияРегламентированныхОтчетов.Версия
		|		ИЗ
		|			Справочник.ЭлектронныеПредставленияРегламентированныхОтчетов КАК ЭлектронныеПредставленияРегламентированныхОтчетов) КАК ВложенныйЗапрос
		|		ПО ОтправкиФСС.ОтчетСсылка = ВложенныйЗапрос.Ссылка";

	Результат = Запрос.Выполнить();
	
	Если Результат.Пустой() Тогда
		Возврат;
	КонецЕсли;
	
	Выборка = Результат.Выбрать();
	
	НачатьТранзакцию();
	
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			ОтправкаОбъект = Выборка.ОтправкаСсылка.ПолучитьОбъект();
			ОтправкаОбъект.ДатаНачалаПериода = Выборка.ДатаНачала;
			ОтправкаОбъект.ДатаОкончанияПериода = Выборка.ДатаОкончания;
			ОтправкаОбъект.Версия = Выборка.Версия;
			ОтправкаОбъект.ВидОтчета = Справочники.ВидыОтправляемыхДокументов.Отчет4ФСС;
			
			ОтправкаОбъект.Период = СокрЛП(Формат(ОтправкаОбъект.ДатаОкончанияПериода, "ДФ=yyyyMMdd") + Формат('39991231' - ОтправкаОбъект.ДатаНачалаПериода, "ДФ=yyyyMMdd"));
			
			Если ОтправкаОбъект.СтатусОтправки = Перечисления.СтатусыОтправки.Отправлен Тогда
				ОтправкаОбъект.ДатаЗакрытия = '39991231235959';
			Иначе
				ОтправкаОбъект.ДатаЗакрытия = ОтправкаОбъект.ДатаПолученияРезультата;
			КонецЕсли;
			
			ОтправкаОбъект.ПредставлениеПериода = ПредставлениеПериода(ОтправкаОбъект.ДатаНачалаПериода, КонецДня(ОтправкаОбъект.ДатаОкончанияПериода), "ФП=Истина");
			ОтправкаОбъект.ПредставлениеВидаДокумента = РегламентированнаяОтчетность.ПредставлениеВидаДокумента(ОтправкаОбъект.Версия);
			
			ОтправкаОбъект.ОбменДанными.Загрузка = Истина;
			
			ОтправкаОбъект.Записать();
			
		Исключение
			
			ОтменитьТранзакцию();
			Возврат;
			
		КонецПопытки;
		
	КонецЦикла;
	ЗафиксироватьТранзакцию();
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли

#Область ОбработчикиСобытий

Процедура ОбработкаПолученияФормы(ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка)
	
	// инициализируем контекст ЭДО - модуль обработки
	ТекстСообщения = "";
	КонтекстЭДО = ДокументооборотСКОВызовСервера.ПолучитьОбработкуЭДО(ТекстСообщения);
	Если КонтекстЭДО = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	КонтекстЭДО.ОбработкаПолученияФормы("Справочник", "ОтправкиФСС", ВидФормы, Параметры, ВыбраннаяФорма, ДополнительнаяИнформация, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти
