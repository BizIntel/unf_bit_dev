
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьЗаголовок(Параметры.Свойство);
	
	СписокВыбораСвойства = ПолучитьИзВременногоХранилища(Параметры.СписокВыбораЗначенийСсылка);
	СписокВыбораСвойства = СписокВыбораСвойства.Выбрать();
	Пока СписокВыбораСвойства.Следующий() Цикл
		
		НоваяСтрока = СписокВыбора.Добавить();
		НоваяСтрока.Значение = СписокВыбораСвойства.Значение;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	ОповеститьОВыборе(Элементы.СписокВыбора.ТекущиеДанные.Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыбораВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ОповеститьОВыборе(Элементы.СписокВыбора.ТекущиеДанные.Значение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УстановитьЗаголовок(Свойство)
	
	СтрокаЗаголовка = "";
	
	Если ЗначениеЗаполнено(Свойство) Тогда
		СтрокаЗаголовка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
			Свойство, "ЗаголовокФормыВыбораЗначения");
	КонецЕсли;
	
	Если ПустаяСтрока(СтрокаЗаголовка) Тогда
		
		Если ЗначениеЗаполнено(Свойство) Тогда
			СтрокаЗаголовка = НСтр("ru = 'Выберите значение свойства %1'");
			
			СтрокаЗаголовка = СтрШаблон(СтрокаЗаголовка,
				Строка(ОбщегоНазначения.ЗначениеРеквизитаОбъекта(
					Свойство, "Заголовок")));
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(СтрокаЗаголовка) Тогда
		АвтоЗаголовок = Ложь;
		Заголовок = СтрокаЗаголовка;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
