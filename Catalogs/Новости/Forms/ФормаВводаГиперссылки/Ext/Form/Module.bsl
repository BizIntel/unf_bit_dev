#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	Если Параметры.Свойство("ТаблицаМетаданных")
			И ТипЗнч(Параметры.ТаблицаМетаданных) = Тип("ДанныеФормыКоллекция") Тогда
		ЭтаФорма.ТаблицаМетаданных.Загрузить(Параметры.ТаблицаМетаданных.Выгрузить());
	Иначе
		ЭтаФорма.ТаблицаМетаданных.Загрузить(ОбработкаНовостейПовтИсп.ПолучитьТаблицуМетаданных());
	КонецЕсли;

	Элементы.ПараметрДействия_НавигационнаяСсылка.СписокВыбора.Очистить();
	Элементы.ПараметрДействия_РазделСправки.СписокВыбора.Очистить();
	Для каждого ТекущийОбъектМетаданных Из ЭтаФорма.ТаблицаМетаданных Цикл
		Если НЕ ПустаяСтрока(ТекущийОбъектМетаданных.НавигационнаяСсылка) Тогда
			Элементы.ПараметрДействия_НавигационнаяСсылка.СписокВыбора.Добавить(ТекущийОбъектМетаданных.НавигационнаяСсылка, ТекущийОбъектМетаданных.ИмяМетаданных);
		КонецЕсли;
		Если НЕ ПустаяСтрока(ТекущийОбъектМетаданных.РазделСправки) Тогда
			Элементы.ПараметрДействия_РазделСправки.СписокВыбора.Добавить(ТекущийОбъектМетаданных.РазделСправки, ТекущийОбъектМетаданных.ИмяМетаданных);
		КонецЕсли;
	КонецЦикла;
	Элементы.ПараметрДействия_НавигационнаяСсылка.СписокВыбора.СортироватьПоПредставлению(); // В представлении - правильная сортировка
	Элементы.ПараметрДействия_РазделСправки.СписокВыбора.СортироватьПоЗначению(); // В значении - правильная сортировка

	// Список уникальных идентификаторов действий (для автонумерации)
	ЭтаФорма.СписокУИНДействий = Параметры.СписокУИНДействий;

КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	// Проверяемые реквизиты:
	//  Действие            - всегда;
	//  УИНДействия         - всегда;
	//  ИнтернетСсылка      - для действия "Переход по интернет ссылке";
	//  КартинкаДанные      - для действия "Показать картинку";
	//  КартинкаЗаголовок   - для действия "Показать картинку";
	//  НавигационнаяСсылка - для действия "Переход по навигационной ссылке";
	//  РазделСправки       - для действия "Открытие раздела справки".

	Если ПустаяСтрока(ЭтаФорма.Действие) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнено поле Действие'");
		Сообщение.Поле  = "Действие";
		Сообщение.Сообщить();
	КонецЕсли;

	Если ПустаяСтрока(ЭтаФорма.УИНДействия) Тогда
		Отказ = Истина;
		Сообщение = Новый СообщениеПользователю;
		Сообщение.Текст = НСтр("ru='Не заполнен идентификатор гиперссылки (должен заполниться автоматически после выбора Действия)'");
		Сообщение.Поле  = "УИНДействия";
		Сообщение.Сообщить();
	КонецЕсли;

	Если ЭтаФорма.Действие = "Переход по навигационной ссылке" Тогда
		Если ПустаяСтрока(ЭтаФорма.ПараметрДействия_НавигационнаяСсылка) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнена навигационная ссылка'");
			Сообщение.Поле  = "ПараметрДействия_НавигационнаяСсылка";
			Сообщение.Сообщить();
		КонецЕсли;
	ИначеЕсли ЭтаФорма.Действие = "Оповещение" Тогда
		// Ничего не делать
	ИначеЕсли ЭтаФорма.Действие = "Открытие раздела справки" Тогда
		Если ПустаяСтрока(ЭтаФорма.ПараметрДействия_РазделСправки) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнен раздел справки'");
			Сообщение.Поле  = "ПараметрДействия_РазделСправки";
			Сообщение.Сообщить();
		КонецЕсли;
	ИначеЕсли ЭтаФорма.Действие = "Запуск процедуры с параметрами" Тогда
		// Ничего не делать
	ИначеЕсли ЭтаФорма.Действие = "Переход по интернет ссылке" Тогда
		Если ПустаяСтрока(ЭтаФорма.ПараметрДействия_ИнтернетСсылка) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнена интернет-ссылка'");
			Сообщение.Поле  = "ПараметрДействия_ИнтернетСсылка";
			Сообщение.Сообщить();
		КонецЕсли;
	ИначеЕсли ЭтаФорма.Действие = "Показать картинку" Тогда
		Если ПустаяСтрока(ЭтаФорма.КартинкаЗаголовок) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не заполнен заголовок картинки'");
			Сообщение.Поле  = "КартинкаЗаголовок";
			Сообщение.Сообщить();
		КонецЕсли;
		Если ПустаяСтрока(ЭтаФорма.КартинкаДанные) Тогда
			Отказ = Истина;
			Сообщение = Новый СообщениеПользователю;
			Сообщение.Текст = НСтр("ru='Не загружена картинка. Нажмите <Выбрать картинку из файла>'");
			Сообщение.Поле  = "КартинкаЗаголовок";
			Сообщение.Сообщить();
		КонецЕсли;
	ИначеЕсли ЭтаФорма.Действие = "Открытие новости" Тогда
		// Ничего не делать
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ДействиеПриИзменении(Элемент)

	ПрефиксНомера = "";
	// 1. Отобразить правильную закладку
	Если ЭтаФорма.Действие = "Переход по навигационной ссылке" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаНавигационнаяСсылка;
		ПрефиксНомера = "1C:GoTo";
	ИначеЕсли ЭтаФорма.Действие = "Оповещение" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаРазное;
		ПрефиксНомера = "1C:Notif";
	ИначеЕсли ЭтаФорма.Действие = "Открытие раздела справки" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаРазделСправки;
		ПрефиксНомера = "1C:OpenHelp";
	ИначеЕсли ЭтаФорма.Действие = "Запуск процедуры с параметрами" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаРазное;
		ПрефиксНомера = "1C:App";
	ИначеЕсли ЭтаФорма.Действие = "Переход по интернет ссылке" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаИнтернетСсылка;
		ПрефиксНомера = "1C:Web";
		Если ПустаяСтрока(ЭтаФорма.ПараметрДействия_ИнтернетСсылка) Тогда
			ЭтаФорма.ПараметрДействия_ИнтернетСсылка = "http://";
		КонецЕсли;
	ИначеЕсли ЭтаФорма.Действие = "Показать картинку" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаКартинки;
		ПрефиксНомера = "1C:ShowPict";
	ИначеЕсли ЭтаФорма.Действие = "Открытие новости" Тогда
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаРазное;
		ПрефиксНомера = "1C:OpenNews";
	Иначе
		Элементы.СтраницыПараметров.ТекущаяСтраница = Элементы.СтраницаПусто;
	КонецЕсли;

	// 2. Присвоить уникальный номер
	Для С=1 По 999 Цикл
		НовыйУИН = ПрефиксНомера + Формат(С, "ЧЦ=3; ЧВН=");
		Если ЭтаФорма.СписокУИНДействий.НайтиПоЗначению(НовыйУИН) = Неопределено Тогда
			ЭтаФорма.УИНДействия = НовыйУИН;
			Прервать;
		КонецЕсли;
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияПерейтиПоИнтернетСсылкеНажатие(Элемент)

	Если НЕ ПустаяСтрока(ЭтаФорма.ПараметрДействия_ИнтернетСсылка) Тогда
		ПерейтиПоНавигационнойСсылке(ЭтаФорма.ПараметрДействия_ИнтернетСсылка);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НавигационнаяСсылкаАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)

	Если (Ожидание > 0) И (СтрДлина(Текст) > 0) Тогда
		// Найти в списке похожие значения и вывести их
		ДанныеВыбора = Новый СписокЗначений;
		Для каждого ЭлементСписка Из Элементы.ПараметрДействия_НавигационнаяСсылка.СписокВыбора Цикл
			Если Найти(ВРег(ЭлементСписка.Значение), ВРег(Текст)) > 0 Тогда
				ДанныеВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
			КонецЕсли;
		КонецЦикла;

		Если ДанныеВыбора.Количество() = 1 Тогда
			СтандартнаяОбработка = Ложь;
			ЭтаФорма.ПараметрДействия_НавигационнаяСсылка = ДанныеВыбора[0].Значение;
		ИначеЕсли ДанныеВыбора.Количество() > 0 Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура НавигационнаяСсылкаОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)

	// Найти в списке похожие значения и вывести их
	ДанныеВыбора = Новый СписокЗначений;
	Для каждого ЭлементСписка Из Элементы.ПараметрДействия_НавигационнаяСсылка.СписокВыбора Цикл
		Если Найти(ВРег(ЭлементСписка.Значение), ВРег(Текст)) > 0 Тогда
			ДанныеВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЕсли;
	КонецЦикла;

	Если ДанныеВыбора.Количество() = 1 Тогда
		СтандартнаяОбработка = Ложь;
		ЭтаФорма.ПараметрДействия_НавигационнаяСсылка = ДанныеВыбора[0].Значение;
	ИначеЕсли ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РазделСправкиАвтоПодбор(Элемент, Текст, ДанныеВыбора, Параметры, Ожидание, СтандартнаяОбработка)

	Если (Ожидание > 0) И (СтрДлина(Текст) > 0) Тогда
		// Найти в списке похожие значения и вывести их
		ДанныеВыбора = Новый СписокЗначений;
		Для каждого ЭлементСписка Из Элементы.ПараметрДействия_РазделСправки.СписокВыбора Цикл
			Если Найти(ВРег(ЭлементСписка.Значение), ВРег(Текст)) > 0 Тогда
				ДанныеВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
			КонецЕсли;
		КонецЦикла;

		Если ДанныеВыбора.Количество() = 1 Тогда
			СтандартнаяОбработка = Ложь;
			ЭтаФорма.ПараметрДействия_РазделСправки = ДанныеВыбора[0].Значение;
		ИначеЕсли ДанныеВыбора.Количество() > 0 Тогда
			СтандартнаяОбработка = Ложь;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура РазделСправкиОкончаниеВводаТекста(Элемент, Текст, ДанныеВыбора, СтандартнаяОбработка)

	// Найти в списке похожие значения и вывести их
	ДанныеВыбора = Новый СписокЗначений;
	Для каждого ЭлементСписка Из Элементы.ПараметрДействия_РазделСправки.СписокВыбора Цикл
		Если Найти(ВРег(ЭлементСписка.Значение), ВРег(Текст)) > 0 Тогда
			ДанныеВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЕсли;
	КонецЦикла;

	Если ДанныеВыбора.Количество() = 1 Тогда
		СтандартнаяОбработка = Ложь;
		ЭтаФорма.ПараметрДействия_РазделСправки = ДанныеВыбора[0].Значение;
	ИначеЕсли ДанныеВыбора.Количество() > 0 Тогда
		СтандартнаяОбработка = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДекорацияДобавитьКартинкуНажатие(Элемент)

	АдресВХранилище = "";
	ИмяФайла = "";

	ОписаниеОповещенияПослеВыбораФайла = Новый ОписаниеОповещения(
		"ПослеВыбораФайла",
		ЭтаФорма,
		Неопределено);

	НачатьПомещениеФайла(
		ОписаниеОповещенияПослеВыбораФайла, // ОписаниеОповещенияОЗавершении
		АдресВХранилище, // Адрес
		ИмяФайла, // НачальноеИмяФайла
		Истина, // Интерактивно
		ЭтаФорма.УникальныйИдентификатор); // УникальныйИдентификаторФормы

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаЗаписатьИЗакрыть(Команда)

	ОчиститьСообщения();

	Если ПроверитьЗаполнение() Тогда
		Результат = КомандаЗаписатьИЗакрытьСервер();
		ЭтаФорма.Закрыть(Результат);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция КомандаЗаписатьИЗакрытьСервер()

	Результат = Новый Структура;
	Результат.Вставить("УИНДействия", ЭтаФорма.УИНДействия);
	Результат.Вставить("Действие", ЭтаФорма.Действие);
	Результат.Вставить("ВсплывающаяПодсказка", ЭтаФорма.ВсплывающаяПодсказка);
	Результат.Вставить("ИнтернетСсылка", ЭтаФорма.ПараметрДействия_ИнтернетСсылка);
	Результат.Вставить("КартинкаДанные", ЭтаФорма.КартинкаДанные);
	Результат.Вставить("КартинкаРазмер", ЭтаФорма.КартинкаРазмер);
	Результат.Вставить("НавигационнаяСсылка", ЭтаФорма.ПараметрДействия_НавигационнаяСсылка);
	Результат.Вставить("КартинкаЗаголовок", ЭтаФорма.КартинкаЗаголовок);
	Результат.Вставить("РазделСправки", ЭтаФорма.ПараметрДействия_РазделСправки);

	Возврат Результат;

КонецФункции

&НаСервере
// Функция загружает файл в текущую строку.
// 
// Параметры:
//  АдресВХранилище     - Строка;
//  ИдентификаторСтроки - Число.
//
Функция ПолучитьBase64ИзФайлаСервер(АдресВХранилище)

	Результат = Новый Структура("Данные, Размер", "", 0);

	ДвоичныеДанные = ПолучитьИзВременногоХранилища(АдресВХранилище);

	// ... а вот добавленный реквизит-колонка ДанныеСтрока64 недоступен на сервере
	Результат.Вставить("Данные", Base64Строка(ДвоичныеДанные));
	Результат.Вставить("Размер", ДвоичныеДанные.Размер());

	Возврат Результат;

КонецФункции

&НаКлиенте
// Процедура обрабатывает добавление файла
//
// Параметры:
//  Результат               - Булево - Ложь - в параметре <Интерактивно> установлен интерактивный режим (Истина) и пользователь отказался от выполнения операции в диалоге выбора файла, 
//  АдресВХранилище         - Строка - расположение нового файла, 
//  ВыбранноеИмяФайла       - Строка - Через этот параметр возвращается путь к файлу, указанный в диалоге выбора файла. Для неинтерактивного режима выбранное имя файла соответствует начальному имени файла. В режиме запуска "Веб-клиент" значение параметра зависит от типа браузера. Для Mozilla Firefox 3 в параметре возвращается только имя файла без пути. Для Microsoft Internet Explorer возвращаемое значение зависит от настройки текущей зоны. Подробности: http://msdn.microsoft.com/en-us/library/ms535128(VS.85).aspx, 
//  ДополнительныеПараметры - Произвольный - значение, которое было указано при создании объекта ОписаниеОповещения.
//
Процедура ПослеВыбораФайла(Результат, АдресВХранилище, ВыбранноеИмяФайла, ДополнительныеПараметры = Неопределено) Экспорт

	Если Результат = Истина Тогда
		СтруктураСохраненногоФайла = ПолучитьBase64ИзФайлаСервер(АдресВХранилище);
		ЭтаФорма.КартинкаДанные = СтруктураСохраненногоФайла.Данные;
		ЭтаФорма.КартинкаРазмер = СтруктураСохраненногоФайла.Размер;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти
