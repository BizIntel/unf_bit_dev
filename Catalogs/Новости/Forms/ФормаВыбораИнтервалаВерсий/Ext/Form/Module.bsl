#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ЭтаФорма.Продукт  = Параметры.Продукт;
	ЭтаФорма.ВерсияОТ = Параметры.ВерсияОТ;
	ЭтаФорма.ВерсияДО = Параметры.ВерсияДО;

	Если ПустаяСтрока(ЭтаФорма.Продукт) Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	Если ПустаяСтрока(ЭтаФорма.ВерсияОТ) Тогда
		ЭтаФорма.ВерсияОТ = "00.00.000.0000";
	КонецЕсли;
	Если ПустаяСтрока(ЭтаФорма.ВерсияДО) Тогда
		ЭтаФорма.ВерсияДО = "99.99.999.9999";
	КонецЕсли;

	ЭтаФорма.Версия = ЭтаФорма.ВерсияОТ;

	Если (ЭтаФорма.ВерсияОТ = "00.00.000.0000") И (ЭтаФорма.ВерсияДО = "99.99.999.9999") Тогда
		ЭтаФорма.ВариантВерсии = 0; // Любая версия
	ИначеЕсли (ЭтаФорма.ВерсияОТ <> "00.00.000.0000") И (ЭтаФорма.ВерсияДО <> "99.99.999.9999") Тогда
		Если ЭтаФорма.ВерсияОТ = ЭтаФорма.ВерсияДО Тогда // Точная версия
			ЭтаФорма.ВариантВерсии = 1;
		Иначе // Интервал версий
			ЭтаФорма.ВариантВерсии = 2;
		КонецЕсли;
	ИначеЕсли (ЭтаФорма.ВерсияОТ <> "00.00.000.0000") И (ЭтаФорма.ВерсияДО = "99.99.999.9999") Тогда
		ЭтаФорма.ВариантВерсии = 3; // От версии и выше
	ИначеЕсли (ЭтаФорма.ВерсияОТ = "00.00.000.0000") И (ЭтаФорма.ВерсияДО <> "99.99.999.9999") Тогда
		ЭтаФорма.ВариантВерсии = 4; // До версии включительно
	Иначе
		ЭтаФорма.ВариантВерсии = 2;
	КонецЕсли;

	УправлениеФормой(ЭтаФорма);

	ЭтаФорма.ЗакрыватьПриВыборе = Истина;
	ЭтаФорма.ЗакрыватьПриЗакрытииВладельца = Истина;
	ЭтаФорма.РежимОткрытияОкна  = РежимОткрытияОкнаФормы.БлокироватьОкноВладельца;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВариантВерсииПриИзменении(Элемент)

	УправлениеФормой(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ВерсияПриИзменении(Элемент)

	ЭтаФорма.Версия = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ЭтаФорма.Версия);
	ЭтаФорма.ВерсияОТ = ЭтаФорма.Версия;
	ЭтаФорма.ВерсияДО = ЭтаФорма.Версия;

КонецПроцедуры

&НаКлиенте
Процедура ВерсияОТПриИзменении(Элемент)

	ЭтаФорма.ВерсияОТ = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ЭтаФорма.ВерсияОТ);
	ЭтаФорма.Версия = ЭтаФорма.ВерсияОТ;

КонецПроцедуры

&НаКлиенте
Процедура ВерсияДОПриИзменении(Элемент)

	ЭтаФорма.ВерсияДО = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ЭтаФорма.ВерсияДО);
	ЭтаФорма.Версия = ЭтаФорма.ВерсияДО;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура КомандаОК(Команда)

	Если ЭтаФорма.ВариантВерсии = 0 Тогда // Любая версия
		ЭтаФорма.ВерсияОТ = "00.00.000.0000";
		ЭтаФорма.ВерсияДО = "99.99.999.9999";
	ИначеЕсли ЭтаФорма.ВариантВерсии = 1 Тогда // Точная версия
		ЭтаФорма.ВерсияОТ = ЭтаФорма.Версия;
		ЭтаФорма.ВерсияДО = ЭтаФорма.Версия;
	ИначеЕсли ЭтаФорма.ВариантВерсии = 2 Тогда // Интервал
	ИначеЕсли ЭтаФорма.ВариантВерсии = 3 Тогда // От версии и выше
		ЭтаФорма.ВерсияДО = "99.99.999.9999";
	ИначеЕсли ЭтаФорма.ВариантВерсии = 4 Тогда // До версии включительно
		ЭтаФорма.ВерсияОТ = "00.00.000.0000";
	КонецЕсли;

	Результат = Новый Структура("ВерсияОТ, ВерсияДО",
		ЭтаФорма.ВерсияОТ,
		ЭтаФорма.ВерсияДО);

	ЭтаФорма.ОповеститьОВыборе(Результат); // Эта же команда закроет форму

КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)

	ЭтаФорма.Закрыть(КодВозвратаДиалога.Отмена);

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	лкЭлементы = Форма.Элементы;

	лкЭлементы.ВерсияОТ_Интервал.Доступность = Ложь;
	лкЭлементы.ВерсияДО_Интервал.Доступность = Ложь;
	лкЭлементы.ВерсияОТ.Доступность = Ложь;
	лкЭлементы.ВерсияДО.Доступность = Ложь;
	лкЭлементы.Версия.Доступность = Ложь;

	Если Форма.ВариантВерсии = 0 Тогда // Любая версия
	ИначеЕсли Форма.ВариантВерсии = 1 Тогда // Точная версия
		лкЭлементы.Версия.Доступность = Истина;
	ИначеЕсли Форма.ВариантВерсии = 2 Тогда // Интервал
		лкЭлементы.ВерсияОТ_Интервал.Доступность = Истина;
		лкЭлементы.ВерсияДО_Интервал.Доступность = Истина;
	ИначеЕсли Форма.ВариантВерсии = 3 Тогда // От версии и выше
		лкЭлементы.ВерсияОТ.Доступность = Истина;
	ИначеЕсли Форма.ВариантВерсии = 4 Тогда // До версии включительно
		лкЭлементы.ВерсияДО.Доступность = Истина;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

