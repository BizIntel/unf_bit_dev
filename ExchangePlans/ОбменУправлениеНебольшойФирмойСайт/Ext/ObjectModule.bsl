#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПередУдалением(Отказ)
	
	Задание = ОбменССайтомРегламентныеЗадания.НайтиЗадание(ИдентификаторРегламентногоЗадания);
	Если Задание <> Неопределено Тогда
		ОбменССайтомРегламентныеЗадания.УдалитьЗадание(Задание);
	КонецЕсли;
	ИдентификаторРегламентногоЗадания = Неопределено;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если ПустаяСтрока(Код) Тогда
		УстановитьНовыйКод();
	КонецЕсли;
	
	Если ПустаяСтрока(Наименование) Тогда
		СформироватьНаименование()
	КонецЕсли;
	
	Если НЕ ОбменТоварами
		И НЕ ОбменЗаказами Тогда
		
		Отказ = Истина;
		Сообщение = НСтр("ru = 'Режим обмена не выбран!'");
		Поле = "ОбменТоварами";
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(Сообщение, ЭтотОбъект, Поле);
		
	КонецЕсли;

	ДобавитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты);
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	Код = "";
	ИдентификаторРегламентногоЗадания = НеОпределено;
КонецПроцедуры

#КонецОбласти

#Область ПрограммныйИнтерфейс

// Формирует уникальное наименование объекта
//
// Параметры:
// нет
// 
// Возвращаемое значение:
// нет
//
Процедура СформироватьНаименование() Экспорт
	
	Если ОбменТоварами И ОбменЗаказами Тогда
		
		Префикс = НСтр("ru = 'Обмен товарами и заказами'");
		
	ИначеЕсли ОбменЗаказами Тогда
		
		Префикс = НСтр("ru = 'Обмен заказами'");
		
	Иначе
		
		Префикс = НСтр("ru = 'Выгрузка товаров'");
		
	КонецЕсли;
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(ОбменУправлениеНебольшойФирмойСайт.Наименование) КАК Наименование
	|ИЗ
	|	ПланОбмена.ОбменУправлениеНебольшойФирмойСайт КАК ОбменУправлениеНебольшойФирмойСайт
	|ГДЕ
	|	ОбменУправлениеНебольшойФирмойСайт.Наименование ПОДОБНО &Шаблон
	|
	|ИМЕЮЩИЕ
	|	(НЕ МАКСИМУМ(ОбменУправлениеНебольшойФирмойСайт.Наименование) ЕСТЬ NULL )";
	
	Запрос.УстановитьПараметр("Шаблон", Префикс + "%");
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Наименование = Префикс;
		Возврат;
	КонецЕсли;
	
	Выборка = РезультатЗапроса.Выбрать();
	Выборка.Следующий();
	Суффикс = Прав(СокрЛП(Выборка.Наименование), 4);
	
	Попытка
		СуффиксЧислом = Число(Суффикс);
	Исключение
		Наименование = Префикс + " 0001";
		Возврат;
	КонецПопытки;
	
	Наименование = Префикс + " " + Формат(СуффиксЧислом + 1, "ЧЦ=4; ЧВН=; ЧГ=");
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ДобавитьПроверяемыеРеквизиты(ПроверяемыеРеквизиты)
	
	Если ВыгружатьНаСайт Тогда
		
		ПроверяемыеРеквизиты.Добавить("АдресСайта");
		ПроверяемыеРеквизиты.Добавить("ИмяПользователя");
		
	Иначе
		
		ПроверяемыеРеквизиты.Добавить("КаталогВыгрузки");
		
	КонецЕсли;

	Если ОбменЗаказами Тогда
		
		Если НЕ ВыгружатьНаСайт Тогда
			ПроверяемыеРеквизиты.Добавить("ФайлЗагрузки");
		КонецЕсли;
		
		ПроверяемыеРеквизиты.Добавить("СпособИдентификацииКонтрагентов");
		
		Если СпособИдентификацииКонтрагентов = Перечисления.СпособыИдентификацииКонтрагентов.ПредопределенноеЗначение Тогда
			ПроверяемыеРеквизиты.Добавить("КонтрагентДляПодстановкиВЗаказы");
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли