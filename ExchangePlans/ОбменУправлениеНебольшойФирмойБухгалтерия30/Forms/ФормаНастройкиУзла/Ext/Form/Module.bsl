////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ИмяПланаОбмена = Метаданные.ПланыОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия30.Имя;
	ОбменДаннымиСервер.ФормаНастройкиУзлаПриСозданииНаСервере(ЭтаФорма, ИмяПланаОбмена);
	
	РежимСинхронизации = 
		?(Не РучнойОбмен, "АвтоматическаяСинхронизация", "РучнаяСинхронизация")
	;
	
	РежимСинхронизацииОрганизаций =
		?(ИспользоватьОтборПоОрганизациям, "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям", "СинхронизироватьДанныеПоВсемОрганизациям")
	;
	
	РежимСинхронизацииДокументов =
		?(ИспользоватьОтборПоВидамДокументов, "СинхронизироватьДанныеТолькоПоВыбраннымВидамДокументов", "СинхронизироватьДанныеПоВсемДокументам")
	;
	
	УстановитьВидимостьНаСервере();
	ПриИзмененииРежимаСинхронизацииДанных();
	ОбновитьНаименованиеКомандФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбновитьДанныеОбъекта(ВыбранноеЗначение);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	РучнойОбмен = 
		?(РежимСинхронизации = "АвтоматическаяСинхронизация", Ложь, Истина)
	;
	
	Если РежимСинхронизации <> "АвтоматическаяСинхронизация" Тогда
		
		ИспользоватьОтборПоОрганизациям = Ложь;
		Организации.Очистить();
		
		ИспользоватьОтборПоВидамДокументов = Ложь;
		ВидыДокументов.Очистить();
		
	Иначе
		
		Если Организации.Количество() > 0 Тогда
			Если РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям" Тогда
				ИспользоватьОтборПоОрганизациям = Истина;
			Иначе
				ИспользоватьОтборПоОрганизациям = Ложь;
				Организации.Очистить();
			КонецЕсли;
		Иначе
			ИспользоватьОтборПоОрганизациям = Ложь;
		КонецЕсли;
		
		Если ВидыДокументов.Количество() > 0 Тогда
			Если РежимСинхронизацииДокументов = "СинхронизироватьДанныеТолькоПоВыбраннымВидамДокументов" Тогда
				ИспользоватьОтборПоВидамДокументов = Истина;
			Иначе
				ИспользоватьОтборПоВидамДокументов = Ложь;
				ВидыДокументов.Очистить();
			КонецЕсли;
		Иначе
			ИспользоватьОтборПоВидамДокументов = Ложь;
		КонецЕсли;
		
	КонецЕсли;
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораПоОрганизациям(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("АдресТаблицыОрганизаций", ПолучитьАдресТабличнойЧасти("Организации"));
	ПараметрыФормы.Вставить("ИмяТаблицыДляЗаполнения", "Организации");
	
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия30.Форма.ФормаВыбораОрганизаций",
		ПараметрыФормы,
		ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура РежимСинхронизацииОрганизацийПриИзменении(Элемент)
	
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура РежимСинхронизацииДокументовПриИзменении(Элемент)
	
	Если РежимСинхронизацииДокументов = "СинхронизироватьДанныеТолькоПоДокументамОтобраннымВручную" Тогда
		НаправлениеСинхронизации = "ОдносторонняяСинхронизация";
	КонецЕсли;
	
	УстановитьВидимостьНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуОтбораПоВидамДокументов(Команда)
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ВидыДокументов", ПолучитьМассивОбъектовТабличнойЧасти("ВидыДокументов", "ИмяОбъектаМетаданных"));
	
	ОткрытьФорму("ПланОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия30.Форма.ФормаВыбораВидовДокументов",
		ПараметрыФормы,
		ЭтаФорма);
		
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборПоОрганизациям(Команда)
	
	ТекстЗаголовка = НСтр("ru='Подтверждение'");
	ТекстВопроса   = НСтр("ru='Очистить отбор по организациям?'");
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьОтборПоОрганизациямЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборПоОрганизациямЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    Если Ответ=КодВозвратаДиалога.Да Тогда
        Организации.Очистить();
        УстановитьВидимостьНаСервере();
        ОбновитьНаименованиеКомандФормы();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборПоВидамДокументов(Команда)
	
	ТекстЗаголовка = НСтр("ru='Подтверждение'");
	ТекстВопроса   = НСтр("ru='Очистить отбор по видам документов?'");
	Ответ = Неопределено;

	ПоказатьВопрос(Новый ОписаниеОповещения("ОчиститьОтборПоВидамДокументовЗавершение", ЭтотОбъект), ТекстВопроса, РежимДиалогаВопрос.ДаНет,,,ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОтборПоВидамДокументовЗавершение(Результат, ДополнительныеПараметры) Экспорт
    
    Ответ = Результат;
    Если Ответ=КодВозвратаДиалога.Да Тогда
        ВидыДокументов.Очистить();
        УстановитьВидимостьНаСервере();
        ОбновитьНаименованиеКомандФормы();
    КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура АвтоматическаяСинхронизацияПриИзменении(Элемент)
	
	ПриИзмененииРежимаСинхронизацииДанных();
	
КонецПроцедуры

&НаКлиенте
Процедура РучнаяСинхронизацияПриИзменении(Элемент)
	
	ПриИзмененииРежимаСинхронизацииДанных();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УстановитьВидимостьНаСервере()
	
	//Видимость отбора по организациям
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
		Элементы.ГруппаОтборПоОрганизациям.Видимость = Ложь;
	Иначе
		Элементы.ГруппаОтборПоОрганизациям.Видимость = Истина;
		ОтборПоОрганизациямИспользуется = РежимСинхронизацииОрганизаций = "СинхронизироватьДанныеТолькоПоВыбраннымОрганизациям";
		ОтборПоОрганизациямНастроен = Организации.Количество() > 0;
		Элементы.ОткрытьФормуОтбораПоОрганизациям.Видимость = ОтборПоОрганизациямИспользуется;
		Элементы.ОчиститьОтборПоОрганизациям.Видимость = ОтборПоОрганизациямИспользуется И ОтборПоОрганизациямНастроен;
	КонецЕсли;
	
	//Видимость отбора по видам документов
	ОтборПоВидамДокументовИспользуется = РежимСинхронизацииДокументов = "СинхронизироватьДанныеТолькоПоВыбраннымВидамДокументов";
	ОтборПоВидамДокументовНастроен = ВидыДокументов.Количество() > 0;
	Элементы.ОткрытьФормуОтбораПоВидамДокументов.Видимость = ОтборПоВидамДокументовИспользуется;
	Элементы.ОчиститьОтборПоВидамДокументов.Видимость = ОтборПоВидамДокументовИспользуется И ОтборПоВидамДокументовНастроен;

КонецПроцедуры

&НаСервере
Функция ПолучитьМассивОбъектовТабличнойЧасти(ИмяТабличнойЧасти, ИмяКолонки)

	Возврат ЭтаФорма[ИмяТабличнойЧасти].Выгрузить().ВыгрузитьКолонку(ИмяКолонки);

КонецФункции

&НаСервере
Функция ПолучитьАдресТабличнойЧасти(ИмяТабличнойЧасти)
	
	ТаблицаОрганизаций = ЭтаФорма[ИмяТабличнойЧасти].Выгрузить();
	ТаблицаОрганизаций.Колонки.Добавить("Использовать");
	ТаблицаОрганизаций.ЗаполнитьЗначения(Истина, "Использовать");
	
	Возврат ПоместитьВоВременноеХранилище(ТаблицаОрганизаций);
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеОбъекта(СтруктураПараметров)
	
	ИмяТаблицыДляЗаполнения = СтруктураПараметров.ИмяТаблицыДляЗаполнения;
	
	ЭтаФорма[ИмяТаблицыДляЗаполнения].Очистить();
	
	ТаблицаВыбранныхЗначений = ПолучитьИзВременногоХранилища(СтруктураПараметров.АдресТаблицыВоВременномХранилище);
	Для каждого СтрокаТаблицы Из ТаблицаВыбранныхЗначений Цикл
		
		Если СтрокаТаблицы.Использовать Тогда
			НоваяСтрока = ЭтаФорма[ИмяТаблицыДляЗаполнения].Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		КонецЕсли;
		
	КонецЦикла;
	
	УстановитьВидимостьНаСервере();
	ОбновитьНаименованиеКомандФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаименованиеКомандФормы()
	
	//Обновим заголовок выбранных организаций
	Если Организации.Количество() > 0 Тогда
		
		ВыбранныеОрганизации = Организации.Выгрузить().ВыгрузитьКолонку("Организация");
		НовыйЗаголовокОрганизаций = ПланыОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия30.СокращенноеПредставлениеКоллекцииЗначений(ВыбранныеОрганизации);
		
	Иначе
		
		НовыйЗаголовокОрганизаций = НСтр("ru = 'Выбрать организации '");
		
	КонецЕсли;
	
	Элементы.ОткрытьФормуОтбораПоОрганизациям.Заголовок = НовыйЗаголовокОрганизаций;
	
	//Обновим заголовок выбранных видов документов
	Если ВидыДокументов.Количество() > 0 Тогда
		
		ВыбранныеВидыДокументов = ВидыДокументов.Выгрузить().ВыгрузитьКолонку("Представление");
		НовыйЗаголовокВидовДокументов = ПланыОбмена.ОбменУправлениеНебольшойФирмойБухгалтерия30.СокращенноеПредставлениеКоллекцииЗначений(ВыбранныеВидыДокументов);
		
	Иначе
		
		НовыйЗаголовокВидовДокументов = НСтр("ru = 'Выбрать виды документов '");
		
	КонецЕсли;
	
	Элементы.ОткрытьФормуОтбораПоВидамДокументов.Заголовок = НовыйЗаголовокВидовДокументов;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииРежимаСинхронизацииДанных()

	Если РежимСинхронизации = "АвтоматическаяСинхронизация" Тогда
		
		Элементы.ГруппаУсловияОтправкиДанных.Доступность = Истина;
		
	Иначе
		
		Элементы.ГруппаУсловияОтправкиДанных.Доступность = Ложь;
		
	КонецЕсли;

КонецПроцедуры


