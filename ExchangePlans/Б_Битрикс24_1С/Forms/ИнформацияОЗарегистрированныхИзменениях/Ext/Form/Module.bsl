
#Область ОбработчикиСобытийФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПриЧтенииСозданииНаСервере(Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииСозданииНаСервере(Отказ, СтандартнаяОбработка)

	УзелОбмена = Параметры.УзелОбмена;
	
	ОбновитьДеревоИзмененийСервер();

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ДеревоИзменений.ПолучитьЭлементы().Количество() = 0 Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru = 'Изменения не зарегистрированы.'")
			,,,
			БиблиотекаКартинок.Информация32);
			
		Отказ = Истина;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиСобытийЭлементовТаблицыФормы
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ТАБЛИЦЫ ФОРМЫ "Дерево изменений"

&НаКлиенте
Процедура ДеревоОбъектНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
&НаКлиенте
Процедура ДеревоОбъектОчистка(Элемент, СтандартнаяОбработка)
	
	Родитель = Элементы.ДеревоИзменений.ТекущиеДанные.ПолучитьРодителя();
	
	Если Родитель = НеОпределено Тогда
		Возврат;
	КонецЕсли;

	УдалитьРегистрациюСервер(Элементы.ДеревоИзменений.ТекущиеДанные.Объект);
	Родитель.ПолучитьЭлементы().Удалить(Элементы.ДеревоИзменений.ТекущиеДанные);
	
КонецПроцедуры

&НаСервере
Процедура УдалитьРегистрациюСервер(ДанныеСсылка)
	
	ПланыОбмена.УдалитьРегистрациюИзменений(УзелОбмена, ДанныеСсылка);
	
КонецПроцедуры

#КонецОбласти


#Область ОбработчикиКомандФормы

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Обновить(Команда)
	ОбновитьДеревоИзмененийСервер();
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоИзмененийСервер()
	
	СтруктураИзменений = Новый Структура;
	ПланыОбмена.Б_Битрикс24_1С.ЗаполнитьСтруктуруИзмененийДляУзла(УзелОбмена, СтруктураИзменений);
	
	СтрокиДерева = ДеревоИзменений.ПолучитьЭлементы();
	СтрокиДерева.Очистить();
	
	Для Каждого текКлюч из СтруктураИзменений Цикл
		
		СтрокаДерева = СтрокиДерева.Добавить();
		СтрокаДерева.ВидОбъекта = ВернутьСинонимОбъекта(текКлюч.Ключ);
		
		Для Каждого ЭлементМассива Из СтруктураИзменений[текКлюч.Ключ] Цикл
			СтрокаОбъекта = СтрокаДерева.ПолучитьЭлементы().Добавить();
			СтрокаОбъекта.Объект = ЭлементМассива;
		КонецЦикла;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Функция ВернутьСинонимОбъекта(ИмяОбъекта)
	
	Результат = ИмяОбъекта;
	
	Если НЕ (ИмяОбъекта = "Организации" ИЛИ ИмяОбъекта = "Контрагенты") тогда
		
		Результат = Метаданные.Документы[ИмяОбъекта].Синоним;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции



#КонецОбласти

