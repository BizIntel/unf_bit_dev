
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Для каждого ТекДокумент из Метаданные.Документы Цикл
		
		Если ТекДокумент.Реквизиты.Найти("СуммаДокумента")<> Неопределено тогда
			Если Метаданные.ПланыОбмена.Б_Битрикс24_1С.Состав.Найти(ТекДокумент) <> неопределено тогда
				Элементы.СписокДокументовЗначение.СписокВыбора.Добавить(ТекДокумент.Имя);	
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Если ЭтоАдресВременногоХранилища(Параметры.АдресНастроек) Тогда
		
		АдресНастроек = Параметры.АдресНастроек;
		
		НастройкиОбмена 	= ПолучитьИзВременногоХранилища(АдресНастроек);
		
		СтруктураССхемамиК = ПланыОбмена.Б_Битрикс24_1С.ПолучитьСтруктуруСхемКомпоновки();
		
		КоличествоКонтрагентовВПакете = ПолучитьЗначениеКлючаСтруктурыНастроек(НастройкиОбмена.Контрагенты, "КоличествоКонтрагентовВПакете");
		НастройкиКомпоновкиДанныхКонтрагентов = ПолучитьЗначениеКлючаСтруктурыНастроек(НастройкиОбмена.Контрагенты, "НастройкиКомпоновкиДанныхКонтрагентов");
		
		АдресСхемы = ПоместитьВоВременноеХранилище(СтруктураССхемамиК.Контрагенты, УникальныйИдентификатор);
		
		КомпоновщикНастроекКомпоновкиДанныхКонтрагентов.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы)); 
		КомпоновщикНастроекКомпоновкиДанныхКонтрагентов.ЗагрузитьНастройки(НастройкиКомпоновкиДанныхКонтрагентов);
		КомпоновщикНастроекКомпоновкиДанныхКонтрагентов.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
		
		КоличествоДокументовВПакете = ПолучитьЗначениеКлючаСтруктурыНастроек(НастройкиОбмена.Документы, "КоличествоДокументовВПакете");
		ТочкаАктуальности 			= ПолучитьЗначениеКлючаСтруктурыНастроек(НастройкиОбмена.Документы, "ТочкаАктуальности");
		СписокДокументов 			= ПолучитьЗначениеКлючаСтруктурыНастроек(НастройкиОбмена.Документы, "СписокДокументов");

		
		НастройкиКомпоновкиДанныхДокументов = ПолучитьЗначениеКлючаСтруктурыНастроек(НастройкиОбмена.Документы, "НастройкиКомпоновкиДанныхДокументов");
		
		АдресСхемы = ПоместитьВоВременноеХранилище(СтруктураССхемамиК.Документы, УникальныйИдентификатор);
		
		КомпоновщикНастроекКомпоновкиДанныхДокументов.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(АдресСхемы)); 
		КомпоновщикНастроекКомпоновкиДанныхДокументов.ЗагрузитьНастройки(НастройкиКомпоновкиДанныхДокументов);
		КомпоновщикНастроекКомпоновкиДанныхДокументов.Восстановить(СпособВосстановленияНастроекКомпоновкиДанных.ПроверятьДоступность);
		
		Если НастройкиОбмена.Контрагенты.ВыгрузкаКонтрагентов = Ложь И НастройкиОбмена.Контакты.ВыгрузкаКонтактов = Ложь тогда 
			Элементы.НастройкиКонтрагентов.Видимость = Ложь;	
		КонецЕсли;
		
		Если НастройкиОбмена.Документы.ВыгрузкаДокументов = Ложь тогда 
			Элементы.НастройкиДокументов.Видимость = Ложь;	
		КонецЕсли;
		
	Иначе
		Сообщить("Настройки некорректны");
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура Применить(Команда)
		
	Закрыть(ПрименитьНаСервере());
	
КонецПроцедуры
////////////////////////////////////////////////////////////////////////////////

&НаСервере
Функция ПрименитьНаСервере()
	
	Настройки = ПолучитьИзВременногоХранилища(АдресНастроек);

	Настройки.Контрагенты.КоличествоКонтрагентовВПакете 	= КоличествоКонтрагентовВПакете;	
	Настройки.Контрагенты.НастройкиКомпоновкиДанныхКонтрагентов = КомпоновщикНастроекКомпоновкиДанныхКонтрагентов.ПолучитьНастройки();
	
	
	Настройки.Документы.КоличествоДокументовВПакете 		= КоличествоДокументовВПакете;	
	Настройки.Документы.ТочкаАктуальности 					= ТочкаАктуальности;	
	Настройки.Документы.СписокДокументов 					= СписокДокументов;	
	Настройки.Документы.НастройкиКомпоновкиДанныхДокументов	= КомпоновщикНастроекКомпоновкиДанныхДокументов.ПолучитьНастройки();
	
	
	лАдресНастроек = ПоместитьВоВременноеХранилище(Настройки, УникальныйИдентификатор);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("АдресНастроек"	, лАдресНастроек);

	Возврат ПараметрыФормы;
	 
КонецФункции

Функция ПолучитьЗначениеКлючаСтруктурыНастроек(СтруктураНастроек, Ключ)
	
	Результат = Неопределено;
	
	Если СтруктураНастроек.Свойство(Ключ) тогда
		Результат = СтруктураНастроек[Ключ];
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура КоличествоКонтрагентовВПакетеПриИзменении(Элемент)
	
	Если КоличествоКонтрагентовВПакете > 50 тогда
		КоличествоКонтрагентовВПакете = 50;	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоДокументовВПакетеПриИзменении(Элемент)
		
	Если КоличествоДокументовВПакете > 50 тогда
		КоличествоДокументовВПакете = 50;	
	КонецЕсли;

КонецПроцедуры
