
Функция УзелОбменаЭтойИБСервер() Экспорт
	
	//метод не работает как надо
	//ЭтотУзел = ПланыОбмена.Б_ОбменССайтом.ЭтотУзел();
	
	ПредопределенныйУзел = Планыобмена.Б_Битрикс24_1С.ПустаяСсылка();
	
	Выборка = ПланыОбмена.Б_Битрикс24_1С.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.ЭтотУзел тогда
			ПредопределенныйУзел = Выборка.Ссылка;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ПредопределенныйУзел;
	
КонецФункции

Функция ПолучитьСтруктуруСхемКомпоновки() Экспорт
	
	Результат = Новый Структура();
	
	Результат.Вставить("Контрагенты"		, ПланыОбмена.Б_Битрикс24_1С.ПолучитьМакет("СхемаВыгрузкиКонтрагентов")); 	
	Результат.Вставить("Контакты"			, ПланыОбмена.Б_Битрикс24_1С.ПолучитьМакет("СхемаВыгрузкиКонтактов")); 	
	Результат.Вставить("Документы"			, ПланыОбмена.Б_Битрикс24_1С.ПолучитьМакет("СхемаВыгрузкиДокумента")); 	
	
	Возврат Результат;
	
КонецФункции

Процедура ЗаполнитьСтруктуруИзмененийДляУзла(УзелПланаОбмена, СтруктураВозврата) Экспорт

	лСохраненныеНастройки = УзелПланаОбмена.СохраненныеНастройки.Получить();
	лВыгружаемыеДокументы = лСохраненныеНастройки.Документы.СписокДокументов;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Узел", УзелПланаОбмена);
	Запрос.Текст = 		"ВЫБРАТЬ
	|	Контрагенты.Ссылка,
	|	""Контрагенты"" КАК ТипСсылки
	|ИЗ
	|	Справочник.Контрагенты.Изменения КАК Контрагенты
	|ГДЕ
	|	Контрагенты.Узел = &Узел
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|ВЫБРАТЬ
	|	Организации.Ссылка,
	|	""Организации"" КАК ТипСсылки
	|ИЗ
	|	Справочник.Организации.Изменения КАК Организации
	|ГДЕ
	|	Организации.Узел = &Узел";
	
	Для каждого лДокумент из лВыгружаемыеДокументы Цикл
		
		Если ЗначениеЗаполнено(лДокумент.Значение) тогда
		
			Запрос.Текст = Запрос.Текст + "
			|			
			|ОБЪЕДИНИТЬ ВСЕ
			|
			|ВЫБРАТЬ
			|	ДокументИзменения.Ссылка,
			|	""" + лДокумент.Значение + """
			|ИЗ
			|	Документ." + лДокумент.Значение + ".Изменения КАК ДокументИзменения
			|ГДЕ
			|	ДокументИзменения.Узел = &Узел";
		КонецЕсли;	
	КонецЦикла;
	
	тзнИзменений = Запрос.Выполнить().Выгрузить();
	
	тзнТипов = тзнИзменений.Скопировать();
	тзнТипов.Свернуть("ТипСсылки");
	
	Для Каждого ТекТип из тзнТипов Цикл
		СтруктураВозврата.Вставить(ТекТип.ТипСсылки, Новый Массив);	
	КонецЦикла;
	
	Для каждого ТекЭлемент из тзнИзменений Цикл
		СтруктураВозврата[ТекЭлемент.ТипСсылки].Добавить(ТекЭлемент.Ссылка);
	КонецЦикла;
	
КонецПроцедуры

