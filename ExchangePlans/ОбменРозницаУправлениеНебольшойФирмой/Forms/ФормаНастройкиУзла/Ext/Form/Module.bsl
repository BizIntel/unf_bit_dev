#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	ИспользоватьНесколькоОрганизаций = ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций");
	
	ОбменДаннымиСервер.ФормаНастройкиУзлаПриСозданииНаСервере(ЭтаФорма, Метаданные.ПланыОбмена.ОбменРозницаУправлениеНебольшойФирмой.Имя);
	
	ОбновитьНаименованиеКомандФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	ОбменДаннымиКлиент.ФормаНастройкиПередЗакрытием(Отказ, ЭтотОбъект, ЗавершениеРаботы);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	ОбновитьДанныеОбъекта(ВыбранноеЗначение);
	Модифицированность = Истина;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьСписокВыбранныхОрганизацийНажатие(Элемент)
	КоллекцияФильтров = Неопределено;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИмяЭлементаФормыДляЗаполнения",          "Организации");
	ПараметрыФормы.Вставить("ИмяРеквизитаЭлементаФормыДляЗаполнения", "Организация");
	ПараметрыФормы.Вставить("ИмяТаблицыВыбора",                       "Справочник.Организации");
	ПараметрыФормы.Вставить("ЗаголовокФормыВыбора",                   НСтр("ru = 'Выберите организации для отбора:'"));
	ПараметрыФормы.Вставить("МассивВыбранныхЗначений",                СформироватьМассивВыбранныхЗначений(ПараметрыФормы));
	ПараметрыФормы.Вставить("ПараметрыВнешнегоСоединения",            Неопределено);
	ПараметрыФормы.Вставить("КоллекцияФильтров",                      КоллекцияФильтров);

	ОткрытьФорму("ПланОбмена.ОбменРозницаУправлениеНебольшойФирмой.Форма.ФормаВыбораДополнительныхУсловий",
		ПараметрыФормы,
		ЭтаФорма);
	КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокВыбранныхСкладовНажатие(Элемент)
	
	КоллекцияФильтров = Новый Массив;
	Фильтр = Новый Структура;
	Фильтр.Вставить("Условие"," = ");
	Фильтр.Вставить("РеквизитОтбора","ТипСтруктурнойЕдиницы");
	Фильтр.Вставить("ИмяПараметра","ТипСтруктурнойЕдиницы");
	Фильтр.Вставить("ЗначениеПараметра", ПредопределенноеЗначение("Перечисление.ТипыСтруктурныхЕдиниц.Розница"));
	КоллекцияФильтров.Добавить(Фильтр);
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИмяЭлементаФормыДляЗаполнения",          "Склады");
	ПараметрыФормы.Вставить("ИмяРеквизитаЭлементаФормыДляЗаполнения", "Склад");
	ПараметрыФормы.Вставить("ИмяТаблицыВыбора",                       "Справочник.СтруктурныеЕдиницы");
	ПараметрыФормы.Вставить("ЗаголовокФормыВыбора",                   НСтр("ru = 'Выберите склады для отбора:'"));
	ПараметрыФормы.Вставить("МассивВыбранныхЗначений",                СформироватьМассивВыбранныхЗначений(ПараметрыФормы));
	ПараметрыФормы.Вставить("ПараметрыВнешнегоСоединения",            Неопределено);
	ПараметрыФормы.Вставить("КоллекцияФильтров",                      КоллекцияФильтров);

	ОткрытьФорму("ПланОбмена.ОбменРозницаУправлениеНебольшойФирмой.Форма.ФормаВыбораДополнительныхУсловий",
		ПараметрыФормы,
		ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьСписокВыбранныхВидовЦенНажатие(Элемент)
	КоллекцияФильтров = Неопределено;
	
	ПараметрыФормы = Новый Структура();
	ПараметрыФормы.Вставить("ИмяЭлементаФормыДляЗаполнения",          "ВидыЦен");
	ПараметрыФормы.Вставить("ИмяРеквизитаЭлементаФормыДляЗаполнения", "ВидЦен");
	ПараметрыФормы.Вставить("ИмяТаблицыВыбора",                       "Справочник.ВидыЦен");
	ПараметрыФормы.Вставить("ЗаголовокФормыВыбора",                   НСтр("ru = 'Выберите виды цен для отбора:'"));
	ПараметрыФормы.Вставить("МассивВыбранныхЗначений",                СформироватьМассивВыбранныхЗначений(ПараметрыФормы));
	ПараметрыФормы.Вставить("ПараметрыВнешнегоСоединения",            Неопределено);
	ПараметрыФормы.Вставить("КоллекцияФильтров",                      КоллекцияФильтров);

	ОткрытьФорму("ПланОбмена.ОбменРозницаУправлениеНебольшойФирмой.Форма.ФормаВыбораДополнительныхУсловий",
		ПараметрыФормы,
		ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьИЗакрыть(Команда)
	
	Если Не ИспользоватьОтборПоОрганизациям И Организации.Количество() <> 0 Тогда
		Организации.Очистить();
	ИначеЕсли ИспользоватьОтборПоОрганизациям И Организации.Количество() = 0 Тогда
		ИспользоватьОтборПоОрганизациям = Ложь;
	КонецЕсли;
	
	Если Не ИспользоватьОтборПоСкладам И Склады.Количество() <> 0 Тогда
		Склады.Очистить();
	ИначеЕсли ИспользоватьОтборПоСкладам И Склады.Количество() = 0 Тогда
		ИспользоватьОтборПоСкладам = Ложь;
	КонецЕсли;
	
	Если Не ИспользоватьОтборПоВидамЦен И ВидыЦен.Количество() <> 0 Тогда
		Склады.Очистить();
	ИначеЕсли ИспользоватьОтборПоСкладам И Склады.Количество() = 0 Тогда
		ИспользоватьОтборПоСкладам = Ложь;
	КонецЕсли;
	
	ОбменДаннымиКлиент.ФормаНастройкиУзлаКомандаЗакрытьФорму(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоОрганизациямПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоСкладамПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОтборПоВидамЦенПриИзменении(Элемент)
	УправлениеФормой();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УправлениеФормой()
	
	Элементы.ИспользоватьОтборПоОрганизациям.Видимость   = ИспользоватьНесколькоОрганизаций;
	Элементы.ОткрытьСписокВыбранныхОрганизаций.Видимость = ИспользоватьНесколькоОрганизаций;
	
	Элементы.ОткрытьСписокВыбранныхОрганизаций.Доступность = ИспользоватьОтборПоОрганизациям;
	Элементы.ОткрытьСписокВыбранныхСкладов.Доступность     = ИспользоватьОтборПоСкладам;
	Элементы.ОткрытьСписокВыбранныхВидовЦен.Доступность    = ИспользоватьОтборПоВидамЦен;
	
КонецПроцедуры

&НаСервере
Функция СформироватьМассивВыбранныхЗначений(ПараметрыФормы)
	
	ТабличнаяЧасть           = ЭтаФорма[ПараметрыФормы.ИмяЭлементаФормыДляЗаполнения];
	ТаблицаВыбранныхЗначений = ТабличнаяЧасть.Выгрузить(,ПараметрыФормы.ИмяРеквизитаЭлементаФормыДляЗаполнения);
	МассивВыбранныхЗначений  = ТаблицаВыбранныхЗначений.ВыгрузитьКолонку(ПараметрыФормы.ИмяРеквизитаЭлементаФормыДляЗаполнения);
	
	Возврат МассивВыбранныхЗначений;
	
КонецФункции

&НаСервере
Процедура ОбновитьДанныеОбъекта(СтруктураПараметров)
	
	ЭтаФорма[СтруктураПараметров.ИмяТаблицыДляЗаполнения].Очистить();
	
	СписокВыбранныхЗначений = ПолучитьИзВременногоХранилища(СтруктураПараметров.АдресТаблицыВоВременномХранилище);
	
	Если СписокВыбранныхЗначений.Количество() > 0 Тогда
		СписокВыбранныхЗначений.Колонки.Представление.Имя = СтруктураПараметров.ИмяКолонкиДляЗаполнения;
		ЭтаФорма[СтруктураПараметров.ИмяТаблицыДляЗаполнения].Загрузить(СписокВыбранныхЗначений);
	КонецЕсли;
	
	ОбновитьНаименованиеКомандФормы();
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаименованиеКомандФормы()
	
	Если ЭтаФорма.Организации.Количество() > 0 Тогда
		ВыбранныеЭлементы = ЭтаФорма.Организации.Выгрузить().ВыгрузитьКолонку("Организация");
		НовыйЗаголовок = СтрСоединить(ВыбранныеЭлементы,", ");
	Иначе
		НовыйЗаголовок = НСтр("ru = 'Выбрать организации'");
	КонецЕсли;
	Элементы.ОткрытьСписокВыбранныхОрганизаций.Заголовок = НовыйЗаголовок;
	
	Если ЭтаФорма.Склады.Количество() > 0 Тогда
		ВыбранныеЭлементы = ЭтаФорма.Склады.Выгрузить().ВыгрузитьКолонку("Склад");
		НовыйЗаголовок = СтрСоединить(ВыбранныеЭлементы,", ");
	Иначе
		НовыйЗаголовок = НСтр("ru = 'Выбрать магазины'");
	КонецЕсли;
	Элементы.ОткрытьСписокВыбранныхСкладов.Заголовок = НовыйЗаголовок;
	
	Если ЭтаФорма.ВидыЦен.Количество() > 0 Тогда
		ВыбранныеЭлементы = ЭтаФорма.ВидыЦен.Выгрузить().ВыгрузитьКолонку("ВидЦен");
		НовыйЗаголовок = СтрСоединить(ВыбранныеЭлементы,", ");
	Иначе
		НовыйЗаголовок = НСтр("ru = 'Выбрать виды цен'");
	КонецЕсли;
	Элементы.ОткрытьСписокВыбранныхВидовЦен.Заголовок = НовыйЗаголовок;
	
	КонецПроцедуры

#КонецОбласти