#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Параметры.Свойство("ИдентификаторСервиса", ИдентификаторСервиса);
	Параметры.Свойство("ТекущаяПодпись", ТекущаяПодпись);
	Если НЕ ЗначениеЗаполнено(ИдентификаторСервиса) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не указан идентификатор сервиса (agent_id)'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()),,,, Отказ);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если ВладелецФормы = Неопределено Тогда
		РежимОткрытияОкна = РежимОткрытияОкнаФормы.БлокироватьВесьИнтерфейс;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы Тогда
		Возврат;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийШапкиФормы

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если НЕ ЗначениеЗаполнено(КлючевоеСлово) Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Введите ключевое слово'",
			ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка()));
		Возврат;
	КонецЕсли;
	
	Подпись = ХэшированиеШтатныеСредства(ИдентификаторСервиса, КлючевоеСлово);
	ДополнительныеПараметры = Новый Структура("Изменено", НЕ Подпись = ТекущаяПодпись);
	Оповещение = Новый ОписаниеОповещения("ПослеКомандыОК", ЭтотОбъект);
	Если Подпись = ТекущаяПодпись Тогда
		ТекстПредупреждения = НСтр("ru = 'Ключевое слово не изменено'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	Иначе
		ТекстПредупреждения = НСтр("ru = 'Изменено ключевое слово'", ОбщегоНазначенияКлиентСервер.КодОсновногоЯзыка());
	КонецЕсли;
	ПоказатьПредупреждение(Оповещение, ТекстПредупреждения,, Заголовок);
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьКлючевоеСлово(Команда)
	
	Элементы.КлючевоеСлово.РежимПароля = НЕ Элементы.КлючевоеСлово.РежимПароля;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеКомандыОК(ДополнительныеПараметры) Экспорт
	
	Закрыть(Подпись);
КонецПроцедуры

&НаСервере
Функция ХэшированиеШтатныеСредства(ИдентификаторСервиса, КлючХешированияДанных = "")
	
	// Пока не кодируем в двоичные данные.
	// ИдентификаторСервисаДвоичныеДанные = Закодировать(ИдентификаторСервиса);
	// КлючОбменаДвоичныеДанные = Закодировать(КлючХешированияДанных);
	
	ИдентификаторСервисаДвоичныеДанные = ИдентификаторСервиса;
	КлючОбменаДвоичныеДанные = КлючХешированияДанных;
	
	ХешированиеДанных = Новый ХешированиеДанных(ХешФункция.SHA1);
	ХешированиеДанных.Добавить(КлючОбменаДвоичныеДанные);
	ХешированиеДанных.Добавить(ИдентификаторСервисаДвоичныеДанные);
	
	ТипДвоичныеДанные = ФабрикаXDTO.Тип("http://www.w3.org/2001/XMLSchema", "hexBinary");
	
	ДвоичныеДанные = ФабрикаXDTO.Создать(ТипДвоичныеДанные, ХешированиеДанных.ХешСумма);
	
	Возврат ДвоичныеДанные.ЛексическоеЗначение;
КонецФункции

#КонецОбласти