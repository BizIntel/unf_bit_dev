#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

Процедура ОбновитьТекущиеКадровыеДанныеСотрудников(СписокСотрудников = Неопределено) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Сотрудники.Ссылка КАК Сотрудник,
	|	ПриемНаРаботу.Организация,
	|	ПриемНаРаботу.Период КАК ДатаПриема
	|ПОМЕСТИТЬ ВТДатаПриема
	|ИЗ
	|	Справочник.Сотрудники КАК Сотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники.СрезПоследних(
	|				,
	|				&Условие
	|					И Регистратор ССЫЛКА Документ.ПриемНаРаботу) КАК ПриемНаРаботу
	|		ПО (ПриемНаРаботу.Сотрудник = Сотрудники.Ссылка)
	|			И (&Условие)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Организация,
	|	Сотрудники.Сотрудник,
	|	Сотрудники.Период КАК ДатаУвольнения
	|ПОМЕСТИТЬ ВТДатыУвольнения
	|ИЗ
	|	РегистрСведений.Сотрудники.СрезПоследних(
	|			,
	|			&Условие
	|				И Регистратор ССЫЛКА Документ.Увольнение) КАК Сотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Сотрудники.Организация,
	|	Сотрудники.Сотрудник,
	|	Сотрудники.Период
	|ПОМЕСТИТЬ ВТКадровыеПеремещенияСрез
	|ИЗ
	|	РегистрСведений.Сотрудники.СрезПоследних(
	|			,
	|			&Условие
	|				И Должность <> ЗНАЧЕНИЕ(Справочник.Должности.ПустаяСсылка)) КАК Сотрудники
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	КадровыеПеремещенияСрез.Организация,
	|	КадровыеПеремещенияСрез.Сотрудник,
	|	Сотрудники.Должность,
	|	Сотрудники.СтруктурнаяЕдиница
	|ПОМЕСТИТЬ ВТКадровыеПеремещения
	|ИЗ
	|	ВТКадровыеПеремещенияСрез КАК КадровыеПеремещенияСрез
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.Сотрудники КАК Сотрудники
	|		ПО (Сотрудники.Период = КадровыеПеремещенияСрез.Период)
	|			И (Сотрудники.Организация = КадровыеПеремещенияСрез.Организация)
	|			И (Сотрудники.Сотрудник = КадровыеПеремещенияСрез.Сотрудник)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеНачисленияИУдержания.Организация,
	|	ПлановыеНачисленияИУдержания.Сотрудник,
	|	МАКСИМУМ(ПлановыеНачисленияИУдержания.Период) КАК Период
	|ПОМЕСТИТЬ ВТПлановыеНачисленияИУдержанияСрез
	|ИЗ
	|	РегистрСведений.ПлановыеНачисленияИУдержания КАК ПлановыеНачисленияИУдержания
	|ГДЕ
	|	&Условие
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыеНачисленияИУдержания.Организация,
	|	ПлановыеНачисленияИУдержания.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПлановыеНачисленияИУдержанияСрез.Организация,
	|	ПлановыеНачисленияИУдержанияСрез.Сотрудник,
	|	ПлановыеНачисленияИУдержания.Валюта,
	|	ВЫБОР
	|		КОГДА ПлановыеНачисленияИУдержания.Валюта = НациональнаяВалюта.Значение
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	СУММА(ВЫБОР
	|			КОГДА ПлановыеНачисленияИУдержания.ВидНачисленияУдержания.Тип = ЗНАЧЕНИЕ(Перечисление.ТипыНачисленийИУдержаний.Начисление)
	|				ТОГДА ПлановыеНачисленияИУдержания.Сумма
	|			ИНАЧЕ -ПлановыеНачисленияИУдержания.Сумма
	|		КОНЕЦ) КАК Сумма
	|ПОМЕСТИТЬ ВТНачисленияИУдержания
	|ИЗ
	|	ВТПлановыеНачисленияИУдержанияСрез КАК ПлановыеНачисленияИУдержанияСрез
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ПлановыеНачисленияИУдержания КАК ПлановыеНачисленияИУдержания
	|		ПО ПлановыеНачисленияИУдержанияСрез.Период = ПлановыеНачисленияИУдержания.Период
	|			И ПлановыеНачисленияИУдержанияСрез.Организация = ПлановыеНачисленияИУдержания.Организация
	|			И ПлановыеНачисленияИУдержанияСрез.Сотрудник = ПлановыеНачисленияИУдержания.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ Константа.НациональнаяВалюта КАК НациональнаяВалюта
	|		ПО (ИСТИНА)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлановыеНачисленияИУдержанияСрез.Организация,
	|	ПлановыеНачисленияИУдержанияСрез.Сотрудник,
	|	ПлановыеНачисленияИУдержания.Валюта,
	|	НациональнаяВалюта.Значение
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПринятыеСотрудники.Организация,
	|	ПринятыеСотрудники.Сотрудник,
	|	ПринятыеСотрудники.ДатаПриема,
	|	ВЫБОР
	|		КОГДА ВТДатыУвольнения.ДатаУвольнения < ПринятыеСотрудники.ДатаПриема
	|			ТОГДА &ПустаяДата
	|		ИНАЧЕ ВТДатыУвольнения.ДатаУвольнения
	|	КОНЕЦ КАК ДатаУвольнения,
	|	ВТКадровыеПеремещения.Должность КАК ТекущаяДолжность,
	|	ВТКадровыеПеремещения.СтруктурнаяЕдиница,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НачисленияИУдержанияНацВалюта.Сумма, 0) > 0
	|			ТОГДА НачисленияИУдержанияНацВалюта.Валюта
	|		ИНАЧЕ НачисленияИУдержания.Валюта
	|	КОНЕЦ КАК ВалютаТарифнойСтавки,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(НачисленияИУдержанияНацВалюта.Сумма, 0) > 0
	|			ТОГДА ЕСТЬNULL(НачисленияИУдержанияНацВалюта.Сумма, 0)
	|		ИНАЧЕ ЕСТЬNULL(НачисленияИУдержания.Сумма, 0)
	|	КОНЕЦ КАК ТекущаяТарифнаяСтавка
	|ИЗ
	|	ВТДатаПриема КАК ПринятыеСотрудники
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТДатыУвольнения КАК ВТДатыУвольнения
	|		ПО ПринятыеСотрудники.Организация = ВТДатыУвольнения.Организация
	|			И ПринятыеСотрудники.Сотрудник = ВТДатыУвольнения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТКадровыеПеремещения КАК ВТКадровыеПеремещения
	|		ПО ПринятыеСотрудники.Организация = ВТКадровыеПеремещения.Организация
	|			И ПринятыеСотрудники.Сотрудник = ВТКадровыеПеремещения.Сотрудник
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияИУдержания КАК НачисленияИУдержанияНацВалюта
	|		ПО ПринятыеСотрудники.Организация = НачисленияИУдержанияНацВалюта.Организация
	|			И ПринятыеСотрудники.Сотрудник = НачисленияИУдержанияНацВалюта.Сотрудник
	|			И (НачисленияИУдержанияНацВалюта.Приоритет = 1)
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТНачисленияИУдержания КАК НачисленияИУдержания
	|		ПО ПринятыеСотрудники.Организация = НачисленияИУдержания.Организация
	|			И ПринятыеСотрудники.Сотрудник = НачисленияИУдержания.Сотрудник
	|			И (НачисленияИУдержания.Приоритет = 2)";
	Если СписокСотрудников = Неопределено Тогда
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "Истина");
	Иначе
		Запрос.Текст = СтрЗаменить(Запрос.Текст, "&Условие", "Сотрудник В (&СписокСотрудников)");
		Запрос.УстановитьПараметр("СписокСотрудников", СписокСотрудников);
	КонецЕсли;
	Запрос.УстановитьПараметр("ПустаяДата",'00010101');
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Запись = РегистрыСведений.ТекущиеКадровыеДанныеСотрудников.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(Запись, Выборка);
		Запись.Записать();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли