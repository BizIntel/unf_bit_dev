
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("ПрайсЛист") Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	// Определение вида цены прайслиста
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ПрайсЛистыВидыЦен.ВидЦен
	|ИЗ
	|	Справочник.ПрайсЛисты.ВидыЦен КАК ПрайсЛистыВидыЦен
	|ГДЕ
	|	ПрайсЛистыВидыЦен.Ссылка = &Ссылка");
	
	Запрос.УстановитьПараметр("Ссылка", Параметры.Прайслист);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		ВидЦены = Выборка.ВидЦен;
	Иначе
		ВидЦены = ИнтеграцияОбменШтрихМ.РозничныйВидЦены();
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(ЦеныНоменклатуры,"РозничныйВидЦен", ВидЦены);
	
	
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьЦену(Команда)
	УстановитьЦенуНаКлиенте();
КонецПроцедуры


&НаКлиенте
Процедура ЦеныНоменклатурыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	УстановитьЦенуНаКлиенте();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЦенуНаКлиенте()
	
	Если ЗначениеЗаполнено(Элементы.ЦеныНоменклатуры.ТекущиеДанные.Ссылка)
		И НЕ Элементы.ЦеныНоменклатуры.ТекущиеДанные.ЭтоГруппа Тогда
		ПослеВыбораЦены = Новый ОписаниеОповещения("ПослеВыбораЦены", ЭтотОбъект);
		ПоказатьВводЧисла(ПослеВыбораЦены, Элементы.ЦеныНоменклатуры.ТекущиеДанные.Цена, НСтр("ru='Введите цену товара'"));
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораЦены(Результат,Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		УстановитьЦену(
			Элементы.ЦеныНоменклатуры.ТекущиеДанные.Ссылка,
			ВидЦены,
			Элементы.ЦеныНоменклатуры.ТекущиеДанные.ДатаУстановки,
			Результат);
		
		ОповеститьОбИзменении(Элементы.ЦеныНоменклатуры.ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьЦену(Номенклатура, ВидЦены, ДатаУстановки, Цена)
	
	НоваяЦена = РегистрыСведений.ЦеныНоменклатуры.СоздатьМенеджерЗаписи();
	НоваяЦена.Номенклатура = Номенклатура;
	Если ТекущаяДата() < ДатаУстановки Тогда
		НоваяЦена.Период = ДатаУстановки+1;
	Иначе
		НоваяЦена.Период = ТекущаяДата();
	КонецЕсли;
	НоваяЦена.ВидЦен = ВидЦены;
	НоваяЦена.Цена = Цена;
	
	НоваяЦена.Записать();
	
КонецПроцедуры
