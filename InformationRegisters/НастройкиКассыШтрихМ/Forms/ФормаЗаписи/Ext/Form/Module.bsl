#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если НЕ Параметры.Свойство("Отбор") ИЛИ НЕ Параметры.Отбор.Свойство("КассаККМ") ИЛИ
		(Параметры.Отбор.Свойство("КассаККМ") И НЕ ЗначениеЗаполнено(Параметры.Отбор.КассаККМ)) Тогда
		
		Элементы.ГруппаДанных.Видимость = Ложь;
		Элементы.ГруппаНетДанных.Видимость = Истина;
		
	Иначе
		Элементы.ГруппаДанных.Видимость = Истина;
		Элементы.ГруппаНетДанных.Видимость = Ложь;
		Запись.КассаККМ = Параметры.Отбор.КассаККМ;
		
		ПриЧтенииСозданииНаСервере();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	ПриЧтенииСозданииНаСервере();
КонецПроцедуры

#КонецОбласти

#Область СобытияЭлементовФормы

&НаКлиенте
Процедура ПерейтиКЗаполнениюЦен(Команда)
	
	ОткрытьФорму(
		"РегистрСведений.НастройкиКассыШтрихМ.Форма.ФормаРедактированияЦен",
		Новый Структура("ПрайсЛист", Запись.ПрайсЛист),,,,,,
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура РегистрационныйНомерПриИзменении(Элемент)
	ЗаписатьЗапись();
КонецПроцедуры

&НаКлиенте
Процедура СерийныйНомерПриИзменении(Элемент)
	ЗаписатьЗапись();
КонецПроцедуры

&НаКлиенте
Процедура ТокенПриИзменении(Элемент)
	ЗаписатьЗапись();
КонецПроцедуры

&НаКлиенте
Процедура ПрайсЛистПриИзменении(Элемент)
	ЗаписатьЗапись();
КонецПроцедуры

&НаКлиенте
Процедура ПечатьПрайсЛиста(Команда)
	ОткрытьФорму(
		"Обработка.ФормированиеПрайсЛистов.Форма", 
		Новый Структура("ПрайсЛист", Запись.ПрайсЛист));
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКоманд
//
&НаКлиенте
Процедура ПоказатьПароль(Команда)
	
	Элементы.Токен.РежимПароля = НЕ Элементы.Токен.РежимПароля;
	
КонецПроцедуры


&НаКлиенте
Процедура Подключить(Команда)
	
	Если НЕ ЗначениеЗаполнено(Запись.РегистрационныйНомер) Тогда
		ТекстСообщения = НСтр("ru = 'Введите регистрационный номер, полученный при регистрации ККМ в ФНС.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "Запись.РегистрационныйНомер");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Запись.Токен) Тогда
		ТекстСообщения = НСтр("ru = 'Введите пароль для подключения (token),
		|полученный на ККМ при регистрации у оператора фискальных данных (ОФД).'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ЗаписьТокен");
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Запись.ПрайсЛист) Тогда
		ТекстСообщения = НСтр("ru = 'Для работы с кассой необходим прайс-лист для указания цен.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения,, "ПрайсЛист");
		Возврат;
	КонецЕсли;
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаОЗаписиОбъекта", ЭтотОбъект);
	
	Если Модифицированность Тогда
		ЗаголовокВопроса = Нстр("ru = 'Подключение кассового аппарата'");
		ТекстВопроса = Нстр("ru = 'Для подключения необходимо сохранить введенные данные. Сохранить?'");
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, Нстр("ru = 'Сохранить'"));
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, Нстр("ru = 'Отменить'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Да, ЗаголовокВопроса);
	Иначе
		ВыполнитьОбработкуОповещения(ОписаниеОповещения, КодВозвратаДиалога.Да);
	КонецЕсли;
КонецПроцедуры

//
&НаКлиенте
Процедура ВыгрузитьТовары(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаВыгрузкаТоваров", ЭтотОбъект);
	
	ЗаголовокВопроса = Нстр("ru = 'Выгрузка товаров и цен для кассового аппарата'");
	ТекстВопроса = Нстр("ru = 'Все товары и цены будут выгружены в кассовый аппарат. Выгрузить?'");
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, Нстр("ru = 'Выгрузить'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, Нстр("ru = 'Отменить'"));
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Да, ЗаголовокВопроса);
КонецПроцедуры

//
&НаКлиенте
Процедура ОчиститьТовары(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаУдалениеТоваров", ЭтотОбъект);
	
	ЗаголовокВопроса = Нстр("ru = 'Удаление товаров и цен с кассового аппарата'");
	ТекстВопроса = Нстр("ru = 'Вы действительно хотите удалить все товары и цены с кассового аппарата?'");
	СписокКнопок = Новый СписокЗначений;
	СписокКнопок.Добавить(КодВозвратаДиалога.Да, Нстр("ru = 'Удалить'"));
	СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, Нстр("ru = 'Отменить'"));
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Да, ЗаголовокВопроса);
КонецПроцедуры

//
&НаКлиенте
Процедура ЗагрузитьЧеки(Команда)
	
	ВыборПериода = Новый СписокЗначений;
	ВыборПериода.Добавить("ТекущаяНеделя", НСтр("ru='Текущая неделя'"));
	ВыборПериода.Добавить("ТекущийМесяц", НСтр("ru='Текущий месяц'"));
	ВыборПериода.Добавить("ПрошлыйМесяц", НСтр("ru='Прошлый и текущий месяц'"));
	ВыборПериода.Добавить("ТекущийКвартал", НСтр("ru='Текущий квартал'"));
	ВыборПериода.Добавить("ТекущийГод", НСтр("ru='Текущий год'"));
	ВыборПериода.Добавить("Все", НСтр("ru='За все периоды'"));
	
	ПоказатьВыборИзМеню(Новый ОписаниеОповещения("ПослеВыбораПериода", ЭтотОбъект),ВыборПериода,Элементы.ЗагрузитьЧеки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораПериода(Результат, Параметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		
		ПериодЗагрузки.ДатаОкончания = ТекущаяДата();
		
		Если Результат.Значение = "ТекущаяНеделя" Тогда
			ПериодЗагрузки.ДатаНачала = НачалоНедели(ТекущаяДата());
		ИначеЕсли Результат.Значение = "ТекущийМесяц" Тогда
			ПериодЗагрузки.ДатаНачала = НачалоМесяца(ТекущаяДата());
		ИначеЕсли Результат.Значение = "ПрошлыйМесяц" Тогда
			ПериодЗагрузки.ДатаНачала = НачалоМесяца(НачалоМесяца(ТекущаяДата())-1);
		ИначеЕсли Результат.Значение = "ТекущийКвартал" Тогда
			ПериодЗагрузки.ДатаНачала = НачалоКвартала(ТекущаяДата());
		ИначеЕсли Результат.Значение = "ТекущийГод" Тогда
			ПериодЗагрузки.ДатаНачала = НачалоГода(ТекущаяДата());
		ИначеЕсли Результат.Значение = "Все" Тогда
			ПериодЗагрузки.ДатаНачала = Дата(2016,1,1,1,1,1);
		КонецЕсли;
		
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаЗагрузкаЧеков", ЭтотОбъект);
		
		ЗаголовокВопроса = Нстр("ru = 'Загрузка чеков с кассового аппарата'");
		ТекстВопроса = Нстр("ru = 'Будут загружены все кассовые чеки за выбранный период. Загрузить?'");
		СписокКнопок = Новый СписокЗначений;
		СписокКнопок.Добавить(КодВозвратаДиалога.Да, Нстр("ru = 'Загрузить'"));
		СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, Нстр("ru = 'Отменить'"));
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Да, ЗаголовокВопроса);
		
	КонецЕсли;
	
КонецПроцедуры
//
&НаКлиенте
Процедура ПослеВопросаВыгрузкаТоваров(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВыгрузитьТоварыНаСервере();
		ЗаголовокПредупреждения = НСтр("ru = 'Выгрузка товаров и цен'");
		ТекстПредупреждения = НСтр("ru = 'Выгрузка товаров и цен займет около 3 минут."
			" Вы можете продолжить работу с программой в это время.'");
		ДополнительныеПараметры = Новый Структура("РежимТовары", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПредупрежденияОРегистрации", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ЗаголовокПредупреждения);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПослеВопросаСнятиеСРегистрации(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗаголовокПредупреждения = Нстр("ru = 'Отключение кассового аппарата.'");
		
		Результат = ПодключитьОтключитьКассуПоТарифу(Ложь);
		Если Результат Тогда
			ДополнительныеПараметры = Новый Структура("Режим", 2);
			ТекстПредупреждения = НСтр("ru = 'Отключение может занять некоторое время.'");
		Иначе
			ТекстПредупреждения = НСтр("ru = 'Операция проверки и получения лицензии завершилась с ошибкой.
						|Обратитесь в службу поддержки.'");
		КонецЕсли;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПредупрежденияОРегистрации", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ЗаголовокПредупреждения);
	КонецЕсли;
КонецПроцедуры

//
&НаКлиенте
Процедура ПослеВопросаУдалениеТоваров(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОчиститьТоварыНаСервере();
		ЗаголовокПредупреждения = НСтр("ru = 'Удаление товаров'");
		ТекстПредупреждения = НСтр("ru = 'Удаление товаров займет около 3 мин."
			" Вы можете продолжить работу с программой в это время.'");
		ДополнительныеПараметры = Новый Структура("РежимТоварыОчистка", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПредупрежденияОРегистрации", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ЗаголовокПредупреждения);
	КонецЕсли;
	
КонецПроцедуры
//
&НаКлиенте
Процедура ПослеВопросаЗагрузкаЧеков(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ЗагрузитьЧекиНаСервере();
		ЗаголовокПредупреждения = НСтр("ru = 'Загрузка кассовых чеков'");
		ТекстПредупреждения = НСтр("ru = 'Загрузка кассовых чеков может занять продолжительное время."
			" Вы можете продолжить работу с программой в это время.'");
		ДополнительныеПараметры = Новый Структура("РежимДокументы", Истина);
		ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПредупрежденияОРегистрации", ЭтотОбъект, ДополнительныеПараметры);
		ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ЗаголовокПредупреждения);
	КонецЕсли;

КонецПроцедуры
//
&НаКлиенте
Процедура ПослеПредупрежденияОРегистрации(Знач ДополнительныеПараметры) Экспорт
	
	ЕстьОшибки = Ложь;
	СообщениеОбОшибке = "";
	
	Если НЕ ДополнительныеПараметры = Неопределено Тогда
		Если ДополнительныеПараметры.Свойство("Режим") Тогда
			ВыполнитьРегистрациюНаСервере(ДополнительныеПараметры.Режим, ЕстьОшибки, СообщениеОбОшибке);
			Если ЕстьОшибки Тогда
				ЗаголовокПредупреждения = Нстр("ru = 'Подключение кассового аппарата.'");
				ПоказатьПредупреждение(, СообщениеОбОшибке,, ЗаголовокПредупреждения);
			Иначе
				ОповеститьОбИзменении(Запись.КассаККМ);
				ПриЧтенииСозданииНаСервере();
			КонецЕсли;
		ИначеЕсли ДополнительныеПараметры.Свойство("РежимТовары") Тогда
			ВыполнитьВыгрузкуТоваровНаСервере(ЕстьОшибки, СообщениеОбОшибке);
			Если ЕстьОшибки Тогда
				ЗаголовокПредупреждения = Нстр("ru = 'Выгрузка товаров на кассовый аппарат.'");
				ПоказатьПредупреждение(, СообщениеОбОшибке,, ЗаголовокПредупреждения);
			Иначе
				ОповеститьОбИзменении(Запись.КассаККМ);
				ПриЧтенииСозданииНаСервере();
			КонецЕсли;
		ИначеЕсли ДополнительныеПараметры.Свойство("РежимТоварыОчистка") Тогда
			ВыполнитьОчисткуТоваровНаСервере(ЕстьОшибки, СообщениеОбОшибке);
			Если ЕстьОшибки Тогда
				ЗаголовокПредупреждения = Нстр("ru = 'Очистка товаров на кассовом аппарате.'");
				ПоказатьПредупреждение(, СообщениеОбОшибке,, ЗаголовокПредупреждения);
			Иначе
				ОповеститьОбИзменении(Запись.КассаККМ);
				ПриЧтенииСозданииНаСервере();
			КонецЕсли;
		ИначеЕсли ДополнительныеПараметры.Свойство("РежимДокументы") Тогда
			ВыполнитьЗагрузкуЧековНаСервере(ЕстьОшибки, СообщениеОбОшибке);
			Если ЕстьОшибки Тогда
				ЗаголовокПредупреждения = Нстр("ru = 'Загрузка чеков с кассового аппарата.'");
				ПоказатьПредупреждение(, СообщениеОбОшибке,, ЗаголовокПредупреждения);
			Иначе
				ОповеститьОбИзменении(Запись.КассаККМ);
				ПриЧтенииСозданииНаСервере();
			КонецЕсли;
		КонецЕсли;
	Иначе
		ПриЧтенииСозданииНаСервере();
	КонецЕсли;

КонецПроцедуры


&НаКлиенте
Процедура ПослеВопросаОЗаписиОбъекта(Знач Результат, Знач ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Успешно = Истина;
		Если Модифицированность Или НЕ ЗначениеЗаполнено(Запись.КассаККМ) Тогда
			Успешно = Записать();
		КонецЕсли;
		Если Успешно И ЗначениеЗаполнено(Запись.КассаККМ) Тогда
			ЗаголовокПредупреждения = Нстр("ru = 'Подключение кассового аппарата'");
			Если СостояниеРегистрации =
					ПредопределенноеЗначение("Перечисление.СостоянияРегистрацииКассовогоАппарата.Подключен") Тогда
				ОписаниеОповещения = Новый ОписаниеОповещения("ПослеВопросаСнятиеСРегистрации", ЭтотОбъект);
				
				ЗаголовокВопроса = Нстр("ru = 'Отключение кассового аппарата'");
				ТекстВопроса = Нстр("ru = 'После отключения данные по чекам не смогут быть загружены. Отключить?'");
				СписокКнопок = Новый СписокЗначений;
				СписокКнопок.Добавить(КодВозвратаДиалога.Да, Нстр("ru = 'Отключить'"));
				СписокКнопок.Добавить(КодВозвратаДиалога.Отмена, Нстр("ru = 'Отменить'"));
				ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, СписокКнопок,, КодВозвратаДиалога.Да, ЗаголовокВопроса);
			Иначе
				Результат = ПодключитьОтключитьКассуПоТарифу(Истина);
				Если Результат Тогда
					ДополнительныеПараметры = Новый Структура("Режим", 1);
					ТекстПредупреждения = НСтр("ru = 'Подключение может занять некоторое время.'");
				Иначе
					ТекстПредупреждения = НСтр("ru = 'Закончились свободные лицензии для регистрации касс.
					|Для регистрации кассового аппарата вам необходимо увеличить количество разрешенных касс по тарифу
					|в личном кабинете абонента.'");
				КонецЕсли;
				
				ОписаниеОповещения = Новый ОписаниеОповещения("ПослеПредупрежденияОРегистрации", ЭтотОбъект, ДополнительныеПараметры);
				ПоказатьПредупреждение(ОписаниеОповещения, ТекстПредупреждения,, ЗаголовокПредупреждения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПриЧтенииСозданииНаСервере()
	
	МенеджерЗаписи = РегистрыСведений.НастройкиКассыШтрихМ.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.КассаККМ = Запись.КассаККМ;
	МенеджерЗаписи.Прочитать();
	
	Если НЕ МенеджерЗаписи.Выбран() Тогда
		МенеджерЗаписи.КассаККМ = Запись.КассаККМ;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(МенеджерЗаписи,"Запись");
	
	
	Токен = Формат(Запись.Токен, "ЧГ=");
	
	ИнформацияОКассе = Новый Структура("РегистрационныйНомер", Запись.РегистрационныйНомер);
	
	ЗарегистрированнаяКасса = ОбменСКассовымСерверомШтрихМ.ЗарегистрированнаяКассаНаСервере(ИнформацияОКассе, Ложь, "");
	
	УстановитьСостояниеОбъекта();
	
	ПериодЗагрузки.ДатаНачала = Запись.ДатаНачалаЗапросаЧеков;
	ПериодЗагрузки.ДатаОкончания = Запись.ДатаОкончанияЗапросаЧеков;
	
	ОрганизацияККМ = ОбщегоНазначения.ЗначениеРеквизитаОбъекта( Запись.КассаККМ, "Владелец");
	// видимость прайслиста
	Если КоличествоПрайсЛистовОрганизации(ОрганизацияККМ) < 2 Тогда
		Элементы.ПрайсЛист.Видимость = Ложь;
		
		Если НЕ ЗначениеЗаполнено(Запись.ПрайсЛист) Тогда
			
			Запись.ПрайсЛист = ИнтеграцияОбменШтрихМ.ПолучитьПрайсЛистПоУмолчанию(ОрганизацияККМ);
			
		КонецЕсли;
		
	Иначе
		Элементы.ПрайсЛист.Видимость = Ложь;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьЗапись()
	
	Если НЕ Модифицированность Тогда
		Возврат;
	КонецЕсли;
	
	//Попытка
		НастройкиКассы = РеквизитФормыВЗначение("Запись");
		
		НастройкиКассы.Записать(Истина);
		
		ЗначениеВРеквизитФормы(НастройкиКассы, "Запись");
		
		Модифицированность = Ложь;
	//Исключение
	//	
	//КонецПопытки;
	
КонецПроцедуры

//
&НаСервере
Процедура ЗагрузитьЧекиНаСервере()
	
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	МенеджерЗаписи.ЗапроситьЧекиНаСервереШтрихМ = Истина;
	МенеджерЗаписи.ДатаНачалаЗапросаЧеков = ПериодЗагрузки.ДатаНачала;
	МенеджерЗаписи.ДатаОкончанияЗапросаЧеков = ПериодЗагрузки.ДатаОкончания;
	МенеджерЗаписи.Записать();
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
	УстановитьСостояниеОбменаЧеки();
	
КонецПроцедуры
//
&НаСервере
Процедура ВыгрузитьТоварыНаСервере()
	
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	МенеджерЗаписи.ЗарегистрироватьТоварыНаСервереШтрихМ = Истина;
	МенеджерЗаписи.Записать();
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
	УстановитьСостояниеОбменаТовары();
	
КонецПроцедуры
//
&НаСервере
Процедура ВыполнитьРегистрациюНаСервере(Режим, ЕстьОшибки, СообщениеОбОшибке)
	
	ОбменСКассовымСерверомШтрихМПереопределяемый.ВыполнитьРегистрациюНаСервере(Запись.КассаККМ, Режим,
		ЕстьОшибки, СообщениеОбОшибке);
	
КонецПроцедуры
//
&НаСервере
Процедура ОчиститьТоварыНаСервере()
	
	МенеджерЗаписи = РеквизитФормыВЗначение("Запись");
	МенеджерЗаписи.ОчиститьТоварыНаСервереШтрихМ = Истина;
	МенеджерЗаписи.Записать();
	ЗначениеВРеквизитФормы(МенеджерЗаписи, "Запись");
	
	УстановитьСостояниеОбменаТовары();
КонецПроцедуры

&НаСервере
Процедура УстановитьСостояниеОбменаТовары()
	
	СостояниеПодключен = СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.Подключен;
	
	Ожидание = Ложь;
	Если Запись.ЗарегистрироватьТоварыНаСервереШтрихМ Тогда
		Элементы.ВыгрузитьТовары.Доступность = Ложь;
		Элементы.ВыгрузитьТовары.Заголовок = НСтр("ru = 'Идёт выгрузка товаров и цен'");
		Элементы.ОчиститьТовары.Доступность = Ложь;
		Элементы.ОчиститьТовары.Заголовок = НСтр("ru = 'Удалить товары и цены'");
		
		Ожидание = Истина;
		//Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"),
		//	Запись.КассаККМ.Наименование, Элементы.ВыгрузитьТовары.Заголовок);
	ИначеЕсли Запись.ПроверитьРегистрациюТоваровНаСервереШтрихМ Тогда
		Элементы.ВыгрузитьТовары.Доступность = Ложь;
		Элементы.ВыгрузитьТовары.Заголовок = НСтр("ru = 'Проверка загрузки цен'");
		Элементы.ОчиститьТовары.Доступность = Ложь;
		Элементы.ОчиститьТовары.Заголовок = НСтр("ru = 'Удалить товары и цены'");
		
		Ожидание = Истина;
		//Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"),
		//	Запись.КассаККМ.Наименование, Элементы.ВыгрузитьТовары.Заголовок);
	ИначеЕсли Запись.ОчиститьТоварыНаСервереШтрихМ Тогда
		Элементы.ВыгрузитьТовары.Доступность = Ложь;
		Элементы.ВыгрузитьТовары.Заголовок = НСтр("ru = 'Выгрузить товары и цены'");
		Элементы.ОчиститьТовары.Доступность = Ложь;
		Элементы.ОчиститьТовары.Заголовок = НСтр("ru = 'Идёт удаление товаров и цен'");
		
		Ожидание = Истина;
		//Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"),
		//	Запись.КассаККМ.Наименование, Элементы.ОчиститьТовары.Заголовок);
	Иначе
		Элементы.ВыгрузитьТовары.Заголовок = НСтр("ru = 'Выгрузить товары и цены'");
		Элементы.ОчиститьТовары.Заголовок = НСтр("ru = 'Удалить товары и цены'");
		Элементы.ВыгрузитьТовары.Доступность = СостояниеПодключен;
		Элементы.ОчиститьТовары.Доступность = СостояниеПодключен;
	КонецЕсли;
	
	Элементы.Подключить.Доступность = Элементы.Подключить.Доступность И Не Ожидание;
КонецПроцедуры
//
&НаСервере
Процедура УстановитьСостояниеОбменаЧеки()
	
	СостояниеПодключен = СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.Подключен;
	
	Ожидание = Ложь;
	Если Запись.ЗапроситьЧекиНаСервереШтрихМ Тогда
		Элементы.ЗагрузитьЧеки.Доступность = Ложь;
		Элементы.ЗагрузитьЧеки.Заголовок = НСтр("ru = 'Идёт загрузка чеков'");
		Ожидание = Истина;
		//Заголовок = СтрШаблон(НСтр("ru = '%1 (%2)'"),
		//	Запись.КассаККМ.Наименование, Элементы.ЗагрузитьЧеки.Заголовок);
	Иначе
		Элементы.ЗагрузитьЧеки.Заголовок = НСтр("ru = 'Загрузить чеки'");
	КонецЕсли;
	
	Элементы.Подключить.Доступность = Элементы.Подключить.Доступность И Не Ожидание;
КонецПроцедуры

&НаСервере
Процедура ВыполнитьВыгрузкуТоваровНаСервере(ЕстьОшибки, СообщениеОбОшибке)
	
	ОбменСКассовымСерверомШтрихМПереопределяемый.ВыполнитьВыгрузкуТоваровНаСервере(Запись.КассаККМ,
		ЕстьОшибки, СообщениеОбОшибке);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьОчисткуТоваровНаСервере(ЕстьОшибки, СообщениеОбОшибке)
	
	ИнформацияОКассе = Новый ТаблицаЗначений;
	ИнформацияОКассе.Колонки.Добавить("РегистрационныйНомер");
	ИнформацияОКассе.Колонки.Добавить("Результат");
	ИнформацияОКассе.Колонки.Добавить("СообщениеОбмена");
	СтрокаИнформации = ИнформацияОКассе.Добавить();
	СтрокаИнформации.РегистрационныйНомер = Запись.РегистрационныйНомер;
	ОбменСКассовымСерверомШтрихМ.ВыполнитьОчисткуТоваровНаКассовыхАппаратах(ИнформацияОКассе,
		ЕстьОшибки, СообщениеОбОшибке);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьЗагрузкуЧековНаСервере(ЕстьОшибки, СообщениеОбОшибке)
	
	ПараметрыМетода = Новый Структура();
	ИнформацияОКассе = Новый ТаблицаЗначений;
	ИнформацияОКассе.Колонки.Добавить("РегистрационныйНомер");
	ИнформацияОКассе.Колонки.Добавить("ДатаНачалаЗапросаЧеков");
	ИнформацияОКассе.Колонки.Добавить("ДатаОкончанияЗапросаЧеков");
	ИнформацияОКассе.Колонки.Добавить("Результат");
	ИнформацияОКассе.Колонки.Добавить("СообщениеОбмена");
	СтрокаИнформации = ИнформацияОКассе.Добавить();
	СтрокаИнформации.РегистрационныйНомер = Запись.РегистрационныйНомер;
	СтрокаИнформации.ДатаНачалаЗапросаЧеков = Запись.ДатаНачалаЗапросаЧеков;
	СтрокаИнформации.ДатаОкончанияЗапросаЧеков = Запись.ДатаОкончанияЗапросаЧеков;
	
	ПараметрыМетода.Вставить("ИнформацияОКассе", ИнформацияОКассе);
	ПараметрыМетода.Вставить("ЕстьОшибки", ЕстьОшибки);
	ПараметрыМетода.Вставить("СообщениеОбОшибке", СообщениеОбОшибке);
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияВФоне(УникальныйИдентификатор);
	ПараметрыВыполнения.ОжидатьЗавершение = 0;
	ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Фоновое получение кассовых чеков'");
	
	ДлительныеОперации.ВыполнитьВФоне(
		"ОбменСКассовымСерверомШтрихМПереопределяемый.ПолучитьКассовыеДокументыДляКассовыхАппаратов", ПараметрыМетода,
		ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Функция ПодключитьОтключитьКассуПоТарифу(Подключить)
	
	// Тариф проверям только во Фреше.
	Если НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат Истина;
	КонецЕсли;
	
	Если Подключить Тогда
		Результат = ТарификацияПереопределяемый.ПодключениеКассыПоТарифу(Запись.РегистрационныйНомер);
		Если Результат Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Занята лицензия %1 на сервере лицензий'"), Запись.РегистрационныйНомер);
			ОбменСКассовымСерверомШтрихМ.ЗаписатьВПротоколКассовогоАппарата(Запись.РегистрационныйНомер, ТекстСообщения);
			Запись.ИдентификаторТарификации = Запись.РегистрационныйНомер;
			Модифицированность = Истина;
			ЗаписатьЗапись();
		КонецЕсли;
		
	Иначе
		Результат = ТарификацияПереопределяемый.ОтменаРегистрацииКассыНаСервереТарификации(Запись.ИдентификаторТарификации);
		Если Результат Тогда
			ТекстСообщения = СтрШаблон(НСтр("ru = 'Освобождена лицензия %1 на сервере лицензий'"), Запись.ИдентификаторТарификации);
			ОбменСКассовымСерверомШтрихМ.ЗаписатьВПротоколКассовогоАппарата(Запись.РегистрационныйНомер, ТекстСообщения);
			Запись.ИдентификаторТарификации = "";
			Модифицированность = Истина;
			ЗаписатьЗапись();
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
КонецФункции

&НаСервере
Процедура УстановитьСостояниеОбъекта()
	
	СостояниеРегистрации = ОбменСКассовымСерверомШтрихМПереопределяемый.СостояниеРегистрацииККМ(Запись.КассаККМ);
	
	Элементы.РегистрационныйНомер.ТолькоПросмотр = Истина;
	Элементы.Токен.ТолькоПросмотр = Истина;
	Подключен = Ложь;
	
	Если СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.НеПодключен Тогда
		Элементы.Подключить.Заголовок = Нстр("ru = 'Подключить'");
		Элементы.Подключить.Доступность = Истина;
		Элементы.РегистрационныйНомер.ТолькоПросмотр = Ложь;
		Элементы.Токен.ТолькоПросмотр = Ложь;
	ИначеЕсли СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.Подключен Тогда
		Элементы.Подключить.Заголовок = Нстр("ru = 'Отключить'");
		Элементы.Подключить.Доступность = Истина;
		Элементы.Токен.ТолькоПросмотр = Ложь;
		Подключен = ЗначениеЗаполнено(Запись.КассаККМ);
	ИначеЕсли СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.ОжидаетПодключение Тогда
		Элементы.Подключить.Заголовок = Нстр("ru = 'Идёт подключение'");
		Элементы.Подключить.Доступность = Ложь;
	ИначеЕсли СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.ОжидаетПереподключение Тогда
		Элементы.Подключить.Заголовок = Нстр("ru = 'Идёт переподключение'");
		Элементы.Подключить.Доступность = Ложь;
	ИначеЕсли СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.ОжидаетОтключение Тогда
		Элементы.Подключить.Заголовок = Нстр("ru = 'Идёт отключение'");
		Элементы.Подключить.Доступность = Ложь;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Запись.КассаККМ) Тогда
		Элементы.Подключить.Доступность = Ложь;
	КонецЕсли;
	
	Элементы.ЗагрузитьЧеки.Доступность = Подключен;
	
	Заголовок = СтрШаблон(
		НСтр("ru = '%1 (%2)'"),
		ОбщегоНазначения.ЗначениеРеквизитаОбъекта( Запись.КассаККМ,"Наименование"),
		СостояниеРегистрации);
	
	Если Подключен Тогда
		УстановитьСостояниеОбменаТовары();
		УстановитьСостояниеОбменаЧеки();
	КонецЕсли;
	
	Если СостояниеРегистрации = Перечисления.СостоянияРегистрацииКассовогоАппарата.НеПодключен И ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		ИнформацияПоТарифу = Тарификация.КоличествоЛицензийУникальнойУслуги("1С_Касса", "КоличествоКасс");
		Если ИнформацияПоТарифу.СвободноЛицензий = 0 Тогда
			Элементы.СтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаНетЛицензий;
			Элементы.ДекорацияНетЛицензий.Заголовок = 
				НСтр("ru = 'Нет возможности подключения к сервису. Обратитесь к обслуживающему вас партнеру комании 1С.'");
		Иначе
			Элементы.СтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаПодключения;
		КонецЕсли;
	Иначе
		Элементы.СтраницыПодключения.ТекущаяСтраница = Элементы.СтраницаПодключения;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЗарегистрированнаяКасса) Тогда
		ПротоколОбмена = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ЗарегистрированнаяКасса, "СообщениеПриРегистрации");
	Иначе
		ПротоколОбмена = Запись.СообщениеПриРегистрации;
	КонецЕсли;
КонецПроцедуры

&НаСервере
Функция КоличествоПрайсЛистовОрганизации(Организация) 
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ ПрайсЛисты.Ссылка) КАК КоличествоПрайсов
	|ИЗ
	|	Справочник.ПрайсЛисты КАК ПрайсЛисты
	|ГДЕ
	|	НЕ ПрайсЛисты.ПометкаУдаления
	|	И ПрайсЛисты.Организация = &Организация");
	
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.КоличествоПрайсов;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции


#КонецОбласти
