#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)

	ЕстьОшибки = Ложь;

	МассивДопустимыхТиповОС= РегистрыСведений.СвойстваЛокальныхКомпьютеровДляОблачногоАрхива.ПолучитьЗначенияДопустимыхТиповОС();

	Для каждого ТекущаяЗапись Из ЭтотОбъект Цикл
		ТекущаяЗапись.ТипОС = СтрЗаменить(ТекущаяЗапись.ТипОС, " ", "_"); // Стандартные функции возвращают, например, "Windows x86" вместо "Windows_x86".
		Если МассивДопустимыхТиповОС.Найти(ТекущаяЗапись.ТипОС) = Неопределено Тогда
			ЕстьОшибки = Истина;
			ТекстСообщения = СтрШаблон(
				НСтр("ru='Недопустимое значение типа ОС: [%1]'"),
				ТекущаяЗапись.ТипОС);
			Сообщение = Новый СообщениеПользователю();
			Сообщение.Текст = ТекстСообщения;
			Сообщение.Поле  = "ТипОС";
			Сообщение.УстановитьДанные(ЭтотОбъект);
			Сообщение.Сообщить();
		КонецЕсли;
	КонецЦикла;

	Отказ = ЕстьОшибки;

КонецПроцедуры

Процедура ПередЗаписью(Отказ, Замещение)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Для каждого ТекущаяЗапись Из ЭтотОбъект Цикл
		ТекущаяЗапись.MachineGuid = НРег(ТекущаяЗапись.MachineGuid);
		ТекущаяЗапись.ВерсияАгентаКопирования = ИнтернетПоддержкаПользователейКлиентСервер.ВнутреннееПредставлениеНомераВерсии(ТекущаяЗапись.ВерсияАгентаКопирования);
	КонецЦикла;

КонецПроцедуры

#КонецОбласти

#КонецЕсли