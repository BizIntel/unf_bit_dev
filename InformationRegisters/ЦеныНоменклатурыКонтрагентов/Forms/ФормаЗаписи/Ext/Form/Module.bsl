
&НаСервереБезКонтекста
// Получает набор данных с сервера для процедуры НоменклатураПриИзменении.
//
Функция ПолучитьДанныеНоменклатураПриИзменении(Номенклатура)
	
	Возврат Номенклатура.ЕдиницаИзмерения;
	
КонецФункции // ПолучитьДанныеНоменклатураПриИзменении()	

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаКлиенте
// Процедура - обработчик события ПередЗакрытием формы.
//
Процедура ПередЗакрытием(Отказ, СтандартнаяОбработка)
	Если ЗаписьБылЗаписана Тогда
		Оповестить("ИзмененаЦенаКонтрагента", ЗаписьБылЗаписана);
	КонецЕсли;
КонецПроцедуры

&НаСервере
// Процедура - обработчик события ПередЗаписью формы.
//
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если Модифицированность Тогда
		ТекущийОбъект.Автор = Пользователи.ТекущийПользователь();
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПослеЗаписи формы.
//
Процедура ПослеЗаписи(ПараметрыЗаписи)
	ЗаписьБылЗаписана = Истина;
КонецПроцедуры

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаписьБылЗаписана = Ложь;
	
	Если ЗначениеЗаполнено(Запись.ВидЦенКонтрагента) Тогда
		Контрагент = Запись.ВидЦенКонтрагента.Владелец;	
	КонецЕсли; 
	
	Если НЕ ЗначениеЗаполнено(Запись.ИсходныйКлючЗаписи.ВидЦенКонтрагента) Тогда
		
		Запись.Автор = Пользователи.ТекущийПользователь();
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Контрагент") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Контрагент) Тогда
			Контрагент = Параметры.ЗначенияЗаполнения.Контрагент;
		КонецЕсли;
		
		Если Параметры.Свойство("Контрагент") И ЗначениеЗаполнено(Параметры.Контрагент) Тогда
			Контрагент = Параметры.Контрагент;	
		КонецЕсли;
		
		Если Параметры.ЗначенияЗаполнения.Свойство("Номенклатура") И ЗначениеЗаполнено(Параметры.ЗначенияЗаполнения.Номенклатура) Тогда
			Запись.ЕдиницаИзмерения = Параметры.ЗначенияЗаполнения.Номенклатура.ЕдиницаИзмерения;	
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события ПриИзменении поля ввода Номенклатура.
//
Процедура НоменклатураПриИзменении(Элемент)
	
	Запись.ЕдиницаИзмерения = ПолучитьДанныеНоменклатураПриИзменении(Запись.Номенклатура);
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработчик события НачалоВыбора поля ввода ВидЦен.
//
Процедура ВидЦенНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ ЗначениеЗаполнено(Контрагент) Тогда
		
		СтандартнаяОбработка = Ложь;
		ТекстСообщения = НСтр("ru = 'Укажите контрагента для выбора видов цен.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , , "Контрагент");
		
	КонецЕсли;
	
КонецПроцедуры // ВидЦенНачалоВыбора()


