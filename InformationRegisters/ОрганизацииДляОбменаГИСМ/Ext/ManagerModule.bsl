
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ИспользоватьПодразделения = ИнтеграцияГИСМ.ИспользоватьПодразделения();
	Если ИспользоватьПодразделения Тогда
	
		ТипПодразделение = Метаданные.ОпределяемыеТипы.Подразделение.Тип.Типы()[0];
		ПолноеИмяПодразделение = Метаданные.НайтиПоТипу(ТипПодразделение).ПолноеИмя();
		МенеджерОбъектаПодразделение = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ПолноеИмяПодразделение);
	
	КонецЕсли;
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 100
	|	Организации.Ссылка КАК Ссылка,
	|	Организации.УдалитьGLN КАК GLN
	|ИЗ
	|	ТаблицаОрганизации КАК Организации
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОрганизацииДляОбменаГИСМ КАК ОрганизацииДляОбменаГИСМ
	|		ПО ОрганизацииДляОбменаГИСМ.Организация = Организации.Ссылка
	|ГДЕ
	|	Организации.УдалитьGLN <> """"
	|	И ОрганизацииДляОбменаГИСМ.GLN ЕСТЬ NULL
	|");
	
	ТипОрганизация = Метаданные.ОпределяемыеТипы.Организация.Тип.Типы()[0];
	ПредставлениеТипа = Метаданные.НайтиПоТипу(ТипОрганизация).ПолноеИмя();
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "ТаблицаОрганизации", ПредставлениеТипа);
	
	ЕстьОшибки = Ложь;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПредставлениеТипа);
			ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Разделяемый;
			Блокировка.Заблокировать();
			
			Набор = РегистрыСведений.ОрганизацииДляОбменаГИСМ.СоздатьНаборЗаписей();
			Набор.Отбор.Организация.Установить(Выборка.Ссылка);
			Если ИспользоватьПодразделения Тогда
				Набор.Отбор.Подразделение.Установить(МенеджерОбъектаПодразделение.ПустаяСсылка());
			КонецЕсли;
			
			НоваяЗапись = Набор.Добавить();
			НоваяЗапись.Организация = Выборка.Ссылка;
			НоваяЗапись.GLN = Выборка.GLN;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ЕстьОшибки = Истина;
			
			ТекстСообщения = НСтр("ru = 'Не удалось обработать объект: %Объект% по причине: %Причина%'");
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Объект%", Выборка.Ссылка);
			ТекстСообщения = СтрЗаменить(ТекстСообщения, "%Причина%", ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
									УровеньЖурналаРегистрации.Предупреждение,
									Выборка.Ссылка.Метаданные(),
									Выборка.Ссылка,
									ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = Выборка.Количество() < 100 И Не ЕстьОшибки;
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли