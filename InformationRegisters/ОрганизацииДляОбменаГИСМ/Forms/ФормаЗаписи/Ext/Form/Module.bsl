#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Элементы.Подразделение.Видимость = ИнтеграцияГИСМ.ИспользоватьПодразделения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПолучитьGLNПоИННКППГИСМ(Команда)
	
	ОчиститьСообщения();
	
	РеквизитыОрганизации = ИнтеграцияГИСМВызовСервера.ИННКППGLNОрганизации(Запись.Организация, Запись.Подразделение);
	
	Если Не ЗначениеЗаполнено(РеквизитыОрганизации.ИНН) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Не заполнено ИНН организации'"), Запись.Организация, "Запись.Организация");
		Возврат;
	КонецЕсли;
	
	СообщениеЗапросаGLN = ИнтеграцияГИСМВызовСервера.СообщениеЗапросаGLN(Запись.Организация, РеквизитыОрганизации.ИНН, РеквизитыОрганизации.КПП);
	Если ЗначениеЗаполнено(СообщениеЗапросаGLN.ТекстОшибки) Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(СообщениеЗапросаGLN.ТекстОшибки);
		Возврат;
	КонецЕсли;
	
	Сообщения = Новый Массив;
	Сообщения.Добавить(СообщениеЗапросаGLN);
	СообщенияПоОрганизациям = ИнтеграцияГИСМКлиент.СообщенияПоОрганизациям(Сообщения);
	
	Данные = ИнтеграцияГИСМКлиент.СообщенияСледующейОрганизацииКПодписанию(СообщенияПоОрганизациям);
	Если Данные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Контекст = Новый Структура;
	ИнтеграцияГИСМКлиент.Подписать(
		Данные.Сообщения,
		Данные.Организация,
		Новый ОписаниеОповещения("ПолучитьGLNПоИННКППГИСМ_ПриЗавершенииОперацииПодписи", ЭтотОбъект, Контекст));
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПолучитьGLNПоИННКППГИСМ_ПриЗавершенииОперацииПодписи(Сообщения, ДополнительныеПараметры) Экспорт
	
	Если Сообщения = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если Сообщения.Количество() <> 1 Тогда
		Возврат;
	КонецЕсли;
	
	Результат = ИнтеграцияГИСМВызовСервера.ОбработатьПодписанноеСообщениеЗапросаGLN(Сообщения[0]);
	
	Если ЗначениеЗаполнено(Результат.GLN) Тогда
		Запись.GLN = Результат.GLN;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти




