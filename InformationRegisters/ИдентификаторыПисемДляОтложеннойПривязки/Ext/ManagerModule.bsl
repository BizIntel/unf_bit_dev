#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Записывает идентификатор, по которому не удалось найти Событие в момент загрузки письма.
//
// Параметры:
//  УчетнаяЗапись			 - СправочникСсылка.УчетныеЗаписиЭлектроннойПочты - учетная запись,
//  ИдентификаторСообщения	 - Строка - идентификатор сообщения (Message-ID), которое должно быть привязано к Событию в момент загрузки,
//  ЭтоОснование			 - Булево - признак того, что загруженное сообщение должно быть в реквизите ДокументОснование,
//  Событие					 - ДокументСсылка.Событие - событие, к которому будет привязано письмо с указанным идентификатором.
//
Процедура ЗаписатьИдентификаторСообщения(Знач УчетнаяЗапись, Знач ИдентификаторСообщения, Знач ЭтоОснование, Знач Событие) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.УчетнаяЗапись = УчетнаяЗапись;
	МенеджерЗаписи.Ключ = ОбменСGoogle.КлючИзИдентификатора(ИдентификаторСообщения,
	"РС.ИдентификаторыПисемДляОтложеннойПривязки.Записать()");
	МенеджерЗаписи.ЭтоОснование = ЭтоОснование;
	МенеджерЗаписи.Событие = Событие;
	МенеджерЗаписи.ИдентификаторСообщения = ИдентификаторСообщения;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры

Функция СвязанныеСобытия(Знач УчетнаяЗапись, Знач ИдентификаторСообщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ИдентификаторыПисемДляОтложеннойПривязки.Событие,
	|	ИдентификаторыПисемДляОтложеннойПривязки.ЭтоОснование
	|ИЗ
	|	РегистрСведений.ИдентификаторыПисемДляОтложеннойПривязки КАК ИдентификаторыПисемДляОтложеннойПривязки
	|ГДЕ
	|	ИдентификаторыПисемДляОтложеннойПривязки.УчетнаяЗапись = &УчетнаяЗапись
	|	И ИдентификаторыПисемДляОтложеннойПривязки.Ключ = &Ключ");
	Запрос.УстановитьПараметр("УчетнаяЗапись", УчетнаяЗапись);
	Запрос.УстановитьПараметр("Ключ", ОбменСGoogle.КлючИзИдентификатора(ИдентификаторСообщения,
	"РС.ИдентификаторыПисемДляОтложеннойПривязки.СвязанныеСобытия()"));
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция УдалитьИдентификаторСообщения(Знач УчетнаяЗапись, Знач ИдентификаторСОобщения) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	НаборЗаписей = СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.УчетнаяЗапись.Установить(УчетнаяЗапись);
	НаборЗаписей.Отбор.Ключ.Установить(ОбменСGoogle.КлючИзИдентификатора(ИдентификаторСообщения,
	"РС.ИдентификаторыПисемДляОтложеннойПривязки.Удалить()"));
	НаборЗаписей.Записать();
	
КонецФункции

#КонецОбласти

#КонецЕсли