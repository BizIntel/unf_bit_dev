
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Параметры.НастройкаОбмена) Тогда
		ТекущаяЗапись = РегистрыСведений.ПараметрыОбменСБанками.СоздатьМенеджерЗаписи();
		ТекущаяЗапись.НастройкаОбмена = Параметры.НастройкаОбмена;
		ТекущаяЗапись.Прочитать();
		
		// Для новой настройки записей в регистре еще нет
		Если Не ТекущаяЗапись.Выбран() Тогда
			ТекущаяЗапись.НастройкаОбмена = Параметры.НастройкаОбмена;
		КонецЕсли;
	
		ЗначениеВРеквизитФормы(ТекущаяЗапись, "Запись");
		
		ПрограммаБанка = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Параметры.НастройкаОбмена, "ПрограммаБанка");
		
	КонецЕсли;
	
	Если Не ПрограммаБанка = Перечисления.ПрограммыБанка.СбербанкОнлайн
		ИЛИ НЕ Пользователи.ЭтоПолноправныйПользователь() Тогда
		Элементы.Метка.Видимость = Ложь;
	КонецЕсли;
	
	Элементы.Журналирование.Видимость = ПрограммаБанка =  Перечисления.ПрограммыБанка.СбербанкОнлайн
		ИЛИ ПрограммаБанка =  Перечисления.ПрограммыБанка.ОбменЧерезВК;
		
	Элементы.КаталогДляЖурналирования.Видимость = ПрограммаБанка =  Перечисления.ПрограммыБанка.ОбменЧерезВК
		И ТекущаяЗапись.ИспользоватьЖурналирование;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	#Если ВебКлиент Тогда
		Элементы.КаталогДляЖурналирования.КнопкаВыбора = Ложь;
	#КонецЕсли

КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	Если ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК")
		И Запись.ИспользоватьЖурналирование И Не ЗначениеЗаполнено(Запись.КаталогДляЖурналирования) Тогда
		ТекстСообщения = НСтр("ru = 'Укажите каталог для журналирования.'");
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, , "Запись.КаталогДляЖурналирования", , Отказ);
		Возврат;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Оповестить("ИзмененаНастройкаОбменСБанками", Запись.НастройкаОбмена);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ИспользоватьЖурналированиеПриИзменении(Элемент)
	
	Элементы.КаталогДляЖурналирования.Видимость = Запись.ИспользоватьЖурналирование
		И ПрограммаБанка = ПредопределенноеЗначение("Перечисление.ПрограммыБанка.ОбменЧерезВК");
	
КонецПроцедуры

&НаКлиенте
Процедура КаталогДляЖурналированияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	Режим = РежимДиалогаВыбораФайла.ВыборКаталога;
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(Режим);
	ДиалогОткрытияФайла.Заголовок = НСтр("ru = 'Выберите каталог для сохранения файлов журнала.'");
	Оповещение = Новый ОписаниеОповещения("ПослеВыбораКаталога", ЭтотОбъект);
	ДиалогОткрытияФайла.Показать(Оповещение);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораКаталога(ВыбранныеФайлы, ДополнительныеПараметры) Экспорт
	
	Если ВыбранныеФайлы = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Запись.КаталогДляЖурналирования = ВыбранныеФайлы.Получить(0);
	
КонецПроцедуры

#КонецОбласти
