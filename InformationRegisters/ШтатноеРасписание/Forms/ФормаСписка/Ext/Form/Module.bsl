////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБЩЕГО НАЗНАЧЕНИЯ

&НаКлиенте
// В процедуре устанавливаются отборы на табличную часть штатного расписания
//
Процедура УстановитьОтбор()
	
	
	УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(Список,"Организация",Организация);

	Если НЕ УчетПоНесколькимПодразделениям Тогда
		
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(Список,"СтруктурнаяЕдиница",ОсновноеПодразделение);
		
	ИначеЕсли Элементы.Подразделения.ТекущиеДанные <> Неопределено Тогда
		
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(Список,"СтруктурнаяЕдиница",Элементы.Подразделения.ТекущиеДанные.Ссылка);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
// Процедура - обработчик события ПриСозданииНаСервере формы.
//
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	Если Константы.УчетПоКомпании.Получить() Тогда
		
		Организация = Константы.Компания.Получить();
		Элементы.Организация.Видимость = Ложь;
		
	ИначеЕсли Параметры.Отбор.Свойство("Организация") Тогда
		
		Организация = Параметры.Отбор.Организация;
		Элементы.Организация.Доступность = Ложь;
		
	Иначе
		
		ЗначениеНастройки = УправлениеНебольшойФирмойПовтИсп.ПолучитьЗначениеПоУмолчаниюПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяОрганизация");
		
		Если ЗначениеЗаполнено(ЗначениеНастройки) Тогда
			
			Организация = ЗначениеНастройки;
			
		Иначе
			
			Организация = Справочники.Организации.ОсновнаяОрганизация;
			
		КонецЕсли;
		
	КонецЕсли;
	
	УчетПоНесколькимПодразделениям = Константы.ФункциональнаяОпцияУчетПоНесколькимПодразделениям.Получить();
	ОсновноеПодразделение = Справочники.СтруктурныеЕдиницы.ОсновноеПодразделение;
	Если НЕ УчетПоНесколькимПодразделениям Тогда
		
		Элементы.Подразделения.Видимость = Ложь;
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(Список,"Организация",Организация);
		УправлениеНебольшойФирмойКлиентСервер.УстановитьЭлементОтбораСписка(Список,"СтруктурнаяЕдиница",ОсновноеПодразделение);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

&НаКлиенте
// Процедура - обработка события ПриИзменении реквизита Организация
//
Процедура ОрганизацияПриИзменении(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры

&НаКлиенте
// Процедура - обработка события ПриАктивизацииСтроки таблицы Подразделения
//
Процедура ПодразделенияПриАктивизацииСтроки(Элемент)
	
	УстановитьОтбор();
	
КонецПроцедуры






