
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

#Область ОбновлениеИнформационнойБазы

Процедура ОбработатьДанныеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	ПолноеИмяРегистра = "РегистрСведений.ОчередьПолученияКвитанцийОФиксацииГИСМ";
	
	ТекстЗапроса = "
		|ВЫБРАТЬ
		|	СсылкиДляОбработки.Сообщение                  КАК Сообщение,
		|	ОчередьПолученияКвитанцийОФиксацииГИСМ.Данные КАК Данные,
		|	ЕСТЬNULL(ОчередьПолученияКвитанцийОФиксацииГИСМ.Сообщение.ВладелецФайла.Организация.УдалитьGLN, """") КАК GLN
		|ИЗ
		|	&ВТДляОбработкиСсылка КАК СсылкиДляОбработки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОчередьПолученияКвитанцийОФиксацииГИСМ КАК ОчередьПолученияКвитанцийОФиксацииГИСМ
		|		ПО ОчередьПолученияКвитанцийОФиксацииГИСМ.Сообщение = СсылкиДляОбработки.Сообщение
		|";
	
	МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Результат = ОбновлениеИнформационнойБазы.СоздатьВременнуюТаблицуИзмеренийНезависимогоРегистраСведенийДляОбработки(
		Параметры.Очередь,
		ПолноеИмяРегистра,
		МенеджерВременныхТаблиц);
	
	Если НЕ Результат.ЕстьДанныеДляОбработки Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	Если НЕ Результат.ЕстьЗаписиВоВременнойТаблице Тогда
		Параметры.ОбработкаЗавершена = Ложь;
		Возврат;
	КонецЕсли;
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&ВТДляОбработкиСсылка", Результат.ИмяВременнойТаблицы);
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = МенеджерВременныхТаблиц;
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		
		Попытка
			
			НачатьТранзакцию();
			
			Блокировка = Новый БлокировкаДанных;
			ЭлементБлокировки = Блокировка.Добавить(ПолноеИмяРегистра);
			ЭлементБлокировки.УстановитьЗначение("Сообщение", Выборка.Сообщение);
			ЭлементБлокировки.Режим = РежимБлокировкиДанных.Исключительный;
			Блокировка.Заблокировать();
		
			Набор = РегистрыСведений.ОчередьПолученияКвитанцийОФиксацииГИСМ.СоздатьНаборЗаписей();
			Набор.Отбор.Сообщение.Установить(Выборка.Сообщение);
			
			НоваяЗапись = Набор.Добавить();
			НоваяЗапись.Данные = Выборка.Данные;
			
			Данные = Выборка.Данные.Получить();
			Если Данные <> Неопределено Тогда
				Если ЗначениеЗаполнено(Данные.GLN) Тогда
					НоваяЗапись.GLN = Данные.GLN;
				Иначе
					НоваяЗапись.GLN = Выборка.GLN;
				КонецЕсли;
			КонецЕсли;
			
			ОбновлениеИнформационнойБазы.ЗаписатьДанные(Набор);
			
			ЗафиксироватьТранзакцию();
			
		Исключение
			
			ОтменитьТранзакцию();
			
			ТекстСообщения = СтрШаблон(
				НСтр("ru = 'Не удалось обработать сообщение: %1 из очереди получения квитанций по причине: %2'"),
				Выборка.Сообщение,
				ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			
			ЗаписьЖурналаРегистрации(
				ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(),
				УровеньЖурналаРегистрации.Предупреждение,
				Метаданные.Справочники.ГИСМПрисоединенныеФайлы,
				Выборка.Сообщение,
				ТекстСообщения);
			
		КонецПопытки;
		
	КонецЦикла;
	
	Параметры.ОбработкаЗавершена = ОбновлениеИнформационнойБазы.ОбработкаДанныхЗавершена(
		Параметры.Очередь,
		"РегистрСведений.ОчередьПолученияКвитанцийОФиксацииГИСМ");
	
КонецПроцедуры

Процедура ЗарегистрироватьДанныеКОбработкеДляПереходаНаНовуюВерсию(Параметры) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ОчередьПолученияКвитанцийОФиксацииГИСМ.Сообщение КАК Сообщение
	|ИЗ
	|	РегистрСведений.ОчередьПолученияКвитанцийОФиксацииГИСМ КАК ОчередьПолученияКвитанцийОФиксацииГИСМ
	|ГДЕ
	|	ОчередьПолученияКвитанцийОФиксацииГИСМ.GLN = """"
	|");
	
	РезультатЗапроса = Запрос.Выполнить();
	ТаблицаДанных = РезультатЗапроса.Выгрузить();
	
	ДополнительныеПараметры = ОбновлениеИнформационнойБазы.ДополнительныеПараметрыОтметкиОбработки();
	ДополнительныеПараметры.ЭтоНезависимыйРегистрСведений = Истина;
	ДополнительныеПараметры.ПолноеИмяРегистра = "РегистрСведений.ОчередьПолученияКвитанцийОФиксацииГИСМ";
	ОбновлениеИнформационнойБазы.ОтметитьКОбработке(Параметры, ТаблицаДанных, ДополнительныеПараметры);
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#КонецЕсли