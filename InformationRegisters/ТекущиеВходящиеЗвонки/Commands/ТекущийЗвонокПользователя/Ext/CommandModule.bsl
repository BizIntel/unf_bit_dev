
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ДанныеЗвонка = ПолучитьДанныеЗвонка();
	
	Если Не ЗначениеЗаполнено(ДанныеЗвонка.ПользовательКому) Тогда
		
		ПоказатьОповещениеПользователя(
			НСтр("ru='Входящий звонок'"),
			,
			НСтр("ru='Текущие вызовы отсутствуют'"),
			БиблиотекаКартинок.Информация32
		);
		Возврат;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗвонка.АбонентОтКого) Тогда
		
		ШаблонПояснения = НСтр("ru='от %1,
			|с номера: %2,
			|в %3'");
		
		ПредставлениеДаты = ?(НачалоДня(ДанныеЗвонка.ДатаЗвонка) = НачалоДня(ТекущаяДата()),
								Формат(ДанныеЗвонка.ДатаЗвонка, "ДФ=HH.mm"),
								Формат(ДанныеЗвонка.ДатаЗвонка, "ДЛФ=DD"));
		
		ПоказатьОповещениеПользователя(
			НСтр("ru='Входящий звонок'"),
			ПолучитьНавигационнуюСсылку(ДанныеЗвонка.АбонентОтКого),
			СтрШаблон(ШаблонПояснения, ДанныеЗвонка.ПредставлениеАбонента, ДанныеЗвонка.НомерТелефонаАбонента, ПредставлениеДаты),
			БиблиотекаКартинок.ТелефонныйЗвонокВходящий
		);
		
		Если ДанныеЗвонка.ДействиеТекущегоЗвонкаНайденногоАбонента = "ОткрытьКарточкуАбонента"
			Или ДанныеЗвонка.ДействиеТекущегоЗвонкаНайденногоАбонента = "ОткрыватьСобытиеПриЗакрытииКарточкиАбонента" Тогда
			
			Если ТипЗнч(ДанныеЗвонка.АбонентОтКого) = Тип("СправочникСсылка.Контрагенты") Тогда
				ИмяФормы = "Справочник.Контрагенты.ФормаОбъекта";
			ИначеЕсли ТипЗнч(ДанныеЗвонка.АбонентОтКого) = Тип("СправочникСсылка.КонтактныеЛица") Тогда
				ИмяФормы = "Справочник.КонтактныеЛица.ФормаОбъекта";
			КонецЕсли;
			
			ПараметрыФормы = Новый Структура();
			ПараметрыФормы.Вставить("Ключ", ДанныеЗвонка.АбонентОтКого);
			Если ДанныеЗвонка.ДействиеТекущегоЗвонкаНайденногоАбонента = "ОткрыватьСобытиеПриЗакрытииКарточкиАбонента" Тогда
				ПараметрыФормы.Вставить("СобытиеДляПоказаПослеЗакрытия", ДанныеЗвонка.Событие);
			КонецЕсли;
			ОткрытьФорму(ИмяФормы, ПараметрыФормы);
			
		ИначеЕсли ДанныеЗвонка.ДействиеТекущегоЗвонкаНайденногоАбонента = "ОткрытьСобытие" Тогда
			
			ПоказатьЗначение(, ДанныеЗвонка.Событие);
			
		КонецЕсли;
		
	Иначе
		
		Если ДанныеЗвонка.ДействиеТекущегоЗвонкаНеизвестногоАбонента = "ЗадаватьВопрос" Тогда
			
			ПараметрыФормы = Новый Структура("ПользовательКому", ДанныеЗвонка.ПользовательКому);
			
			ОткрытьФорму("РегистрСведений.ТекущиеВходящиеЗвонки.Форма.НеизвестныйАбонент",
				ПараметрыФормы,
				ПараметрыВыполненияКоманды.Источник,
				ПараметрыВыполненияКоманды.Уникальность,
				ПараметрыВыполненияКоманды.Окно,
				ПараметрыВыполненияКоманды.НавигационнаяСсылка
			);
			
		ИначеЕсли ДанныеЗвонка.ДействиеТекущегоЗвонкаНеизвестногоАбонента = "СозданиеКонтрагента" Тогда
			
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("НомерТелефона",	ДанныеЗвонка.НомерТелефонаАбонента);
			ЗначенияЗаполнения.Вставить("Покупатель",		Истина);
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Справочник.Контрагенты.ФормаОбъекта", ПараметрыФормы);
			
		ИначеЕсли ДанныеЗвонка.ДействиеТекущегоЗвонкаНеизвестногоАбонента = "СозданиеСобытия" Тогда
			
			ЗначенияЗаполнения = Новый Структура;
			ЗначенияЗаполнения.Вставить("НомерТелефона",			ДанныеЗвонка.НомерТелефонаАбонента);
			ЗначенияЗаполнения.Вставить("ТипСобытия",				ПредопределенноеЗначение("Перечисление.ТипыСобытий.ТелефонныйЗвонок"));
			ЗначенияЗаполнения.Вставить("ВходящееИсходящееСобытие", ПредопределенноеЗначение("Перечисление.ВходящееИсходящееСобытие.Входящее"));
			
			ПараметрыФормы = Новый Структура;
			ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
			
			ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПолучитьДанныеЗвонка()
	
	Результат = ТелефонияСервер.ПолучитьНастройкиТелефонии();
	ДанныеЗвонка = Новый Структура("ПользовательКому,АбонентОтКого,ДатаЗвонка,НомерТелефонаАбонента,Событие,ПредставлениеАбонента");
	ТекущийПользователь = Пользователи.ТекущийПользователь();
	
	Выборка = РегистрыСведений.ТекущиеВходящиеЗвонки.Выбрать(Новый Структура("ПользовательКому", ТекущийПользователь));
	
	Если Выборка.Следующий() Тогда
		
		ЗаполнитьЗначенияСвойств(ДанныеЗвонка, Выборка);
		
		Если ЗначениеЗаполнено(ДанныеЗвонка.АбонентОтКого)
			Или Результат.ДействиеТекущегоЗвонкаНеизвестногоАбонента <> "ЗадаватьВопрос" Тогда
			
			МенеджерЗаписи = РегистрыСведений.ТекущиеВходящиеЗвонки.СоздатьМенеджерЗаписи();
			МенеджерЗаписи.ПользовательКому = ТекущийПользователь;
			МенеджерЗаписи.Удалить();
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ДанныеЗвонка.АбонентОтКого) Тогда
		ДанныеЗвонка.ПредставлениеАбонента = Строка(ДанныеЗвонка.АбонентОтКого);
	КонецЕсли;
	
	ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(Результат, ДанныеЗвонка, Истина);
	
	Возврат Результат;
	
КонецФункции
