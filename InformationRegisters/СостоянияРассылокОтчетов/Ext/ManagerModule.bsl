#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции

// Для внутреннего использования.
// 
Процедура ЗафиксироватьЗапускРассылки(Рассылка) Экспорт
	Если ТипЗнч(Рассылка) <> Тип("СправочникСсылка.РассылкиОтчетов")
		Или Не ЗначениеЗаполнено(Рассылка) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Рассылка = Рассылка;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Рассылка = Рассылка;
	МенеджерЗаписи.ПоследнийЗапуск = ТекущаяДатаСеанса();
	МенеджерЗаписи.НомерСеанса = НомерСеансаИнформационнойБазы();
	МенеджерЗаписи.Выполнена = Ложь;
	МенеджерЗаписи.СОшибками = Истина;
	
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

// Для внутреннего использования.
// 
Процедура ЗафиксироватьРезультатВыполненияРассылки(Рассылка, РезультатВыполнения) Экспорт
	Если ТипЗнч(Рассылка) <> Тип("СправочникСсылка.РассылкиОтчетов")
		Или Не ЗначениеЗаполнено(Рассылка) Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	МенеджерЗаписи = СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Рассылка = Рассылка;
	МенеджерЗаписи.Прочитать();
	
	МенеджерЗаписи.Рассылка = Рассылка;
	МенеджерЗаписи.Выполнена = (
		РезультатВыполнения.ВыполненаВПапку
		ИЛИ РезультатВыполнения.ВыполненаВСетевойКаталог
		ИЛИ РезультатВыполнения.ВыполненаНаFTP
		ИЛИ РезультатВыполнения.ВыполненаПоЭлектроннойПочте);
	
	МенеджерЗаписи.СОшибками = РезультатВыполнения.БылиОшибки ИЛИ РезультатВыполнения.БылиПредупреждения;
	Если Не МенеджерЗаписи.СОшибками Тогда
		МенеджерЗаписи.УспешныйЗапуск = МенеджерЗаписи.ПоследнийЗапуск;
	КонецЕсли;
	
	МенеджерЗаписи.Записать(Истина);
КонецПроцедуры

#КонецОбласти

#КонецЕсли
