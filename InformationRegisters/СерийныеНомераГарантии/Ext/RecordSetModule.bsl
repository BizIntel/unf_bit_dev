
// Процедура - обработчик события ПередЗаписью набора записей.
//
Процедура ПередЗаписью(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	Если НЕ СтруктураВременныеТаблицы.Свойство("ДвиженияСерийныеНомераГарантииИзменение") ИЛИ
		СтруктураВременныеТаблицы.Свойство("ДвиженияСерийныеНомераГарантииИзменение") И НЕ СтруктураВременныеТаблицы.ДвиженияСерийныеНомераГарантииИзменение Тогда
				
		// Если временная таблица "ДвиженияСерийныеНомераИзменение" не существует или не содержит записей
		// об изменении набора, значит набор записывается первый раз или для набора был выполнен контроль остатков.
		// Текущее состояние набора помещается во временную таблицу "ДвиженияСерийныеНомераПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно текущего.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	СерийныеНомера.НомерСтроки КАК НомерСтроки,
		|	СерийныеНомера.Номенклатура КАК Номенклатура,
		|	СерийныеНомера.Характеристика КАК Характеристика,
		|	СерийныеНомера.СерийныйНомер КАК СерийныйНомер,
		|	СерийныеНомера.Операция
		|ПОМЕСТИТЬ ДвиженияСерийныеНомераГарантииПередЗаписью
		|ИЗ
		|	РегистрСведений.СерийныеНомераГарантии КАК СерийныеНомера
		|ГДЕ
		|	СерийныеНомера.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	Иначе
		
		// Если временная таблица "ДвиженияСерийныеНомераИзменение" существует и содержит записи
		// об изменении набора, значит набор записывается не первый раз и для набора не был выполнен контроль остатков.
		// Текущее состояние набора и текущее состояние изменений помещаются во временную таблцу "ДвиженияСерийныеНомераПередЗаписью",
		// чтобы при записи получить изменение нового набора относительно первоначального.
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	ДвиженияСерийныеНомераИзменение.НомерСтроки КАК НомерСтроки,
		|	ДвиженияСерийныеНомераИзменение.Номенклатура КАК Номенклатура,
		|	ДвиженияСерийныеНомераИзменение.Характеристика КАК Характеристика,
		|	ДвиженияСерийныеНомераИзменение.СерийныйНомер КАК СерийныйНомер,
		|	ДвиженияСерийныеНомераИзменение.Операция КАК Операция
		|ПОМЕСТИТЬ ДвиженияСерийныеНомераГарантииПередЗаписью
		|ИЗ
		|	ДвиженияСерийныеНомераГарантииИзменение КАК ДвиженияСерийныеНомераИзменение
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	СерийныеНомера.НомерСтроки,
		|	СерийныеНомера.Номенклатура,
		|	СерийныеНомера.Характеристика,
		|	СерийныеНомера.СерийныйНомер,
		|	СерийныеНомера.Операция
		|ИЗ
		|	РегистрСведений.СерийныеНомераГарантии КАК СерийныеНомера
		|ГДЕ
		|	СерийныеНомера.Регистратор = &Регистратор
		|	И &Замещение");
		
		Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
		Запрос.УстановитьПараметр("Замещение", Замещение);
		
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		
	КонецЕсли;
	
	// Временная таблица "ДвиженияСерийныеНомераГарантииИзменение" уничтожается
	// Удаляется информация о ее существовании.
	
	Если СтруктураВременныеТаблицы.Свойство("ДвиженияСерийныеНомераГарантииИзменение") Тогда
		
		Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияСерийныеНомераГарантииИзменение");
		Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
		Запрос.Выполнить();
		СтруктураВременныеТаблицы.Удалить("ДвиженияСерийныеНомераГарантииИзменение");
	
	КонецЕсли;
	
КонецПроцедуры // ПередЗаписью()

// Процедура - обработчик события ПриЗаписи набора записей.
//
Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка
		ИЛИ НЕ ДополнительныеСвойства.Свойство("ДляПроведения")
		ИЛИ НЕ ДополнительныеСвойства.ДляПроведения.Свойство("СтруктураВременныеТаблицы") Тогда
		Возврат;
	КонецЕсли;
	
	СтруктураВременныеТаблицы = ДополнительныеСвойства.ДляПроведения.СтруктураВременныеТаблицы;
	
	// Рассчитывается изменение нового набора относительно текущего с учетом накопленных изменений
	// и помещается во временную таблицу "ДвиженияЗапасыНаСкладахИзменение".
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ
	|	ДвиженияСерийныеНомераГарантииИзменение.Номенклатура КАК Номенклатура,
	|	ДвиженияСерийныеНомераГарантииИзменение.Характеристика КАК Характеристика,
	|	ДвиженияСерийныеНомераГарантииИзменение.Операция КАК Операция,
	|	ДвиженияСерийныеНомераГарантииИзменение.СерийныйНомер КАК СерийныйНомер,
	|	ДвиженияСерийныеНомераГарантииИзменение.СерийныйНомер.Продан КАК СерийныйНомерПродан,
	|	СУММА(ДвиженияСерийныеНомераГарантииИзменение.ВидИзменения) КАК ВидИзменения
	|ИЗ
	|	(ВЫБРАТЬ
	|		ДвиженияСерийныеНомераГарантииПередЗаписью.Номенклатура КАК Номенклатура,
	|		ДвиженияСерийныеНомераГарантииПередЗаписью.Характеристика КАК Характеристика,
	|		ДвиженияСерийныеНомераГарантииПередЗаписью.Операция КАК Операция,
	|		ДвиженияСерийныеНомераГарантииПередЗаписью.СерийныйНомер КАК СерийныйНомер,
	|		-1 КАК ВидИзменения
	|	ИЗ
	|		ДвиженияСерийныеНомераГарантииПередЗаписью КАК ДвиженияСерийныеНомераГарантииПередЗаписью
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ДвиженияСерийныеНомераПриЗаписи.Номенклатура,
	|		ДвиженияСерийныеНомераПриЗаписи.Характеристика,
	|		ДвиженияСерийныеНомераПриЗаписи.Операция,
	|		ДвиженияСерийныеНомераПриЗаписи.СерийныйНомер,
	|		1
	|	ИЗ
	|		РегистрСведений.СерийныеНомераГарантии КАК ДвиженияСерийныеНомераПриЗаписи
	|	ГДЕ
	|		ДвиженияСерийныеНомераПриЗаписи.Регистратор = &Регистратор) КАК ДвиженияСерийныеНомераГарантииИзменение
	|
	|СГРУППИРОВАТЬ ПО
	|	ДвиженияСерийныеНомераГарантииИзменение.Номенклатура,
	|	ДвиженияСерийныеНомераГарантииИзменение.Характеристика,
	|	ДвиженияСерийныеНомераГарантииИзменение.Операция,
	|	ДвиженияСерийныеНомераГарантииИзменение.СерийныйНомер
	|
	|ИМЕЮЩИЕ
	|	СУММА(ДвиженияСерийныеНомераГарантииИзменение.ВидИзменения) <> 0
	|");
	
	Запрос.УстановитьПараметр("Регистратор", Отбор.Регистратор.Значение);
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаИзРезультатаЗапроса = РезультатЗапроса.Выбрать();
	Пока ВыборкаИзРезультатаЗапроса.Следующий() Цикл
		
		Если ДополнительныеСвойства.ДляПроведения.Свойство("РежимЗаписи") Тогда
			
			Если ДополнительныеСвойства.ДляПроведения.РежимЗаписи = РежимЗаписиДокумента.Проведение Тогда
				Если ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийныхНомеров.Расход Тогда
				
					УстановитьСостояниеПродан = Истина;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийныхНомеров.Приход Тогда
					
					УстановитьСостояниеПродан = Неопределено;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийныхНомеров.Возврат Тогда	
					
					УстановитьСостояниеПродан = Ложь;
					
				КонецЕсли;
			ИначеЕсли ДополнительныеСвойства.ДляПроведения.РежимЗаписи = РежимЗаписиДокумента.ОтменаПроведения Тогда
				Если ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийныхНомеров.Расход Тогда
				
					УстановитьСостояниеПродан = Ложь;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийныхНомеров.Приход Тогда
					
					УстановитьСостояниеПродан = Неопределено;
					
				ИначеЕсли ВыборкаИзРезультатаЗапроса.Операция = Перечисления.ОперацииСерийныхНомеров.Возврат Тогда	
					
					УстановитьСостояниеПродан = Истина;
					
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если УстановитьСостояниеПродан <>Неопределено И УстановитьСостояниеПродан <> ВыборкаИзРезультатаЗапроса.СерийныйНомерПродан Тогда
			СерийныйНомерОбъект = ВыборкаИзРезультатаЗапроса.СерийныйНомер.ПолучитьОбъект();
			СерийныйНомерОбъект.Продан = УстановитьСостояниеПродан;
			СерийныйНомерОбъект.Записать();
		КонецЕсли;
		
	КонецЦикла;
	
	// Новые изменения были помещены во временную таблицу "ДвиженияСерийныеНомераГарантииИзменение".
	// Добавляется информация о ее существовании и наличии в ней записей об изменении.
	
	// Временная таблица "ДвиженияЗапасыНаСкладахПередЗаписью" уничтожается
	Запрос = Новый Запрос("УНИЧТОЖИТЬ ДвиженияСерийныеНомераГарантииПередЗаписью");
	Запрос.МенеджерВременныхТаблиц = СтруктураВременныеТаблицы.МенеджерВременныхТаблиц;
	Запрос.Выполнить();
	
КонецПроцедуры // ПриЗаписи()
