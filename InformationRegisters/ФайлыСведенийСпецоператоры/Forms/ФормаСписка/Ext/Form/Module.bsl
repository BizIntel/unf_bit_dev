&НаКлиенте
Перем КонтекстЭДОКлиент Экспорт;

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	ОписаниеОповещения = Новый ОписаниеОповещения("ПриОткрытииЗавершение", ЭтотОбъект);
	ДокументооборотСКОКлиент.ПолучитьКонтекстЭДО(ОписаниеОповещения);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДействияФормыОчистить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ДействияФормыОчиститьЗавершение", ЭтотОбъект); 
	ПоказатьВопрос(ОписаниеОповещения, "Очистить кэш?", РежимДиалогаВопрос.ДаНет);
	
КонецПроцедуры

&НаКлиенте
Процедура Сохранить(Команда)
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено И КонтекстЭДОКлиент <> Неопределено Тогда
		 КонтекстЭДОКлиент.СохранитьФайлАвтонастройки(ТекущиеДанные.УчетнаяЗапись); 
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПриОткрытииЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	КонтекстЭДОКлиент = Результат.КонтекстЭДО;
	
КонецПроцедуры

&НаКлиенте
Процедура ДействияФормыОчиститьЗавершение(Ответ, ДополнительныеПараметры) Экспорт
		
	Если Ответ <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	ОчиститьКэш();
	
	Элементы.Список.Обновить();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура ОчиститьКэш()
	
	НаборЗаписей = РегистрыСведений.ФайлыСведенийСпецоператоры.СоздатьНаборЗаписей();
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти
