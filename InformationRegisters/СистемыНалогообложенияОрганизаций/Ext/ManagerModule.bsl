#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Процедура ОбновитьИспользованиеОСНО() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СистемыНалогообложенияОрганизаций.СистемаНалогообложения
	|ИЗ
	|	РегистрСведений.СистемыНалогообложенияОрганизаций КАК СистемыНалогообложенияОрганизаций
	|ГДЕ
	|	СистемыНалогообложенияОрганизаций.СистемаНалогообложения = ЗНАЧЕНИЕ(Перечисление.СистемыНалогообложения.Общая)";
	ИспользуетсяОСНО = НЕ Запрос.Выполнить().Пустой();
	
	УстановитьПривилегированныйРежим(Истина);
	Константы.ФункциональнаяОпцияИспользуетсяОСНО.Установить(ИспользуетсяОСНО);
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

Процедура ОбновитьИспользованиеРозничнойПродажиАлкоголя() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	СистемыНалогообложенияОрганизаций.СистемаНалогообложения
	|ИЗ
	|	РегистрСведений.СистемыНалогообложенияОрганизаций КАК СистемыНалогообложенияОрганизаций
	|ГДЕ
	|	СистемыНалогообложенияОрганизаций.РозничнаяПродажаАлкоголя";
	ИспользуетсяРозничнаяПродажаАлкоголя = НЕ Запрос.Выполнить().Пустой();
	
	Если ИспользуетсяРозничнаяПродажаАлкоголя Тогда
		УстановитьПривилегированныйРежим(Истина);
		Если Не ПолучитьФункциональнуюОпцию("УчетРозничныхПродаж") Тогда
			Константы.ФункциональнаяОпцияУчетРозничныхПродаж.Установить(Истина);
		КонецЕсли;
		
		Если Не Константы.ВестиСведенияДляДекларацийПоАлкогольнойПродукции.Получить() Тогда
			Константы.ВестиСведенияДляДекларацийПоАлкогольнойПродукции.Установить(Истина);
		КонецЕсли;
		УстановитьПривилегированныйРежим(Ложь);
	КонецЕсли;
	
КонецПроцедуры

Функция ОпределитьСистемуНалогообложенияОрганизации(Организация, Знач Дата) экспорт
	
	Если Не ЗначениеЗаполнено(Организация) Тогда
		Возврат Новый Структура("ПлательщикУСН, ПлательщикЕНВД, ПрименяетсяПатент",Ложь, Ложь, Ложь);;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Возврат РегистрыСведений.СистемыНалогообложенияОрганизаций.ПолучитьПоследнее(Дата, Новый Структура("Организация", Организация));
	
КонецФункции

Функция ПолучитьКодСистемыНалогообложенияОрганизации(Организация, ДатаДокумента = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаДокумента) Тогда
		ДатаДокумента = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СистемыНалогообложенияОрганизацийСрезПоследних.Организация,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.СистемаНалогообложения,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.ПлательщикУСН,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.ПлательщикЕНВД,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.ПрименяетсяПатент,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.ОбъектНалогообложения,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.СтавкаНалога,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.ПлательщикТорговыйСбор,
	|	СистемыНалогообложенияОрганизацийСрезПоследних.РозничнаяПродажаАлкоголя
	|ИЗ
	|	РегистрСведений.СистемыНалогообложенияОрганизаций.СрезПоследних(&ДатаДокумента, Организация = &Организация) КАК СистемыНалогообложенияОрганизацийСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаДокумента", ДатаДокумента);
	Запрос.УстановитьПараметр("Организация"  , Организация);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Если Выборка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Общая Тогда
			Возврат 0;
		ИначеЕсли Выборка.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная Тогда
			Возврат 1;
		Иначе
			Возврат 2;
		КонецЕсли;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

#КонецЕсли
