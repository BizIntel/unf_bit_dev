
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если Параметры.Ключ.Пустой() Тогда
		Если Параметры.ЕстьСобственныеДанныеЗаполнения Тогда
			Менеджер = РегистрыСведений.СистемыНалогообложенияОрганизаций.СоздатьМенеджерЗаписи();
			ЗаполнитьЗначенияСвойств(Менеджер, Параметры.СобственныеДанныеЗаполнения);
			Менеджер.Прочитать();
			Если Менеджер.Выбран() Тогда
				ЗначениеВРеквизитФормы(Менеджер, "Запись");
			Иначе
				ЗаполнитьЗначенияСвойств(Запись, Параметры.СобственныеДанныеЗаполнения);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
	ВидыОбъектовНалогообложения = Новый Структура;
	ВидыОбъектовНалогообложения.Вставить("Доходы", Перечисления.ВидыОбъектовНалогообложения.Доходы);
	ВидыОбъектовНалогообложения.Вставить("ДоходыМинусРасходы", Перечисления.ВидыОбъектовНалогообложения.ДоходыМинусРасходы);
	
	
	Если Запись.Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ЮридическоеЛицо Тогда
		Элементы.ГруппаУСН.Видимость = Ложь;
		Элементы.ПрименяетсяПатент.Видимость = Ложь;
	Иначе
		Элементы.ПрименяетсяПатент.Видимость = Истина;
		Элементы.ГруппаУСН.Видимость = Запись.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписьСистемыНалогообложения",Новый Структура("Организация", Запись.Организация));
КонецПроцедуры

&НаКлиенте
Процедура ОбъектНалогообложенияПриИзменении(Элемент)
	Если Запись.ОбъектНалогообложения = ВидыОбъектовНалогообложения.Доходы Тогда
		Запись.СтавкаНалога = 6;
	ИначеЕсли Запись.ОбъектНалогообложения = ВидыОбъектовНалогообложения.ДоходыМинусРасходы Тогда
		Запись.СтавкаНалога = 15;
	Иначе
		Запись.СтавкаНалога = 0;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПриИзменении(Элемент)
	СистемаНалогообложенияПриИзмененииНаСервере();
КонецПроцедуры

&НаСервере
Процедура СистемаНалогообложенияПриИзмененииНаСервере()
	
	Если Запись.Организация.ЮридическоеФизическоеЛицо = Перечисления.ЮридическоеФизическоеЛицо.ФизическоеЛицо Тогда
		Элементы.ГруппаУСН.Видимость = Запись.СистемаНалогообложения = Перечисления.СистемыНалогообложения.Упрощенная;
		Элементы.ПрименяетсяПатент.Видимость = Истина;
	Иначе
		Элементы.ГруппаУСН.Видимость = Ложь;
		Элементы.ПрименяетсяПатент.Видимость = Ложь;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	СистемаНалогообложенияПриИзмененииНаСервере();
КонецПроцедуры
