
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьНадписьВерсия();
	
	ОбменНаСервере = ?(Запись.ОбменНаСервере, 1, 0);
	РабочееМесто = МенеджерОборудованияВызовСервера.ПолучитьРабочееМестоКлиента();
	
	Если РабочееМесто = Запись.РабочееМесто И ЗначениеЗаполнено(РабочееМесто) Тогда
		ИспользоватьНастройкуДляТекущегоРабочегоМеста = Истина;
	КонецЕсли;
	
	ПриИзмененииВариантаПодключения();
	
	Если НЕ ПустаяСтрока(Запись.ИдентификаторФСРАР) Тогда
		ФорматОбмена = ИспользуемыйФорматОбмена(Запись.ИдентификаторФСРАР);
	Иначе
		ФорматОбмена = Перечисления.ФорматыОбменаЕГАИС.V1;
	КонецЕсли;
	
	Элементы.ГруппаОбработкаОбслуживания.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьДополнительныеОтчетыИОбработки");
	Элементы.ОбменНаСервере.Видимость = НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая()
		И НЕ ОбщегоНазначенияПовтИсп.РазделениеВключено();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ТекущийОбъект.ОбменНаСервере = ОбменНаСервере;
	
	Если ИспользоватьНастройкуДляТекущегоРабочегоМеста Тогда
		ТекущийОбъект.РабочееМесто = РабочееМесто;
	Иначе
		ТекущийОбъект.РабочееМесто = Справочники.РабочиеМеста.ПустаяСсылка();
	КонецЕсли;
	
	МенеджерЗаписи = РегистрыСведений.ФорматыОбменаЕГАИС.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.ИдентификаторФСРАР = Запись.ИдентификаторФСРАР;
	МенеджерЗаписи.Прочитать();
	
	Если МенеджерЗаписи.Выбран() И МенеджерЗаписи.ФорматОбмена = ФорматОбмена Тогда
		ИзмененФорматОбмена = Ложь;
	Иначе
		ИзмененФорматОбмена = Истина;
		МенеджерЗаписи.ИдентификаторФСРАР = Запись.ИдентификаторФСРАР;
		МенеджерЗаписи.ФорматОбмена = ФорматОбмена;
		МенеджерЗаписи.Записать();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если ИзмененФорматОбмена Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ВыгрузкаИнформацииОФорматеОбмена_Подтверждение", ЭтотОбъект),
			НСтр("ru='Изменился формат обмена с ЕГАИС. Выгрузить информацию об изменении формата в УТМ?'"),
			РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ИзмененФорматОбмена И НЕ ЗавершениеРаботы Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОбработкаОбслуживанияПриИзменении(Элемент)
	
	УстановитьНадписьВерсия();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбменНаСервереПриИзменении(Элемент)
	
	Если ОбменНаСервере = 1 Тогда
		ИспользоватьНастройкуДляТекущегоРабочегоМеста = Ложь;
	КонецЕсли;
	
	ПриИзмененииВариантаПодключения();
	
КонецПроцедуры

&НаКлиенте
Процедура ИдентификаторФСРАРПриИзменении(Элемент)
	
	ФорматОбмена = ИспользуемыйФорматОбмена(Запись.ИдентификаторФСРАР);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ВыгрузкаИнформацииОФорматеОбмена_Подтверждение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	ИзмененФорматОбмена = Ложь;
	
	Если РезультатВопроса <> КодВозвратаДиалога.Да Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Массив;
	Отбор.Добавить(Новый Структура("Поле, Значение", "ИдентификаторФСРАР", Запись.ИдентификаторФСРАР));
	Отбор.Добавить(Новый Структура("Поле, Значение", "РабочееМесто", Запись.РабочееМесто));
	
	ДоступныеМодули = ИнтеграцияЕГАИСВызовСервера.ДоступныеТранспортныеМодули(Отбор);
	Если ДоступныеМодули.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ТранспортныйМодуль = ДоступныеМодули[0];
	
	ВидДокумента = ПредопределенноеЗначение("Перечисление.ВидыДокументовЕГАИС.ИнформацияОФорматеОбмена");
	
	ВходныеПараметры = ИнтеграцияЕГАИСКлиентСервер.ПараметрыИсходящегоЗапроса(ВидДокумента);
	ВходныеПараметры.ИдентификаторФСРАР = ТранспортныйМодуль.ИдентификаторФСРАР;
	ВходныеПараметры.ФорматОбмена = ТранспортныйМодуль.ФорматОбмена;
	
	ИнтеграцияЕГАИСКлиент.НачатьФормированиеИсходящегоЗапроса(
		Новый ОписаниеОповещения("ВыгрузкаИнформацииОФорматеОбмена_Завершение", ЭтотОбъект),
		ВидДокумента,
		ВходныеПараметры,
		ТранспортныйМодуль);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыгрузкаИнформацииОФорматеОбмена_Завершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если НЕ Результат.Результат Тогда
		Возврат;
	КонецЕсли;
	
	ПоказатьПредупреждение(, НСтр("ru='Информация об изменении формата обмена успешно выгружена в УТМ.'"));
	
КонецПроцедуры

&НаСервере
Процедура УстановитьНадписьВерсия()
	
	Элементы.ОбработкаОбслуживанияВерсия.Видимость = НЕ Запись.ОбработкаОбслуживания.Пустая();
	Элементы.НадписьОбработкаОбслуживанияНеВыбрана.Видимость = Запись.ОбработкаОбслуживания.Пустая();
	
	Если НЕ Запись.ОбработкаОбслуживания.Пустая() Тогда
		ВерсияОбработки = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Запись.ОбработкаОбслуживания, "Версия");
		
		Если ОбщегоНазначенияКлиентСервер.СравнитьВерсии(ВерсияОбработки, ИнтеграцияЕГАИСКлиентСервер.ВерсияПодсистемыЕГАИС()) < 0 Тогда
			Элементы.ОбработкаОбслуживанияВерсия.ЦветТекста = Новый Цвет(255, 0, 0);
			Элементы.ОбработкаОбслуживанияВерсия.Подсказка = НСтр("ru = 'Используется устаревшая версия обработки'");
		Иначе
			Элементы.ОбработкаОбслуживанияВерсия.ЦветТекста = Новый Цвет(0, 128, 0);
			Элементы.ОбработкаОбслуживанияВерсия.Подсказка = НСтр("ru = 'Используемая версия актуальна'");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииВариантаПодключения()
	
	Подсказка = "";
	Если ОбменНаСервере = 0 Тогда
		Подсказка = НСтр("ru = 'Подключение к УТМ будет осуществляться с компьютера пользователя'");
	ИначеЕсли ОбменНаСервере = 1 Тогда
		Подсказка = НСтр("ru = 'Подключение к УТМ будет осуществляться с компьютера, на котором установлен сервер 1С:Предприятия'");
	КонецЕсли;
	
	Элементы.ОбменНаСервере.Подсказка = Подсказка;
	
	Если ОбменНаСервере = 1 И НЕ ОбщегоНазначения.ИнформационнаяБазаФайловая() Тогда
		Элементы.ИспользоватьНастройкуДляТекущегоРабочегоМеста.Видимость = Ложь;
	Иначе
		Элементы.ИспользоватьНастройкуДляТекущегоРабочегоМеста.Видимость = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ИспользуемыйФорматОбмена(ИдентификаторФСРАР)
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ИдентификаторФСРАР", ИдентификаторФСРАР);
	
	Запрос.Текст =
	"ВЫБРАТЬ
	|	ФорматыОбменаЕГАИС.ФорматОбмена КАК ФорматОбмена
	|ИЗ
	|	РегистрСведений.ФорматыОбменаЕГАИС КАК ФорматыОбменаЕГАИС
	|ГДЕ
	|	ФорматыОбменаЕГАИС.ИдентификаторФСРАР = &ИдентификаторФСРАР";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если НЕ РезультатЗапроса.Пустой() Тогда
		Возврат РезультатЗапроса.Выгрузить()[0].ФорматОбмена;
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

#КонецОбласти
