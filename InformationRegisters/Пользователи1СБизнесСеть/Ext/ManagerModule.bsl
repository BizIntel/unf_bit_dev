#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область СлужебныеПроцедурыИФункции	
	
// Обработчик обновления БЭД 1.3.5.3.
// Переносит записи по организациям из регистра сведений Пользователи1СБизнесСеть
// в новый регистр Организации1СБизнесСеть.
//
Процедура ПереносЗаписейОрганизаций() Экспорт
	
	МенеджерЗаписиОрганизации = РегистрыСведений.Организации1СБизнесСеть.СоздатьМенеджерЗаписи();
	МенеджерЗаписиПользователи = РегистрыСведений.Пользователи1СБизнесСеть.СоздатьМенеджерЗаписи();
	
	Запрос = Новый Запрос;
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Пользователи1СБизнесСеть.СсылкаНаОбъект КАК Ссылка
	|ИЗ
	|	РегистрСведений.Пользователи1СБизнесСеть КАК Пользователи1СБизнесСеть
	|ГДЕ
	|	Пользователи1СБизнесСеть.СсылкаНаОбъект = &Ссылка_Справочник_Организации";
	
	ИмяСправочникаОрганизации = ЭлектронноеВзаимодействиеСлужебныйПовтИсп.ИмяПрикладногоСправочника("Организации");
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "= &Ссылка_Справочник_Организации", "ССЫЛКА Справочник." + ИмяСправочникаОрганизации);
	Запрос.Текст = ТекстЗапроса;
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	// Перенос подключенных организаций.
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Ссылка) Тогда
			МенеджерЗаписиОрганизации.Организация = Выборка.Ссылка;
			МенеджерЗаписиОрганизации.Записать(Истина);
		КонецЕсли;
		МенеджерЗаписиПользователи.СсылкаНаОбъект = Выборка.Ссылка;
		МенеджерЗаписиПользователи.Удалить();
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли