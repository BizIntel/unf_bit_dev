
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Не Запись.ИсходныйКлючЗаписи.Пустой() Тогда
		
		Запрос = Новый Запрос(
		"ВЫБРАТЬ
		|	Т.Данные
		|ИЗ
		|	РегистрСведений.ОчередьПолученияДанныхГИСМ КАК Т
		|ГДЕ
		|	Т.GLN = &GLN
		|	И Т.output_id = &output_id");
		
		Запрос.УстановитьПараметр("GLN", Запись.GLN);
		Запрос.УстановитьПараметр("output_id", Запись.output_id);
		
		Выборка = Запрос.Выполнить().Выбрать();
		Если Выборка.Следующий() Тогда
			
			Сообщения = Выборка.Данные.Получить();
			
			Если Сообщения <> Неопределено Тогда
				
				ТекстовыйДокументКонвертSOAP.УстановитьТекст(Сообщения.ТекстВходящегоСообщенияXML);
				
				ТекстСообщенияXML = ИнтеграцияГИСМВызовСервера.ТекстВходящегоСообщенияXML(Сообщения.ТекстВходящегоСообщенияXML);
				ТекстСообщенияXML = ИнтеграцияГИСМВызовСервера.ФорматироватьXMLСПараметрами(ТекстСообщенияXML, ИнтеграцияГИСМ.ПараметрыФорматированияXML(Истина, "  "));
				
				ТекстовыйДокументТекстСообщенияXML.УстановитьТекст(ТекстСообщенияXML);
				
			Иначе
				
				ТекстовыйДокументКонвертSOAP.УстановитьТекст(НСтр("ru = '<SOAP-конверт не загружен>'"));
				ТекстовыйДокументТекстСообщенияXML.УстановитьТекст(НСтр("ru = '<SOAP-конверт не загружен>'"));
				
			КонецЕсли;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	Если ПодготовитьКПовторнойЗагрузке Тогда
		ТекущийОбъект.Данные = Новый ХранилищеЗначения(Неопределено);
		ПодготовитьКПовторнойЗагрузке = Ложь;
		ТекстовыйДокументКонвертSOAP.УстановитьТекст(НСтр("ru = '<SOAP-конверт не загружен>'"));
		ТекстовыйДокументТекстСообщенияXML.УстановитьТекст(НСтр("ru = '<SOAP-конверт не загружен>'"));
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПодготовитьКПовторнойЗагрузке(Команда)
	
	ПодготовитьКПовторнойЗагрузке = Истина;
	Записать();
	
КонецПроцедуры

#КонецОбласти
