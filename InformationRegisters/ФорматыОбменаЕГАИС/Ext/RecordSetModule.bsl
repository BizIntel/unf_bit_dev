
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ, Замещение)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Истина);
	
	Для Каждого Запись Из ЭтотОбъект Цикл
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("Код", Запись.ИдентификаторФСРАР);
		Запрос.УстановитьПараметр("ФорматОбмена", Запись.ФорматОбмена);
		
		Запрос.Текст =
		"ВЫБРАТЬ
		|	КлассификаторОрганизацийЕГАИС.Ссылка КАК Ссылка,
		|	КлассификаторОрганизацийЕГАИС.ФорматОбмена КАК ФорматОбмена
		|ИЗ
		|	Справочник.КлассификаторОрганизацийЕГАИС КАК КлассификаторОрганизацийЕГАИС
		|ГДЕ
		|	КлассификаторОрганизацийЕГАИС.Код = &Код
		|	И КлассификаторОрганизацийЕГАИС.ФорматОбмена <> &ФорматОбмена";
		
		РезультатЗапроса = Запрос.Выполнить();
		Если РезультатЗапроса.Пустой() Тогда
			Продолжить;
		КонецЕсли;
		
		Выборка = РезультатЗапроса.Выбрать();
		Выборка.Следующий();
		
		ОрганизацияЕГАИС = Выборка.Ссылка.ПолучитьОбъект();
		ОрганизацияЕГАИС.ФорматОбмена = Запись.ФорматОбмена;
		ОрганизацияЕГАИС.Записать();
		
	КонецЦикла;
	
	УстановитьПривилегированныйРежим(Ложь);
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
