
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ДокументыЖурнала = Метаданные.ЖурналыДокументов.ДокументыПоКонтрагенту.РегистрируемыеДокументы;
	Для Каждого ОбъектМетаданных Из ДокументыЖурнала Цикл
		Если ПравоДоступа("Изменение", ОбъектМетаданных)
			И ОбщегоНазначения.ОбъектМетаданныхДоступенПоФункциональнымОпциям(ОбъектМетаданных) Тогда
			
			ТипыДокументов.Добавить(ОбъектМетаданных.Имя, ОбъектМетаданных.Синоним);
		КонецЕсли;
	КонецЦикла;
	
	Если ТипыДокументов.Количество() = 0 Тогда
		ОбщегоНазначенияКлиентСервер.СообщитьПользователю(НСтр("ru = 'Нарушение прав доступа!'"), , , , Отказ);
		Возврат;
	КонецЕсли;
	
	ТипыДокументов.СортироватьПоПредставлению();
	
	// Установка текущей строки списка
	Если ЗначениеЗаполнено(Параметры.НачальноеЗначение) Тогда
		ОбъектМетаданных = Метаданные.НайтиПоТипу(Параметры.НачальноеЗначение);
		Если ОбъектМетаданных <> Неопределено Тогда
			ТекущаяСтрока = ТипыДокументов.НайтиПоЗначению(ОбъектМетаданных.Имя);
			Если ТекущаяСтрока <> Неопределено Тогда
				Элементы.ТипыДокументов.ТекущаяСтрока = ТекущаяСтрока.ПолучитьИдентификатор();
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ТипыДокументовВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ТипыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	ТекущиеДанные = Элементы.ТипыДокументов.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОповеститьОВыборе(ТекущиеДанные.Значение);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
