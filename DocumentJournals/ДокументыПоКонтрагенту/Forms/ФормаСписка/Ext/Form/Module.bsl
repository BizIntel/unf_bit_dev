
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеНебольшойФирмойСервер.УстановитьОформлениеКолонкиДата(Список);
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат; // Возврат при получении формы для анализа.
	КонецЕсли;
	
	Если Параметры.Контрагент.Пустая() Тогда
		ВызватьИсключение НСтр("ru = 'Журнал документов по контрагенту открывается из карточки контрагента.'");
	КонецЕсли;
	
	Заголовок = НСтр("ru='Документы по'") + " """ + Параметры.Контрагент + """";
	КонтрагентЗаполнения = Параметры.Контрагент;
	
	ОбщегоНазначенияКлиентСервер.УстановитьПараметрДинамическогоСписка(
		Список, "ОтборКонтрагент", Параметры.Контрагент, Истина);
	
	УстановитьОтборТипСобытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ТипЗнч(ВыбранноеЗначение) <> Тип("Строка") Или ПустаяСтрока(ВыбранноеЗначение) Тогда
		Возврат;
	КонецЕсли;
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("Контрагент", КонтрагентЗаполнения);
	
	ОткрытьФорму("Документ." + ВыбранноеЗначение + ".ФормаОбъекта", Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения));

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура SMS(Команда)
	
	СоздатьСобытиеПоКонтрагенту("СообщениеSMS");
	
КонецПроцедуры

&НаКлиенте
Процедура ЛичнаяВстреча(Команда)
	
	СоздатьСобытиеПоКонтрагенту("ЛичнаяВстреча");
	
КонецПроцедуры

&НаКлиенте
Процедура Прочее(Команда)
	
	СоздатьСобытиеПоКонтрагенту("Прочее");
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефонныйЗвонок(Команда)
	
	СоздатьСобытиеПоКонтрагенту("ТелефонныйЗвонок");
	
КонецПроцедуры

&НаКлиенте
Процедура ЭлектронноеПисьмо(Команда)
	
	СоздатьСобытиеПоКонтрагенту("ЭлектронноеПисьмо");
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОтборЗаказыИСчетаПриИзменении(Элемент)
	
	УстановитьОтборТипСобытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборНакладныеПриИзменении(Элемент)
	
	УстановитьОтборТипСобытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборОплатыПриИзменении(Элемент)
	
	УстановитьОтборТипСобытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ОтборСобытияПриИзменении(Элемент)
	
	УстановитьОтборТипСобытия(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Не Копирование Тогда
		
		Отказ = Истина;
		
		ПараметрыФормы = Новый Структура;
		
		Если Элементы.Список.ТекущиеДанные <> Неопределено Тогда
			ПараметрыФормы.Вставить("НачальноеЗначение", ТипЗнч(Элементы.Список.ТекущиеДанные.Ссылка));
		КонецЕсли;
		
		ОткрытьФорму("ЖурналДокументов.ДокументыПоКонтрагенту.Форма.ВыборТипаДокумента", ПараметрыФормы, ЭтотОбъект, Истина);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтборТипСобытия(Форма)
	
	СписокТипов = ПолучитьСписокТипов(Форма.ОтборЗаказыИСчета, Форма.ОтборНакладные, Форма.ОтборОплаты, Форма.ОтборСобытия);
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Форма.Список,
		"Тип",
		СписокТипов,
		,
		,
		СписокТипов.Количество() > 0
	);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьСписокТипов(ЗаказыИСчета, Накладные, Оплаты, События)
	
	СписокТипов = Новый Массив;
	
	Если ЗаказыИСчета Тогда
		СписокТипов.Добавить(Тип("ДокументСсылка.ЗаказПокупателя"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ЗаказПоставщику"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СчетНаОплату"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
	КонецЕсли;
	
	Если Накладные Тогда
		СписокТипов.Добавить(Тип("ДокументСсылка.АвансовыйОтчет"));
		СписокТипов.Добавить(Тип("ДокументСсылка.АктВыполненныхРабот"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ВводНачальныхОстатков"));
		СписокТипов.Добавить(Тип("ДокументСсылка.Доверенность"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ДополнительныеРасходы"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомиссионера"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетКомитенту"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетОПереработке"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтчетПереработчика"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПередачаВА"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПеремещениеЗапасов"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПриходнаяНакладная"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПрочиеРасходы"));
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходнаяНакладная"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СчетФактура"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СчетФактураПолученный"));
	КонецЕсли;
	
	Если Оплаты Тогда
		СписокТипов.Добавить(Тип("ДокументСсылка.Взаимозачет"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ДоговорКредитаИЗайма"));
		СписокТипов.Добавить(Тип("ДокументСсылка.НачисленияПоКредитамИЗаймам"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОперацияПоПлатежнымКартам"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПлатежноеПоручение"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеВКассу"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеДСПлан"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ПоступлениеНаСчет"));
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходДСПлан"));
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходИзКассы"));
		СписокТипов.Добавить(Тип("ДокументСсылка.РасходСоСчета"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СверкаВзаиморасчетов"));
	КонецЕсли;
	
	Если События Тогда
		СписокТипов.Добавить(Тип("ДокументСсылка.ЗаданиеНаРаботу"));
		СписокТипов.Добавить(Тип("ДокументСсылка.МассоваяРассылка"));
		СписокТипов.Добавить(Тип("ДокументСсылка.ОтзывСогласияНаОбработкуПерсональныхДанных"));
		СписокТипов.Добавить(Тип("ДокументСсылка.Событие"));
		СписокТипов.Добавить(Тип("ДокументСсылка.СогласиеНаОбработкуПерсональныхДанных"));
	КонецЕсли;
	
	Возврат СписокТипов;
	
КонецФункции

&НаКлиенте
Процедура СоздатьСобытиеПоКонтрагенту(ИмяТипаСобытия)
	
	ЗначенияЗаполнения = Новый Структура;
	ЗначенияЗаполнения.Вставить("ТипСобытия", ПредопределенноеЗначение("Перечисление.ТипыСобытий." + ИмяТипаСобытия));
	ЗначенияЗаполнения.Вставить("Контрагент", КонтрагентЗаполнения);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ЗначенияЗаполнения", ЗначенияЗаполнения);
	
	ОткрытьФорму("Документ.Событие.ФормаОбъекта", ПараметрыФормы, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти
