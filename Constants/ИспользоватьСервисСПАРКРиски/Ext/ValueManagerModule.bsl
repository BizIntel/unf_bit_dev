
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	ИспользоватьСервис = ЭтотОбъект.Значение;
	
	// Отключение неразделенного задания.
	Задание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ВсеОбновленияСПАРКРискиОбщее);
	Если Задание <> Неопределено
		И Задание.Использование <> ИспользоватьСервис Тогда
		Задание.Использование = ИспользоватьСервис;
		Задание.Записать();
	КонецЕсли;
	
	// Отключение разделенного задания.
	Если Не ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		
		// Отключается только при работе в локальном режиме.
		// При работе в модели сервиса не отключается,
		// т.к. требуется отключение в каждой области данных при
		// неограниченном количестве областей. В этом случае выполняется проверка
		// внутри регламентного задания.
		Задание = РегламентныеЗадания.НайтиПредопределенное(Метаданные.РегламентныеЗадания.ВсеОбновленияСПАРКРискиРазделенное);
		Если Задание <> Неопределено
			И Задание.Использование <> ИспользоватьСервис Тогда
			Задание.Использование = ИспользоватьСервис;
			Задание.Записать();
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли