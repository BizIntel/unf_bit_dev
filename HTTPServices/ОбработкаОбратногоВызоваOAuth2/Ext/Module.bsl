#Область ПрограммныйИнтерфейс

Функция oauth2callbackGET(Запрос)
	
	code = Запрос.ПараметрыЗапроса.Получить("code");
	state = Запрос.ПараметрыЗапроса.Получить("state");
	
	Если Не ЗначениеЗаполнено(code) Тогда
		Возврат СообщениеОбОшибке(
		400,
		"Bad Request",
		НСтр("ru = 'Ошибка авторизации: отсутствует параметр ""code""'"));
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(state) Тогда
		Возврат СообщениеОбОшибке(
		400,
		"Bad Request",
		НСтр("ru = 'Ошибка авторизации: отсутствует параметр ""state""'"));
	КонецЕсли;
	
	ПараметрыАвторизации = СтроковыеФункцииКлиентСервер.ПолучитьПараметрыИзСтроки(
	РаскодироватьСтроку(state, СпособКодированияСтроки.КодировкаURL));
	
	Если Не ПараметрыАвторизации.Свойство("id") Тогда
		Возврат СообщениеОбОшибке(
		400,
		"Bad Request",
		НСтр("ru = 'Ошибка авторизации: отсутствует ключ ""id"" в параметре ""state""'"));
	КонецЕсли;
	
	Попытка
		
		УстановитьРазделениеСеанса(ПараметрыАвторизации);
		
		РегистрыСведений.ДанныеАвторизацииGoogle.ЗаписатьТокенЗапроса(
		ПараметрыАвторизации.id,
		code);
		
	Исключение
		
		Возврат СообщениеОбОшибке(
		500,
		"Internal Server Error",
		ОписаниеОшибки());
		
	КонецПопытки;
	
	Ответ = Новый HTTPСервисОтвет(200);
	Если ПараметрыАвторизации.Свойство("closebrowser")
		И НРег(ПараметрыАвторизации.closebrowser) = "true" Тогда
		Ответ.Заголовки["Content-type"] = "text/html";
		Ответ.УстановитьТелоИзСтроки(
		"<html><body onload=""parent.close()"">You can now close this window and continue.</body></html>");
	КонецЕсли;
	Возврат Ответ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура УстановитьРазделениеСеанса(ПараметрыАвторизации)
	
	Если Не ОбщегоНазначенияПовтИсп.СеансЗапущенБезРазделителей() Тогда
		Возврат;
	КонецЕсли;
	
	Если Не ПараметрыАвторизации.Свойство("zone") Тогда
		ВызватьИсключение НСтр("ru = 'Отсутствует ключ ""zone"" в параметре ""state""'");
	КонецЕсли;
	
	ОписаниеТиповЧисло = Новый ОписаниеТипов("Число");
	РазделительСеанса = ОписаниеТиповЧисло.ПривестиЗначение(ПараметрыАвторизации.zone);
	
	Если Не ОбщегоНазначенияПовтИсп.ДоступноИспользованиеРазделенныхДанных() Тогда
		ОбщегоНазначения.УстановитьРазделениеСеанса(Истина, РазделительСеанса);
	КонецЕсли;
	
КонецПроцедуры

Функция СообщениеОбОшибке(КодСостояния, ТекстОшибкиДляКлиента, ТекстОшибкиДляЖурнала)
	
	ЗаписьЖурналаРегистрации(
	"oauth2callback",
	УровеньЖурналаРегистрации.Ошибка,,,
	ТекстОшибкиДляЖурнала);
	
	Ответ = Новый HTTPСервисОтвет(КодСостояния);
	Ответ.Заголовки["Content-type"] = "text/html";
	Ответ.УстановитьТелоИзСтроки(
	СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
	"<html><title>%1</title><body><h1>%1 %2</h1></body></html>",
	КодСостояния,
	ТекстОшибкиДляКлиента));
	Возврат Ответ;
	
КонецФункции

#КонецОбласти